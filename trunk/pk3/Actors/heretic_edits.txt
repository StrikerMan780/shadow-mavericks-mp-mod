ACTOR SMMP_HereticImpBall : HereticImpBall replaces HereticImpBall
{
	Speed 30
	FastSpeed 50
	Damage 3
	Alpha 1.0
	RenderStyle Add
	States
	{
	Spawn:
		FX10 AABBCC 1 Bright A_SpawnItemEx("HereticImpBallTrail", 0, 0, 0, random(-1,1), random(-1,1), random(-1,1), random(-180,180), SXF_CLIENTSIDE, 0)
		Loop
	Death:
		FX10 DEFG 5 Bright
		Stop
	}
}

actor HereticImpBallTrail
{
	Radius 2
	Height 2
	Alpha 1.0
	Renderstyle Add
	+NOGRAVITY
	+NOBLOCKMAP
	+NOINTERACTION
	States
	{
		Spawn:
			FX10 DEFG 1 Bright
			stop
	}
}

actor SM_HereticImp : HereticImp
{
	+FLOATBOB
	States
	{
		Melee:
			IMPX DE 4 A_FaceTarget
			IMPX F 6 A_CustomMeleeAttack(random[ImpMeAttack](5,12), "himp/attack", "himp/attack")
			Goto See
		Missile:
			IMPX A 0 A_Jump(75, "Throw")
			IMPX A 8 A_FaceTarget
			IMPX B 6 A_SkullAttack(12)
			IMPX CBAB 6
			Goto Missile+3
		Throw:
			IMPX DE 4 A_FaceTarget
			IMPX F 6 A_CustomComboAttack("HereticImpBall", 32, random[ImpMsAttack2](5,12), "himp/leaderattack")
			Goto See
		Death:
			IMPX S 0 A_ChangeFlag("FLOATBOB", 0)
			IMPX G 4 A_ImpDeath
			IMPX H 5
			Wait
		XDeath:
			IMPX S 0 A_ChangeFlag("FLOATBOB", 0)
			IMPX S 5 A_ImpXDeath1
			IMPX TU 5
			IMPX V 5 A_Gravity
			IMPX W 5 
			Wait
		Crash:
			IMPX I 7 A_ImpExplode
			IMPX J 7 A_Scream
			IMPX K 7
			IMPX L 10500
			goto Fade
		Fade:
			IMPX L 1 A_FadeOut(0.05)
			loop
		XCrash:
			IMPX X 7
			IMPX Y 7
			IMPX Z 10500
			goto XFade
		XFade:
			IMPX Z 1 A_FadeOut(0.05)
			loop
	}
}

actor SM_HereticImpLeader : SM_HereticImp replaces HereticImpLeader
{
	Health 80
	PainChance 128
	Speed 12
	-MISSILEMORE
	+MISSILEEVENMORE
	+FLOATBOB
	AttackSound "himp/attack"
	States
	{
		Melee:
			IMPX DE 3 A_FaceTarget
			IMPX F 5 A_CustomMeleeAttack(random[ImpMeAttack](5,12), "himp/attack", "himp/attack")
			Goto See
		Missile:
			IMPX A 0 A_Jump(132, "Throw")
			IMPX A 6 A_FaceTarget
			IMPX B 6 A_SkullAttack(12)
			IMPX CBAB 6
			Goto Missile+3
		Throw:
			IMPX DE 3 A_FaceTarget
			IMPX F 5 A_CustomComboAttack("HereticImpBall", 32, random[ImpMsAttack2](5,12), "himp/leaderattack")
			IMPX DE 3 A_FaceTarget
			IMPX F 5 A_CustomComboAttack("HereticImpBall", 32, random[ImpMsAttack2](5,12), "himp/leaderattack")
			IMPX DE 3 A_FaceTarget
			IMPX F 5 A_CustomComboAttack("HereticImpBall", 32, random[ImpMsAttack2](5,12), "himp/leaderattack")
			Goto See
	}
}

actor Float_Fleshspawn : Fleshspawn
{
	+SPAWNFLOAT
}

actor SM_Mummy : Mummy
{
	hitobituary "%o was beaten down by a flesh golem"
	obituary "%o was flayed by a flesh golem"
	PainChance 96
	States
	{
		See:
			MUMM AABBCCDD 2 A_Chase
			Loop
		Melee:
			MUMM E 5 A_FaceTarget
			MUMM F 5 A_CustomMeleeAttack(random[MummyAttack](1,8)*2, "mummy/attack2", "mummy/attack")
			MUMM G 5
			Goto See
		Death:
			MUMM I 5
			MUMM J 5 A_Scream
			MUMM K 5 A_SpawnItemEx("MummySoul", 0,0,10, 0,0,1)
			MUMM L 5
			MUMM M 5 A_NoBlocking
			MUMM NO 5
			MUMM P 10500
			goto Fade
		Fade:
			MUMM P 1 A_FadeOut(0.05)
			loop
	}
}

actor SM_MummyLeader : SM_Mummy 
{
	Health 125
	PainChance 64
	States
	{
		Missile:
			MUMM X 5 A_FaceTarget
			MUMM Y 5 Bright A_FaceTarget
			MUMM X 5 A_FaceTarget
			MUMM Y 5 Bright A_FaceTarget
			MUMM X 5 A_FaceTarget
			MUMM Y 5 Bright A_CustomComboAttack("MummyFX1", 32, random[MummyAttack2](1,8)*2, "mummy/attack2")
			Goto See
	}
}

actor SMMP_Pod : Pod replaces Pod
{
	States
	{
		Death:
			PPOD B 5 A_RemovePod
			PPOD C 4 A_Scream
			PPOD D 0 radius_quake(3, 16, 0, 3, 0)
			PPOD D 4 A_Explode
			PPOD E 0 A_Jump(64, "Death2")
			PPOD EEE 0 A_SpawnItemEx ("Striker_FlyingBlood", 0, 0, 24, MOMX+Random(-10, 10), MOMY+Random(-10,10), MOMZ+Random(2, 20), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
			PPOD EEEEEEEE 0 A_SpawnItemEx("Fleshy_Splatter", 0, 0, 24, MOMX+random(-2, 2), MOMY+random(-2, 2), MOMZ+random(1, 14), Random(-180, 180), SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
			PPOD E 3
			PPOD F 2
			Stop
		Death2:
			PPOD E 0 A_XScream
			PPOD E 3 A_SpawnItemEx("XDeathGibs", 0, 0, 16, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0) 
			PPOD F 2
			Stop
	}
}