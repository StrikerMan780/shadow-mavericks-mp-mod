ACTOR ChaingunShotCount : Inventory
{
Inventory.Amount 1
Inventory.MaxAmount 3
-INVBAR
}

actor SM_Chaingun : Weapon Replaces Chaingun
{
	spawnid 28
	obituary "%o was turned into swiss-cheese by %k's gatling gun."
	radius 20
	height 16
	inventory.pickupmessage "\cdGatling Gun \cc/ \ciDual Micro-Repeaters"
	weapon.selectionorder 700
	weapon.kickback 100
	weapon.ammotype "MechAmmo"
	weapon.ammouse 1
	weapon.ammogive 20	
	states
	{
	Ready:
		CHGG A 0 A_JumpIfInventory("TailsClassCheck", 1, "TailsReady")
		CHGG A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeReady")
		CHGG A 1 A_WeaponReady
		loop
	Deselect:
		CHGG A 0 A_JumpIfInventory("TailsClassCheck", 1, "TailsDeselect")
		CHGG A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeDeselect")
		CHGG A 1 A_Lower
		loop
	Select:
		CHGG A 0 A_JumpIfInventory("TailsClassCheck", 1, "TailsSelect")
		CHGG A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeSelect")
		CHGG A 1 A_Raise
		loop
	Hold:
		CHGG A 0
	Fire:
		CHGG B 0 A_JumpIfInventory("TailsClassCheck", 1, "TailsFire")
		CHGG B 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeFire")
		CHGG B 0 A_JumpIfInventory("ChaingunShotCount", 3, "Fire4")
		CHGG B 0 A_JumpIfInventory("ChaingunShotCount", 2, "Fire3")
		CHGG B 0 A_JumpIfInventory("ChaingunShotCount", 1, "Fire2")
		CHGG B 2 BRIGHT A_GunFlash
		CHGG C 2
		CHGG D 2 BRIGHT A_GunFlash
		CHGG E 2 A_GiveInventory("ChaingunShotCount", 1)
		CHGG F 0 A_Refire
		CHGG F 0 A_TakeInventory("ChaingunShotCount", 3)
		goto Ready
	Fire2:
		CHGG F 2 BRIGHT A_GunFlash
		CHGG G 2
		CHGG H 2 BRIGHT A_GunFlash
		CHGG I 2 A_GiveInventory("ChaingunShotCount", 1)
		CHGG J 0 A_Refire
		CHGG J 0 A_TakeInventory("ChaingunShotCount", 3)
		goto Ready
	Fire3:
		CHGG J 2 BRIGHT A_GunFlash
		CHGG K 2
		CHGG L 2 BRIGHT A_GunFlash
		CHGG M 2 A_GiveInventory("ChaingunShotCount", 1)
		CHGG N 0 A_Refire
		CHGG N 0 A_TakeInventory("ChaingunShotCount", 3)
		goto Ready
	Fire4:
		CHGG N 2 BRIGHT A_GunFlash
		CHGG O 2
		CHGG P 2 BRIGHT A_GunFlash
		CHGG A 2 A_TakeInventory("ChaingunShotCount", 3)
		CHGG B 0 A_ReFire
		goto Ready
	Flash:
		TNT1 A 0 BRIGHT A_PlayWeaponSound("weapons/chngun")
		TNT1 A 0 BRIGHT A_FireBullets (5, 1.5, 1, 3, "SuperChaingunHit")
		TNT1 A 0 BRIGHT A_FireCustomMissile("BulletCasingSpawnerRight",-45+random(-8,8),0,12,3)
		TNT1 A 0 BRIGHT A_Light2
		TNT1 A 0 BRIGHT radius_quake(2, 2, 0, 1, 0)
		TNT1 A 0 BRIGHT A_CheckForReload(2, "Flash1")
		goto Flash2
	Flash1:
		CHGF A 2 BRIGHT
		TNT1 A 1 BRIGHT A_Light0
		stop
	Flash2:
		CHGF B 2 BRIGHT
		TNT1 A 0 BRIGHT A_ResetReloadCounter
		TNT1 A 1 BRIGHT A_Light0
		stop
	NoFlash:
		TNT1 A 1
		stop

	TailsReady:
		DAUT A 1 A_WeaponReady
		loop
	TailsDeselect:
		DAUT A 1 A_Lower
		loop
	TailsSelect:
		DAUT A 1 A_Raise
		loop
	TailsFire:
		DAUT B 0 A_GunFlash("TailsFlash1")
		DAUT B 0 A_PlayWeaponSound("TailsRepeater")
		DAUT B 0 A_FireCustomMissile("RepeaterCasingSpawnerRight",-45+random(-8,8),0,6,7)
		DAUT B 0 BRIGHT A_FireBullets (4.5, 1.5, 1, 5, "RepeaterPuff")
		DAUT B 1 BRIGHT A_SetPitch(pitch-0.25)
		DAUT C 2 A_SetPitch(pitch+0.25)
		DAUT A 1
		DAUT D 0 A_GunFlash("TailsFlash2")
		DAUT D 0 A_PlayWeaponSound("TailsRepeater")
		DAUT D 0 A_FireCustomMissile("RepeaterCasingSpawnerLeft",45+random(-8,8),0,-6,7)
		DAUT D 0 A_FireBullets (4.5, 1.5, 1, 5, "RepeaterPuffLeft")
		DAUT D 1 BRIGHT A_SetPitch(pitch-0.25)
		DAUT E 2 A_SetPitch(pitch+0.25)
		DAUT A 0 A_ReFire
		goto Ready
	TailsFlash1:
		DAUF A 2 BRIGHT
		DAUF B 1 BRIGHT
		stop
	TailsFlash2:
		DAUF C 2 BRIGHT
		DAUF D 1 BRIGHT
		stop

	DukeReady:
		RIPG A 1 A_WeaponReady
		loop
	DukeSelect:
		RIPG A 0 A_PlayWeaponSound("DukeWeaponSelect")
		RIPG A 1 A_Raise
		goto DukeSelect+1
	DukeDeselect:
		RIPG A 1 A_Lower
		loop
	DukeFire:
		RIPG B 0 BRIGHT A_JumpIfInventory("ChaingunShotCount", 3, "DukeFire4")
		RIPG B 0 BRIGHT A_JumpIfInventory("ChaingunShotCount", 2, "DukeFire3")
		RIPG B 0 BRIGHT A_JumpIfInventory("ChaingunShotCount", 1, "DukeFire2")
		RIPG B 0 BRIGHT A_FireCustomMissile("BulletCasingSpawnerRight",-45+random(-8,8),0,5,4)
		RIPG B 0 BRIGHT A_PlayWeaponSound("DukeRipper")
		RIPG B 0 radius_quake(1, 2, 0, 1, 0)
		RIPG B 2 BRIGHT A_FireBullets(5, 2, 1, 6, "NewBulletPuff")
		RIPG A 0 BRIGHT A_GiveInventory("ChaingunShotCount", 1)
		RIPG A 0 A_Refire
		RIPG A 0 A_TakeInventory("ChaingunShotCount", 3)
		goto DukeReady
	DukeFire2:
		RIPG C 2 BRIGHT
		RIPG D 0 BRIGHT A_FireCustomMissile("BulletCasingSpawnerRight",-45+random(-8,8),0,5,4)
		RIPG D 0 BRIGHT A_PlayWeaponSound("DukeRipper")
		RIPG D 0 radius_quake(1, 2, 0, 1, 0)
		RIPG D 2 BRIGHT A_FireBullets(5, 2, -1, 5, "NewBulletPuff")
		RIPG E 1 BRIGHT A_GiveInventory("ChaingunShotCount", 1)
		RIPG A 0 A_Refire
		RIPG A 0 A_TakeInventory("ChaingunShotCount", 3)
		goto DukeReady
	DukeFire3:
		RIPG F 0 BRIGHT A_FireCustomMissile("BulletCasingSpawnerRight",-45+random(-8,8),0,5,4)
		RIPG F 0 BRIGHT A_PlayWeaponSound("DukeRipper")
		RIPG F 0 radius_quake(1, 2, 0, 1, 0)
		RIPG F 2 BRIGHT A_FireBullets(5, 2, -1, 5, "NewBulletPuff")
		RIPG G 2 BRIGHT A_GiveInventory("ChaingunShotCount", 1)
		RIPG A 0 A_Refire
		RIPG A 0 A_TakeInventory("ChaingunShotCount", 3)
		goto DukeReady
	DukeFire4:
		RIPG H 0 BRIGHT A_FireCustomMissile("BulletCasingSpawnerRight",-45+random(-8,8),0,5,4)
		RIPG H 0 BRIGHT A_PlayWeaponSound("DukeRipper")
		RIPG H 0 radius_quake(1, 2, 0, 1, 0)
		RIPG H 2 BRIGHT A_FireBullets(5, 2, -1, 5, "NewBulletPuff")
		RIPG I 1 BRIGHT A_TakeInventory("ChaingunShotCount", 3)
		RIPG A 0 A_Refire
		goto DukeReady

	Spawn:
		MGUN A -1
		stop
	}
}

actor RepeaterPuff
{
	Radius 11
	Height 2
	Scale 0.4
	Alpha 1
	RenderStyle Add
	Mass 100
	Decal RepeaterScorch
	AttackSound "RepeaterHit"
	SeeSound "Empty"
	Obituary "%o was eaten away by %k's BCF-2 Repeaters!"
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+PUFFGETSOWNER
	+FORCEXYBILLBOARD
	+ISMONSTER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Crash
		Crash:
			RPUF A 0 BRIGHT 
			RPUF A 0 BRIGHT A_SpawnItemEx("RepeaterTracerSpawnerRight", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			goto Spark
		Spark:
			RPUF AAAAAA 0 BRIGHT A_SpawnItemEx("RepeaterSpark", 0, 0, 0, Random(-2,2), Random(-2,2), Random(2,8), 0, SXF_CLIENTSIDE, 16)
			RPUF A 0 BRIGHT A_Jump(128, "Spawn2")
			goto Spawn1
		Spawn1:
			RPUF AB 1 BRIGHT Light("RepeaterPuffLight")
			stop
		Spawn2:
			RPUF CD 1 BRIGHT Light("RepeaterPuffLight")
			stop
	}
}

actor RepeaterPuffLeft : RepeaterPuff
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Crash
		Crash:
			RPUF A 0 BRIGHT 
			RPUF A 0 BRIGHT A_SpawnItemEx("RepeaterTracerSpawnerLeft", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			goto Spark
	}
}

actor RepeaterSpark
{
	Height 1
	Radius 1
	Projectile
	-NOGRAVITY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	RenderStyle Add
	Scale 0.08
	Alpha 0.9
	Speed 0
	Damage 0
	States
	{
		Spawn:
			RPUF ABCDABCD 1 BRIGHT
			RPUF ABCD 1 BRIGHT A_FadeOut(0.1)
			goto Spawn+8

		Death:
			TNT1 A 1
			stop
	}
}