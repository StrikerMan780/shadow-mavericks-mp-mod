actor SM_SuperShotgun : Weapon Replaces SuperShotgun
{
	Decal BulletChip
	spawnid 33
	obituary "%k turned %o's torso into a pile of red gruel."
	radius 20
	height 16
	inventory.pickupmessage "\cdGolden SSG \cc/ \ciSilver Bullet Gun \cc/ \cfDin's Fire \cc/ \crSlamshot"
	weapon.selectionorder 400
	weapon.kickback 100
	Weapon.AmmoType "SpreadAmmo"
	weapon.ammouse 2
	weapon.ammogive 8
	states
	{
		Ready:
			SHT2 A 0 A_JumpIfInventory("TailsClassCheck", 1, "SBGReady")
			SHT2 A 0 A_JumpIfInventory("LinkClassCheck", 1, "DinReady")
			SHT2 A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeReady")
			SHT2 A 1 A_WeaponReady
			goto Ready+3
		Deselect:
			SHT2 A 0 A_JumpIfInventory("TailsClassCheck", 1, "SBGDeselect")
			SHT2 A 0 A_JumpIfInventory("LinkClassCheck", 1, "DinDeselect")
			SHT2 A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeDeselect")
			SHT2 A 1 A_Lower
			goto Deselect+3
		Select:
			SHT2 A 0 A_JumpIfInventory("TailsClassCheck", 1, "SBGSelect")
			SHT2 A 0 A_JumpIfInventory("LinkClassCheck", 1, "DinSelect")
			SHT2 A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeSelect")
			SHT2 A 1 A_Raise
			goto Select+3
		Fire:
			SHT2 A 0 A_JumpIfInventory("TailsClassCheck", 1, "SBGFire")
			SHT2 A 0 A_JumpIfInventory("LinkClassCheck", 1, "DinFire")
			SHT2 A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeFire")
			SHT2 A 2
			SHT2 A 0 radius_quake(4, 3, 0, 1, 0)
			SHT2 A 0 A_PlayWeaponSound("weapons/sshotf")
			SHT2 A 0 A_FireBullets (11.2, 7.1, 20, 5, "SSGPuff")
			SHT2 A 2 A_GunFlash
			SHT2 B 0 A_SetPitch(pitch-4)
			SHT2 BCDEF 1 A_SetPitch(pitch+0.8)
			SHT2 AA 1 A_CheckReload
			SHT2 A 2
			SH2R ABCDE 2
			SH2R F 2 A_PlaySoundEx("weapons/sshoto", "auto", 0)
			SH2R GG 0 A_FireCustomMissile("SSGCasing", Random(-14,8),0,2,0)
			SH2R GHI 2
			SH2R J 2
			SH2R K 2 A_PlaySoundEx("weapons/sshotl", "auto", 0)
			SH2R LMNO 2
			SH2R P 0 A_PlaySoundEx("weapons/sshotc", "auto", 0)
			SH2R PQRS 2
			SHT2 A 7 A_ReFire
			goto Ready
		Flash:
			SH2F A 0 A_JumpIfInventory("TailsClassCheck", 1, "SBGFlash")
			SH2F A 2 bright A_Light1
			SH2F B 1 bright A_Light2
			SH2F B 0 bright A_Light0
			stop

		// Silver Bullet Gun
		SBGReady:
			SBGN A 1 A_WeaponReady
			loop
		SBGSelect:
			SBGN A 1 A_Raise
			loop
		SBGDeselect:
			SBGN A 1 A_Lower
			loop
		SBGFire:
			SBGN A 0 A_GunFlash
			SBGN A 0 A_PlayWeaponSound("SilverBulletFire")
			SBGN A 4 A_FireBullets (3, 3, 2, 20, "SilverBulletPuff")
			SBGN A 20
			SBGN B 0 A_PlayWeaponSound("SilverBulletLoad")
			SBGN BB 4 A_FireCustomMissile("SilverBulletCasing",45+random(-8,8),0,0,5)
			SBGN A 9
			SBGN A 0 A_Refire
			goto SBGReady
		SBGFlash:
			SBGF A 3 BRIGHT A_Light2
			SBGF B 2 BRIGHT A_Light1
			SBGF A 0 BRIGHT A_Light0
			stop

		// Din's Fire
		DinReady:
			DINW A 1 A_WeaponReady
			loop
		DinSelect:
			DINW A 1 A_Raise
			loop
		DinDeselect:
			DINW A 1 A_Lower
			loop
		DinFire:
			DINW B 2 BRIGHT A_PlayWeaponSound("DinFire")
			DINW C 2 BRIGHT
			DINW D 2 BRIGHT
			DINW E 2 BRIGHT
			DINW FFFFFFFFFFFFF 0 BRIGHT A_FireCustomMissile("DinFireball",Random(-10,10),0,-7,0)
			DINW F 2 BRIGHT A_FireCustomMissile("DinFireball",Random(-10,10),1,-7,0)
			DINW A 30
			DINW A 0 A_Refire
			goto DinReady

		// SlamShot
		DukeReady:
			D6SG A 1 A_WeaponReady
			loop
		DukeSelect:
			D6SG A 0 A_PlayWeaponSound("SlamShotCock2")
			D6SG A 1 A_Raise
			goto DukeSelect+1
		DukeDeselect:
			D6SG A 1 A_Lower
			loop
		DukeFire:
			D6SG A 0 A_PlayWeaponSound("SlamshotFire")
			D6SG A 0 A_Light2
			D6SG A 0 radius_quake(3, 3, 0, 1, 0)
			D6SG A 0 A_FireBullets(6, 4, 6, 7, "SPASPuff")
			D6SG B 1 BRIGHT A_SetPitch(pitch-2)
			D6SG B 0 A_Light0
			D6SG CDEF 1 A_SetPitch(pitch+0.5)
			D6SG A 1
			D6SG A 0 A_PlayWeaponSound("SlamshotFire")
			D6SG A 0 A_Light2
			D6SG A 0 radius_quake(3, 3, 0, 1, 0)
			D6SG A 0 A_FireBullets(6, 5, 6, 7, "SPASPuff", 0)
			D6SG B 1 BRIGHT A_SetPitch(pitch-2)
			D6SG B 0 A_Light0
			D6SG CDEF 1 A_SetPitch(pitch+0.5)
			D6SG A 3
			D6SG GHIJ 3
			D6SG K 0 A_PlaySoundEx("SlamshotCock1", "Auto")
			D6SG K 1 A_FireCustomMissile("ShellCasingSpawnerLeft",45+random(-8,8),0,1,6)
			D6SG K 1 A_FireCustomMissile("ShellCasingSpawnerLeft",45+random(-8,8),0,1,6)
			D6SG L 4
			D6SG K 2 A_PlaySoundEx("SlamshotCock2", "Auto")
			D6SG JIHG 2
			D6SG A 6
			D6SG A 0 A_Refire
			goto DukeReady
		Spawn:
			SGN2 A -1
			stop
	}
}