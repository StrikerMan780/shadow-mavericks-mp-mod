ACTOR FloatBobSkyViewpoint : SkyViewpoint 11482
{
	+FLOATBOB
}

actor RocketFire
{
 Height 2
 Radius 2
 Speed 3
 RenderStyle Add
 Alpha 0.9
 Scale 0.75
 +NOGRAVITY
 +FORCEXYBILLBOARD
 +DONTSPLASH
 +NOTELEPORT
 +NOBLOCKMAP
 +RIPPER
 +BLOODLESSIMPACT
 +CANNOTPUSH
 -BLOODSPLATTER
 -ACTIVATEPCROSS
 -ACTIVATEIMPACT
 -NOCLIP
 States
 {
 Spawn:
 	64P1 A 0
 	64P1 A 0 A_Jump(127, "Spawn2", "Spawn3")
 	64P1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
 	stop
 Spawn2:
 	64P2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
 	stop
 Spawn3:
 	64P3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
 	stop
 }
}

actor ItemMist
{
	Radius 2
	Height 2
	Damage 0
	Scale 0.12
	RenderStyle Add
	Alpha 0.5
	+NOBLOCKMAP
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+NOINTERACTION
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(127, "Spawn2")
		GPLE ABCDE 1 BRIGHT
		stop
	Spawn2:
		TNT1 A 0
		BLSE ABCDE 1 BRIGHT
		stop
	}
}

actor ItemSpawnEffect
{
	Radius 1
	Height 1
	RenderStyle Add
	Alpha 1.0
	Scale 0.5
	+NOBLOCKMAP
	+FORCEYBILLBOARD
	+DONTSPLASH
	+NOGRAVITY
	+NOINTERACTION
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(127, "Spawn2")
			GSFX A 2 BRIGHT
			GSFX A 1 BRIGHT A_FadeOut(0.1)
			goto Spawn+3
		Spawn2:
			BSFX A 2 BRIGHT
			BSFX A 1 BRIGHT A_FadeOut(0.1)
			goto Spawn2+1
	}
}

actor ItemEffectSpawner
{
	Radius 1
	Height 1
	+NOINTERACTION
	+NOBLOCKMAP
	+DONTSPLASH
	+NOGRAVITY
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	States
	{
		Death:
		Spawn:
			NULL A 0
			NULL A 0 A_SpawnItemEx("ItemSpawnEffect", random(-12, 12), random(-10,10), Random(4, 16), 0, 0, 4, 0, 128, 0)
			NULL A 0 A_SpawnItemEx("ItemMist", 0, 0, 4, random(-4, 4), random(-4,4), random(-4,4), 0, 128, 0)
			stop
	}
}


actor NewItemFog : ItemFog Replaces ItemFog
{
	+NOINTERACTION
	States
	{
		Spawn:
			NULL A 0
			NULL AAAAAAAAA 1 A_SpawnItemEx("ItemEffectSpawner", 0, 0, 0, 0, 0, 0, 0, 128, 0)
			stop
	}
}

actor GunSpark
{
	Projectile
	Radius 2
	Height 2
	Speed 30
	Damage 0
	RenderStyle Add
	Alpha 1.0
	Scale 0.3
	BounceFactor 0.9
	Gravity 0.3
	+CLIENTSIDEONLY
	+ALLOWCLIENTSPAWN
	+HEXENBOUNCE
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+CANBOUNCEWATER
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	-NOGRAVITY
	States
	{
		Spawn:
		PUFF A 0 BRIGHT
		PUFF A 0 A_Recoil(Random(-3,3))
		PUFF A 0 BRIGHT ThrustThingZ(0, Random(0, 7), Random(0,1), 1)
		PUFF A 0 A_JumpIf(WaterLevel >= 1, "Death")
		PUFF A 1 BRIGHT A_FadeOut(0.15)
		goto Spawn+3
		Death:
		TNT1 A 1
		TNT1 A 1
		stop
	}
}

actor DoorJet
{
	Radius 1
	Height 1
	+CLIENTSIDEONLY
	+ALLOWCLIENTSPAWN
	+NOGRAVITY
	+NOBLOCKMAP
	+NOINTERACTION
	states
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("FlameThrowerCool")
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("CoolingSmoke", 0, 0, 0, random(-1, 1), random(-1, 1), random(-1, 1), random(-180,180), 128, 0)
			stop
	}
}

actor FountainParticle
{
	Projectile
	Radius 2
	Height 2
	RenderStyle Add
	Alpha 0.48
	Gravity 0.3
	+CLIENTSIDEONLY
	+ALLOWCLIENTSPAWN
	+FORCEXYBILLBOARD
	+DONTSPLASH
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	-NOGRAVITY
	States
	{
		Spawn:
			BLUE A 1 A_CheckSight("Death")
			loop
		Death:
			TNT1 A 1
			TNT1 A 1
			stop
	}
}

actor FountainParticleSpawner 17521
{
	Radius 4
	Height 8
	+NOGRAVITY
	+NOBLOCKMAP
	+NOINTERACTION
	states
	{
		spawn:
			TNT1 A 0 A_SpawnItemEx("FountainParticle", 0, 0, random(0, 8), random(-1,1), random(-1,1), random(3,6), random(-180,180), 128, 0)
			TNT1 A 1 A_SpawnItemEx("FountainParticle", 0, 0, random(0, 24), random(-1,1), random(-1,1), random(3,6), random(-180,180), 128, 0)
			loop
	}
}