//Noisy cricket
//coded by Ruin and StrikerMan780

actor Noisy_Cricket : Weapon 12345
{
	inventory.pickupmessage "\cdNoisy Cricket \cc/ \ciSonic Wave Cannon"
	obituary "%o was reduced to nothing by %k's noisy cricket."
	radius 20
	height 16
	weapon.kickback 100
	weapon.ammotype "CricketAmmo"
	weapon.ammouse 1
	weapon.ammogive 1
	DamageType "Obliterate"
	attacksound "CricketFire"
	+WEAPON.BFG
	+WEAPON.NOLMS
	states
	{
		Ready:
			CRKG A 0 A_JumpIfInventory("TailsClassCheck", 1, "SWCReady")
			CRKG A 1 A_WeaponReady
			loop
		SWCReady:
			SWCG A 1 A_WeaponReady
			loop
		Deselect:
			CRKG A 0 A_JumpIfInventory("TailsClassCheck", 1, "SWCDeselect")
			CRKG A 1 A_Lower
			loop
		SWCDeselect:
			SWCG A 1 A_Lower
			loop
		Select:
			CRKG A 0 A_JumpIfInventory("TailsClassCheck", 1, "SWCSelect")
			CRKG A 0 A_Jump(127, "LolSelect")
			CRKG A 1 A_Raise
			goto Select+2
		LolSelect:
			CRKG A 0 A_PlayWeaponSound("BreakDamnThing")
			CRKG A 1 A_Raise
			goto LolSelect+1
		SWCSelect:
			SWCG A 1 A_Raise
			loop
		Fire:
			CRKG A 0 A_JumpIfInventory("TailsClassCheck", 1, "SWCFire")
			CRKG A 14 A_PlaySound("CricketCharge")
			CRKG A 1
			CRKG B 0 Radius_Quake(4, 6, 0, 1, 0)
			CRKG B 0 A_PlaySoundEx("CricketWave", "auto", 0)
			CRKG B 0 A_FireCustomMissile("CricketBeam", 0, 1, 8, 0)
			CRKG B 2 A_GunFlash
			CRKG C 2
			CRKG D 2
			CRKG E 0 ThrustThingZ(0, 40, 0, 0)
			CRKG E 2 A_Recoil (50)
			CRKG F 2
			CRKG A 5 A_ReFire
			goto Ready
		SWCFire:
			SWCG A 15 A_PlaySound("RailgunIn")
			SWCG B 0 A_FireCustomMissile("SonicWave", 0, 0, 2, 0)
			SWCG B 2 A_FireBullets (5.6, 0, 1, 1000, "SonicWaveBlast", FBF_USEAMMO | FBF_NORANDOM)
			SWCG B 0 A_Recoil (50)
			SWCG CDEF 3
			SWCG A 17
			goto SWCReady
		Flash:
			CRKF A 2 bright A_Light1
			CRKF A 0 bright A_Light0
			stop
		Spawn:
			CRKK A -1
			stop
	}
}

actor CricketBlast
{
	renderstyle add
	+PUFFONACTORS
	+FIXMAPTHINGPOS
	+NOGRAVITY
	+EXTREMEDEATH
	+PUFFGETSOWNER
	+FORCEXYBILLBOARD
	obituary "%o was vaporized by %k's noisy cricket."
	DamageType "Obliterate"
	alpha 0.9
	Scale 0.8
	states
	{
		Spawn:
			CKTE A 0
			CKTE A 0 A_PlaySound("weapons/grenlx")
			CKTE A 0 A_Explode(500, 200)
			CKTE A 0 A_SpawnItemEx("CricketExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			CKTE AAAAAAA 0 A_SpawnItemEX("SonicWaveMist", 0, 0, 0, Random(-12, 12), Random(-12, 12), Random(-12, 12), Random(-360, 360), SXF_CLIENTSIDE, 0)
			stop
	}
}

actor CricketExplosion : BaseEffect
{
	RenderStyle Add
	Alpha 0.9
	Scale 0.0
	States
	{
		Spawn:
			CKTE A 1 BRIGHT NoDelay A_SetScale(scalex+0.25)
			CKTE A 0 BRIGHT A_JumpIf(ScaleX >= 0.50, "Spawn2")
			loop
		Spawn2:
			CKTE B 1 BRIGHT A_SetScale(scalex+0.25)
			CKTE B 0 BRIGHT A_JumpIf(ScaleX >= 1.0, "Spawn3")
			loop
		Spawn3:
			CKTE C 1 BRIGHT A_SetScale(scalex+0.25)
			CKTE C 0 BRIGHT A_JumpIf(ScaleX >= 1.5, "Spawn4")
			loop
		Spawn4:
			CKTE D 1 BRIGHT A_SetScale(scalex+0.25)
			CKTE D 0 BRIGHT A_JumpIf(ScaleX >= 2.0, "Spawn5")
			loop
		Spawn5:
			CKTE E 1 BRIGHT A_SetScale(scalex+0.25)
			CKTE E 0 BRIGHT A_JumpIf(ScaleX >= 2.5, "Spawn6")
			loop
		Spawn6:
			CKTE F 1 BRIGHT A_SetScale(scalex+0.25)
			CKTE F 0 BRIGHT A_JumpIf(ScaleX >= 3.0, "Spawn7")
			loop
		Spawn7:
			CKTE G 1 BRIGHT A_SetScale(scalex+0.25)
			CKTE G 0 BRIGHT A_JumpIf(ScaleX >= 3.5, "Spawn8")
			loop
		Spawn8:
			CKTE H 1 BRIGHT A_SetScale(scalex+0.25)
			CKTE H 0 BRIGHT A_JumpIf(ScaleX >= 4.0, "Die")
			loop
		Die:
			CKTE H 1 BRIGHT A_SetScale(scalex+0.1)
			CKTE H 0 BRIGHT A_Fadeout(0.1)
			loop
	}
}

Actor SonicWaveTrail
{
	RenderStyle Add
	Radius 1
	Height 1
	Speed  0
	Damage 0
	+CORPSE
	+DONTSPLASH
	+NOGRAVITY
	-ACTIVATEPCROSS
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			CRTL ABCB 3 Bright
			stop
		Death:
			TNT1 A 1 Bright
			TNT1 A 1 Bright
			Stop
	}
}

actor SonicWave : FastProjectile
{
	RenderStyle Add
	radius 2
	height 2
	speed 255
	damage 0
	ReactionTime 10
	SeeSound "CricketWave"
	obituary "%o was disintegrated by %k's sonic wave cannon"
	+FORCEXYBILLBOARD
	PROJECTILE
	+RIPPER
	states
	{
		Spawn:
			CRTL ABCB 1 bright A_SpawnItemEX("SonicWaveTrail", 0, 0, 0, 0, 0, 0, 0, 128, 0)
			CRTL A 0 A_CountDown
			loop
		Death:
			TNT1 AAA 1
			stop
	}
}

actor CricketBeam : FastProjectile
{
	RenderStyle Add
	Alpha 1.0
	radius 2
	height 2
	speed 200
	damage (1000)
	SeeSound "CricketWave"
	obituary "%o was vaporized by %k's noisy cricket"
	DamageType "Obliterate"
	+FORCEXYBILLBOARD
	PROJECTILE
	states
	{
	Spawn:
		SERV A 1 bright
		loop
	Death:
		TNT1 A 0
		TNT1 A 1 A_SpawnItemEx("CricketBlast", 0, 0, 0, 0, 0, 0, random(-180, 180), 0, 0)
		TNT1 A 1
		stop
	}
}