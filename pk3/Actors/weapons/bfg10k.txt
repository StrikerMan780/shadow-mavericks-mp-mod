actor BFG10kShot : FastProjectile
{
	Decal "BlueBFGLightning"
	Projectile
	Radius 8
	Height 4
	Speed 70
	Damage 0
	DamageType BFG10k
	+FORCERADIUSDMG
	+FORCEXYBILLBOARD
	Renderstyle Add
	Alpha 1.0
	DeathSound "weapons/bfg10kx"
	Obituary "%o was blasted by %k's BFG10K."
	States
	{
	Spawn:
		PUFF A 1 BRIGHT
		loop
	Death:
		BFE1 A 0 bright
		BFE1 A 0 bright A_SpawnItemEx("BFG10kExplosionShell", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		BFE1 A 0 bright A_SpawnItemEx("BFG10kExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		BFE1 AAAAAAAA 0 A_SpawnItemEx("BFG10kSpark", 0, 0, 0, frandom(0.0,16.0), 0, frandom(1.0,12.0), random(-180,180), SXF_CLIENTSIDE, 0)
		BFE1 A 0 bright A_Explode(160, 160)
		TNT1 A 2
		Stop
	}
}

actor BFG10kExplosionShell : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	Scale 0.01
	States
	{
		Spawn:
			NULL A 1 BRIGHT A_SetScale(scalex+0.3)
			NULL A 0 BRIGHT A_FadeOut(0.075)
			loop
	}
}

actor BFG10kExplosion : BaseEffect
{
	RenderStyle Add
	Alpha 0.9
	Scale 0.0
	States
	{
		Spawn:
			10KX A 1 BRIGHT NoDelay A_SetScale(scalex+0.25)
			10KX A 0 BRIGHT A_JumpIf(ScaleX >= 0.50, "Spawn2")
			loop
		Spawn2:
			10KX B 1 BRIGHT A_SetScale(scalex+0.25)
			10KX B 0 BRIGHT A_JumpIf(ScaleX >= 1.0, "Spawn3")
			loop
		Spawn3:
			10KX C 1 BRIGHT A_SetScale(scalex+0.25)
			10KX C 0 BRIGHT A_JumpIf(ScaleX >= 1.5, "Spawn4")
			loop
		Spawn4:
			10KX D 1 BRIGHT A_SetScale(scalex+0.25)
			10KX D 0 BRIGHT A_JumpIf(ScaleX >= 2.0, "Spawn5")
			loop
		Spawn5:
			10KX E 1 BRIGHT A_SetScale(scalex+0.25)
			10KX E 0 BRIGHT A_JumpIf(ScaleX >= 2.5, "Spawn6")
			loop
		Spawn6:
			10KX F 1 BRIGHT A_SetScale(scalex+0.25)
			10KX F 0 BRIGHT A_JumpIf(ScaleX >= 3.0, "Spawn7")
			loop
		Spawn7:
			10KX G 1 BRIGHT A_SetScale(scalex+0.25)
			10KX G 0 BRIGHT A_JumpIf(ScaleX >= 3.5, "Spawn8")
			loop
		Spawn8:
			10KX H 1 BRIGHT A_SetScale(scalex+0.25)
			10KX H 0 BRIGHT A_JumpIf(ScaleX >= 4.0, "Die")
			loop
		Die:
			10KX H 1 BRIGHT A_SetScale(scalex+0.1)
			10KX H 0 BRIGHT A_Fadeout(0.1)
			loop
	}
}

actor BFG10kSpark : RPGSpark
{
}

ACTOR BFG10K : Weapon 5013
{
	Game Doom
	SpawnID 165
	Radius 20
	Height 20
	Weapon.Selectionorder 2800
	Weapon.AmmoUse 5
	Weapon.AmmoGive 40
	Weapon.Kickback 100
	Weapon.Ammotype "CellAmmo"
	Weapon.BobSpeed 1.0
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 1.0
    Weapon.BobRangeY 1.0
	AttackSound "BFG10kShot"
	Inventory.Pickupmessage "You got the BFG10000! Hell, yes!"
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOLMS
	+WEAPON.NOAUTOAIM
	States
	{
	Spawn:
		BFG2 A -1
		stop
	Ready:
		BG2G A 1 A_WeaponReady
		loop
	Deselect:
		BG2G A 0 A_StopSound(CHAN_7)
		BG2G A 1 A_Lower
		loop
	Select:
		BG2G A 0 A_PlaySound("weapons/bfg10kidle", CHAN_7, 1.0, true)
		BG2G A 1 A_Raise
		loop
	Fire:
		BG2G A 20 A_PlaySound ("weapons/bfg10kf", CHAN_AUTO)
		goto Hold
	Hold:
		BG2G B 0 A_GunFlash
		BG2G B 0 A_PlayWeaponSound("BFG10kShot")
		BG2G B 0 A_FireCustomMissile ("BFG10KShot", frandom(-0.5,0.5), true, 10, 0, 0, frandom(-0.5, 0.5))
		BG2G B 0 A_Quake(5,3,0,1, "")
		BG2G B 1 A_SetPitch(pitch-1.5)
		BG2G CDEFGH 1 A_SetPitch(pitch+0.25)
		BG2G I 0 A_ReFire
		BG2G I 1 A_PlaySound("weapons/bfg10kcool", CHAN_AUTO)
		BG2G JKLMNO 1
		goto Ready
	Flash:
		TNT1 A 2 bright A_Light1
		TNT1 A 3 bright
		Goto LightDone
		stop
	}
}