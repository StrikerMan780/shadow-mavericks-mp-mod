actor PlasmaRifleGiver : ClassWeaponGiver replaces PlasmaRifle
{	
	//$Category SMMP Weapons
	//$Title PL-8 ''Sunfire'' Rifle
	//$Color 17
	states
	{
		Item:
			PLAS A -1
			loop
		Pickup:
			PLAS A 0 A_JumpIf(ACS_NamedExecuteWithResult("GIVEWEAPON", 6, 40, 0) == TRUE, "Stay")
			goto Remove
		Dropped:
			PLAS A 0 A_SpawnItemEx("PlasmaRifleGiver_Dropped", 0, 0, 0, velx, vely, velz, angle, SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE, 0)
			stop
	}
}

actor PlasmaRifleGiver_Dropped : PlasmaRifleGiver
{
	States
	{
		Spawn:
			PLAS A 63000
			goto FadeAway
		Pickup:
			PLAS A 0 A_JumpIf(ACS_NamedExecuteWithResult("GIVEWEAPON", 6, 20, 1) == TRUE, "Stay")
			goto Remove
	}
}

actor PlasmaCannon : Weapon
{
	Tag "PL-8 ''Sunfire'' Rifle"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 1.0
    Weapon.BobRangeY 1.5
	weapon.selectionorder 100
	weapon.kickback 100
	weapon.ammotype "CellAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	states
	{
		Ready:
			PLSG A 1 A_WeaponReady
			loop
		Deselect:
			PLSG A 1 A_Lower
			loop
		Select:
			PLSG A 1 A_Raise
			loop
		Fire:
			PLSG A 0 radius_quake(1, 2, 0, 1, 0)
			PLSG A 0 A_PlayWeaponSound("weapons/plasmaf")
			PLSG A 0 A_GunFlash
			PLSG A 0 A_FireCustomMissile("NewPlasma", frandom(-1.0, 1.0), true, 7, 1, false, frandom(-1.0, 1.0))
			PLSG B 1 BRIGHT A_SetPitch(pitch-1.0)
			PLSG C 1 BRIGHT A_SetPitch(pitch+0.25)
			PLSG D 1 BRIGHT A_SetPitch(pitch+0.25)
			PLSG D 1 BRIGHT A_SetPitch(pitch+0.25)
			PLSG D 0 BRIGHT A_SetPitch(pitch+0.25)
			PLSG A 10 A_ReFire
			goto Ready
		Flash:
			TNT1 A 2 A_Light2
			TNT1 A 0 A_Light1
			goto LightDone
	}
}

actor NewPlasmaTrail : BaseEffect
{
	Alpha 1.0
	RenderStyle Add
	Scale 0.075
	States
	{
		Spawn:
			TNT1 A 2
			TNT1 A 0 A_Jump(256, "Trail1", "Trail2")
			goto Trail1
		Trail1:
			PLSS A 1 BRIGHT A_FadeOut(0.2)
			TNT1 A 1
			PLSS D 1 BRIGHT A_FadeOut(0.2)
			TNT1 A 1
			PLSS B 1 BRIGHT A_FadeOut(0.2)
			TNT1 A 1
			PLSS C 1 BRIGHT A_FadeOut(0.2)
			TNT1 A 1
			loop
		Trail2:
			PLSE ABCDE 2 BRIGHT A_FadeOut(0.1)
			stop
	}
}

actor NewPlasma : PlasmaBall Replaces PlasmaBall
{
	seesound ""
	decal PlasmaScorch
	Speed 40
	Alpha 1.0
	Obituary "%o stared at %k's pretty orange plasma for too long."
	+FORCEXYBILLBOARD
	RenderStyle Add
	scale 0.075
	States
	{
	Spawn:
		PLSS ABCD 1 BRIGHT A_SpawnItemEx("NewPlasmaTrail", 0, 0, 0, (velx/2)+frandom(-1.0, 1.0), (vely/2)+frandom(-1.0, 1.0), (velz/2)+frandom(-1.0, 1.0), 0, SXF_ABSOLUTEVELOCITY | SXF_CLIENTSIDE, 0)
		loop
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
		goto Death
	Death:
		PLSE AAAAAAA 0 A_SpawnItemEx("PlasmaMist", 0, 0, 0, random(-5,5), Random(-5,5), Random(-5,5), 0, 128, 0)
		PLSE ABCDE 2 Bright
		Stop
	}
}

actor HaloPlasmaRifle : Weapon
{
	Tag "Type-25 Directed Energy Rifle"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 0.5
    Weapon.BobRangeY 1.25
	weapon.selectionorder 100
	weapon.kickback 100
	weapon.ammotype "CellAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	states
	{
		Deselect:
			HLOP A 1 A_Lower
			loop
		Select:
			HLOP A 1 A_Raise
			loop
		Ready:
			HLOP A 1 A_WeaponReady
			loop
		Fire:
			HLOP B 0 BRIGHT A_PlaySound("HaloPlasmaRifle", CHAN_WEAPON)
			HLOP B 0 BRIGHT A_FireCustomMissile("BluePlasmaFlashSpawner", 0, 0, 10, (random(-400,0)/100.0))
			HLOP B 0 BRIGHT A_FireCustomMissile("BluePlasma", (Random(-200,200)/100.0), 1, 8, 0)
			HLOP B 0 BRIGHT A_SetPitch(pitch-1)
			HLOP B 0 BRIGHT A_Jump(128, "Fire2", "Fire3")
			HLOP B 1 BRIGHT
			goto FireDone
		Fire2:
			HLOP B 2 BRIGHT
			goto FireDone
		Fire3:
			HLOP B 3 BRIGHT
			goto FireDone
		FireDone:
			HLOP C 2 A_SetPitch(pitch+0.5)
			HLOP D 1 A_SetPitch(pitch+0.5)
			HLOP D 0 A_ReFire
			HLOP EFG 2
			goto Ready
	}
}

actor FireRod : Weapon
{
	Tag "Fire Rod"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "Smooth"
	Weapon.BobRangeX 0.0
    Weapon.BobRangeY 1.0
	weapon.selectionorder 100
	weapon.kickback 100
	weapon.ammotype "CellAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	states
	{
		Ready:
			FROD A 1 A_WeaponReady
			loop
		Deselect:
			FROD A 1 A_Lower
			loop
		Select:
			FROD A 1 A_Raise
			loop
		Fire:
			FROD B 0 BRIGHT A_PlayWeaponSound("FireRod")
			FROD B 2 BRIGHT A_FireCustomMissile("FireRodShot", Random(-1,1),1,0,0)
			FROD CB 2 BRIGHT
			FROD A 4
			FROD A 0 A_Refire
			goto Ready
	}
}

actor FreezeThrower : Weapon
{
	Tag "Freezethrower"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "Smooth"
	Weapon.BobRangeX 1.25
    Weapon.BobRangeY 1.25
	weapon.selectionorder 100
	weapon.kickback 100
	weapon.ammotype "CellAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	weapon.upsound "DukeWeaponSelect"
	states
	{
		Ready:
			FRTH A 1 A_WeaponReady
			loop
		Deselect:
			FRTH A 0 A_StopSoundEx("Weapon")
			FRTH A 1 A_Lower
			goto Deselect+1
		Select:
			FRTH A 1 A_Raise
			loop
		Fire:
			FRTH B 0 Bright A_PlaySoundEx("FreezeThrowerStart", "Auto", 0)
			FRTH B 2 Bright A_PlaySoundEx("FreezeThrower", "Weapon", 1)
			FRTH C 1 Bright
			FRTH D 2 Bright A_FireCustomMissile("FreezePlasma", 0,1,8,0)
			FRTH B 0 A_Refire
			FRTH A 0 A_PlaySoundEx("FreezeThrowerEnd", "Auto", 0)
			FRTH A 3 A_StopSoundEx("Weapon")
			goto Ready
		Hold:
			FRTH B 2 Bright A_PlaySoundEx("FreezeThrower", "Weapon", 1)
			FRTH C 1 Bright
			FRTH D 2 Bright A_FireCustomMissile("FreezePlasma", 0,1,8,0)
			FRTH B 0 A_Refire
			FRTH A 0 A_PlaySoundEx("FreezeThrowerEnd", "Auto", 0)
			FRTH A 3 A_StopSoundEx("Weapon")
			goto Ready
		AltFire:
			FRTK A 6
			FRTK B 4 A_CustomPunch(6, 0, 0, "KickPuff")
			FRTK A 4
			FRTK A 2
			FRTH A 1 A_Refire
			Goto Ready
	}
}