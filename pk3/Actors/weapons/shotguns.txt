actor ShotgunGiver : ClassWeaponGiver Replaces Shotgun
{	
	states
	{
		Item:
			SHOT A -1
			loop
		Pickup:
			SHOT A 0 A_JumpIf(ACS_ExecuteWithResult(GIVEWEAPON, 0, 8, 0) == TRUE, "Stay")
			goto Remove
		Dropped:
			SHOT A 0 A_SpawnItemEx("ShotgunGiver_Dropped", 0, 0, 0, velx, vely, velz, angle, SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE, 0)
			stop
	}
}

actor ShotgunGiver_Dropped : ShotgunGiver
{
	States
	{
		Spawn:
			SHOT A 63000
			goto FadeAway
		Pickup:
			SHOT A 0 A_JumpIf(ACS_ExecuteWithResult(GIVEWEAPON, 0, 4, 1) == TRUE, "Stay")
			goto Remove
	}
}

actor NewShotgun : Weapon
{
	Tag "Remington 870 Magnum"
	Decal BulletChip
	obituary "%o chewed on %k's boomstick."
	radius 20
	height 16
	weapon.selectionorder 1300
	weapon.kickback 100
	Weapon.AmmoType "SpreadAmmo"
	weapon.ammouse 1
	states
	{
	Ready:
		SHTG A 1 A_WeaponReady
		goto Ready
	Deselect:
		SHTG AA 0 A_Lower
		SHTG A 1 A_Lower
		goto Deselect
	Select:
		SHTG AA 0 A_Raise
		SHTG A 1 A_Raise
		goto Select
	Fire:
		SHTG B 0 A_FireCustomMissile("ShotgunSmoke", random(-1, 1), 0, 3, 7)
		SHTG B 0 A_GunFlash
		SHTG B 0 Radius_Quake(2, 3, 0, 1, 0)
		SHTG B 0 bright A_PlayWeaponSound("weapons/shotgf")
		SHTG B 0 Bright A_FireBullets (6.6, 3.5, 10, 5, "ShotgunPuff")
		SHTG B 1 Bright A_SetPitch (pitch-2)
		SHTG C 1 A_SetPitch(pitch+0.8)
		SHTG D 2 A_SetPitch(pitch+0.6)
		SHTG E 2 A_SetPitch(pitch+0.4)
		SHTG F 2 A_SetPitch(pitch+0.2)
		SHTG G 2
		SHTG H 2 A_PlaySoundEx("ShotgunCock", "auto", 0)
		SHTG IJKL 2
		SHTG M 0 A_FireCustomMissile("ShellCasingSpawnerRight",-45+random(-8,8),0,12,3)
		SHTG MNOPQRS 2
		SHTG A 2
		SHTG A 0 A_Refire
		goto Ready
	Flash:
		SHTF A 2 Bright A_Light1
		SHTF A 0 Bright A_Light0
		stop
	}
}

ACTOR PIPSClip : Ammo
{
	Inventory.MaxAmount 12
	Ammo.BackpackMaxAmount 12
	Ammo.BackpackAmount 0
	Inventory.Icon "MAGICON2"
	+IGNORESKILL
}

actor PIPS : Weapon
{
	Tag "Plasma Injection Pulse Shotgun"
	inventory.pickupmessage "Plasma Injection Pulse Shotgun (Slot 3)"
	weapon.selectionorder 1300
	weapon.kickback 100
	Weapon.AmmoType1 "PIPSClip"
	Weapon.AmmoType2 "SpreadAmmo"
	weapon.ammouse 1
	+WEAPON.NOAUTOAIM
	+WEAPON.AMMO_OPTIONAL
	states
	{
		Select:
			PIPS A 1 A_Raise
			loop
		Deselect:
			PIPS A 1 A_Lower
			loop
		Ready:
			PIPS A 1 A_WeaponReady
			loop
		Fire:
			PIPS B 0 A_JumpIfNoAmmo("ReloadCheck")
			PIPS B 0 Radius_Quake(2, 3, 0, 1, 0)
			PIPS B 0 A_PlayWeaponSound("PIPS")
			PIPS B 0 A_GunFlash
			PIPS BBBBBBBBB 0 A_FireCustomMissile("PIPS_Shot", frandom(-4.0,4.0),0,14,0,0, frandom(-2.0,2.0))
			PIPS B 0 A_FireCustomMissile("PIPS_Shot", frandom(-4.0,4.0),1,14,0,0, frandom(-2.0,2.0))
			PIPS B 0 A_Jump(128, "Fire2")
			PIPS B 1 A_SetPitch(pitch-1.0)
			PIPS CDEFGHIJ 1 A_SetPitch(pitch+0.125)
			PIPS KLMNOP 2
			PIPS A 2
			PIPS A 0 A_Refire
			goto Ready
		Fire2:
			PIP2 B 1 A_SetPitch(pitch-1.0)
			PIP2 CDEFGHIJ 1 A_SetPitch(pitch+0.125)
			PIP2 KLMNOP 2
			PIPS A 2
			PIPS A 0 A_Refire
			goto Ready
			
		AltFire:
			PIPS A 0
			goto ReloadCheck
		
		ReloadCheck:
			PIR1 A 0 A_JumpIfInventory("PIPSClip",12,2)
			PIR1 A 0 A_JumpIfInventory("SpreadAmmo",1,"ReloadStart")
			PIR1 A 0
			goto Ready

		ReloadStart:
			PIR1 A 2 A_PlaySound("PIPSEject", CHAN_AUTO)
			PIR1 BCDEFGH 2
			PIR1 I 2 A_PlaySound("PIPSClipOut", CHAN_AUTO)
			PIR1 JKLMN 2
			PIR1 OPQRSTU 2
			goto ReloadAmmo

		ReloadAmmo:
			TNT1 A 0 A_TakeInventory("SpreadAmmo", 1)
			TNT1 A 0 A_GiveInventory("PIPSClip", 1)
			TNT1 A 0 A_JumpIfInventory("PIPSClip", 12,"ReloadDone")
			TNT1 A 0 A_JumpIfInventory("SpreadAmmo", 1,"ReloadAmmo")
			goto ReloadDone

		ReloadDone:
			PIR1 V 2 A_PlaySound("PIPSClipIn", CHAN_AUTO)
			PIR1 WXYZ 1
			PIR2 ABCDEFGHIJKLMN 1
			PIR2 O 1 A_PlaySound("PIPSCock", CHAN_AUTO)
			PIR2 PQRSTUVWXYZ 2
			goto Ready
			
		Flash:
			PIPF A 2 A_Light2
			PIPF B 1 A_Light1
			TNT1 A 0 A_Light0
			stop
		Spawn:
			SHOT A 1
			stop
	}
}

actor DukeShotgun : Weapon
{
	Tag "Custom Winchester 1300 Defender"
	Decal BulletChip
	obituary "%o ate %k's load."
	weapon.selectionorder 1300
	weapon.kickback 100
	Weapon.AmmoType "SpreadAmmo"
	weapon.ammouse 1
	States
	{
	Ready:
		DHTG A 1 A_WeaponReady
		loop
	Deselect:
		DHTG A 1 A_Lower
		loop
	Select:
		DHTG A 0 A_PlayWeaponSound("DukeShotgunCock")
		DHTG A 0 A_Jump(48, "DukeSpeak")
		DHTG A 1 A_Raise
		goto Select+2
	DukeSpeak:
		DHTG A 0 A_PlaySoundEx("DukeShotgunRaise", "SoundSlot5", 0, 0.3)
		goto Select+2
	Fire:
		DHTG E 0 bright A_PlayWeaponSound("DukeShotgunFire")
		DHTG E 0 A_FireBullets (4, 2, 7, 10, "DukeShotgunBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
		DHTG E 0 A_GunFlash
		DHTG A 2 A_SetPitch(pitch-1.5)
		DHTG EF 2 A_SetPitch(pitch+0.75)
		DHTG E 2
		DHTG A 3
		DHTG BC 3
		DHTG G 3 A_PlaySound("DukeShotgunCock")
		DHTG D 4
		DHTG G 3 A_FireCustomMissile("ShellCasingSpawnerLeft",45+random(-8,8),0,0,6)
		DHTG CB 3
		DHTG A 1
		DHTG A 5 A_ReFire
		goto Ready
	Flash:
		DHTF A 3 bright A_Light1
		DHTF B 2 bright A_Light2
		DHTF B 0 bright A_Light0
		stop
	}
}