actor SuperShotgunGiver : ClassWeaponGiver Replaces SuperShotgun
{	
	states
	{
		Item:
			SGN2 A -1
			loop
		Pickup:
			SGN2 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GIVEWEAPON", 1, 16, 0) == TRUE, "Stay")
			goto Remove
		Dropped:
			SGN2 A 0 A_SpawnItemEx("SuperShotgunGiver_Dropped", 0, 0, 0, velx, vely, velz, angle, SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE, 0)
			stop
	}
}

actor SuperShotgunGiver_Dropped : SuperShotgunGiver
{
	States
	{
		Spawn:
			SGN2 A 63000
			goto FadeAway
		Pickup:
			SGN2 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GIVEWEAPON", 1, 8, 1) == TRUE, "Stay")
			goto Remove
	}
}

actor SM_SuperShotgun : Weapon
{
	Tag "Super Shotgun"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 1.0
    Weapon.BobRangeY 2.0
	Decal BulletChip
	obituary "%k turned %o's torso into a pile of red gruel."
	radius 20
	height 16
	weapon.selectionorder 400
	weapon.kickback 100
	Weapon.AmmoType "SpreadAmmo"
	weapon.ammouse 2
	weapon.ammogive 0
	states
	{
		Ready:
			SHT2 A 1 A_WeaponReady
			goto Ready
		Deselect:
			SHT2 A 1 A_Lower
			goto Deselect
		Select:
			SHT2 A 1 A_Raise
			goto Select
		Fire:
			SHT2 A 0 radius_quake(4, 3, 0, 1, 0)
			SHT2 A 0 A_PlayWeaponSound("weapons/sshotf")
			SHT2 A 0 A_FireBullets (10, 6, 20, 7, "SSGPuff", FBF_USEAMMO | FBF_NORANDOM)
			SHT2 A 1 A_GunFlash
			SHT2 B 0 A_SetPitch(pitch-4)
			SHT2 BCDEFGHIJK 1 A_SetPitch(pitch+0.4)
			SHT2 A 1 A_CheckReload
			SH2R AB 2
			SH2R C 0 A_PlaySoundEx("weapons/sshoto", "auto", 0)
			SH2R CDEF 2
			//SH2R GG 0 A_FireCustomMissile("SSGCasing", Random(-14,8),0,2,0)
			SH2R GHI 2
			SH2R JK 2
			SH2R L 0 A_PlaySoundEx("weapons/sshotl", "auto", 0)
			SH2R LMNO 2
			SH2R PQRSTUVWX 2
			SH2R Y 0 A_PlaySoundEx("weapons/sshotc", "auto", 0)
			SH2R YZ 2
			SH22 ABCD 1
			SHT2 A 0 A_ReFire
			goto Ready
		Flash:
			SH2F A 2 bright A_Light1
			SH2F B 1 bright A_Light2
			SH2F B 0 bright A_Light0
			stop
	}
}

actor Keyring : Weapon
{
	Tag "Keyring"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 1.0
    Weapon.BobRangeY 1.0
	Decal BulletChip
	obituary "%k shot silver shards into %o's skull."
	weapon.selectionorder 400
	weapon.kickback 100
	Weapon.AmmoType "SpreadAmmo"
	weapon.ammouse 2
	weapon.ammogive 0
	states
	{
		Ready:
			SBGN A 1 A_WeaponReady
			loop
		Select:
			SBGN A 1 A_Raise
			loop
		Deselect:
			SBGN A 1 A_Lower
			loop
		Fire:
			SBGN A 0 A_PlayWeaponSound("SilverBulletFire")
			SBGN A 0 A_FireBullets (5, 4, 4, 32, "KeyringPuff", FBF_USEAMMO | FBF_NORANDOM)
			SBGN A 0 A_GunFlash
			SBGN A 2 A_SetPitch(pitch-4.0)
			SBGN BCD 1 A_SetPitch(pitch+1.0)
			SBGN A 3 A_SetPitch(pitch+1.0)
			SBGN EFGHI 2
			SBGN J 1 A_PlaySound("SilverBulletLoad", CHAN_AUTO)
			SBGN K 1 
			SBGN L 1 A_FireCustomMissile("SilverBulletCasing",45+random(-8,8),0,2,8)
			SBGN M 1
			SBGN NOPQRSTUV 2
			SBGN A 7
			SBGN A 0 A_Refire
			goto Ready
		Flash:
			SBGF A 2 BRIGHT A_Light2
			SBGF B 1 BRIGHT A_Light1
			TNT1 A 0 BRIGHT A_Light0
			stop
	}
}

actor DinsFire : Weapon
{
	Tag "Din's Fire"
	Decal BulletChip
	obituary "%k unleashed the wrath of Din's Fire on %o."
	weapon.selectionorder 400
	weapon.kickback 100
	Weapon.AmmoType "SpreadAmmo"
	weapon.ammouse 2
	weapon.ammogive 0
	States
	{
		Ready:
			DINW A 1 A_WeaponReady
			loop
		Select:
			DINW A 1 A_Raise
			loop
		Deselect:
			DINW A 1 A_Lower
			loop
		Fire:
			DINW B 2 BRIGHT A_PlayWeaponSound("DinFire")
			DINW C 2 BRIGHT
			DINW D 2 BRIGHT
			DINW E 2 BRIGHT
			DINW FFFFFFFFFFFFF 0 BRIGHT A_FireCustomMissile("DinFireball",Random(-10,10),0,-7,0)
			DINW F 2 BRIGHT A_FireCustomMissile("DinFireball",Random(-10,10),1,-7,0)
			DINW A 30
			DINW A 0 A_Refire
			goto Ready
	}
}

actor DinFireball
{
	Projectile
	Radius 3
	Height 4
	Scale 0.2
	Damage (8)
	Speed 75
	RenderStyle Add
	Alpha 0.9
	obituary "%k burned %o with Din's Fire"
	DamageType "Fire"
	SeeSound "DinFly"
	Deathsound "DinExplode"
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			DINF A 0
			DINF A 0 ThrustThingZ(0, Random(0, 19), Random(0,1), 1)
			DINF ABCDEFGHIJ 1 BRIGHT A_SpawnItemEx("DinFireballTrail", 0, 0, 0, Random(-1,1), Random(-1,1), Random(-1,1), 0, SXF_CLIENTSIDE, 0)
			goto Spawn+2
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death
		Death:
			DINX ABCDEFGHI 1 BRIGHT A_SpawnItemEx("DinFireballExplosion", Random(-8,8), Random(-8,8), Random(-8,8), 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			stop
	}
}

actor DinFireballTrail
{
	Projectile
	Radius 2
	Height 4
	Scale 0.2
	Speed 0
	Damage 0
	RenderStyle Add
	Alpha 0.9
	Gravity 0.8
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+NOINTERACTION
	States
	{
		Spawn:
		TNT1 A 1
		DINF ABCDEFGHIJ 1 BRIGHT A_FadeOut(0.08)
		loop
	}
}

actor DinFireballExplosion
{
	Radius 2
	Height 4
	Scale 0.3
	RenderStyle Add
	Alpha 0.9
	+NOGRAVITY
	+DONTSPLASH
	+NOCLIP
	+FORCEXYBILLBOARD
	+NOINTERACTION
	States
	{
		Spawn:
		DINX ABCDEFGHI 1 BRIGHT A_FadeOut(0.04)
		loop
	}
}

actor SPAS-12 : Weapon
{
	Tag "SPAS-12"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "Smooth"
	Weapon.BobRangeX 1.25
    Weapon.BobRangeY 1.25
	Decal BulletChip
	obituary "%o was torn to shreds by %k's SPAS-12."
	weapon.selectionorder 400
	weapon.kickback 100
	weapon.upsound "SlamShotCock2"
	Weapon.AmmoType "SpreadAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	attacksound "SlamshotFire"
	States
	{
		Ready:
			D6SG A 1 A_WeaponReady
			loop
		Select:
			D6SG A 1 A_Raise
			loop
		Deselect:
			D6SG A 1 A_Lower
			loop
		Fire:
			D6SG A 0 A_GunFlash
			D6SG B 1 BRIGHT A_SetPitch(pitch-2)
			D6SG B 0 A_FireCustomMissile("ShellCasingSpawnerRight",-45+frandom(-8.0,8.0),0,10,3)
			D6SG CDEF 1 A_SetPitch(pitch+0.5)
			D6SG A 1
			D6SG A 0 A_GunFlash
			D6SG B 1 BRIGHT A_SetPitch(pitch-2)
			D6SG B 0 A_FireCustomMissile("ShellCasingSpawnerRight",-45+frandom(-8.0,8.0),0,10,3)
			D6SG CDEF 1 A_SetPitch(pitch+0.5)
			D6SG A 3
			D6SG GHIJ 3
			D6SG K 2 A_PlaySoundEx("SlamshotCock1", "Auto")
			D6SG L 4
			D6SG K 2 A_PlaySoundEx("SlamshotCock2", "Auto")
			D6SG JIHG 2
			D6SG A 6
			D6SG A 0 A_Refire
			goto Ready
			
		Flash:
			TNT1 A 0 radius_quake(3, 3, 0, 1, 0)
			TNT1 A 0 A_FireBullets(8, 4, 7, 10, "SPASPuff", FBF_NORANDOM | FBF_USEAMMO)
			TNT1 A 2 A_Light2
			TNT1 A 1 A_Light1
			goto LightDone
	}
}