ACTOR BioRifle : Weapon 19982
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the BioRifle!"
	Obituary "%o got slimed by %k's BioRifle!"
	Weapon.AmmoType "CellAmmo"
	Weapon.AmmoGive 100
	Weapon.AmmoType2 "CellAmmo"
	Weapon.AmmoUse 2
	Weapon.AmmoUse2 12
	AttackSound "BioRifle"
	Weapon.Kickback 200
	Weapon.SelectionOrder 1500
	States
	{
	Spawn:
		BIOP A -1
		Loop
	Ready:
		BIOG A 1 A_WeaponReady
		Loop
	Deselect:
		BIOG A 1 A_Lower
		Loop
	Select:
		BIOG A 0 A_Raise
		BIOG A 1 A_Raise
		Loop
	Fire:
		BIOG B 2 BRIGHT A_FireCustomMissile("BioBall",random(-1,1),2,6,0)
		BIOG C 1 BRIGHT
		BIOG D 1 BRIGHT
		BIOG E 1 BRIGHT
		BIOG F 1 BRIGHT
		BIOG GH 1
		BIOG A 3
		BIOG A 0 A_Refire
		Goto Ready
	AltFire:
		BIOG AAAAAAAAA 5 A_PlayWeaponSound("Gasoline")
		BIOG B 1 BRIGHT A_FireCustomMissile("GiantBioBall",0,2,6,0)
		BIOG C 1 BRIGHT
		BIOG D 2 BRIGHT
		BIOG E 2 BRIGHT
		BIOG C 2 BRIGHT
		BIOG A 3
		BIOG A 0 A_Refire
		goto Ready

	}
}

ACTOR BioBurstDrop
{
	renderstyle add
	Radius 1
	Height 1
	alpha 0.5
	Health 1
	Speed 4
	Damage 1
	Mass 9
	Scale 0.5
	ReactionTime 800
	BounceFactor 0
	+HEXENBOUNCE
	+MISSILE
	+STRIFEDAMAGE
	-NOGRAVITY
	+DONTSPLASH
	+CORPSE
	States
	{
	Spawn:
		BIOS A 0
		BIOS A 0 A_Jump(128, "Spawn2", "Spawn3", "Spawn4")
		goto Spawn1
	
	Spawn1:
		BIOS A 1 BRIGHT A_CountDown
		loop
	Spawn2:
		BIOS B 1 BRIGHT A_CountDown
		loop
	Spawn3:
		BIOS C 1 BRIGHT A_CountDown
		loop
	Spawn4:
		BIOS D 1 BRIGHT A_CountDown
		loop
		
	Death:
		TNT1 A 1
		stop
	Crash:
		BPUD A 80 BRIGHT
		BPUD A 3 BRIGHT A_FadeOut
		goto Crash+1
	}
}

ACTOR BioBall
{
	Decal "BioRifle"
	Radius 8
	Height 1
	Health 1
	Damage (17)
	PoisonDamage 50
	Speed 38
	Renderstyle Add
	Alpha 0.9
	SeeSound "BioRifle"
	DeathSound "BioGoo"
	Projectile
	+CORPSE
	+NODAMAGETHRUST
	+DONTSPLASH
	+FORCERADIUSDMG
	-NOGRAVITY
	States
	{
		Spawn:
			SBS3 A 0
			SBS3 A 0 BRIGHT ThrustThingZ(0, random(12, 16), 0, 1)
			SBS3 A 1 BRIGHT A_NoGravity
			SBS3 BC 1 BRIGHT
			SBS3 A 0 BRIGHT A_Gravity
			SBS3 AABBCC 1 BRIGHT
			goto Spawn+5
		Death:
			SBS3 A 0 A_NoGravity
			SBS3 A 0 A_Jump(127, 6)
			SBS3 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT
			SLDG EEEEEEEE 0 A_SpawnItemEx("BioBurstDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(2, 8), 0, 0, 0)
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(112, 100, 1)
			SLDG EFGH 2 BRIGHT
			Stop
		XDeath:
			SBS3 A 0 A_NoGravity
			SBS3 DDDDDDDD 0 A_SpawnItemEx("BioBurstDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(2, 8), 0, 0, 0)
			SBS3 D 0 A_PlaySound("BioBurst")
			SBS3 D 0 A_Explode(32, 64, 1)
			SBS3 DEFGH 1 BRIGHT
			Stop
		Crash:
			SBS4 A 0 A_Jump(127, 6)
			SBS4 A 0 A_Jump(80, 10)
			SBS4 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT A_Explode(2, 14, 0)
			SLDG EEEEEEEE 0 A_SpawnItemEx("BioBurstDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(2, 8), 0, 0, 0)
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(90, 80, 1)
			SLDG EFGH 2 BRIGHT
			Stop
	}
}

ACTOR SlowBioBall : Bioball
{
	Radius 2
	Height 1
	Health 1
	PoisonDamage 50
	Decal BioRifle
	Speed 10
	-NOGRAVITY
	States
	{
		Spawn:
			SBS3 ABC 1 BRIGHT
			loop
	}
}

actor Enemy_BioBall : BioBall
{
	DamageType "EnemyPoison"
	Damage (8)
	PoisonDamage 25
	States
	{
		Spawn:
			SBS3 AABBCC 1 BRIGHT
			loop
	}
}

ACTOR GiantBioBall
{
	Decal "BioRifle"
	Radius 8
	Height 1
	Damage (50)
	PoisonDamage 50
	Speed 38
	Scale 2.0
	Renderstyle Add
	Alpha 0.9
	SeeSound "BioRifle"
	DeathSound "BioGoo"
	Projectile
	+CORPSE
	+NODAMAGETHRUST
	+DONTSPLASH
	+FORCERADIUSDMG
	-NOGRAVITY
	States
	{
		Spawn:
			SBS3 A 0
			SBS3 A 0 BRIGHT ThrustThingZ(0, 14, 0, 1)
			SBS3 A 1 BRIGHT A_NoGravity
			SBS3 BC 1 BRIGHT
			SBS3 A 0 BRIGHT A_Gravity
			SBS3 AABBCC 1 BRIGHT
			goto Spawn+5
		Death:
			SBS3 A 0 A_NoGravity
			SBS3 A 0 A_Jump(127, 6)
			SBS3 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT
			SLDG EEEEEEEE 0 A_CustomMissile("SlowBioBall",17,0,Random(-360,360), 6, random(-360, 360))
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(140, 134, 1)
			SLDG EFGH 2 BRIGHT
			Stop
		XDeath:
			SBS3 A 0 A_NoGravity
			SLDG EEEEEEEE 0 A_CustomMissile("SlowBioBall",17,0,Random(-360,360), 6, random(-360, 360))
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(64, 134, 1)
			SLDG EFGH 2 BRIGHT
			Stop
		Crash:
			SBS4 A 0 A_Jump(127, 6)
			SBS4 A 0 A_Jump(80, 10)
			SBS4 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT A_Explode(5, 24, 0)
			SLDG EEEEEEEE 0 A_CustomMissile("SlowBioBall",17,0,Random(-360,360), 6, random(0, 180))
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(128, 128, 1)
			SLDG EFGH 2 BRIGHT
			Stop
	}
}