actor E_Peen_Joke : Weapon
{
	obituary "%o was pissed on by %k"
	weapon.selectionorder 9999
	weapon.kickback 100
	Weapon.AmmoType1 "None"
	Weapon.AmmoGive 0
	Weapon.AmmoUse1 0
	+WEAPON.WIMPY_WEAPON
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.NOLMS
	states
	{
	Ready:
		TNT1 A 1 A_WeaponReady
		loop
	Deselect:
		TNT1 A 1 A_Lower
		loop
	Select:
		TNT1 A 1 A_Raise
		loop
	Fire:
		TNT1 A 1 A_FireCustomMissile("Piss", frandom(-1.0,1.0), 0, 0, -13)
		TNT1 A 0 A_Jump(18, 2)
		TNT1 A 1 A_ReFire
		goto Ready
		TNT1 A 0 A_Giveinventory("Health", 1)
		goto Fire+2
	Flash:
		TNT1 A 0 bright A_Light0
		stop
	}
}
actor Piss
{
	projectile
	Radius 1
	Height 1
	Damage 0
	Speed 12
	Scale 0.5
	RenderStyle Add
	-NOGRAVITY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	Translation "168:191=160:167"
	states
	{
	spawn:
		BLUD C 0
		BLUD C 0 ThrustThingZ(0, 10, 0, 1)
		BLUD CBA 5
		stop
	Death:
		TNT1 A 1
		stop
	}
}

actor PukeTimer : BaseCheck
{
	inventory.maxamount 40
}

actor DrunkenBastard : Weapon
{
	weapon.selectionorder 9999
	weapon.kickback 100
	Weapon.AmmoType1 "None"
	Weapon.AmmoGive 0
	Weapon.AmmoUse1 0
	+WEAPON.WIMPY_WEAPON
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.NOLMS
	+WEAPON.NOAUTOAIM
	states
	{
	Ready:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_WeaponReady
		TNT1 A 0 A_Jump(64, "IdleRamble")
		loop
	IdleRamble:
		TNT1 A 0 A_PlayWeaponSound("Drunk")
		goto Ready
	Deselect:
		TNT1 A 0 A_JumpIfInventory("DrunkMode", 1, "Ready")
		TNT1 A 1 A_Lower
		loop
	Select:
		TNT1 A 1 A_Raise
		loop
	Fire:
		TNT1 A 0 A_Jump(15, "Scrumpeh")
		TNT1 A 20 A_FireCustomMissile("ThrownBottle", frandom(-4.0,4.0), false, 0, 0, false, frandom(3.0, 6.0))
		TNT1 A 0 A_Jump(30, "FireRamble")
		TNT1 A 0 A_Refire
		goto Ready
	FireRamble:
		TNT1 A 0 A_PlayWeaponSound("Drunk")
		TNT1 A 0 A_Refire
		goto Ready
	Scrumpeh:
		TNT1 A 20 A_FireCustomMissile("ThrownScrumpy", frandom(-4.0,4.0), false, 0, 0, false, frandom(3.0, 6.0))
		TNT1 A 0 A_Refire
		goto Ready
	AltFire:
		TNT1 A 40 A_PlayWeaponSound("Drunk")
		TNT1 A 0 A_PlayWeaponSound("Vomit")
		goto PukeLoop
	PukeLoop:
		TNT1 A 0 A_FireCustomMissile("Puke1", frandom(-6.0,6.0), false, 0, 13, false, frandom(0.0, 8.0))
		TNT1 A 0 A_FireCustomMissile("Puke2", frandom(-6.0,6.0), false, 0, 13, false, frandom(0.0, 8.0))
		TNT1 A 0 A_FireCustomMissile("Puke3", frandom(-6.0,6.0), false, 0, 13, false, frandom(0.0, 8.0))
		TNT1 A 0 A_FireCustomMissile("Puke4", frandom(-6.0,6.0), false, 0, 13, false, frandom(0.0, 8.0))
		TNT1 A 1 A_GiveInventory("PukeTimer", 1)
		TNT1 A 0 A_JumpIfInventory("PukeTimer", 40, "PukeDone")
		loop
	PukeDone:
		TNT1 A 20 A_TakeInventory("PukeTimer", 999)
		goto Ready
	Flash:
		TNT1 A 0 bright A_Light0
		stop
	}
}

actor ThrownBottle
{
	Projectile
	Radius 8
	Height 8
	Speed 32
	Damage (30)
	obituary "%o was clocked over the head with %k's bottles"
	Seesound "skeleton/swing"
	+FORCEXYBILLBOARD
	+FORCEPAIN
	+FORCERADIUSDMG
	-NOGRAVITY
	States
	{
		Spawn:
			BOZE C 0
			BOZE C 0 A_Jump(256, "Bottle1", "Bottle2", "Bottle3", "Bottle4", "Bottle5")
			goto Bottle1
		Bottle1:
			BOZE C 1
			loop
		Bottle2:
			BOZE D 1
			loop
		Bottle3:
			BOZE E 1
			loop
		Bottle4:
			BOZE F 1
			loop
		Bottle5:
			BOZE G 1
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death
		Death:
			TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("MolotovGlass", 0, 0, 0, frandom(-4.0,4.0), frandom(-4.0,4.0), frandom(0.0,8.0), random(-180,180), SXF_CLIENTSIDE, 0)
			TNT1 A 0 A_PlaySound("bottlebreak", CHAN_AUTO)
			TNT1 AA 2
			stop
	}
}

actor ThrownScrumpy : ThrownBottle
{
	Speed 35
	+EXTREMEDEATH
	States
	{
		Spawn:
			BOZE C 1
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death
		Death:
			TNT1 A 0 A_PlaySound("BigExplosion", CHAN_AUTO)
			TNT1 A 0 A_PlaySound("DemoBoom", CHAN_AUTO)
			TNT1 A 0 A_Explode(200, 200, 0, 1, 32)
			TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("MolotovGlass", 0, 0, 0, frandom(-16.0,16.0), frandom(-16.0,16.0), frandom(0.0,16.0), random(-180,180), SXF_CLIENTSIDE, 0)
			TNT1 A 0 A_PlaySound("bottlebreak", CHAN_AUTO)
			TNT1 AAAAAAAAAAAAA 1 A_SpawnItemEx("Duke64Explosion", 0, 0, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), frandom(-1.0,1.0), random(-180,180), SXF_CLIENTSIDE, 0)
			stop
	}
}

actor Puke1
{
	obituary "%o partied too hard with %k"
	projectile
	Radius 1
	Height 1
	Damage (4)
	Speed 25
	Scale 0.5
	RenderStyle Translucent
	Alpha 0.5
	-NOGRAVITY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+NODAMAGETHRUST
	+BLOODLESSIMPACT
	+FORCEPAIN
	Translation "168:191=[128,255,0]:[0,64,0]"
	states
	{
	Spawn:
		BLUD CBA 10
		stop
	Death:
		TNT1 A 1
		stop
	}
}

actor Puke2 : Puke1
{
	obituary "%o was barfed on by %k"
	Translation "168:191=160:167"
}

actor Puke3 : Puke1
{
	obituary "%o was rained on by %k's cookies"
	Translation "168:191=[128,64,0]:[0,64,0]"
}

actor Puke4 : Puke1
{
	obituary "%k thought he'd be a bloat and upchucked on %k"
	Translation "168:191=[255,255,0]:[128,0,0]"
}