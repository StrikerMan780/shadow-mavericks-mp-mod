ACTOR Excalibat : Weapon 23502
{
	Weapon.SelectionOrder 200
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "The Excalibat! Homerun!"
	obituary "%k turned %o into a human baseball!"
	Weapon.AmmoType1 "None"
	Weapon.AmmoGive1 0
	Weapon.AmmoUse1 0
	+PUFFONACTORS
	Weapon.Kickback 999999
	Scale 0.80
	+FLOATBOB
	+WEAPON.NOALERT
	States
	{
	Spawn:
		EXBP A -1 BRIGHT
		LOOP
	Ready:
		EXBA A 1 A_WeaponReady
		LOOP
	Deselect:
		EXBA A 1 A_Lower
		LOOP
	Select:
		EXBA A 1 A_Raise
		LOOP
	Fire:
		EXBA A 2
		EXBA B 2
		EXBA C 2
		EXBA C 0 A_Refire
		goto Swing
	Hold:
		EXBA D 1
		EXBA D 0 A_Refire
		goto Swing
	Swing:
		EXBA E 0 A_Playsound("BatSwing")
		EXBA E 1
		EXBA F 0 A_Blast(BF_DONTWARN, 99999, 80, 40)
		EXBA F 1 A_CustomPunch(100, true, CPF_PULLIN, "ExcalibatPuff", 76, 0)
		EXBA GHIJKLMNOPQRSTU 1
		EXBA A 2
		goto Ready
	}
}

actor ExcalibatPuff : NewBulletPuff
{
	DamageType "Fist"
	SeeSound "*fist"
	AttackSound "BootHit"
	States
	{
		Melee:
			goto Smoke
		Crash:
			goto Smoke
	}
}
