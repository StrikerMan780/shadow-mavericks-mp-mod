actor BloodyChainsaw : Weapon Replaces Chainsaw
{
	spawnid 32
	obituary "%o was torn to gory shreds by %k's chainsaw."
	radius 20
	height 16
	inventory.pickupmessage "A chainsaw!	Find some meat!"
	Inventory.RestrictedTo "Marine"
	weapon.upsound "weapons/sawup"
	weapon.selectionorder 2200
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	states
	{
	Ready:
		SAWD ABCDEFGHIJKLMNOPQRSTUVW 2 A_WeaponReady(WRF_NOFIRE)
		goto ReadyLoop
	ReadyLoop:
		SAW1 A 0 A_Playsound("weapons/sawidle", 7, 1.0, 1)
		SAW1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 A_WeaponReady
		SAW2 ABCDEFGHIJKLMNOPQRSTUVWXY 2 A_WeaponReady
		SAW1 A 0 A_AlertMonsters
		goto ReadyLoop
	Deselect:
		SAW1 A 0 A_PlaySound("ChainsawOff", 7, 1.0, 0)
		goto DeselectLoop
	DeselectLoop:
		SAW1 A 1 A_Lower
		loop
	Select:
		SAWD A 0 A_Raise
		SAWD A 1 A_Raise
		loop
	Fire:
		SAWS A 0 A_AlertMonsters
		SAWS A 1 A_PlaySound("ChainsawRev", 7, 1.0, 0)
		SAWS BC 1
		SAWS DEF 1 A_GunFlash("DoAttack", GFF_NOEXTCHANGE)
		SAWS G 0 A_JumpIfInventory("ChainsawSlashReady", 1, "Slash")
		SAWS GHIJK 1
		SAWS L 0 A_PlaySound("weapons/sawfull", 7, 1.0, 1)
		SAWS LMNOP 1 A_GunFlash("DoAttack", GFF_NOEXTCHANGE)
		SAWS P 0 A_ReFire
		goto AttackEnd
	Hold:
		SAWL A 0 A_PlaySound("weapons/sawfull", 7, 1.0, 1)
		SAWL A 0 A_JumpIfInventory("ChainsawSlashReady", 1, "Slash")
		SAWL ABCD 1 A_GunFlash("DoAttack", GFF_NOEXTCHANGE)
		SAWL E 0 A_JumpIfInventory("ChainsawSlashReady", 1, "Slash")
		SAWL EFGH 1 A_GunFlash("DoAttack", GFF_NOEXTCHANGE)
		SAWL I 0 A_JumpIfInventory("ChainsawSlashReady", 1, "Slash")
		SAWL IJKL 1 A_GunFlash("DoAttack", GFF_NOEXTCHANGE)
		SAWL M 0 A_JumpIfInventory("ChainsawSlashReady", 1, "Slash")
		SAWL MNOP 1 A_GunFlash("DoAttack", GFF_NOEXTCHANGE)
		SAWL A 0 A_ReFire
		goto AttackEnd
	AttackEnd:
		SAWE A 0 A_PlaySound("ChainsawDown", 7, 1.0, 0)
		SAWE ABCDEFG 1 A_GunFlash("DoAttack", GFF_NOEXTCHANGE)
		SAWE HIJKLMOPQRSTUVW 1
		goto ReadyLoop
	
	Slash:
		SAWS A 0 A_PlaySound("ChainsawSlash", 7, 1.0, 0)
		SAWS A 0 A_TakeInventory("ChainsawSlashReady", 1)
		SAWS A 0 A_Recoil(-4)
		SAWS ABC 1
		SWS1 A 0 A_Jump(128, "Slash2")
		goto Slash1
	Slash1:
		SWS1 AB 2 
		SWS1 CDEFGHIJKL 2 A_GunFlash("DoSlashAttack", GFF_NOEXTCHANGE)
		SWS1 MNOPQRSTUVW 2
		SWS1 W 0 A_Refire("Fire")
		goto ReadyLoop
	Slash2:
		SWS2 AB 2 
		SWS2 CDEFGHIJKL 2 A_GunFlash("DoSlashAttack", GFF_NOEXTCHANGE)
		SWS2 MNOPQRSTUVW 2
		SWS2 W 0 A_Refire("Fire")
		goto ReadyLoop
	
	AltFire:
		SAW1 A 0 A_PlaySound("ChainsawOff", 7, 1.0, 0)
		SAW1 ABCDEFGH 2 A_WeaponReady(WRF_NOFIRE)
		SAW1 A 0 A_Refire
		goto PullCord
	AltHold:
		SAW1 AA 2 A_WeaponReady(WRF_NOFIRE)
		SAW1 A 0 A_Refire
		goto PullCord
	PullCord:
		SAWP A 0 A_PlaysoundEX("weapons/sawup",voice, 0)
		SAWP ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_NOFIRE)
		SAWC ABCDEFGHIJKLMNOPQRSTU 1 A_WeaponReady(WRF_NOFIRE)
		goto ReadyLoop
		
	DoAttack:
		TNT1 A 0 A_Quake(1, 2, 0, 1, "")
		TNT1 A 0 A_Saw("", "weapons/sawhit", 10, "ChainsawPuff", SF_NORANDOM, 65, 2.8125, 1)
		TNT1 A 0 A_JumpIfCloser(80, "CheckHealth") 
		stop
	DoSlashAttack:
		TNT1 A 0 A_Quake(2, 2, 0, 1, "")
		TNT1 A 0 A_Saw("", "weapons/sawhit", 25, "ChainsawPuff", SF_NORANDOM, 65, 3.8125, 1)
		stop
	CheckHealth:
		TNT1 A 0 ACS_NamedExecuteAlways("CHAINSAWHEALTHCHECK", 0)
		TNT1 A 0 A_JumpIfInventory("ChainsawHealthCritical", 1, "ConfirmSlash", AAPTR_PLAYER_GETTARGET)
		stop
	ConfirmSlash:
		TNT1 A 0 A_GiveInventory("ChainsawSlashReady", 1)
		stop
	Spawn:
		CSAW A -1
		stop
	}
}

actor ChainsawHealthCritical : BaseCheck
{
}

actor ChainsawSlashReady : BaseCheck
{
}