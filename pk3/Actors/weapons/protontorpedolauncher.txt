actor TorpedoLauncherGiver : ClassWeaponGiver replaces RocketLauncher
{	
	states
	{
		Item:
			LAUN A -1
			loop
		Pickup:
			LAUN A 0 A_JumpIf(ACS_NamedExecuteWithResult("GIVEWEAPON", 5, 2, 0) == TRUE, "Stay")
			goto Remove
		Dropped:
			LAUN A 0 A_SpawnItemEx("TorpedoLauncherGiver_Dropped", 0, 0, 0, velx, vely, velz, angle, SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE, 0)
			stop
	}
}

actor TorpedoLauncherGiver_Dropped : TorpedoLauncherGiver
{
	States
	{
		Spawn:
			LAUN A 63000
			goto FadeAway
		Pickup:
			LAUN A 0 A_JumpIf(ACS_NamedExecuteWithResult("GIVEWEAPON", 5, 1, 1) == TRUE, "Stay")
			goto Remove
	}
}

actor ProtonTL : Weapon
{
	Tag "A55-H013 ''Inferno'' Launcher"
	Weapon.BobSpeed 1.25
	Weapon.BobStyle "Alpha"
	Weapon.BobRangeX 1.0
    Weapon.BobRangeY 1.0
	weapon.selectionorder 2500
	weapon.kickback 100
	weapon.ammotype "ProtonAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	+WEAPON.NOAUTOFIRE
	+WEAPON.EXPLOSIVE
	States
	{
	Ready:
		MISG A 1 A_WeaponReady
		loop
	Deselect:
		MISG A 1 A_Lower
		loop
	Select:
		MISG A 1 A_Raise
		loop
	Fire:
		MISG A 0 BRIGHT A_GunFlash
		MISG A 0 BRIGHT radius_quake(2, 3, 0, 1, 0)
		MISG A 0 BRIGHT A_FireCustomMissile("ProtonTorpedo", 0, 1, 9, 0)
		MISG B 2 BRIGHT A_SetPitch(pitch-2.25)
		MISG CDEFGHIJK 1 A_SetPitch(pitch+0.25)
		MISG A 9
		MISG A 0 A_ReFire
		goto Ready
	Flash:
		TNT1 A 2 bright A_Light1
		TNT1 A 4 bright
		TNT1 A 8 bright A_Light2
		TNT1 D 0 bright A_Light0
		stop
	}
}

actor TorpedoExplosionShell : BaseEffect
{
	RenderStyle Add
	Alpha 0.8
	Scale 0.8
	States
	{
		Spawn:
			NULL A 0
			NULL ABCD 1 BRIGHT
			NULL EFGH 1 BRIGHT A_Fadeout(0.025)
			NULL IJKL 1 BRIGHT A_Fadeout(0.025)
			stop
	}
}

actor ProtonTorpedo replaces Rocket
{
	Projectile
	Radius 11
	Height 8
	obituary "%o was gored by %k's proton torpedoes!"
	-NOTELEPORT
	+EXTREMEDEATH
	+FORCEXYBILLBOARD
	+EXPLODEONDEATH
	Speed 35
	Damage (80)
	RenderStyle Add
	Alpha 0.9
	SeeSound "weapons/rocklf"
	DeathSound "ProtonExplosion"
	Decal Scorch
	States
	{
		Spawn:
			MISL A 1 BRIGHT A_SpawnItemEX("ProtonTrail", 0, 0, 0, Random(-1,1), Random(-1,1), Random(-1,1), 0, 128, 0)
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death
		Death:
			FIR8 A 0 bright A_Explode
			FIR8 A 0 bright radius_quake(3, 16, 0, 4, 0)
			FIR8 A 0 A_SpawnItemEX("TorpedoExplosionShell", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			FIR8 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("RPGSpark", 0, 0, 0, frandom(0.0,16.0), 0, frandom(0.0,16.0), random(-180,180), SXF_CLIENTSIDE, 0)
			FIR8 AA 0 A_SpawnItemEX("RocketFire", Random(-8, 8), Random(-8, 8), Random(-8, 8), Random(-4, 4), Random(-4, 4), Random(-4, 4), Random(-360, 360), 128, 0)
			FIR8 ABCDEFGHIJK 1 BRIGHT A_SpawnItemEX("RocketFire", Random(-8, 8), Random(-8, 8), Random(-8, 8), Random(-3, 3), Random(-3, 3), Random(-3, 3), Random(-360, 360), 128, 0)
			FIR8 LMNOPQRSTUVWXYZ 1 bright
			FI82 ABCD 1 bright
			stop
	}
}

actor ProtonTrail : BaseEffect
{
	RenderStyle Add
	Alpha 0.9
	States
	{
		Spawn:
			TNT1 A 2
			MISL A 1 BRIGHT A_FadeOut(0.1)
			goto Spawn+1
	}
}

actor IceRod : Weapon
{
	Tag "Ice Rod"
	weapon.selectionorder 2500
	weapon.kickback 100
	weapon.ammotype "ProtonAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	+WEAPON.NOAUTOFIRE
	+WEAPON.EXPLOSIVE
	States
	{
		Ready:
			IROD A 1 A_WeaponReady
			loop
		Select:
			IROD A 1 A_Raise
			loop
		Deselect:
			IROD A 1 A_Lower
			loop
		Fire:
			IROD A 1
			IROD B 3
			IROD C 3 BRIGHT
			IROD D 0 BRIGHT radius_quake(2, 3, 0, 1, 0)
			IROD D 0 BRIGHT A_Recoil(5)
			IROD DDDDDDDDDD 0 BRIGHT A_SpawnItemEx("IceRodFog1", 16, 0, 32, 0.5, 0, Random(-0.5, 0.5), Random(-50, 50), 128)
			IROD D 1 BRIGHT A_FireCustomMissile("IceRodShot")
			IROD E 1 BRIGHT
			IROD CB 4
			IROD A 12
			IROD A 0 A_Refire
			goto Ready
	}
}

actor IceRodShot
{
	Projectile
	Obituary "%o was chilled to the soul by %k"
	Radius 8
	Height 11
	Speed 40
	Damage (60)
	RenderStyle Add
	DamageType "IceRod"
	SeeSound "IceRod"
	Alpha 1.0
	decal IceRodHit
	+FORCEXYBILLBOARD
	-NOTELEPORT
	States
	{
		Spawn:
			RODI A 0 BRIGHT A_SpawnItemEx("IceRodTrail1", Random(-8,8), Random(-8,8), Random(-8,8), 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			RODI A 0 BRIGHT A_SpawnItemEx("IceRodFog5", 0, 0, 0, 0, -4, 0, 0, 128, 0)
			RODI A 0 BRIGHT A_SpawnItemEx("IceRodFog5", 0, 0, 0, 0, 4, 0, 0, 128, 0)
			RODI A 1 BRIGHT A_SpawnItemEx("IceRodTrail1", Random(-8,8), Random(-8,8), Random(-8,8), 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			loop
		Death:
			NULL A 0
			NULL A 0 A_Explode(96, 96)
			NULL A 0 A_PlaySoundEx("misc/icebreak", "auto", 0)
			NULL A 0 A_PlaySoundEx("misc/freeze", "auto", 0)
			NULL A 0 A_SpawnItemEx("IceRodFog3", 0, 0, 0, 0, 0, 0, 0, 128, 0)
			NULL AAAAAA 0 A_SpawnItemEx("IceRodFog4", 0, 0, 0, random(-8,8), random(-8,8), random(-8,8), 0, 128, 0)
			NULL AAAAAAAAAA 0 A_SpawnItemEx("IceChunk", 0, 0, 0, random(-3,3), random(-3,3), random(0,5), 0, 128, 0)
			NULL AAAAAAAAAAAAA 0 A_SpawnItemEx("IceRodFog2", Random(-32,32), Random(-32,32), Random(-32,32), 0, 0, 0, 0, 128, 0)
			NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceRodHitGlitter", Random(-48,48), Random(-48,48), Random(-48,48), 0, 0, 0, 0, 128, 0)
			NULL AAAAAA 2 A_SpawnItemEx("IceRodFog2", Random(-32,32), Random(-32,32), Random(-32,32), 0, 0, 0, 0, 128, 0)
			stop
	}
}

actor IceRodTrail1
{
	Radius 1
	Height 1
	Scale 0.009
	Alpha 1.0
	RenderStyle Add
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+NOCLIP
	+NOGRAVITY
	States
	{
		Spawn:
			BLSS AB 1 BRIGHT A_FadeOut(0.02)
			loop
	}
}

actor IceRodHitGlitter
{
	Radius 1
	Height 1
	Scale 0.01
	Alpha 1.0
	RenderStyle Add
	Gravity 0.08
	+FORCEXYBILLBOARD
	+CORPSE
	+NOCLIP
	States
	{
		Spawn:
			BLSS AB 1 BRIGHT A_FadeOut(0.02)
			loop
		Crash:
			TNT1 A 1
			stop
	}
}

Actor IceRodFog1
{
	Radius 1
	Height 1
	RenderStyle Add
	Alpha 0.2
	XScale 0.6
	YScale 0.4
	+NOGRAVITY
	+NOCLIP
	+NOINTERACTION
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		IRFO A 1 A_FadeOut(0.0075)
		loop
	}
}

actor IceRodFog2 : IceRodFog1
{
	XScale 1.4
	YScale 1.4
}

actor IceRodFog3 : IceRodFog1
{
	Alpha 0.3
	XScale 2.0
	YScale 2.0
	States
	{
		Spawn:
			IRFO A 1 A_FadeOut(0.003)
			loop
	}
}

actor IceRodFog4 : IceRodFog1
{
	-NOINTERACTION
	Projectile
	Alpha 0.4
	XScale 0.6
	YScale 0.6
	States
	{
		Spawn:
			IRFO A 1 A_FadeOut(0.04)
			loop
		Death:
			TNT1 AA 1
			stop
	}
}

actor IceRodFog5 : IceRodFog1
{
	Alpha 0.3
	XScale 0.6
	YScale 0.4
	States
	{
		Spawn:
			IRFO A 1 A_FadeOut(0.03)
			loop
	}
}

actor DukeRPG : Weapon
{
	Tag "RPG"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "Smooth"
	Weapon.BobRangeX 1.25
    Weapon.BobRangeY 1.25
	weapon.selectionorder 2500
	weapon.kickback 100
	weapon.ammotype "ProtonAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	weapon.upsound "DukeWeaponSelect"
	+WEAPON.NOAUTOFIRE
	+WEAPON.EXPLOSIVE
	States
	{
		Ready:
			RPGG A 1 A_WeaponReady
			loop
		Deselect:
			RPGG A 1 A_Lower
			loop
		Select:
			RPGG A 1 A_Raise
			loop
		Fire:
			RPGG A 0 A_GunFlash
			RPGG A 0 A_PlayWeaponSound("DukeRpg")
			RPGG A 1 Offset(-4, 28) A_FireCustomMissile("DukeRocket", 0, 1, 8, 1)
			
			RPGG A 0 A_SetAngle(angle-0.2)
			RPGG A 1 Offset(2, 34) A_SetPitch(pitch+0.2)
			
			RPGG A 0 A_SetAngle(angle-0.2)
			RPGG A 1 Offset(4, 36) A_SetPitch(pitch+0.2)
			
			RPGG A 0 A_SetAngle(angle-0.2)
			RPGG A 1 Offset(6, 38) A_SetPitch(pitch+0.2)
			
			RPGG A 0 A_SetAngle(angle-0.2)
			RPGG A 1 Offset(8, 40) A_SetPitch(pitch+0.2)
			
			RPGG A 0 A_SetAngle(angle-0.4)
			RPGG A 1 Offset(12, 44) A_SetPitch(pitch+0.4)
			
			RPGG A 0 A_SetAngle(angle-0.4)
			RPGG A 1 Offset(16, 48) A_SetPitch(pitch+0.4)
			
			RPGG A 0 A_SetAngle(angle-0.4)
			RPGG A 1 Offset(20, 52) A_SetPitch(pitch+0.4)
			
			RPGG A 0 A_SetAngle(angle-0.4)
			RPGG A 1 Offset(24, 56) A_SetPitch(pitch+0.4)
			
			RPGG A 0 A_SetAngle(angle-0.4)
			RPGG A 1 Offset(28, 60) A_SetPitch(pitch+0.4)
			
			RPGG A 0 A_SetAngle(angle-0.2)
			RPGG A 1 Offset(32, 64) A_SetPitch(pitch+0.2)
			
			RPGG A 0 A_SetAngle(angle-0.2)
			RPGG A 1 Offset(34, 66) A_SetPitch(pitch+0.2)
			
			RPGG A 0 A_SetAngle(angle-0.2)
			RPGG A 1 Offset(36, 68) A_SetPitch(pitch+0.2)
			
			RPGG A 0 A_SetAngle(angle-0.1)
			RPGG A 1 Offset(37, 69) A_SetPitch(pitch+0.1)
			
			RPGG A 0 A_SetAngle(angle-0.1)
			RPGG A 1 Offset(38, 70) A_SetPitch(pitch+0.1)

			RPGG A 0 A_SetAngle(angle+0.2)
			RPGG A 1 Offset(36, 68) A_SetPitch(pitch-0.2)
			
			RPGG A 0 A_SetAngle(angle+0.2)
			RPGG A 1 Offset(34, 66) A_SetPitch(pitch-0.2)
			
			RPGG A 0 A_SetAngle(angle+0.4)
			RPGG A 1 Offset(30, 62) A_SetPitch(pitch-0.4)
			
			RPGG A 0 A_SetAngle(angle+0.4)
			RPGG A 1 Offset(26, 58) A_SetPitch(pitch-0.4)
			
			RPGG A 0 A_SetAngle(angle+0.4)
			RPGG A 1 Offset(22, 54) A_SetPitch(pitch-0.4)
			
			RPGG A 0 A_SetAngle(angle+0.4)
			RPGG A 1 Offset(18, 50) A_SetPitch(pitch-0.4)
			
			RPGG A 0 A_SetAngle(angle+0.4)
			RPGG A 1 Offset(14, 46) A_SetPitch(pitch-0.4)
			
			RPGG A 0 A_SetAngle(angle+0.4)
			RPGG A 1 Offset(10, 42) A_SetPitch(pitch-0.4)
			
			RPGG A 0 A_SetAngle(angle+0.4)
			RPGG A 1 Offset(6, 38) A_SetPitch(pitch-0.4)
			
			RPGG A 0 A_SetAngle(angle+0.8)
			RPGG A 1 Offset(-2, 30) A_SetPitch(pitch-0.8)
			
			RPGG A 0 A_SetAngle(angle-0.2)
			RPGG A 1 Offset(-6, 26) A_SetPitch(pitch+0.2)
			
			RPGG A 0 A_SetAngle(angle-0.2)
			RPGG A 1 Offset(-7, 25) A_SetPitch(pitch+0.2)
			
			RPGG A 1 Offset(-8, 26)
			RPGG A 1 Offset(-4, 28)
			RPGG A 1 Offset(-2, 30) 
			
			RPGG A 1 A_WeaponReady(WRF_NOFIRE)
			RPGG A 0 A_Refire
			goto Ready
		Flash:
			RPGF A 3 bright A_Light1
			RPGF B 2 bright A_Light2
			NULL A 0 bright A_Light0
			stop
	}
}

actor RPGSmoke : BaseEffect
{
	RenderStyle Add
	Alpha 0.7
	Scale 0.25
	States
	{
		Spawn:
			TNT1 A 3
			SMK1 A 1
			SMK1 A 1 A_FadeOut(0.04)
			goto Spawn+2
	}
}

actor DukeRocket
{
	Radius 4
	Height 8
	Projectile
	Speed 30
	Damage (140)
	obituary "%o challenged %k to a rocket eating contest and won."
	Scale 0.64
	+EXTREMEDEATH
	+BLOODSPLATTER
	+FORCERADIUSDMG
	States
	{
		Spawn:
			RPGM A 1 BRIGHT A_SpawnItemEX("RPGSmoke", 0, 0, 0, MOMX-(MOMX*1.5)+frandom(-1.0,1.0), MOMY-(MOMY*1.5)+frandom(-1.0,1.0), MOMZ-(MOMZ*1.5)+frandom(-1.0,1.0), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death
		Death:
			NULL A 0
			NULL A 0 A_Quake(3, 20, 0, 256, "")
			NULL A 0 A_PlaySoundEx("DukeExplosion", "Auto", 0, 0.4)
			NULL A 0 A_Explode(140, 140, true, true, 32)
			NULL AAAA 0 A_SpawnItemEX("Duke64ExplosionSmoke", Random(-48,48), Random(-32,48), Random(-28, 36), Random(-1,1), Random(-1,1), Random(-1,1), 0, SXF_CLIENTSIDE, 0)
			NULL AAAAAAAAAAA 0 A_SpawnItemEx("RPGSpark", 0, 0, 0, frandom(0.0,10.0), 0, frandom(2.0,10.0), random(-180,180), SXF_CLIENTSIDE, 0)
			NULL A 0 A_SpawnItemEX("Duke64ExplosionRing", 0, 0, 2, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			NULL AAAAAA 0 A_SpawnItemEx("DukeExplosion2", 0, 0, 0, frandom(-6.0,6.0), frandom(-6.0,6.0), frandom(-6.0,6.0), 0, SXF_CLIENTSIDE, 0)
			NULL A 20 A_SpawnItemEx("DukeExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			stop
	}
}