actor FlakCannon : ClericWeapon 12212
{
	Tag "Mark-III ''Negotiator'' Flak Cannon"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "Alpha"
	Weapon.BobRangeX 1.25
    Weapon.BobRangeY 0.8
	obituary "%o was Ripped to shreds by %k's Flak."
	radius 20
	height 16
	inventory.pickupmessage "Flak Cannon!"
	weapon.selectionorder 200
	weapon.kickback 300
	weapon.ammotype1 "MechAmmo"
	weapon.ammouse1 5
	Weapon.ammouse2 10
	weapon.ammotype2 "MechAmmo"
	weapon.ammogive1 50
	+WEAPON.NOAUTOAIM
	States
	{
		Select:
			FLAK A 1 A_Raise
			loop

		Deselect:
			FLAK A 1 A_Lower
			loop

		Ready:
			FLAK A 1 A_WeaponReady
			loop

		Fire:
			FLAK B 0 A_PlayWeaponSound("FlakCannon")
			FLAK B 0 A_FireCustomMissile("FlakShard", (random(-600,600)/100.0), 1, 8, 0, false, (random(-300,400)/100.0))
			FLAK BBBBBBB 0 A_FireCustomMissile("FlakShard", (random(-600,600)/100.0), 0, 8, 0, false, (random(-300,400)/100.0))
			FLAK B 0 radius_quake(2, 5, 0, 1, 0)
			FLAK B 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageFireEnd")
			goto FireEnd
			
		FireEnd:
			FLAK B 1 A_SetPitch(pitch-6)
			FLAK CDEFGHIJKLMN 1 A_SetPitch(pitch+0.5)
			FLAK OPQRSTUVWXYZ 1
			FLAK A 2
			FLAK A 0 A_Refire
			goto Ready
			
		RageFireEnd:
			FLAK B 1 A_SetPitch(pitch-6)
			FLAK DFHJLN 1 A_SetPitch(pitch+1.0)
			FLAK PRTVXZ 1
			FLAK A 4
			FLAK A 0 A_Refire
			goto Ready

		AltFire:
			FLAK B 0 A_PlayWeaponSound("FlakShell")
			FLAK B 0 A_FireCustomMissile("FlakShell", 0, 10, 10, 4)
			FLAK B 0 radius_quake(2, 5, 0, 1, 0)
			FLAK B 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageAltFireEnd")
			goto AltFireEnd
			
		AltFireEnd:
			FLAK B 1 A_SetPitch(pitch-6)
			FLAK CDEFGHIJKLMN 1 A_SetPitch(pitch+0.5)
			FLAK OPQRSTUVWXYZ 1
			FLAK A 14
			FLAK A 0 A_Refire
			goto Ready
			
		RageAltFireEnd:
			FLAK B 1 A_SetPitch(pitch-6)
			FLAK DFHJLN 1 A_SetPitch(pitch+1.0)
			FLAK PRTVXZ 1
			FLAK A 28
			FLAK A 0 A_Refire
			goto Ready

		Spawn:
			FLKP A -1
			stop
	}
}

Actor FlakTrail : BaseEffect
{
	RenderStyle Add
	Alpha 0.6
	Scale 1.0
	-NOTIMEFREEZE
	States
	{
	Spawn:
		FLRA A 2 BRIGHT
		stop
	}
}

actor FlakPuff : BaseEffect
{
	RenderStyle Add
	Alpha 0.5
	Scale 0.08
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(Waterlevel == 3, "InWater")
		SMO2 ABCDEFGHIJKLMNOPQ 1
		stop
	InWater:
		TNT1 AA 0 BRIGHT A_SpawnItemEX("Bubble", Random(-4,4), Random(-4,4), Random(-4,4), 0, 0, Random(1,2), 0, 128, 240)
		TNT1 A 1
		stop
	}
}

actor FlakShard
{
	Radius 2
	Height 2
	projectile
	+NOGRAVITY
	+SKYEXPLODE
	+BLOODSPLATTER
	+FORCEXYBILLBOARD
	+EXTREMEDEATH
	+USEBOUNCESTATE
	-BLOODLESSIMPACT
	obituary "%o was ripped to shreds by %k's Flak."
	BounceType Hexen
	BounceCount 3
	ReactionTime 20
	Speed 50
	DamageType "Flak"
	Damage (24)
	Gravity 0.5
	BounceSound "Flakbounce"
	var int user_timer;
	States
	{
	Spawn:
		METL A 0
		METL A 1 BRIGHT A_SpawnItemEX("FlakTrail", 0, 0, 0, MOMX/4.0, MOMY/4.0, MOMZ/4.0, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		METL A 0 BRIGHT A_ChangeFlag("EXTREMEDEATH", 0)
		goto Fly
	Fly:
		METL AAAABBBB 1 BRIGHT A_SpawnItemEX("FlakTrail", 0, 0, 0, MOMX/4.0, MOMY/4.0, MOMZ/4.0, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		loop
	Bounce:
		METL C 0 A_Gravity
		METL C 0 A_ChangeFlag("EXTREMEDEATH", 0)
		METL C 0 A_SetUserVar("user_timer", user_timer+1)
		METL C 0 A_JumpIf(user_timer >= 140, "Death")
		METL CCCCDDDD 1 BRIGHT A_SpawnItemEX("FlakPuff", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		goto Cooled
	Cooled:
		METL E 1 A_SpawnItemEX("FlakPuff", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		METL E 0 A_SetUserVar("user_timer", user_timer+1)
		METL E 0 A_JumpIf(user_timer >= 140, "Death")
		loop
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
		TNT1 A 1
		stop
	Death:
		TNT1 AAAAAA 0 A_SpawnItemEX("FlakPuff", 0, 0, 0, random(-1,1), random(-1,1), random(-1,1), random(-360,360), SXF_CLIENTSIDE, 0)
		PUFF AB 2 BRIGHT
		stop
	}
}

Actor FlakSmoke
{
	Radius 1
	Height 1
	Speed  0
	Damage 0
	RenderStyle Add
	Alpha 0.5
	Scale 0.7
	+CORPSE
	+DONTSPLASH
	+NOGRAVITY
	+FORCEXYBILLBOARD
	-ACTIVATEPCROSS
	States
	{
	Spawn:
		TNT1 A 2
		RSMK ABCDE 3 Bright A_Fadeout
		stop
	Death:
		TNT1 A 1 Bright
		TNT1 A 1 Bright
		Stop
	}
}

actor FlakExplosion : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	Scale 0.18
	States
	{
		Spawn:
			XPLO ABCDEFGH 2 BRIGHT
			stop
	}
}

actor FlakShell
{
	Decal Scorch
	Projectile
	Radius 14
	Height 11
	Damage (0)
	Speed 30
	Scale 0.6
	DeathSound "BigExplosion"
	Obituary "%o was Shredded by %k's Flak Shell"
	+FORCEXYBILLBOARD
	+EXTREMEDEATH
	+FORCERADIUSDMG
	-NOGRAVITY
	var int user_shardcount;
	States
	{
	Spawn:
		FLS1 A 0
		FLS1 A 0 ThrustThingZ(0, 24, 0, 1)
		FLS1 A 1 A_SpawnItemEX("FlakSmoke", 0, 0, 0, frandom(-2.0,2.0), frandom(-2.0,2.0), frandom(-2.0,2.0), 0, SXF_CLIENTSIDE, 0)
		FLS1 A 0 A_SpawnItemEx("GrenadeTrail", 0, 0, 0, (Random(-100,100)/100.0), (Random(-100,100)/100.0), (Random(-100,100)/100.0), 0, SXF_CLIENTSIDE, 0)
		goto Spawn+2
	Death:
		FLS1 A 0 A_NoGravity
		FLS1 A 0 A_Explode(105, 96, true, 0, 32)
		FLS1 A 0 A_CheckFloor("Shrapnel_Ground")
		FLS1 A 0 A_CheckCeiling("Shrapnel_Ceiling")
		goto Shrapnel
	Shrapnel:
		TNT1 A 0 A_CustomMissile("FlakShard", 0, 0, frandom(-180.0,180.0), CMF_AIMDIRECTION | CMF_TRACKOWNER, frandom(-45.0, 45.0))
		TNT1 A 0 A_SetUserVar("user_shardcount", user_shardcount+1)
		TNT1 A 0 A_JumpIf(user_shardcount >= 5, "End")
		loop
	Shrapnel_Ground:
		TNT1 A 0 A_CustomMissile("FlakShard", 8, 0, frandom(-45.0,45.0), CMF_AIMDIRECTION | CMF_TRACKOWNER, frandom(1.0, 45.0))
		TNT1 A 0 A_SetUserVar("user_shardcount", user_shardcount+1)
		TNT1 A 0 A_JumpIf(user_shardcount >= 5, "End")
		loop
	Shrapnel_Ceiling:
		TNT1 A 0 A_CustomMissile("FlakShard", -8, 0, frandom(-45.0,45.0), CMF_AIMDIRECTION | CMF_TRACKOWNER, frandom(-45.0, -1.0))
		TNT1 A 0 A_SetUserVar("user_shardcount", user_shardcount+1)
		TNT1 A 0 A_JumpIf(user_shardcount >= 5, "End")
		loop
	End:
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("RPGSpark", 0, 0, 0, frandom(0.0,12.0), 0, frandom(1.0,12.0), random(-180,180), SXF_CLIENTSIDE, 0)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("FlakExplosion", 0, 0, 0, (Random(-200,200)/100.0), (Random(-200,200)/100.0), (Random(-200,200)/100.0), random(-360,360), SXF_CLIENTSIDE, 0)
		TNT1 A 2 A_NoGravity
		stop
	}
}