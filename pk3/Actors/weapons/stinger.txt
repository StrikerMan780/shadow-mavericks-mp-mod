actor StingerGiver : ClassWeaponGiver 21122
{	
	states
	{
		Item:
			STNW A -1
			loop
		Pickup:
			STNW A 0 A_JumpIf(ACS_NamedExecuteWithResult("GIVEWEAPON", 7, 0) == TRUE, "Stay")
			goto Remove
		Dropped:
			STNW A 0 A_SpawnItemEx("StingerGiver_Dropped", 0, 0, 0, velx, vely, velz, angle, SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE, 0)
			stop
	}
}

actor StingerGiver_Dropped : StingerGiver
{
	States
	{
		Spawn:
			STNW A 63000
			goto FadeAway
		Pickup:
			STNW A 0 A_JumpIf(ACS_NamedExecuteWithResult("GIVEWEAPON", 7, 1) == TRUE, "Stay")
			goto Remove
	}
}

ACTOR Stinger : Weapon
{
	Tag "D-12 ''Stinger'' Laser"
	Weapon.BobSpeed 1.0
	Weapon.BobStyle "Smooth"
	Weapon.BobRangeX 1.0
    Weapon.BobRangeY 1.0
	decal Bulletchip
	Obituary "%o was stung by %k"
	Weapon.AmmoType "CellAmmo"
	Weapon.AmmoType2 "CellAmmo"
	Weapon.AmmoGive 0
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	AttackSound "Empty"
	Weapon.Kickback 80
	Weapon.SelectionOrder 1000
	States
	{
	Spawn:
		STNG A 0
		stop
	Ready:
		STNG A 1 A_WeaponReady
		loop
	Deselect:
		STNG A 1 A_Lower
		loop
	Select:
		STNG A 1 A_Raise
		loop
	Fire:
		STNG B 0 A_FireCustomMissile("StingerShot",0,1,0,2)
		STNG B 2 Offset(1,33) BRIGHT A_SetPitch(pitch-0.5)
		STNG A 1 Offset(2,35) A_SetPitch(pitch+0.25)
		STNG A 1 Offset(1,34) A_SetPitch(pitch+0.25)
		STNG C 0 A_FireCustomMissile("StingerShot",0,1,9,2)
		STNG C 2 Offset(-1,34) BRIGHT A_SetPitch(pitch-0.5)
		STNG A 1 Offset(-2,33) A_SetPitch(pitch+0.25)
		STNG A 1 Offset(-1,32) A_SetPitch(pitch+0.25)
		STNG D 0 A_FireCustomMissile("StingerShot",0,1,-1,-8)
		STNG D 2 Offset(-2,32) BRIGHT A_SetPitch(pitch-0.5)
		STNG A 1 Offset(-1,33) A_SetPitch(pitch+0.25)
		STNG A 1 Offset(1,35) A_SetPitch(pitch+0.25)
		STNG E 0 A_FireCustomMissile("StingerShot",0,1,15,-8)
		STNG E 2 Offset(2,34) BRIGHT A_SetPitch(pitch-0.5)
		STNG A 1 Offset(0,32) A_SetPitch(pitch+0.25)
		STNG A 1 Offset(1,34) A_SetPitch(pitch+0.25)
		STNG A 0 A_REFIRE
		Goto Ready
	AltFire:
		STNG A 0 A_JumpIfInventory("StingerCharge", 1, "Charge")
		STNG A 0 A_PlaySound("StingerCharge", CHAN_WEAPON)
		goto Charge
	Charge:
		STNG A 1 Offset(1,33) A_Quake(1, 8, 0, 16, "")
		STNG A 1 Offset(1,32)
		STNG A 1 Offset(-1,33)
		STNG A 1 Offset(1,32)
		STNG A 1 Offset(-1,32)
		STNG A 1 Offset(1,32)
		STNG A 1 Offset(-1,33)
		STNG A 1 Offset(1,33)
		STNG A 1 Offset(-1,32)
		STNG A 1 Offset(-1,33) A_GiveInventory("StingerCharge", 1)
		STNG A 0 A_JumpIfInventory("StingerCharge", 8, "Unleash")
		STNG A 0 A_Refire
		goto Unleash
	Unleash:
		STNG B 0 A_StopSound(CHAN_WEAPON)
		STNG B 0 A_TakeInventory("StingerCharge", 1)
		STNG B 0 A_FireCustomMissile("StingerShot2",frandom(-4.0,4.0),1,0,2,0,frandom(-2.0,2.0))
		STNG B 1 Offset(1,33) BRIGHT A_SetPitch(pitch-0.5)
		STNG A 1 Offset(2,35) A_SetPitch(pitch+0.25)
		STNG C 0 A_FireCustomMissile("StingerShot2",frandom(-4.0,4.0),1,9,2,0,frandom(-2.0,2.0))
		STNG C 1 Offset(-1,34) BRIGHT A_SetPitch(pitch-0.5)
		STNG A 1 Offset(-2,33) A_SetPitch(pitch+0.25)
		STNG D 0 A_FireCustomMissile("StingerShot2",frandom(-4.0,4.0),1,-1,-8,0,frandom(-2.0,2.0))
		STNG D 1 Offset(-2,32) BRIGHT A_SetPitch(pitch-0.5)
		STNG A 1 Offset(-1,33) A_SetPitch(pitch+0.25)
		STNG E 0 A_FireCustomMissile("StingerShot2",frandom(-4.0,4.0),1,15,-8,0,frandom(-2.0,2.0))
		STNG E 1 Offset(2,34) BRIGHT A_SetPitch(pitch-0.5)
		STNG A 1 Offset(0,32) A_SetPitch(pitch+0.25)
		STNG A 0 A_JumpIfNoAmmo("Ready")
		STNG A 0 A_JumpIfInventory("StingerCharge", 1, "Unleash")
		STNG A 18
		STNG A 0 A_Refire
		goto Ready
	}
}

actor StingerCharge : BaseCheck
{
	inventory.maxamount "8"
}

actor StingTrail : BaseEffect
{
	RenderStyle Add
	Scale 0.25
	Alpha 0.8
	States
	{
	Spawn:
		STPE ABCDE 1 BRIGHT A_FadeOut(0.1)
		loop
	}
}

actor StingerSpark : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	Scale 0.032
	Gravity 0.25
	-NOGRAVITY
	-NOINTERACTION
	+HEXENBOUNCE
	+MISSILE
	States
	{
		Spawn:
			GEMB A 1 BRIGHT A_Fadeout(0.025)
			loop
		Death:
			TNT1 AA 1
			stop
	}
}

actor StingerShot
{
	Projectile
	Radius 4
	Height 4
	Speed 64
	Damage (10)
	RenderStyle Add
	Alpha 1.0
	Scale 0.5
	SeeSound "StingerFire"
	DeathSound "StingerHit"
	DamageType "Disintegrate"
	+SEEKERMISSILE
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	States
	{
		Spawn:
			STPL A 0 BRIGHT A_SeekerMissile(1, 1, SMF_LOOK | SMF_PRECISE, 255, 1)
			STPL A 1 BRIGHT A_SpawnItemEX("StingTrail", 0, 0, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), frandom(-1.0,1.0), 0, SXF_CLIENTSIDE, 64)
			STPL B 0 BRIGHT A_SeekerMissile(1, 1, SMF_LOOK | SMF_PRECISE, 255, 1)
			STPL B 1 BRIGHT A_SpawnItemEX("StingTrail", 0, 0, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), frandom(-1.0,1.0), 0, SXF_CLIENTSIDE, 64)
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death
		Death:
			STPE A 0 BRIGHT A_Explode(8, 24, 1)
			STPE ABCD 1 BRIGHT A_SpawnItemEx("StingerSpark", 0, 0, 0, frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(0.0, 4.0), random(-180, 180), SXF_CLIENTSIDE, 0)
			STPE E 1 BRIGHT
			stop
	}
}

actor StingerShot2 : StingerShot
{
	Speed 80
	-SEEKERMISSILE
	States
	{
		Spawn:
			STPL AB 1 BRIGHT A_SpawnItemEX("StingTrail", 0, 0, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), frandom(-1.0,1.0), 0, SXF_CLIENTSIDE, 64)
			loop
	}
}

actor MonsterStingerShot : StingerShot
{
	obituary "%o was disintegrated by a stinger zombie"
	Damage (5)
	Speed 60
	-SEEKERMISSILE
	States
	{
		Spawn:
			STPL AB 1 BRIGHT A_SpawnItemEX("StingTrail", 0, 0, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), frandom(-1.0,1.0), 0, SXF_CLIENTSIDE, 64)
			loop
	}
}

ACTOR CycloneClip : Ammo
{
	Inventory.MaxAmount 50 // size of the clip
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 50 //MUST be the same as Inventory.MaxAmmount
	Inventory.Icon "MAGICON2"
	+IGNORESKILL
}

actor Cyclone : Weapon
{
	Tag "SNC-06 ''Cyclone'' Beam Pistol"
	Decal BulletChip
	obituary "%k turned %o into an etch-a-sketch."
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "Smooth"
	Weapon.BobRangeX 0.5
    Weapon.BobRangeY 1.0
	weapon.selectionorder 1000
	weapon.kickback 100
	Weapon.AmmoType1 "CycloneClip"
	Weapon.AmmoType2 "CellAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	+WEAPON.AMMO_OPTIONAL
	States
	{
	Ready:
		CYC1 A 0 A_PlaySound("CycloneIdle", CHAN_WEAPON, 1.0, true)
		CYC1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		CYC2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady(WRF_ALLOWRELOAD)
		CYC3 ABCDEFGHIJKLMNOPQRSTUVW 1 A_WeaponReady(WRF_ALLOWRELOAD)
		LOOP
	Deselect:
		CYC1 A 0 A_StopSound(CHAN_WEAPON)
		CYC1 A 1 A_Lower
		LOOP
	Select:
		CYC1 A 1 A_Raise
		loop
	Fire:
		CYCF A 0 A_JumpIfNoAmmo("ReloadCheck")
		CYCF A 0 A_PlaySound("CycloneFire", CHAN_WEAPON, 1.0, true)
		CYCF A 0 A_GunFlash
		CYCF AB 1 BRIGHT A_RailAttack (5, 3.0, false, "none", "00FF00", RGF_SILENT | RGF_NOPIERCING | RGF_FULLBRIGHT, 0, "CyclonePuff", 0.0, 0.0, 256, 1, 4.0, 0.0, "CycloneBeam", 2.0)
		CYCF C 1 BRIGHT A_RailAttack (5, 3.0, true, "none", "00FF00", RGF_SILENT | RGF_NOPIERCING | RGF_FULLBRIGHT, 0, "CyclonePuff", 0.0, 0.0, 256, 1, 4.0, 0.0, "CycloneBeam", 2.0)
		CYCF DE 1 BRIGHT A_RailAttack (5, 3.0, false, "none", "00FF00", RGF_SILENT | RGF_NOPIERCING | RGF_FULLBRIGHT, 0, "CyclonePuff", 0.0, 0.0, 256, 1, 4.0, 0.0, "CycloneBeam", 2.0)
		CYCF F 1 BRIGHT A_RailAttack (5, 3.0, true, "none", "00FF00", RGF_SILENT | RGF_NOPIERCING | RGF_FULLBRIGHT, 0, "CyclonePuff", 0.0, 0.0, 256, 1, 4.0, 0.0, "CycloneBeam", 2.0)
		CYCF GH 1 BRIGHT A_RailAttack (5, 3.0, false, "none", "00FF00", RGF_SILENT | RGF_NOPIERCING | RGF_FULLBRIGHT, 0, "CyclonePuff", 0.0, 0.0, 256, 1, 4.0, 0.0, "CycloneBeam", 2.0)
		CYCF I 1 BRIGHT A_RailAttack (5, 3.0, true, "none", "00FF00", RGF_SILENT | RGF_NOPIERCING | RGF_FULLBRIGHT, 0, "CyclonePuff", 0.0, 0.0, 256, 1, 4.0, 0.0, "CycloneBeam", 2.0)
		CYCF JK 1 BRIGHT A_RailAttack (5, 3.0, false, "none", "00FF00", RGF_SILENT | RGF_NOPIERCING | RGF_FULLBRIGHT, 0, "CyclonePuff", 0.0, 0.0, 256, 1, 4.0, 0.0, "CycloneBeam", 2.0)
		CYCF L 1 BRIGHT A_RailAttack (5, 3.0, true, "none", "00FF00", RGF_SILENT | RGF_NOPIERCING | RGF_FULLBRIGHT, 0, "CyclonePuff", 0.0, 0.0, 256, 1, 4.0, 0.0, "CycloneBeam", 2.0)
		CYCF A 0 A_Refire
		CYCF A 0 A_PlaySound("CycloneEnd", CHAN_AUTO)
		goto Ready
	Flash:
		CYFL ABCDEFGHIJKL 1 BRIGHT
		stop
		
	Reload:
		CYCL A 0
		goto ReloadCheck
			
	ReloadCheck:
		CYR1 A 0 A_JumpIfInventory("CycloneClip",50,2)
		CYR1 A 0 A_JumpIfInventory("CellAmmo",1,"ReloadStart")
		CYR1 A 0
		goto Ready

	ReloadStart:
		CYR1 A 0 A_StopSound(CHAN_WEAPON)
		CYR1 ABCEFGHIJK 1
		CYR1 L 1 A_PlaySound("CycloneClipOut", CHAN_WEAPON)
		CYR1 MNOPQRSTUVWXYZ 1
		CYR2 ABCDEFGHI 1
		goto ReloadAmmo

	ReloadAmmo:
		CYR2 J 0 A_TakeInventory("CellAmmo",1)
		CYR2 J 0 A_GiveInventory("CycloneClip",1)
		CYR2 J 0 A_JumpIfInventory("CycloneClip",50,"ReloadDone")
		CYR2 J 0 A_JumpIfInventory("CellAmmo",1,"ReloadAmmo")
		goto ReloadDone

	ReloadDone:
		CYR2 J 1 A_PlaySound("CycloneClipIn", CHAN_AUTO)
		CYR2 KLMNOPQRSTUVWXYZ 1
		CYR3 ABCDEFGHIJKLMN 1
		goto Ready
	}
}

actor CycloneBeam : BaseEffect
{
	Radius 1
	RenderStyle Add
	Alpha 1.0
	Scale 0.05
	States
	{
		Spawn:
			GEMB A 1 BRIGHT A_SetScale(ScaleX-0.01, ScaleY-0.01)
			GEMB A 0 BRIGHT A_FadeOut(0.32)
			loop
	}
}

actor CyclonePuff : BasePuff
{
	obituary "%k turned %o into an etch-a-sketch."
	RenderStyle Add
	Alpha 1.0
	Scale 0.3
	+PUFFONACTORS
	+ALWAYSPUFF
	+BLOODLESSIMPACT
	+FORCEPAIN
	states
	{
		Spawn:
			TPT1 ABCDEFGHIJKLMNO 1 BRIGHT
			stop
	}
}

actor Expander : Weapon
{
	Tag "Microwave Expander"
	Decal BulletChip
	obituary "%o was deep-fried by %k."
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "Smooth"
	Weapon.BobRangeX 1.25
    Weapon.BobRangeY 1.25
	weapon.selectionorder 1000
	weapon.kickback 100
	Weapon.AmmoType "CellAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	weapon.upsound "DukeWeaponSelect"
	States
	{
	Ready:
		XPAN AAAAAAAAAABBCCDDCCBB 1 A_WeaponReady
		LOOP
	Deselect:
		XPAN A 1 A_Lower
		LOOP
	Select:
		XPAN A 1 A_Raise
		loop
	Fire:
		XPAN E 0 BRIGHT A_PlayWeaponSound("ExpanderFire")
		XPAN E 2 BRIGHT A_FireBullets(2,2,1,20,"ExpanderPuff", FBF_USEAMMO | FBF_NORANDOM)
		XPAN FG 1 BRIGHT
		XPAN A 2
		XPAN A 0 A_Refire
		goto Ready
	}
}

ACTOR ExpanderPuff : BasePuff
{
    Scale 0.05
    Alpha 0.85
    Renderstyle Add
    obituary "%o was deep-fried by %k."
    +PUFFONACTORS
    DamageType Microwave
    States
    {
    Spawn:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("DeepFry")
      TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
      goto Crash
    Crash:
      EXPP AAAABBB 1 BRIGHT A_SetScale(ScaleX+0.1)
      goto StopScale
	StopScale:
	  EXPP BCCCCDDDD 1 BRIGHT
	  goto ScaleDown
	ScaleDown:
	  EXPP AAAABBBBCCCCDD 1 BRIGHT A_SetScale(ScaleX-0.05)
	  goto Kill
	Kill:
		EXPP A 0
		stop
    }
}