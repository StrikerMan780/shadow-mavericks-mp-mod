actor Needler : Weapon 10021
{
	radius 20
	height 16
	attacksound "NeedlerFire"
	inventory.pickupmessage "You got the needler!"
	weapon.selectionorder 200
	weapon.kickback 100
	weapon.ammotype1 "MechAmmo"
	weapon.ammotype2 "CellAmmo"
	weapon.ammouse 1
	weapon.ammouse2 1
	weapon.ammogive 50
	weapon.ammogive2 50
	Weapon.BobSpeed 1.25
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 1.25
    Weapon.BobRangeY 1.25
	decal BulletChip
	Scale 0.6
	+WEAPON.PRIMARY_USES_BOTH
	+WEAPON.NOAUTOAIM
	states
	{
		Ready:
			NDL1 A 1 A_WeaponReady
			loop
		Deselect:
			NDL1 A 1 A_Lower
			loop
		Select:
			NDL1 A 1 A_Raise
			loop
		Fire:
			NDL1 B 1 Offset (1, 32)
			NDL1 C 1 Offset (1, 33) A_GunFlash
			NDL1 DE 1 Offset (2, 32)
			NDL1 F 1 Offset (1, 33) A_GunFlash
			NDL1 GH 1 Offset (-1, 32)
			NDL1 I 1 Offset (1, 31) A_GunFlash
			NDL1 JK 1 Offset (-1, 33)
			NDL1 L 1 Offset (1, 32) A_GunFlash
			NDL1 MN 1 Offset (-1, 31)
			NDL1 O 0 A_Refire("Hold1")
			goto Ready
		Hold1:
			NDL1 O 1 Offset (1, 33) A_GunFlash
			NDL1 PQ 1 Offset (-1, 31)
			NDL1 R 1 Offset (-1, 32) A_GunFlash
			NDL1 ST 1 Offset (1, 32)
			NDL1 U 1 Offset (2, 31) A_GunFlash
			NDL1 VW 1 Offset (-1, 33)
			NDL1 X 1 Offset (2, 32) A_GunFlash
			NDL1 YZ 1 Offset (1, 31)
			NDL2 A 0 A_Refire("Hold2")
			goto Ready
		Hold2:
			NDL2 A 1 Offset (-1, 32) A_GunFlash
			NDL2 BC 1 Offset (1, 32)
			NDL2 D 1 Offset (1, 33) A_GunFlash
			NDL2 EF 1 Offset (-1, 32)
			NDL2 G 1 Offset (-2, 32) A_GunFlash
			NDL2 HI 1 Offset (-1, 33)
			NDL2 J 1 Offset (1, 33) A_GunFlash
			NDL2 KL 1 Offset (1, 32)
			NDL2 M 0 A_Refire("Hold3")
			goto Ready
		Hold3:
			NDL2 M 1 Offset (-1, 32) A_GunFlash
			NDL2 NO 1 Offset (1, 31)
			NDL2 P 1 Offset (-1, 32) A_GunFlash
			NDL2 QR 1 Offset (1, 33)
			NDL2 S 1 Offset (1, 32) A_GunFlash
			NDL2 TU 1 Offset (1, 31)
			NDL2 V 1 Offset (-1, 32) A_GunFlash
			NDL2 WX 1 Offset (-1, 31)
			NDL2 Y 0 A_Refire("Hold4")
			goto Ready
		Hold4:
			NDL2 Y 1 Offset (1, 32) A_GunFlash		
			NDL2 Z 1 Offset (1, 31)
			NDL3 A 1 Offset (-1, 33)
			NDL3 B 1 Offset (-1, 32) A_GunFlash
			NDL3 CD 1 Offset (1, 33)
			NDL3 E 1 Offset (-1, 32) A_GunFlash
			NDL3 FG 1 Offset (1, 31)
			NDL3 H 1 Offset (-1, 32) A_GunFlash
			NDL3 IJ 1 Offset (2, 32)	
			NDL1 A 1 Offset (1, 32)
			NDL1 A 0 A_Refire("Fire")
			goto Ready
		Flash:
			NDLF A 0 BRIGHT A_Light2
			NDLF A 0 BRIGHT A_Quake(2, 2, 0, 1, "")
			NDLF A 0 BRIGHT A_FireCustomMissile("NeedlerFlare", 0, false, 6, 4)
			NDLF A 0 BRIGHT A_FireCustomMissile("NeedlerCrystal", frandom(-2.0, 2.0), true, 6, 4, 0, frandom(-2.0, 2.0))
			NDLF A 0 BRIGHT A_Light0
			NDLF A 0 BRIGHT A_SetAngle(angle+frandom(-1.0,1.0))
			NDLF A 1 BRIGHT A_SetPitch(pitch-0.2)
			TNT1 A 1 BRIGHT A_SetPitch(pitch+0.2)
			TNT1 A 1 BRIGHT A_SetPitch(pitch-0.2)
			stop
		Spawn:
			NEEP A -1
			stop
	}
}

actor NeedlerFlare : BaseEffect
{
	RenderStyle Add
	Scale 0.25
	Damage 0
	Speed 45
	SpawnId 244
	PROJECTILE
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		NEFL A 0
		NEFL A 1 bright
		Stop
	}
}

Actor NeedlerTrail : BaseEffect
{
	RenderStyle Add
	Alpha 0.5
	Scale 1.0
	-NOTIMEFREEZE
	States
	{
	Spawn:
		BLUE A 2 BRIGHT
		stop
	}
}

actor NeedlerImpact : BaseEffect
{
	RenderStyle Add
	Alpha 0.5
	Scale 0.25
	States
	{
		Spawn:
			BLUE A 1 BRIGHT A_SetScale(scalex-0.01)
			BLUE A 0 BRIGHT A_JumpIf(scalex < 0.01, "Death")
			loop
		Death:
			TNT1 A 0
			stop
	}
}

actor NeedlerCrystal
{
  Decal Needler
  Projectile
  Height 5
  Radius 5
  Speed 78
  Damage (10)
  Scale 0.5
  RenderStyle Add
  Alpha 0.8
  obituary "%o was spiked by %k's needler."
  seesound "NeedlerFire"
  deathsound "NeedlerHit"
  +BLOODSPLATTER
  -BLOODLESSIMPACT
  +FORCEXYBILLBOARD
  States
  {
  Spawn:
	GLSS A 1 BRIGHT A_SpawnItemEX("NeedlerTrail", 0, 0, 0, MOMX/4.0, MOMY/4.0, MOMZ/4.0, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
	loop
  XDeath:
	TNT1 A 0
	TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
	goto Death
  Death:
	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx("NeedlerImpact", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
	TNT1 AA 0 A_CustomMissile ("NeedlerShard", 0, 0, Random(-180, 180), CMF_AIMDIRECTION | CMF_ABSOLUTEPITCH | CMF_TRACKOWNER, Random(-180, 180))
	TNT1 AA 1
	stop
  }
}

actor NeedlerShard
{
	Projectile
	Height 2
	Radius 2
	Scale 0.5
	Speed 5
	Damage (5)
	obituary "%o was cut to pieces by %k's needler shards."
	BounceType Hexen
	BounceCount 3
	BounceFactor 0.8
	RenderStyle Add
	Alpha 0.8
	ReactionTime 200
	+BLOODSPLATTER
	+FORCEXYBILLBOARD
	-BLOODLESSIMPACT
	-NOGRAVITY
	States
	{
		Spawn:
			BSHD ABCDEF 1 BRIGHT A_CountDown
			loop
		Death:
			BSHD F 2 BRIGHT A_FadeOut
			loop
	}
}

actor EnemyNeedlerCrystal : NeedlerCrystal
{
	decal Needler
	OBITUARY "%o was spiked by a Needler Troop."
	Speed 60
	Damage (4)
	+FORCEXYBILLBOARD
}