actor Needler : Weapon 10021
{
	obituary "%o was spiked by %k's needler."
	radius 20
	height 16
	attacksound "NeedlerFire"
	inventory.pickupmessage "You got the needler!"
	weapon.selectionorder 200
	weapon.kickback 100
	weapon.ammotype1 "MechAmmo"
	weapon.ammotype2 "CellAmmo"
	weapon.ammouse 1
	weapon.ammouse2 1
	weapon.ammogive 50
	weapon.ammogive2 50
	decal BulletChip
	Scale 0.6
	+WEAPON.PRIMARY_USES_BOTH
	states
	{
		Ready:
			NEED A 1 A_WeaponReady
			loop
		Deselect:
			NEED A 1 A_Lower
			loop
		Select:
			NEED A 1 A_Raise
			loop
		Fire:
			TNT1 A 0 radius_quake(2, 2, 0, 1, 0)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_FireCustomMissile("NeedlerFlare", 0, 0, 2, -8)
			NEED A 2 A_FireCustomMissile("NeedlerCrystal", random(-2, 2), 1, 2, 0)
			NEED B 2
			TNT1 A 0 radius_quake(2, 2, 0, 1, 0)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_FireCustomMissile("NeedlerFlare", 0, 0, 2, -8)
			NEED C 2 A_FireCustomMissile("NeedlerCrystal", random(-2, 2), 1, 2, 0)
			NEED D 2
			TNT1 A 0 radius_quake(2, 2, 0, 1, 0)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_FireCustomMissile("NeedlerFlare", 0, 0, 2, -8)
			NEED C 2 A_FireCustomMissile("NeedlerCrystal", random(-2, 2), 1, 2, 0)
			NEED B 2
			TNT1 A 0 radius_quake(2, 2, 0, 1, 0)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_FireCustomMissile("NeedlerFlare", 0, 0, 2, -8)
			NEED A 2 A_FireCustomMissile("NeedlerCrystal", random(-2, 2), 1, 2, 0)
			NEED C 2
			NEED A 0 A_Refire
			goto Ready
		Flash:
			NEEF A 2 BRIGHT A_Light2
			NEEF A 0 BRIGHT A_Light0
			stop
		Spawn:
			NEEP A -1
			stop
	}
}

actor NeedlerFlare : BaseEffect
{
	RenderStyle Add
	Scale 0.25
	Damage 0
	Speed 45
	SpawnId 244
	PROJECTILE
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		NEFL A 0
		NEFL A 1 bright
		Stop
	}
}

actor NeedlerCrystal
{
  Decal Needler
  Projectile
  Height 5
  Radius 5
  Speed 78
  Damage (12)
  Scale 0.5
  RenderStyle Add
  Alpha 0.8
  seesound "NeedlerFire"
  deathsound "NeedlerHit"
  +BLOODSPLATTER
  -BLOODLESSIMPACT
  +FORCEXYBILLBOARD
  States
  {
  Spawn:
  	GLSS A 0
  	GLSS A 0 ThrustThingZ(0, random(0, 8), random(0,1), 1)
	GLSS A 1 BRIGHT
	goto Spawn+2
  XDeath:
	TNT1 A 0
	TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
	goto Death
  Death:
	TNT1 A 0
	TNT1 A 0 A_Scream
	TNT1 AA 0 A_CustomMissile ("NeedlerShard", 0, 0, Random(-360, 360), 6, Random(-360, 360))
	TNT1 AA 1
	stop
  }
}

actor NeedlerShard
{
	Projectile
	Height 2
	Radius 2
	Scale 0.5
	Speed 3
	Damage (8)
	BounceCount 2
	BounceFactor 0.8
	RenderStyle Add
	Alpha 0.8
	ReactionTime 200
	+DOOMBOUNCE
	+BLOODSPLATTER
	+FORCEXYBILLBOARD
	-BLOODLESSIMPACT
	-NOGRAVITY
	States
	{
		Spawn:
			BSHD ABCDEF 1 BRIGHT A_CountDown
			loop
		Death:
			BSHD F 2 BRIGHT A_FadeOut
			loop
	}
}

actor EnemyNeedlerCrystal : NeedlerCrystal
{
	decal Needler
	OBITUARY "%o was spiked by a Needler Troop."
	Speed 60
	Damage (4)
	+FORCEXYBILLBOARD
}