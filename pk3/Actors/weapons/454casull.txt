ACTOR CasullClip : Ammo
{
	Inventory.MaxAmount 7 // size of the clip
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 7 //MUST be the same as Inventory.MaxAmmount
	Inventory.Icon "MAGICON1"
	+IGNORESKILL
}

actor 454Casull : ClericWeapon 18362
{
	Tag "Hellsing Arms .454 Casull"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 0.75
    Weapon.BobRangeY 1.25
	decal BulletChip
	obituary "%o was torn apart by %k's .454 Casull Rounds"
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Pickup a .454 Casull, Alucard's Signature Weapon!"
	attacksound "CasullFire"
	weapon.selectionorder 500
	weapon.kickback 190
	Weapon.AmmoType1 "CasullClip"
	Weapon.AmmoType2 "MechAmmo"
	Weapon.AmmoGive2 24
	Weapon.AmmoUse1 1
	+WEAPON.AMMO_OPTIONAL
	scale 0.2
	states
	{
	Spawn:
		CASP A -1
		loop
	Ready:
		CASL A 0 A_WeaponReady
		goto ReloadCheck
		CASL A 1 A_WeaponReady
		goto Ready+1
	Deselect:
		CASL A 1 A_Lower
		loop
	Select:
		CASL A 1 A_Raise
		loop
	Fire:
		CASL B 0 A_JumpIfNoAmmo("ReloadCheck")
		CASL B 0 A_GunFlash
		CASL B 0 A_Quake(6, 2, 0, 32, "")
		CASL B 0 BRIGHT A_FireBullets (2, 1.5, 1, 60, "SilverBulletPuff2", FBF_USEAMMO | FBF_NORANDOM)
		CASL B 0 BRIGHT A_FireCustomMissile("BulletCasing",-45+random(-8,8),0,1,9)
		CASL B 2 BRIGHT A_SetPitch(pitch-4)
		CASL CDEF 2 A_SetPitch(pitch+1)
		CASL GHI 2
		CASL J 0 A_ReFire
		CASL JKL 2
		goto Ready+1
	ReloadCheck:
		CASR A 0 A_JumpIfInventory("CasullClip",7,2)
		CASR A 0 A_JumpIfInventory("MechAmmo",2,"ReloadStart")
		CASR A 0
		goto Ready+1

	ReloadStart:
		CASR A 0 A_PlaySoundEx("9mmClipOut", "Auto", 0)
		CASR ABCDEFGHIJKLMNOP 2
		CASR Q 2 A_PlaySoundEx("9mmClipIn", "Auto", 0)
		goto ReloadAmmo

	ReloadAmmo:
		NULL A 0 A_TakeInventory("MechAmmo",2)
		NULL A 0 A_GiveInventory("CasullClip",1)
		NULL A 0 A_JumpIfInventory("CasullClip",7,"ReloadDone")
		NULL A 0 A_JumpIfInventory("MechAmmo",2,"ReloadAmmo")
		goto ReloadDone

	ReloadDone:
		CASR RST 1
		CASR UVWXYZ 2
		goto Ready+1
	AltFire:
		CASL A 0
		goto ReloadCheck
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 1 A_Light0
		stop
	}
}