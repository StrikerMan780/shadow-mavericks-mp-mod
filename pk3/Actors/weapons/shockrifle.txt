actor ShockRifle : Weapon 18004
{
	Tag "ASMD Shock Rifle"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 1.0
    Weapon.BobRangeY 1.0
	Decal ShockRifleScorch
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Picked up an ASMD Shock Rifle!"
	Obituary "%o was fatally enlightened by %k's shock beam"
	Weapon.AmmoType1 "CellAmmo"
	Weapon.AmmoType2 "CellAmmo"
	Weapon.AmmoGive 60
	Weapon.AmmoUse1	5
	Weapon.AmmoUse2 5
	Weapon.UpSound "ShockRifleRaise"
	AttackSound "ShockRifleFire"
	+WEAPON.NOAUTOAIM
	States
	{
		Spawn:
			LIGP A -1
			loop
		Ready:
			LIGH A 1 A_WeaponReady
			loop
		Select:
			LIGH A 1 A_Raise
			loop
		Deselect:
			LIGH A 1 A_Lower
			loop
		Fire:
			LIGH B 0 A_GunFlash
			LIGH B 0 A_RailAttack(45, 6, true, "none", "AA 00 FF", RGF_NOPIERCING | RGF_FULLBRIGHT, 0, "ShockRiflePuff", 0.0, 0.0, 8192, 16, 3.0, 1.0, "ShockBeamTrail")
			LIGH B 0 A_SetPitch(pitch-2.0)
			LIGH BCDEFGHIJKLMNOPQ 1 A_SetPitch(pitch+0.125)
			LIGH A 5
			LIGH A 0 A_Refire
			goto Ready
		Altfire:
			LIG2 B 0 A_FireCustomMissile("ShockRifleBall", 0, 1, 8, -16, 1)
			LIG2 B 0 A_SetPitch(pitch-3.0)
			LIG2 BCDEFGHIJKLMNOPQ 1 A_SetPitch(pitch+0.1875)
			LIG2 RS 1 A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY)
			LIG2 TUV 1 A_WeaponReady(WRF_NOBOB)
			LIGH A 0 A_Refire
			goto Ready
		Flash:
			LIGF A 2 BRIGHT A_Light2
			LIGF B 1 BRIGHT A_Light1
			TNT1 A 0 A_Light0
			stop
	}
}

actor ShockBeamTrail : BaseEffect
{
	Radius 2
	Height 4
	RenderStyle Add
	Alpha 1.0
	Scale 0.20
	States
	{
		Spawn:
			SRSP A 1 BRIGHT A_Fadeout(0.075)
			loop
	}
}

Actor ShockRiflePuff
{
	Height 2
	Scale 0.25
	Alpha 1.0
	RenderStyle Add
	Mass 0x7fffffff
	Decal ShockRifleScorch
	DamageType "ShockBeam"
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+PUFFGETSOWNER
	+FORCEXYBILLBOARD
	+ALWAYSPUFF
	+DONTBLAST
	-NONETID
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
		goto Crash
	Crash:
		TNT1 A 0 A_Blast(BF_DONTWARN, 128, 32, 4, "BlastEffect", "")
		TNT1 AAAAAA 0 A_SpawnItemEx("ShockRifleSpark", 0, 0, 0, frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.0, 2.0), random(-180, 180), SXF_CLIENTSIDE, 0)
		SRCF A 2 BRIGHT Light("ShockBeamHit")
		goto Fade
	Fade:
		SRCF A 1 BRIGHT Light("ShockBeamHit") A_FadeOut(0.1)
		loop
	}
}

actor ShockRifleBall
{
	Species "ShockRifle"
	Mass 0x7fffffff
	Radius 16
	Height 32
	Speed 20
	Damage (25)
	RenderStyle Add
	Obituary "%o was wasted by %k's shock core"
	Alpha 1.0
	Scale 0.25
	DamageType "Electric"
	BloodType "ShockBallFlare"
	PainChance "ShockBeam", 256
	Projectile
	-NOBLOCKMAP
	+BOSS
	+SHOOTABLE
	+THRUSPECIES
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+NOBLOODDECALS
	+NODAMAGE
	+NOTARGETSWITCH
	SeeSound "ShockRifleBall"
	States
	{
	Spawn:
		SRBA ABCDEFGHIJKLMNOP 1 BRIGHT Light("ShockBall")
		loop
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
		goto Death
	Pain.ShockBeam:
		TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
		TNT1 A 0 A_ChangeFlag("MISSILE", 0)
		TNT1 A 0 A_ChangeFlag("EXTREMEDEATH", 1)
		TNT1 A 0 A_SpawnItemEx("ShockComboExplosionShell", 0, 0, 16, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TNT1 A 0 A_SpawnItemEx("ShockComboExplosionRing", 0, 0, 16, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TNT1 A 0 A_SpawnItemEx("ShockComboExplosionRing2", 0, 0, 16, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TNT1 A 0 A_SpawnItemEx("ShockComboFlash", 0, 0, 16, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("ShockRifleSpark", 0, 0, 0, frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(0.0, 6.0), random(-180, 180), SXF_CLIENTSIDE, 0)
		TNT1 AAAA 0 A_SpawnItemEx("ShockBallFlare", 0, 0, 16, frandom(-2.0, 4.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(-180, 180), SXF_CLIENTSIDE, 0)
		TNT1 A 0 A_GiveToTarget("ShockComboFlasher", 1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_PlaySound("ShockCombo", CHAN_AUTO)
		TNT1 A 0 A_Explode(200, 188, true, false, 64)
		TNT1 A 0 A_Blast(BF_DONTWARN, 255, 188, 20, "BlastEffect", "")
		TNT1 A 0 A_Quake(3, 35, 0, 188, "") 
		goto DeathFade
	Death:
		TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
		TNT1 AAAA 0 A_SpawnItemEx("ShockBallFlare", 0, 0, 16, frandom(-0.25, 0.25), frandom(-0.25, 0.25), frandom(-0.25, 0.25), random(-180, 180), SXF_CLIENTSIDE, 0)
		TNT1 A 0 A_Explode(20, 64, true, false, 32)
		TNT1 A 0 A_Blast(BF_DONTWARN, 255, 64, 10, "BlastEffect", "")
		TNT1 A 0 A_PlaySound("ShockRifleExplosion", CHAN_AUTO)
		goto DeathFade
	DeathFade:
		SRBI A 1 BRIGHT A_Fadeout(0.05)
		loop
	}
}

actor ShockComboFlasher : CustomInventory
{
	inventory.maxamount 1
	-INVENTORY.INVBAR
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	States
	{
		Spawn:
			TNT1 A -1
			stop
		Use:
			TNT1 A 1 A_SetBlend("8e 76 d6", 0.5, 5)
			stop
	}
}

actor ShockComboExplosionShell : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	Scale 1.25
	States
	{
	Spawn:
		NULL ABCDEFGH 2 BRIGHT Light("ShockCombo")
		NULL IJKL 2 BRIGHT Light("ShockCombo") A_FadeOut(0.18)
		stop
	}
}

actor ShockComboExplosionRing : BaseEffect
{
	RenderStyle Add
	Alpha 0.4
	Scale 1.25
	States
	{
	Spawn:
		NULL ABC 1 BRIGHT
		NULL DEFGH 1 BRIGHT A_FadeOut(0.07)
		stop
	}
}

actor ShockComboExplosionRing2
{
	Height 1
	Radius 1
	Speed 0
	RenderStyle Add
	Alpha 1.0
	Scale 0.025
	+NOGRAVITY
	+NOBLOCKMAP
	+NOCLIP
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		SRCR A 1 BRIGHT A_SetScale(scalex+0.1)
		SRCR A 0 BRIGHT A_FadeOut(0.07)
		loop
	}
}

actor ShockComboFlash : BaseEffect
{
	VSpeed 0
	RenderStyle Add
	Alpha 1.0
	Scale 1.75
	States
	{
	Spawn:
		SRCF A 2 BRIGHT
		goto Fade
	Fade:
		SRCF A 1 BRIGHT A_FadeOut(0.07)
		loop
	}
}

actor ShockRifleSpark : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	Scale 0.1
	Gravity 0.1
	-NOGRAVITY
	-NOINTERACTION
	+HEXENBOUNCE
	+MISSILE
	States
	{
		Spawn:
			SRSP A 1 BRIGHT A_Fadeout(0.025)
			loop
		Death:
			TNT1 AA 1
			stop
	}
}

actor ShockBallFlare : BaseEffect
{
	VSpeed 0
	RenderStyle Add
	Alpha 1.0
	Scale 0.25
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256, "Flare1", "Flare2", "Flare3", "Flare4")
		goto Flare1
	Flare1:
		SRBF A 1 BRIGHT A_FadeOut(0.05)
		loop
	Flare2:
		SRBF B 1 BRIGHT A_FadeOut(0.05)
		loop
	Flare3:
		SRBF C 1 BRIGHT A_FadeOut(0.05)
		loop
	Flare4:
		SRBF D 1 BRIGHT A_FadeOut(0.05)
		loop
	}
}