actor WookieBowcaster : Weapon 13321
{
	//$Category SMMP Weapons
	//$Title Wookie Bowcaster
	//$Color 17
	Tag "Wookie Bowcaster"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 1.25
    Weapon.BobRangeY 1.0
	Scale 0.86
	radius 20
	height 16
	inventory.pickupmessage "You got the Wookie Bowcaster! RRRAAAR!"
	weapon.selectionorder 9999
	weapon.kickback 100
	Weapon.Ammotype1 "FusionAmmo"
	Weapon.AmmoUse1 3
	Weapon.Ammotype2 "FusionAmmo"
	Weapon.AmmoUse2 3
	weapon.ammogive 40
	States
	{
		Ready:
			ECRO A 1 A_WeaponReady
			loop
		Select:
			ECRO A 1 A_Raise
			loop
		Deselect:
			ECRO A 1 A_Lower
			loop
		Fire:
			ECRO A 0 A_PlayWeaponSound("weapons/bowcharge")
			ECRO A 0 A_GunFlash("ChargeFlash")
			goto Charge
			
		Charge:
			ECRO A 0 A_GiveInventory("BowCharge", 1)
			ECRO A 0 A_Quake(1, 2, 0, 1, "")
			ECRO A 0 A_JumpIfInventory("BowFlashCheck", 1, "MakeFlash")
			goto ChargeJump
		MakeFlash:
			ECRO A 0 A_GunFlash("HoldFlash")
			goto ChargeJump
		ChargeJump:
			ECRO A 0 A_Jump(256, "Charge1", "Charge2", "Charge3", "Charge4", "Charge5", "Charge6")
			goto Charge1
		Charge1:
			ECRO A 1 Offset(1, 33)
			ECRO A 0 A_Refire("Charge")
			goto Check
		Charge2:
			ECRO A 1 Offset(-1, 32)
			ECRO A 0 A_Refire("Charge")
			goto Check
		Charge3:
			ECRO A 1 Offset(-1, 34)
			ECRO A 0 A_Refire("Charge")
			goto Check
		Charge4:
			ECRO A 1 Offset(1, 32)
			ECRO A 0 A_Refire("Charge")
			goto Check
		Charge5:
			ECRO A 1 Offset(1, 34)
			ECRO A 0 A_Refire("Charge")
			goto Check
		Charge6:
			ECRO A 1 Offset(-1, 33)
			ECRO A 0 A_Refire("Charge")
			goto Check
			
		ChargeFlash:
			ECRF ABC 6 BRIGHT
			ECRF D 3 BRIGHT
			ECRF E 3 BRIGHT A_GiveInventory("BowFlashCheck", 1)
			stop
		
		HoldFlash:
			ECRF E 0 BRIGHT A_Jump(256, "HoldFlash1", "HoldFlash2", "HoldFlash3", "HoldFlash4", "HoldFlash5", "HoldFlash6", "HoldFlash7", "HoldFlash8", "HoldFlash9", "HoldFlash10", "HoldFlash11", "HoldFlash12")
			stop
		HoldFlash1:
			ECRF E 2 BRIGHT
			stop
		HoldFlash2:
			ECRF F 2 BRIGHT
			stop
		HoldFlash3:
			ECRF G 2 BRIGHT
			stop
		HoldFlash4:
			ECRF H 2 BRIGHT
			stop
		HoldFlash5:
			ECRF I 2 BRIGHT
			stop
		HoldFlash6:
			ECRF J 2 BRIGHT
			stop
		HoldFlash7:
			ECRF K 2 BRIGHT
			stop
		HoldFlash8:
			ECRF L 2 BRIGHT
			stop
		HoldFlash9:
			ECRF M 2 BRIGHT
			stop
		HoldFlash10:
			ECRF N 2 BRIGHT
			stop
		HoldFlash11:
			ECRF O 2 BRIGHT
			stop
		HoldFlash12:
			ECRF P 2 BRIGHT
			stop
			
		Check:
			ECRO A 0 A_JumpIfInventory("BowCharge", 100, "Fire5")
			ECRO A 0 A_JumpIfInventory("BowCharge", 75, "Fire4")
			ECRO A 0 A_JumpIfInventory("BowCharge", 50, "Fire3")
			ECRO A 0 A_JumpIfInventory("BowCharge", 25, "Fire2")
			goto Fire1
			
		Fire1:
			ECRO B 0 A_PlayWeaponSound("weapons/bowshoot")
			ECRO B 0 A_FireCustomMissile("BowShot",0,1,6,0)
			goto FireAnim
		Fire2:
			ECRO B 0 A_PlayWeaponSound("weapons/bowshoot")
			ECRO B 0 A_FireCustomMissile("BowShot",-1,1,6,0)
			ECRO B 0 A_FireCustomMissile("BowShot",1,1,6,0)
			goto FireAnim
		Fire3:
			ECRO B 0 A_PlayWeaponSound("weapons/bowshoot")
			ECRO B 0 A_FireCustomMissile("BowShot",0,1,6,0)
			ECRO B 0 A_FireCustomMissile("BowShot",-3,1,6,0)
			ECRO B 0 A_FireCustomMissile("BowShot",3,1,6,0)
			goto FireAnim
		Fire4:
			ECRO B 0 A_PlayWeaponSound("weapons/bowshoot")
			ECRO B 0 A_FireCustomMissile("BowShot",-1,1,6,0)
			ECRO B 0 A_FireCustomMissile("BowShot",1,1,6,0)
			ECRO B 0 A_FireCustomMissile("BowShot",-3,1,6,0)
			ECRO B 0 A_FireCustomMissile("BowShot",3,1,6,0)
			goto FireAnim
		Fire5:
			ECRO B 0 A_PlayWeaponSound("weapons/bowshoot")
			ECRO B 0 A_FireCustomMissile("BowShot",0,1,6,0)
			ECRO B 0 A_FireCustomMissile("BowShot",-3,1,6,0)
			ECRO B 0 A_FireCustomMissile("BowShot",3,1,6,0)
			ECRO B 0 A_FireCustomMissile("BowShot",-6,1,6,0)
			ECRO B 0 A_FireCustomMissile("BowShot",6,1,6,0)
			goto FireAnim
			
		AltFire:
			ECRO B 0 A_PlayWeaponSound("weapons/bowshootalt")
			ECRO B 0 A_FireCustomMissile("BowShotBounce",0,1,6,0)
			goto FireAnim
			
		FireAnim:
			ECRO B 0 Bright A_GunFlash
			ECRO B 2 Bright A_SetPitch(pitch-1.5)
			ECRO CDEFGH 2 A_SetPitch(pitch+0.25)
			ECRO A 0 A_TakeInventory("BowFlashCheck", 999)
			ECRO A 12 A_TakeInventory("BowCharge", 999)
			ECRO A 0 A_Refire
			goto Ready
		
		Flash:
			ECRF DCBA 1 BRIGHT
			stop
			
		Spawn:
			ECRP A -1
			loop
	}
}

ACTOR BowFlashCheck : Inventory
{
	Inventory.MaxAmount 1
	Inventory.Icon STTPRCNT
	-INVBAR
}

ACTOR BowCharge : Inventory
{
	Inventory.MaxAmount 100
	Inventory.Icon STTPRCNT
	-INVBAR
}

actor BowExplosion : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	Scale 0.5
	States
	{
		Spawn:
			FX03 BCDEFG 2 BRIGHT
			stop
	}
}

actor BowBounce : BowExplosion
{
	Scale 0.35
	States
	{
		Spawn:
			FX03 BCDEFG 1 BRIGHT A_FadeOut(0.14)
			stop
	}
}

actor BowShot
{
	Radius 11
	Height 8
	Projectile
	Speed 70
	Obituary "%k hunted down %o chewbacca-style."
	Damage (100)
	Decal CrossbowScorch
	DamageType "Bowcaster"
	RenderStyle Add
	Alpha 1.0
	DeathSound "weapons/bowhit"
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			FX03 A 1 BRIGHT Light("BOWCASTERSHOT")
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death
		Death:
			TNT1 A 0 A_SpawnItemEx("BowExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			TNT1 AA 4
			stop
	}
}

actor BowShotBounce : BowShot
{
	Obituary "%o was bolted by %k's bowcaster."
	Speed 60
	BounceType Doom
	BounceFactor 1.0
	WallBounceFactor 1.0
	BounceCount 3
	BounceSound "weapons/bowbounce"
	SeeSound "weapons/bowshootalt"
	+USEBOUNCESTATE
	States
	{
		Bounce:
			FX03 A 1 BRIGHT Light("BOWCASTERSHOT") A_SpawnItemEx("BowBounce", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			goto Spawn
	}
}