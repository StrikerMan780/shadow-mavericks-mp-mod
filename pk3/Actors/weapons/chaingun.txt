ACTOR ChaingunShotCount : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 3
	-INVBAR
}

actor SM_Chaingun : Weapon
{
	Tag "T-23A Explosive Minigun"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 1.25
    Weapon.BobRangeY 1.0
	obituary "%o was turned into swiss-cheese by %k's gatling gun."
	weapon.selectionorder 700
	weapon.kickback 100
	weapon.ammotype "MechAmmo"
	weapon.ammouse 1
	weapon.ammogive 0	
	states
	{
	Ready:
		CHGG A 1 A_WeaponReady
		loop
	Deselect:
		CHGG A 1 A_Lower
		loop
	Select:
		CHGG A 1 A_Raise
		loop
	Hold:
		CHGG A 0
	Fire:
		CHGG B 0 A_JumpIfInventory("ChaingunShotCount", 3, "Fire4")
		CHGG B 0 A_JumpIfInventory("ChaingunShotCount", 2, "Fire3")
		CHGG B 0 A_JumpIfInventory("ChaingunShotCount", 1, "Fire2")
		CHGG B 2 BRIGHT A_GunFlash
		CHGG C 2
		CHGG D 2 BRIGHT A_GunFlash
		CHGG E 2 A_GiveInventory("ChaingunShotCount", 1)
		CHGG F 0 A_Refire
		CHGG F 0 A_TakeInventory("ChaingunShotCount", 3)
		goto Ready
	Fire2:
		CHGG F 2 BRIGHT A_GunFlash
		CHGG G 2
		CHGG H 2 BRIGHT A_GunFlash
		CHGG I 2 A_GiveInventory("ChaingunShotCount", 1)
		CHGG J 0 A_Refire
		CHGG J 0 A_TakeInventory("ChaingunShotCount", 3)
		goto Ready
	Fire3:
		CHGG J 2 BRIGHT A_GunFlash
		CHGG K 2
		CHGG L 2 BRIGHT A_GunFlash
		CHGG M 2 A_GiveInventory("ChaingunShotCount", 1)
		CHGG N 0 A_Refire
		CHGG N 0 A_TakeInventory("ChaingunShotCount", 3)
		goto Ready
	Fire4:
		CHGG N 2 BRIGHT A_GunFlash
		CHGG O 2
		CHGG P 2 BRIGHT A_GunFlash
		CHGG A 2 A_TakeInventory("ChaingunShotCount", 3)
		CHGG B 0 A_ReFire
		goto Ready
	Flash:
		TNT1 A 0 BRIGHT A_PlayWeaponSound("weapons/chngun")
		TNT1 A 0 BRIGHT A_FireBullets (5, 1.5, 1, 6, "SuperChaingunHit", FBF_USEAMMO | FBF_NORANDOM)
		TNT1 A 0 BRIGHT A_FireCustomMissile("BulletCasingSpawnerRight",-45+random(-8,8),0,12,3)
		TNT1 A 0 BRIGHT A_Light2
		TNT1 A 0 BRIGHT radius_quake(2, 2, 0, 1, 0)
		TNT1 A 0 BRIGHT A_CheckForReload(2, "Flash1")
		goto Flash2
	Flash1:
		CHGF A 2 BRIGHT
		TNT1 A 1 BRIGHT A_Light0
		stop
	Flash2:
		CHGF B 2 BRIGHT
		TNT1 A 0 BRIGHT A_ResetReloadCounter
		TNT1 A 1 BRIGHT A_Light0
		stop
	NoFlash:
		TNT1 A 1
		stop
	}
}

Actor SuperChaingunHit : BasePuff
{
	Scale 0.4
	Alpha 0.9
	RenderStyle Add
	Mass 100
	Decal BulletChip
	+PUFFONACTORS
	+FORCERADIUSDMG
	+ISMONSTER
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
		goto Crash
	Crash:
		HBEX A 0 A_SpawnItemEx("ChaingunTracerSpawner", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
		HBEX A 0 A_Explode(5, 24, 0, false, 24)
		HBEX ABCDEFGHIJKLMNOP 1 BRIGHT
		stop
	}
}

actor BCF-2Repeaters : Weapon
{
	Tag "BCF-2 Repeaters"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "Smooth"
	Weapon.BobRangeX 1.25
    Weapon.BobRangeY 0.64
	obituary "%o was corroded by %k's BCF-2 Repeaters."
	weapon.selectionorder 700
	weapon.kickback 100
	weapon.ammotype "MechAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	attacksound "TailsRepeater"
	States
	{
		Ready:
			DAUT A 1 A_WeaponReady
			loop
		Deselect:
			DAUT A 1 A_Lower
			loop
		Select:
			DAUT A 1 A_Raise
			loop
		Fire:
			DAUT B 0 A_GunFlash("Flash1")
			DAUT B 0 A_FireCustomMissile("RepeaterCasingSpawnerRight",-45+random(-8,8),0,6,7)
			DAUT B 0 BRIGHT A_FireBullets (4.5, 1.5, 1, 10, "RepeaterPuff", FBF_USEAMMO | FBF_NORANDOM)
			DAUT B 1 BRIGHT A_SetPitch(pitch-0.25)
			DAUT C 2 A_SetPitch(pitch+0.25)
			DAUT A 1
			DAUT D 0 A_GunFlash("Flash2")
			DAUT D 0 A_FireCustomMissile("RepeaterCasingSpawnerLeft",45+random(-8,8),0,-6,7)
			DAUT D 0 A_FireBullets (4.5, 1.5, 1, 10, "RepeaterPuffLeft", FBF_USEAMMO | FBF_NORANDOM)
			DAUT D 1 BRIGHT A_SetPitch(pitch-0.25)
			DAUT E 2 A_SetPitch(pitch+0.25)
			DAUT A 0 A_ReFire
			goto Ready
		Flash1:
			DAUF A 2 BRIGHT
			DAUF B 1 BRIGHT
			stop
		Flash2:
			DAUF C 2 BRIGHT
			DAUF D 1 BRIGHT
			stop
	}
}

actor RepeaterPuff
{
	Radius 11
	Height 2
	Scale 0.4
	Alpha 1
	RenderStyle Add
	Mass 100
	Decal RepeaterScorch
	AttackSound "RepeaterHit"
	SeeSound "Empty"
	Obituary "%o was eaten away by %k's BCF-2 Repeaters!"
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+PUFFGETSOWNER
	+FORCEXYBILLBOARD
	+ISMONSTER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Crash
		Crash:
			RPUF A 0 BRIGHT 
			RPUF A 0 BRIGHT A_SpawnItemEx("RepeaterTracerSpawnerRight", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			goto Spark
		Spark:
			RPUF AAAAAA 0 BRIGHT A_SpawnItemEx("RepeaterSpark", 0, 0, 0, Random(-2,2), Random(-2,2), Random(2,8), 0, SXF_CLIENTSIDE, 16)
			RPUF A 0 BRIGHT A_Jump(128, "Spawn2")
			goto Spawn1
		Spawn1:
			RPUF AB 1 BRIGHT Light("RepeaterPuffLight")
			stop
		Spawn2:
			RPUF CD 1 BRIGHT Light("RepeaterPuffLight")
			stop
	}
}

actor RepeaterPuffLeft : RepeaterPuff
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Crash
		Crash:
			RPUF A 0 BRIGHT 
			RPUF A 0 BRIGHT A_SpawnItemEx("RepeaterTracerSpawnerLeft", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			goto Spark
	}
}

actor RepeaterSpark
{
	Height 1
	Radius 1
	Projectile
	-NOGRAVITY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	RenderStyle Add
	Scale 0.08
	Alpha 0.9
	Speed 0
	Damage 0
	States
	{
		Spawn:
			RPUF ABCDABCD 1 BRIGHT
			RPUF ABCD 1 BRIGHT A_FadeOut(0.1)
			goto Spawn+8

		Death:
			TNT1 A 1
			stop
	}
}

actor RipperChaingun : Weapon
{
	Tag "EDF ''Ripper'' Chaingun"
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "Smooth"
	Weapon.BobRangeX 1.25
    Weapon.BobRangeY 1.25
	obituary "%o was hosed down by %k's ripper."
	weapon.selectionorder 700
	weapon.kickback 100
	weapon.ammotype "MechAmmo"
	weapon.ammouse 1
	weapon.ammogive 0
	weapon.upsound "DukeWeaponSelect"
	attacksound "DukeRipper"
	states
	{
		Ready:
			RIPG A 1 A_WeaponReady
			loop
		Select:
			RIPG A 1 A_Raise
			loop
		Deselect:
			RIPG A 1 A_Lower
			loop
		Fire:
			RIPG B 1 BRIGHT A_GunFlash
			RIPG C 1 BRIGHT
			RIPG D 1 BRIGHT 
			RIPG E 1 BRIGHT A_GunFlash
			RIPG F 1 BRIGHT
			RIPG G 1 BRIGHT
			RIPG H 1 BRIGHT A_GunFlash
			RIPG I 1 BRIGHT
			RIPG A 1 BRIGHT
			RIPG A 0 A_Refire
			goto Ready
		Flash:
			TNT1 A 0 A_FireBullets(5, 2, -1, 9, "NewBulletPuff", FBF_USEAMMO | FBF_NORANDOM)
			TNT1 A 0 A_FireCustomMissile("BulletCasingSpawnerRight",-45+random(-8,8),0,5,4)
			TNT1 A 0 radius_quake(1, 2, 0, 1, 0)
			TNT1 A 2 A_Light2
			TNT1 A 1 A_Light1
			goto LightDone
	}
}