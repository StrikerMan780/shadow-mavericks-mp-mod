actor Thanatos : Weapon 24304
{
	Inventory.PickupMessage "Discovered the unholy GrimReaper XI!"
	Inventory.RestrictedTo "Marine"
	weapon.selectionorder 9999
	weapon.kickback 100
	Weapon.AmmoType1 "DemonicPower"
	Weapon.AmmoType2 "SpreadAmmo"
	Weapon.AmmoGive1 20
	Weapon.AmmoGive2 12
	Weapon.AmmoUse1 5
	Weapon.AmmoUse2 0
	Weapon.BobSpeed 1.5
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobRangeX 0.75
    Weapon.BobRangeY 1.25
	Tag "Thanatos ''GrimReaper XI'' Shotgun"
	XScale 0.075
	YScale 0.07
	+FORCEXYBILLBOARD
	+WEAPON.NOAUTOAIM
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	states
	{
	Spawn:
		THAP A -1
		loop
	Ready:
		THS1 A 0 A_JumpIfInventory("Thanatos_Blade_Mode", 1, "BladeReady")
		THS1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		THS2 ABCDEFGHIJKLMNOPQRSTUVWXY 1 A_WeaponReady
		goto Ready+1 
	Deselect:
		THS1 A 0 A_JumpIfInventory("Thanatos_Blade_Mode", 1, "BladeDeselect")
		THS1 A 0 A_StopSound(7)
		THS1 A 1 A_Lower
		goto Deselect+1
	Select:
		THS1 A 0 A_JumpIfInventory("Thanatos_Blade_Mode", 1, "BladeSelect")
		THS1 A 1 A_Raise
		goto Select+1
	Empty: 
		THAN A 0 A_PlaySound("WeaponEmpty", CHAN_AUTO)
		THAN AAAAAAAAAA 1 A_WeaponReady(WRF_NOFIRE)
		goto Ready
	Fire:
		THAN B 0 A_JumpIfInventory("Thanatos_Blade_Mode", 1, "BladeCheckFire")
		THAN B 0 A_JumpIfInventory("SpreadAmmo", 1, "CheckFire")
		goto Empty
	CheckFire:
		THAN B 0 A_JumpIfNoAmmo("UnpoweredFire")
		goto PoweredFire
	PoweredFire:
		THAN B 0 A_GunFlash("PoweredFlash")
		THAN B 0 A_PlaySound("BishopProjectile", CHAN_AUTO)
		THAN BB 0 A_FireCustomMissile("ThanatosShot", frandom(-3.0,3.0), 0, 10, 0, 0, frandom(-1.0, 1.0))
		THAN B 0 A_FireCustomMissile("ThanatosShot", frandom(-3.0,3.0), 1, 10, 0, 0, frandom(-1.0, 1.0))
		goto FireAnim
	UnpoweredFire:
		THAN B 0 A_GunFlash("NormalFlash")
		THAN BB 0 A_FireCustomMissile("ThanatosShot_NoPower", frandom(-3.0,3.0), 0, 10, 0, 0, frandom(-1.0, 1.0))
		THAN B 0 A_FireCustomMissile("ThanatosShot_NoPower", frandom(-3.0,3.0), 0, 10, 0, 0, frandom(-1.0, 1.0))
		goto FireAnim
	FireAnim:
		THAN B 0 A_AlertMonsters
		THAN B 0 A_Quake (3, 5, 0, 64, "") 
		THAN B 0 A_PlaySound("ThanatosFire", CHAN_WEAPON)
		THAN B 0 A_TakeInventory("SpreadAmmo", 1)
		THAN B 1 Bright A_SetPitch(pitch-3.0)
		THAN CDEFGHIJKL 1 A_SetPitch(pitch+0.3)
		THAN MNOP 1
		THAN Q 1 A_PlaySound("ThanatosPump", CHAN_AUTO)
		THAN RSTUVWXYZ 1
		THA2 ABCDEFGHI 1
		THAN A 0 A_ReFire
		goto Ready
		
	AltFire:
		THAN A 0 A_JumpIfInventory("Thanatos_Blade_Mode", 1, "SwitchToShotgun")
		goto SwitchToBlades
			
	SwitchToBlades:
		TCH1 A 0 A_GiveInventory("Thanatos_Blade_Mode", 1)
		TCH1 A 0 A_Print("Cursed Blades")
		TCH1 A 0 A_PlaySound("ThanatosBladeOn", CHAN_AUTO)
		TCH1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		TCH2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		TCH3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		TCH3 Z 0
		goto BladeReady
		
	SwitchToShotgun:
		TCH4 A 0 A_TakeInventory("Thanatos_Blade_Mode", 1)
		TCH4 A 0 A_Print("Shotgun")
		TCH4 A 0 A_StopSound(7)
		TCH4 A 0 A_PlaySound("ThanatosBladeOff", CHAN_AUTO)
		TCH4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		TCH5 ABCDEFGHIJKLMNOPQRSTUVWXY 1
		TCH5 Y 0
		goto Ready
	
	BladeDeselect:
		THB1 A 0 A_StopSound(7)
		THB1 A 1 A_Lower
		loop
	BladeSelect:
		THB1 A 1 A_Raise
		loop
	BladeReady:
		THB1 A 0 A_Playsound("ThanatosBladeIdle", 7, 1.0, 1)
		THB1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_WeaponReady
		THB2 ABCDEFGHIJKLMNOPQRSTUVWXY 1 A_WeaponReady
		loop 
	BladeCheckFire:
		THAN B 0 A_JumpIfNoAmmo("SwitchToShotgun")
		goto BladeFire
	BladeFire:
		TBF1 A 0 A_StopSound(7)
		TBF1 A 0 A_PlaySound("ThanatosBladeFire", CHAN_WEAPON)
		TBF1 A 0 A_FireCustomMissile("ThanatosBlade", 0, 1, 10, 2, 0, 0)
		TBF1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		TBF2 ABCDE 1
		THB1 A 0 A_Refire
		goto BladeReady
	
	NormalFlash:
		TFL1 A 1 bright A_Light2
		TFL1 B 1 bright A_Light1
		TNT1 A 0 bright A_Light0
		stop
		
	PoweredFlash:
		TFL2 A 1 bright A_Light2
		TFL2 B 1 bright A_Light1
		TNT1 A 0 bright A_Light0
		stop
	}
}

actor Thanatos_Blade_Mode : BaseCheck
{
}

actor ThanatosExplode : BaseEffect
{
	Scale 0.5
	RenderStyle Add
	Alpha 1.0
	States
	{
		Spawn:
			BPFX ABCDEFGHIJKLMNOPQR 1 Bright
			stop
	}
}

actor ThanatosShot : FastProjectile
{
	Projectile
	Obituary "%o was smitten by %k's GrimReaper XI!"
	Radius 4
	Height 4
	Speed 100
	Damage (40)
	Scale 0.32
	RenderStyle Add
	DamageType "Aura"
	Alpha 1.0
	States
	{
		Spawn:
			BPFX A 1 bright
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			TNT1 A 0 A_PlaySound("BishopMissileExplode", CHAN_AUTO)
			TNT1 A 0 A_SpawnItemEx("ThanatosBurn", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS, 0)
			TNT1 A 1 A_SpawnItemEx("ThanatosExplode", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			stop
		Death:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("BishopMissileExplode", CHAN_AUTO)
			TNT1 A 1 A_SpawnItemEx("ThanatosExplode", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			TNT1 A 1
			stop
	}
}

actor ThanatosShot_NoPower : FastProjectile
{
	Decal "BulletChip"
	Projectile
	Obituary "%o was slugged by %k's GrimReaper XI!"
	Radius 2
	Height 2
	Speed 150
	Damage (40)
	RenderStyle Add
	Alpha 1.0
	+BLOODSPLATTER
	States
	{
		Spawn:
			PUFF A 1 bright
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			stop
		Crash:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death
		Death:
			TNT1 A 0
			TNT1 A 1 A_SpawnItemEx("NewBulletPuff_NoMarker", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			TNT1 A 1
			stop
	}
}

actor ThanatosBurn
{
	Obituary "%o's soul was reaped by %k!"
	Projectile
	Radius 2
	Height 2
	Damage (0)
	Speed 0
	DamageType "Aura"
	ReactionTime 60
	+SEEKERMISSILE
	+NOCLIP
	+FORCERADIUSDMG
	+BLOODLESSIMPACT
	+PAINLESS
	+NODAMAGETHRUST
	+PIERCEARMOR
	States
	{
 	Spawn:
		TNT1 A 0
		TNT1 A 0 Bright A_SeekerMissile (180, 180, SMF_LOOK, 256, 1) 
		goto Burn
	Burn:
		TNT1 A 1 Bright A_Warp(AAPTR_TRACER, 0, 0, 32, 0, WARPF_ABSOLUTEOFFSET)
		TNT1 A 0 Bright A_Explode(1, 64, 0, false, 64)
		TNT1 A 0 Bright A_SpawnItemEx("ThanatosAura", frandom(-32.0, 32.0), frandom(-32.0, 32.0), frandom(-32.0, 32.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 64)
		TNT1 A 0 Bright A_CountDown
		loop
	Death:
		TNT1 AA 1
		stop
	}
}

actor ThanatosBurn2 : ThanatosBurn
{
	Obituary "%o's soul was reaped by %k!"
	States
	{
 	Spawn:
		TNT1 A 0
		goto Burn
	Burn:
		TNT1 A 1 Bright A_Explode(1, 64, 0, false, 64)
		TNT1 A 0 Bright A_SpawnItemEx("ThanatosAura", frandom(-32.0, 32.0), frandom(-32.0, 32.0), frandom(-32.0, 32.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 64)
		TNT1 A 0 Bright A_CountDown
		loop
	}
}

actor ThanatosAura : BaseEffect
{
	Scale 0.3
	RenderStyle Add
	Alpha 1.0
	States
	{
		Spawn:
			AUR1 A 0
			AUR1 A 0 A_Jump(256, "Fire1", "Fire2", "Fire3", "Fire4", "Fire5", "Fire6", "Fire7", "Fire8", "Fire9", "Fire10")
			goto Fire1
		Fire1:
			AUR1 ABCDEFGHIJKLMNOPQR 1 BRIGHT
			stop
		Fire2:
			AUR2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
			AU22 AB 1 BRIGHT
			stop
		Fire3:
			AUR3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
			stop
		Fire4:
			AUR4 ABCDEFGHIJKLMNOPQRST 1 BRIGHT
			stop
		Fire5:
			AUR5 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
			AU52 AB 1 BRIGHT
			stop
		Fire6:
			AUR6 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
			AU62 AB 1 BRIGHT
			stop
		Fire7:
			AUR7 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
			AU72 AB 1 BRIGHT
			stop
		Fire8:
			AUR8 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
			AU82 ABCD 1 BRIGHT
			stop
		Fire9:
			AUR9 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
			AU92 ABCD 1 BRIGHT
			stop
		Fire10:
			AUR0 ABCDEFGHIJKLMNO 1 BRIGHT
			stop
	}
}

actor ThanatosBlade
{
	Projectile
	Obituary "%o was banished by %k's Thanatos Blades!"
	Radius 4
	Height 4
	Speed 50
	Damage (1)
	DamageType "Aura"
	BounceType Hexen
	BounceCount 4
	BounceFactor 1.0
	BounceSound "ThanatosBladeBounce"
	ReactionTime 40
	+SEEKERMISSILE
	+FORCERADIUSDMG
	+BLOODSPLATTER
	States
	{
		Spawn:
			PUFF A 1 A_SpawnItemEx("ThanatosAura", 0, 0, 0, 0, 0, frandom(0.5, 1.0), 0, SXF_CLIENTSIDE, 200)
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			TNT1 A 0 A_PlaySound("StickyLatchedOn", CHAN_AUTO)
			TNT1 A 0 A_PlaySound("LinkSwordHit", CHAN_AUTO)
			TNT1 A 0 A_SeekerMissile (180, 180, SMF_LOOK, 256, 1) 
			goto Latched
		Latched:
			PUFF A 1 A_Warp(AAPTR_TRACER, 0, 0, 32, 0, WARPF_ABSOLUTEOFFSET)
			TNT1 A 0 Bright A_CountDown
			loop
		Death:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("ThanatosBladeExplode", CHAN_AUTO)
			TNT1 A 0 A_Explode(256, 80, 0, false, 32)
			TNT1 A 0 A_SpawnItemEx("ThanatosBurn2", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS, 0)
			TNT1 A 1 A_SpawnItemEx("ThanatosBladeExplode", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			TNT1 A 1
			stop
	}
}

actor ThanatosBladeExplode : BaseEffect
{
	XScale 0.65
	YScale 0.6
	RenderStyle Add
	Alpha 1.0
	States
	{
		Spawn:
			THEX ABCDEFGHIJKL 2 Bright
			stop
	}
}