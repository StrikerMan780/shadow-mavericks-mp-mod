actor ProtonGrenadeLauncher : Weapon 5011
{
	Tag "FG-32 ''Vortex'' Launcher"
	radius 20
	height 16
	SpawnID 154
	obituary "%o ate %k's Pineapple."
	inventory.pickupmessage "You Picked up the Proton Grenade Launcher!"
	weapon.selectionorder 2400
	weapon.kickback 300
	weapon.ammotype1 "ProtonAmmo"
	weapon.ammotype2 "ProtonAmmo"
	weapon.ammouse1 1
	weapon.ammouse2 1
	weapon.ammogive1 2
	weapon.ammogive2 0
	+WEAPON.NOAUTOFIRE
	+WEAPON.EXPLOSIVE
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	States
	{
		Select:
			GRLG A 0 A_JumpIfInventory("TailsClassCheck", 1, "TailsSelect")
			GRLG A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeSelect")
			GRLG A 1 A_Raise
			goto Select+2

		Deselect:
			GRLG A 0 A_JumpIfInventory("TailsClassCheck", 1, "TailsDeselect")
			GRLG A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeDeselect")
			GRLG A 1 A_Lower
			goto Deselect+2

		Ready:
			GRLG A 0 A_JumpIfInventory("TailsClassCheck", 1, "TailsReady")
			GRLG A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeReady")
			GRLG A 1 A_WeaponReady
			goto Ready+2

		Hold:
		Fire:
			GRLG A 0 A_JumpIfInventory("TailsClassCheck", 1, "TailsFire")
			GRLG A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeFire")
			GRLG A 0 A_AlertMonsters
			GRLG A 0 A_GunFlash
			GRLG A 0 A_PlayWeaponSound("weapons/grenlf")
			GRLG B 0 BRIGHT A_FireCustomMissile("GBTWGrenade", 0, true, 7, 3, true, 6)
			goto FireAnim
			
		AltFire:
			GRLG A 0 A_JumpIfInventory("TailsClassCheck", 1, "TailsFire")
			GRLG A 0 A_JumpIfInventory("DukeClassCheck", 1, "DukeAltFire")
			GRLG A 0 A_AlertMonsters
			GRLG A 0 A_GunFlash
			GRLG A 0 A_PlayWeaponSound("weapons/grenlf")
			GRLG B 0 BRIGHT A_FireCustomMissile("GBTWGrenade_Impact", 0, true, 7, 3, true, 6)
			goto FireAnim
			
		FireAnim:
			GRLG B 2 BRIGHT A_SetPitch (pitch-2)
			GRLG C 2 A_SetPitch(pitch+0.8)
			GRLG D 2 A_SetPitch(pitch+0.6)
			GRLG E 2 A_SetPitch(pitch+0.4)
			GRLG F 2 A_SetPitch(pitch+0.2)
			GRLG GHIJKLMNOPQRST 2
			GRLG A 0 A_Refire
			goto Ready+2
			
		Flash:
			GRLF A 2 bright A_Light2
			GRLF B 1 bright A_Light1
			TNT1 A 0 bright A_Light0
			stop

		TailsSelect:
			PUNT A 1 A_Raise
			loop
		TailsDeselect:
			PUNT A 1 A_Lower
			loop
		TailsReady:
			PUNT A 1 A_WeaponReady
			loop
		TailsFire:
			PUNT A 2
			PUNT B 0 A_PlaySoundEx("skeleton/swing", "auto", 0)
			PUNT BC 1
			PUNT D 1 A_FireCustomMissile("StickyBomb", 0, true, -5, 0, true, 5)
			PUNT EFGH 1
			TNT1 A 15
			TNT1 A 0 A_Refire
			goto Ready

		DukeSelect:
			D6GL A 0 A_PlayWeaponSound("DukeWeaponSelect")
			D6GL A 1 A_Raise
			goto DukeSelect+1
		DukeDeselect:
			D6GL A 1 A_Lower
			loop
		DukeReady:
			D6GL A 1 A_WeaponReady
			loop
		DukeFire:
			D6GL B 0 A_AlertMonsters
			D6GL B 0 A_PlayWeaponSound("Duke64GrenadeLaunch")
			D6GL B 3 A_FireCustomMissile("Duke64Grenade", 0, 1, 15, 0)
			D6GL CDE 3
			D6GL A 10
			D6GL A 0 A_Refire
			goto DukeReady
		DukeAltFire:
			D6GL B 0 A_PlayWeaponSound("Duke64GrenadeLaunch")
			D6GL B 3 A_FireCustomMissile("PlutoniumGrenade", 0, 1, 15, 0)
			D6GL CDE 3
			D6GL A 10
			D6GL A 0 A_Refire
			goto DukeReady
		Spawn:
			GLAU A -1
			stop
	}
}

actor GBTWGrenade Replaces Grenade
{
	Radius 8
	Height 8
	Projectile
	-NOGRAVITY
	+HEXENBOUNCE
	+EXPLODEONWATER
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+EXTREMEDEATH
	ReactionTime 175
	BounceFactor 0.75
	BounceCount 5
	obituary "%o ate %k's Pineapple."
	Speed 40
	SeeSound "weapons/grbnce"
	Damage 8
	Gravity 1.0
	States
	{
		Spawn:
			SGRN A 0
			SGRN A 0 A_StopSound
			SGRN AAAAAAAAAA 1 A_SpawnItemEX("GrenadeTrail", (random(-200,200)/100.0), (random(-200,200)/100.0), (random(-200,200)/100.0), 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			goto Fly
		
		Fly:
			SGRN A 1 BRIGHT A_SpawnItemEX("RedGrenadeSpark", 0, 0, 0, (random(-400,400)/100.0), (random(-400,400)/100.0), (random(-400,400)/100.0), random(-180, 180), SXF_CLIENTSIDE, 0)
			SGRN AA 0 BRIGHT A_SpawnItemEX("GrenadePuff", (random(-200,200)/100.0), (random(-200,200)/100.0), (random(-200,200)/100.0), (random(-50,50)/100.0), (random(-50,50)/100.0), (random(-50,50)/100.0), random(-180, 180), SXF_CLIENTSIDE, 0)
			SGRN A 0 A_CountDown
			loop

		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death

		Death:
			SGRN A 0 A_SetTranslucent(0.9, 1)
			SGRN A 0 radius_quake(3, 16, 0, 4, 0)
			SGRN A 0 A_PlaySound("BigExplosion", CHAN_AUTO)
			SGRN A 0 A_Explode
			SGRN A 0 A_SpawnItemEX("GrenadeExplosionShell", 0, 0, 0, 0, 0, 0, 0, 128, 0)
			SGRN AAAAAAA 0 A_SpawnItemEX("GrenadeSpark", 0, 0, 0, Random(-5,5), Random(-5,5), Random(6, 12), 0, SXF_CLIENTSIDE, 0)
			SGRN AAAAAAAAAAAAAA 0 A_SpawnItemEx("RPGSpark", 0, 0, 0, frandom(0.0,16.0), 0, frandom(1.0,12.0), random(-180,180), SXF_CLIENTSIDE, 0)
			SGRN AAAAAA 0 A_SpawnItemEX("RocketFire", 0, 0, 0, Random(-5,5), Random(-5,5), Random(-5, 5), 0, SXF_CLIENTSIDE, 0)
			TNT1 AAA 1
			stop
	}
}

actor GBTWGrenade_Impact : GBTWGrenade
{
	Decal Scorch
	obituary "%o sucked on %k's noob tube."
	Speed 50
	Damage 10
	-HEXENBOUNCE
}

actor GrenadeTrail : BaseEffect
{
	Scale 0.125
	RenderStyle Add
	Alpha 1.0
	States
	{
	Spawn:
		TNT1 A 2
		FRTB A 1 Bright
		FRTB B 1 Bright A_Fadeout(0.075)
		FRTB C 1 Bright A_Fadeout(0.075)
		FRTB D 1 Bright A_Fadeout(0.075)
		FRTB E 1 Bright A_Fadeout(0.075)
		FRTB F 1 Bright A_Fadeout(0.075)
		FRTB G 1 Bright A_Fadeout(0.075)
		FRTB H 1 Bright A_Fadeout(0.075)
		FRTB I 1 Bright A_Fadeout(0.075)
		Stop
	}
}

actor GrenadeSparkTrail : GrenadeTrail
{
	Scale 0.075
}

ACTOR GrenadeSpark : BaseEffect
{
	Projectile
	Speed 10
	Gravity 0.7
	Scale 0.8
	RenderStyle Add
	Alpha 0.9
	+CORPSE
	-NOGRAVITY
	-NOINTERACTION
	States
	{
	Death:
	Spawn:
		GSPA AAAABBBB 1 BRIGHT A_SpawnItemEx("GrenadeSparkTrail", 0, 0, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), frandom(-1.0,1.0), 0, SXF_CLIENTSIDE, 0)
		GSPA A 0 A_JumpIf(waterlevel == 3, "Disappear")
		loop
	Crash:
		GSPA C 0 A_PlaySound("SparkHit")
		GSPA CCCC 0 A_SpawnItemEx("GrenadePuff", frandom(-8.0,8.0), frandom(-8.0,8.0), 0, 0, 0, frandom(0.5,2.0), 0, SXF_CLIENTSIDE, 0)
		GSPA CDEFGHIJKL 3 BRIGHT
		stop
	Disappear:
		TNT1 A 0
		TNT1 A 1
		stop
	}
}

actor GrenadePuff : BaseEffect
{
	RenderStyle Add
	Alpha 0.5
	Scale 0.1
	States
	{
	Spawn:
		TNT1 A 2
		TNT1 A 0 A_JumpIf(Waterlevel == 3, "InWater")
		SMO2 ABCDEFGHIJKLMNOPQ 2
		stop
	InWater:
		TNT1 AA 0 BRIGHT A_SpawnItemEX("Bubble", Random(-4,4), Random(-4,4), Random(-4,4), 0, 0, Random(1,2), 0, SXF_CLIENTSIDE, 240)
		TNT1 A 1
		stop
	}
}

actor RedGrenadeSpark : RedEmber
{
	RenderStyle Add
	Gravity 0.3
	Alpha 1.0
	Scale 0.08
	-NOGRAVITY
	States
	{
		Spawn:
			TNT1 A 2
			goto Fly
		Fly:
			REMB A 1 BRIGHT A_FadeOut(0.1)
			loop
	}
}

actor GrenadeExplosionRing : BaseEffect
{
	RenderStyle Add
	Alpha 0.6
	States
	{
	Spawn:
		NULL ABC 2 BRIGHT
		NULL DEFGH 2 BRIGHT A_FadeOut(0.1)
		stop
	}
}

actor GrenadeExplosionSwirl : BaseEffect
{
	RenderStyle Add
	Alpha 0.75
	States
	{
	Spawn:
		NULL ABC 1 BRIGHT
		NULL DEFGHGFEDCBA 1 BRIGHT A_FadeOut(0.05)
		stop
	}
}

actor GrenadeExplosionShell : BaseEffect
{
	RenderStyle Add
	Alpha 0.8
	Scale 0.8
	States
	{
		Spawn:
			NULL A 0
			NULL A 0 BRIGHT A_SpawnItemEX("GrenadeExplosionRing", 0, 0, 4, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			NULL ABCDABCDABCD 1 BRIGHT A_Fadeout(0.025)
			NULL E 0 BRIGHT A_SpawnItemEX("GrenadeExplosionSwirl", 0, 0, 4, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			NULL EFGH 1 BRIGHT A_Fadeout(0.025)
			stop
	}
}

actor StickyBombExplosion
{
	Radius 1
	Height 1
	RenderStyle Add
	Alpha 1.0
	Scale 0.8
	+NOBLOCKMAP
	+DONTSPLASH
	+NOTELEPORT
	+NOINTERACTION
	+DROPOFF
	States
	{
	Spawn:
		STKE ABCDEFGHIJKLMNOP 1 BRIGHT
		stop
	}
}

actor StickyBomb
{
	Radius 6
	Height 8
	Projectile
	-LOWGRAVITY
	-NOGRAVITY
	+EXPLODEONWATER
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	+SEEKERMISSILE
	obituary "%o was stuck by %k."
	Speed 32
	Damage 1
	Gravity 0.7
	Scale 0.4
	States
	{
		Spawn:
			STIK AB 4
			loop

		XDeath:
			TNT1 A 0
			TNT1 A 0 A_PlaySoundEx("StickyLatchedOn", "Auto", 0)
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			TNT1 A 0 A_SeekerMissile(0, 0, SMF_LOOK, 256, 1)
			TNT1 A 0 A_NoGravity
			STIK DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD 1 A_Fire(32)
			goto Explode

		Crash:
			TNT1 A 0
			TNT1 A 0 A_PlaySoundEx("StickyHitWall", "Auto", 0)
			TNT1 A 0
			STIK C 40
			goto Explode

		Death:
			TNT1 A 0
			TNT1 A 0 A_PlaySoundEx("StickyHitWall", "Auto", 0)
			TNT1 A 0 A_NoGravity
			STIK C 40
			goto Explode

		Explode:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_ChangeFlag("EXTREMEDEATH", 1)
			TNT1 A 0 radius_quake(3, 16, 0, 3, 0)
			TNT1 A 0 A_Explode
			TNT1 A 0 A_NoGravity
			TNT1 A 0 A_SetTranslucent(0.9, 1)
			TNT1 A 0 A_PlaySoundEx("TechPlasmaSuperBoom", "Auto", 0)
			
			TNT1 A 0 A_SpawnItemEX("GrenadeExplosionShell", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			TNT1 A 0 A_SpawnItemEX("StickyBombExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			TNT1 AAAAAAAAAA 0 A_SpawnItemEx("RPGSpark", 0, 0, 0, Random(-16,16), Random(-16,16), Random(-16,16), random(-180,180), SXF_CLIENTSIDE, 0)
			
			TNT1 A 2
			stop
	}
}
