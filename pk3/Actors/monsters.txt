#include "actors/monsters/cacodemons.txt"
#include "actors/monsters/revenant.txt"
#include "actors/monsters/demon.txt"
#include "actors/monsters/imps.txt"
#include "actors/monsters/zombiemen.txt"
#include "actors/monsters/arachnotrons.txt"
#include "actors/monsters/barons.txt"
#include "actors/monsters/turret.txt"

actor RandomZombieManSpawner : RandomSpawner Replaces Zombieman
{
	DropItem "GBTWZombieman", 255 100
	DropItem "MagnumZombie", 255 25
	DropItem "TourettesZombie", 255 16
	DropItem "AngryGermanZombie", 255 5
}

actor RandomTroopSpawner : RandomSpawner Replaces ChainGunGuy
{
	DropItem "GBTWChaingunGuy", 255 100
	DropItem "P90Troop", 255 25
	DropItem "MinigunGuy", 255 20
	DropItem "NeedlerTroop", 255 10
	DropItem "TF2HeavyChaingunGuy", 255 6
}

actor RandomSuperZombieSpawner : RandomSpawner Replaces ShotgunGuy
{
	DropItem "GBTWShotgunGuy", 255 100
	DropItem "TourettesShotgunZombie", 255 32
	DropItem "OICWZombie", 255 16
	DropItem "SuperShotgunGuy", 255 6
	DropItem "FreezerZombie", 255 5
	DropItem "StingerZombie", 255 3
	DropItem "BioZombie", 255 3
}

actor RandomImpSpawner : RandomSpawner Replaces DoomImp
{
	DropItem "GBTWDoomImp", 255 100
	DropItem "Vulgar", 255 32
	DropItem "DarkImp", 255 16
	DropItem "Grell", 255 8
}


actor RandomDemonSpawner : RandomSpawner Replaces Demon
{
	DropItem "SM_Demon", 255 100
	DropItem "LeeroyZombie", 255 2
}

actor RandomSpectreSpawner : RandomSpawner Replaces Spectre
{
	DropItem "SM_Spectre", 255 100
	DropItem "NightmareDemon", 255 16
}

actor RandomCacodemonSpawner : RandomSpawner Replaces Cacodemon
{
	DropItem "GBTWCacodemon", 255 100
	DropItem "Fleshspawn_Spawner", 255 32
	DropItem "CacoShooper", 255 1
}

actor RandomBaronSpawner : RandomSpawner Replaces BaronOfHell
{
	DropItem "GBTWBaronOfHell", 255 100
	DropItem "Cybruiser", 255 25
}

actor RandomArachnotronSpawner : RandomSpawner Replaces Arachnotron
{
	DropItem "SMArachnotron", 255 100
	DropItem "SMPlasmaArachnotron", 255 100
}

ACTOR FreezerZombie : ZombieMan 20248
{
	Health 60
	DropItem "CellAmmo"
	Dropitem "FreezeBolt", 36
	AttackSound "freezebolt"
	bloodcolor "20 20 20"
	Obituary "%o was Frozen Solid by a FreezeBolt Zombie"
	Painchance 50
	Translation "128:143=192:207"
	States
	{
		Wander:
			POSS A 0 A_JumpIfHealthLower(1, "Death")
			POSS A 0 A_Look
			POSS A 4 A_Wander
			POSS B 0 A_Look
			POSS B 4 A_Wander
			POSS C 0 A_Look
			POSS C 4 A_Wander
			POSS D 0 A_Look
			POSS D 4 A_Wander
			loop
		Missile:
			POSS E 6 A_FaceTarget
			POSS F 2 A_CustomBulletAttack(22.5, 0, 1, 3, "FreezeShock")
			POSS E 2 A_FaceTarget
			POSS F 2 A_CustomBulletAttack(22.5, 0, 1, 3, "FreezeShock")
			POSS E 2 A_FaceTarget
			POSS F 2 A_CustomBulletAttack(22.5, 0, 1, 3, "FreezeShock")
			POSS E 2 A_FaceTarget
			Goto See

		Death:
			TNT1 A 0 A_GiveToTarget("DemonicPower", 1)
			POSS H 5
			POSS I 5 A_Scream
			POSS J 0 A_UnSetShootable
			POSS J 5 A_UnSetSolid
			POSS K 5
			POSS L 30
			TNT1 A 1 A_IceGuyDie
			stop
		Pain:
			POSS GG 1
			goto See
		XDeath:
			TNT1 A 0 A_GiveToTarget("DemonicPower", 1)
			POSS M 1
			POSS N 1 A_XScream
			TNT1 AA 0
			TNT1 A 1 A_IceGuyDie
			stop
	}
}

actor SM_WolfensteinSS : WolfensteinSS replaces WolfensteinSS
{
	DropItem "MP40Giver_Dropped"
	DropItem "HealingSerum", 16
	states
	{
		Missile:
			SSWV EF 10 A_FaceTarget
			SSWV G 0 A_FaceTarget
			SSWV G 0 A_PlaySound("mp40fire", CHAN_WEAPON)
			SSWV G 1 BRIGHT A_CustomMissile("MonsterBullet", 32, 4, frandom(-5, 5), CMF_AIMOFFSET | CMF_OFFSETPITCH, frandom(-1.0, 1.0))
			SSWV F 2 A_FaceTarget
			SSWV G 0 A_FaceTarget
			SSWV G 0 A_PlaySound("mp40fire", CHAN_WEAPON)
			SSWV G 1 BRIGHT A_CustomMissile("MonsterBullet", 32, 4, frandom(-5, 5), CMF_AIMOFFSET | CMF_OFFSETPITCH, frandom(-1.0, 1.0))
			SSWV F 2 A_FaceTarget
			SSWV G 0 A_FaceTarget
			SSWV G 0 A_PlaySound("mp40fire", CHAN_WEAPON)
			SSWV G 1 BRIGHT A_CustomMissile("MonsterBullet", 32, 4, frandom(-5, 5), CMF_AIMOFFSET | CMF_OFFSETPITCH, frandom(-1.0, 1.0))
			SSWV F 6
			SSWV F 1 A_CPosRefire
			goto Missile+1
		XDeath:
			SSWV N 0 A_GiveToTarget("DemonicPower", 1)
			TNT1 A 0 A_GiveToTarget("SpeakOneLiner", 1)
			SSWV N 5 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
			SSWV O 5 A_XScream
			SSWV P 5 A_NoBlocking
			SSWV QRSTU 5
			SSWV V 10500
			goto Fade

		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

actor TF2HeavyChaingunGuy : GBTWChaingunGuy
{
	health 250
	translation "32:47=176:191"
	activesound "HeavyAct"
	seesound	"HeavySee"
	painsound	"HeavyPain"
	deathsound	"HeavyDeath"
	DropItem "ChaingunGiver_Dropped"
	DropItem "Whopper"
	DropItem "Drink"
	DropItem "Fries"
}

actor SM_SpiderMasterMind : SpiderMasterMind Replaces SpiderMasterMind
{
	Dropitem "Minigun"
}

actor SM_CyberDemon : CyberDemon Replaces CyberDemon
{
	DropItem "TorpedoLauncherGiver_Dropped"
	DropItem "GrenadeLauncherGiver_Dropped"
	DamageFactor "Electric", 1.25
	DamageFactor "Silver", 2.0
	DamageFactor "Fire", 0.5
	DamageFactor "Holy", 6.0
	States
	{
		Missile:
			CYBR E 0 A_GiveInventory("Firing", 1)
			CYBR E 6 A_FaceTarget
			CYBR F 6 A_CustomMissile("ProtonTorpedo", 56, -21, 0, 0, 0)
			CYBR E 6 A_FaceTarget
			CYBR E 12 A_FaceTarget
			CYBR F 6 A_CustomMissile("ProtonTorpedo", 56, -21, 0, 0, 0)
			CYBR E 6 A_FaceTarget
			CYBR E 12 A_FaceTarget
			CYBR F 6 A_CustomMissile("ProtonTorpedo", 56, -21, 0, 0, 0)
			CYBR E 0 A_TakeInventory("Firing", 1)
			CYBR E 6 A_FaceTarget
			Goto See
		Pain:
			CYBR G 0 A_TakeInventory("Firing", 1)
			CYBR G 10 A_Pain
			Goto See
		Death:
			CYBR H 10 A_GiveToTarget("DemonicPower", 100)
			CYBR I 10 A_Scream
			CYBR JKL 10
			CYBR M 10 A_NoBlocking
			CYBR NO 10
			CYBR P 30
			CYBR P 10500 A_BossDeath
			goto Fade
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

actor SM_LostSoul : LostSoul Replaces LostSoul
{
	Health 50
	DeathSound "misc/unused"
	States
	{
		See:
			SKUL AB 3 Bright A_Chase
			Loop
	}
}

Actor NeedlerTroop : GBTWChaingunGuy 27800
{
	OBITUARY "%o was spiked by a Needler Troop."
	Health 110
	Radius 20
	Height 56
	Mass 135
	Speed 12
	Painchance 64
	SEESOUND "Monsters/NBSight"
	ACTIVESOUND "Monsters/NBAct"
	PAINSOUND "Monsters/NBPain"
	DEATHSOUND "Monsters/NBDeath"
	Translation 1
	DropItem "Needler", 164
	DropItem "MolotovCocktail", 48
	DropItem "MechAmmo"
	MONSTER
	+FLOORCLIP
	+DONTHURTSPECIES
	States
	{
		Spawn:
			NLBG AB 10 A_Look
			Loop
		See:
			NLBG AA 4 A_Chase
			NLBG A 0 A_PlaySound("Monsters/NBWalk")
			NLBG BBCC 4 A_Chase
			NLBG CC 0 A_PlaySound("Monsters/NBWalk")
			NLBG DD 4 A_Chase
			Loop
		Missile:
			NLBG E 10 A_FaceTarget
			NLBG F 0 A_CustomMissile("NeedlerFlare", 34, 7, 0, 0)
			NLBG F 2 A_CustomMissile("EnemyNeedlerCrystal", 32, 0, Random(-5, 5), 4)
			NLBG E 2 A_FaceTarget
			NLBG F 0 A_CustomMissile("NeedlerFlare", 34, 7, 0, 0)
			NLBG F 2 A_CustomMissile("EnemyNeedlerCrystal", 32, 0, Random(-5, 5), 4)
			NLBG E 2
			NLBG E 0 BRIGHT A_CposRefire
			Goto Missile+1
		Pain:
			NLBG G 3
			NLBG G 3 A_Pain
			Goto See
		Death:
			TNT1 A 0 A_GiveToTarget("DemonicPower", 1)
			NLBG H 5
			NLBG I 5 A_Scream
			NLBG J 5 A_NoBlocking
			NLBG K 5
			NLBG L 10500
			goto Fade
		XDeath:
			TNT1 A 0 A_GiveToTarget("DemonicPower", 1)
			TNT1 A 0 A_GiveToTarget("SpeakOneLiner", 1)
			NLBG O 5 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
			NLBG P 5 A_XScream
			NLBG Q 5 A_NoBlocking
			NLBG Q 0
			NLBG RTUV 5
			NLBG W 10500
			goto Fade
		Raise:
			NLBG KJIH 5
			Goto See
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

actor Grell
{
	health 75
	radius 18
	height 48
	mass 200
	speed 10
	FloatSpeed 2
	Obituary "%o was plagued by a grell."
	painchance 128
	seesound "grell/sight"
	painsound "grell/pain"
	deathsound "grell/death"
	activesound "grell/active"
	Scale 0.8
	Monster
	+DROPOFF
	+NOGRAVITY
	+DONTHURTSPECIES
	+FLOATBOB
	+SPAWNFLOAT
	+FIXMAPTHINGPOS
	+FLOAT
	+FLOORCLIP
	States
	{
		Spawn:
			GREL A 10 A_Look
			Loop
		See:
			GREL AABBCC 4 A_Chase
			Loop
		Missile:
			GREL D 0 A_PlaySound("grell/attack")
			GREL D 4 A_FaceTarget
			GREL E 4 Bright A_FaceTarget
			GREL FFF 2 Bright A_CustomMissile("VulgarShot", 24, 0, random(-6,6), 0, random(-4,4))
			Goto See
		Pain:
			GREL G 3
			GREL G 3 A_Pain
			Goto See
		Death:
			GREL H 0 A_ChangeFlag("FLOATBOB", 0)
			GREL H 5 A_NoBlocking
			GREL I -1 A_Scream
			goto Crash
		Crash:
			GREL J 4 A_PlaySound("grell/thud", CHAN_AUTO)
			GREL K 4
			GREL LM 4 A_SetFloorClip
			GREL N 10500
			goto Fade
		Raise:
			GREL M 5 A_UnSetFloorClip
			GREL LKJIH 5
			GREL H 0 A_ChangeFlag("FLOATBOB", 1)
			Goto See
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

actor Grell_NoSpawnFloat : Grell
{
	-SPAWNFLOAT
	States
	{
		See:
			GREL A 0
			GREL A 0 ThrustThingZ(0, random(1, 4), 0, 0)
			goto SeeLoop
		SeeLoop:
			GREL AABBCC 4 A_Chase
			Loop
		Missile:
			GREL D 0 A_PlaySound("grell/attack")
			GREL D 4 A_FaceTarget
			GREL E 4 Bright A_FaceTarget
			GREL FFF 2 Bright A_CustomMissile("VulgarShot", 24, 0, random(-6,6), 0, random(-4,4))
			Goto SeeLoop
		Pain:
			GREL G 3
			GREL G 3 A_Pain
			Goto SeeLoop
	}
}

ACTOR Fleshspawn
{
	Obituary "%o was burned by a FleshSpawn."
	HitObituary "%o was devoured by a FleshSpawn."
	health 50
	radius 14
	height 28
	mass 80
	speed 8
	damage 2
	DamageFactor "Flak", 1.5
	DamageFactor "Hellfire", 2.0
	painchance 224
	missiletype "Fleshspawnball"
	bloodcolor "blue"
	attacksound "flesh/melee"
	seesound "flesh/sight"
	painsound "flesh/pain"
	deathsound "flesh/death"
	activesound "flesh/active"
	MONSTER
	+FLOAT
	+NOGRAVITY
	+DONTHURTSPECIES
	+NOINFIGHTING
	+NOTARGET
	States
	{
		Spawn:
			FLSP AB 8 A_Look
			loop
		See:
			FLSP AABB 2 A_Chase
			loop
		Missile:
			FLSP C 0 A_Jump(128,"Charge")
			FLSP C 8 A_FaceTarget
			FLSP G 4 A_FaceTarget
			FLSP H 4 Bright A_CustomMissile("Fleshspawnball", 12, 0, 0)
			goto See
		Charge:
			FLSP C 0 A_Jump(96, 6)
			FLSP C 8 A_FaceTarget
			FLSP D 4 A_FaceTarget
			FLSP E 4 A_SkullAttack
			FLSP FE 4
			Goto Charge+4
			FLSP C 8 A_FaceTarget
			FLSP D 4 A_FaceTarget
			FLSP E 4 A_SkullAttack
			FLSP FEFE 4
			FLSP C 6 A_FaceTarget
			FLSP D 4 A_FaceTarget
			FLSP E 4 A_SkullAttack
			FLSP FE 4
			Goto Charge+4
		Pain:
			FLSP I 3
			FLSP I 3 A_Pain
			Goto See
		Death:
			FLSP I 5
			FLSP J 5 A_Scream
			FLSP K 5
			FLSP L 5 A_NoBlocking
			FLSP MN 5 A_SetFloorClip
			FLSP O 10500
			goto Fade
		XDeath:
			FLSP P 3
			FLSP Q 0 A_FaceTarget
			FLSP Q 0 A_SpawnItemEx("FleshSpawnGibs", 0, 0, 0, 0, 0, 0, 0, 128)
			FLSP Q 0 A_CustomMissile("Fleshspawngib1", 14, 0, random(-180,180), 2, random(10,40))
			FLSP Q 0 A_CustomMissile("Fleshspawngib2", 6, 2, random(-180,180), 2, random(0,25))
			FLSP Q 0 A_CustomMissile("Fleshspawngib2B", 10, -2, random(-180,180), 2, random(0,25))
			FLSP Q 0 A_CustomMissile("Fleshspawngib3", 8, 0, random(-180,180), 2, random(0,35))
			FLSP Q 0 A_CustomMissile("Fleshspawngib4", 12, 5, random(-180,180), 2, random(-5,40))
			FLSP Q 0 A_CustomMissile("Fleshspawngib4B", 5, -5, random(-180,180), 2, random(0,30))
			FLSP Q 0 A_CustomMissile("Fleshspawngib5", 6, 3, random(-180,180), 2, random(10,60))
			FLSP Q 0 A_CustomMissile("Fleshspawngib5", 8, 0, random(-180,180), 2, random(-10,55))
			FLSP Q 0 A_CustomMissile("Fleshspawngib6", 12, 0, 0, 2, 0)
			FLSP Q 3 A_XScream
			FLSP R 3 A_NoBlocking
			FLSP STU 3
			Stop
		Raise:
			FLSP O 5 A_UnSetFloorClip
			FLSP NMLKJI 5
			Goto See
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

Actor FleshSpawnGibs
{
	+NoClip
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_CustomMissile("Fleshspawngib1", 14, 0, random(-180,180), 2, random(10,40))
		TNT1 A 0 A_CustomMissile("Fleshspawngib2", 6, 2, random(-180,180), 2, random(0,25))
		TNT1 A 0 A_CustomMissile("Fleshspawngib2B", 10, -2, random(-180,180), 2, random(0,25))
		TNT1 A 0 A_CustomMissile("Fleshspawngib3", 8, 0, random(-180,180), 2, random(0,35))
		TNT1 A 0 A_CustomMissile("Fleshspawngib4", 12, 5, random(-180,180), 2, random(-5,40))
		TNT1 A 0 A_CustomMissile("Fleshspawngib4B", 5, -5, random(-180,180), 2, random(0,30))
		TNT1 A 0 A_CustomMissile("Fleshspawngib5", 6, 3, random(-180,180), 2, random(10,60))
		TNT1 A 0 A_CustomMissile("Fleshspawngib5", 8, 0, random(-180,180), 2, random(-10,55))
		TNT1 A 0 A_CustomMissile("Fleshspawngib6", 12, 0, 0, 2, 0)
		Stop
	}
}

actor Fleshspawnball
{
	Damage 2
	Speed 10
	Radius 3
	Height 5
	SeeSound "caco/attack"
	DeathSound "caco/shotx"
	RenderStyle Add
	Alpha 0.9
	Projectile
	states
	{
	Spawn:
		FLBL ABCBABCBABCBABCBIJKLM 4 Bright
		Stop
	Death:
		FLBL DEFGH 4 Bright
		Stop
	}
}

ACTOR Fleshspawngib1
{
	Speed 8
	Mass 100
	Radius 2
	Height 2
	PROJECTILE
	-NOGRAVITY
	+DROPOFF
	+ALLOWCLIENTSPAWN
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		FGB1 ABCD 4
		loop
	Death:
		FGB1 E 4200
		goto Fade
	Fade:
		"####" "#" 1 A_FadeOut(0.05)
		loop
	}
}

ACTOR Fleshspawngib2 : FleshSpawnGib1
{
	States
	{
	Spawn:
		FGB2 ABCD 4
		loop
	Death:
		FGB2 I 4200
		goto Fade
   }
}

ACTOR Fleshspawngib2B : FleshSpawnGib1
{
	States
	{
	Spawn:
		FGB2 EFGH 4
		loop
	Death:
		FGB2 J 4200
		goto Fade
	}
}

ACTOR Fleshspawngib3 : FleshSpawnGib1
{
	States
	{
	Spawn:
		FGB3 ABCD 4
		loop
	Death:
		FGB3 E 4200
		goto Fade
	}
}

ACTOR Fleshspawngib4 : FleshSpawnGib1
{
	States
	{
	Spawn:
		FGB4 ABCD 4
		loop
	Death:
		FGB4 I 4200
		goto Fade
	}
}

ACTOR Fleshspawngib4B : FleshSpawnGib1
{
	Scale 0.8
	States
	{
	Spawn:
		FGB4 EFGH 4
		loop
	Death:
		FGB4 J 4200
		goto Fade
	}
}

ACTOR Fleshspawngib5 : FleshSpawnGib1
{
	States
	{
	Spawn:
		FGB5 ABCD 4
		loop
	Death:
		FGB5 E 4200
		goto Fade
	}
}

ACTOR Fleshspawngib6 : FleshSpawnGib1
{
	Speed 0
	States
	{
	Spawn:
		FGB6 A 4
		loop
	Death:
		FGB6 BC 4
		FGB6 D 4200
		goto Fade
   }
}

actor Fleshspawn_Spawner
{
	radius 14
	height 28
	+ISMONSTER
	-SOLID
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Fleshspawn", 14, 14, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG, 0)
			TNT1 A 0 A_SpawnItemEx("Fleshspawn", 14, -14, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG, 0)
			TNT1 A 0 A_SpawnItemEx("Fleshspawn", -14, 14, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG, 0)
			TNT1 A 0 A_SpawnItemEx("Fleshspawn", -14, -14, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG, 0)
			TNT1 A 0 A_SpawnItemEx("Fleshspawn", 14, 14, 28, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG, 0)
			TNT1 A 0 A_SpawnItemEx("Fleshspawn", 14, -14, 28, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG, 0)
			TNT1 A 0 A_SpawnItemEx("Fleshspawn", -14, 14, 28, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG, 0)
			TNT1 A 0 A_SpawnItemEx("Fleshspawn", -14, -14, 28, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG, 0)
			TNT1 A 1
			stop
	}
}

actor LeeroyZombie : GBTWZombieMan 17291
{
	obituary "%o was Kamikaze'd by a Leeroy Zombie."
	health 100
	speed 12
	painchance 40
	SpawnId 240
	DamageType "Leeroy"
	DamageFactor "Leeroy", 0.0
	seesound "LeeroySee"
	Translation "128:151=112:127"
	MONSTER
	DropItem "MechAmmo", 128
	DropItem "MolotovCocktail", 36
	+FLOORCLIP
	+DONTHURTSPECIES
	+LOOKALLAROUND
	+NORADIUSDMG
	states
	{
	Spawn:
		POSS AA 5 A_Look
		loop
	See:
		POSS A 0 A_StopSoundEx("SoundSlot6")
		POSS AABBCCDD 3 A_Chase
		POSS D 0 A_JumpIf(waterlevel == 3, "Detonate")
		loop
	Missile:
		POSS E 10 A_FaceTarget
		POSR A 0 A_PlaySoundEx("LeeroyJenkins", "Auto", 0, 0.99)
		POSR A 0 A_PlaySoundEx("LeeroyRun", "SoundSlot6", 1, -1)
		POSR A 0 A_AlertMonsters
		POSR A 0 A_JumpIfCloser(120, "Detonate")
		POSR A 0 A_FaceTarget
		POSR A 2 A_Recoil(-4)
		POSR A 0 A_FaceTarget
		POSR A 2 A_Recoil(-3)
		POSR B 0 A_FaceTarget
		POSR B 0 A_CustomBulletAttack(40, 20, 1, 5, "LeeroyBulletPuff", 2048, 1)
		POSR B 0 A_JumpIfCloser(120, "Detonate")
		POSR B 2 BRIGHT A_Recoil(-3)
		POSR B 0 A_FaceTarget
		POSR B 2 BRIGHT A_Recoil(-3)
		POSR C 0 A_JumpIfCloser(120, "Detonate")
		POSR C 0 A_CPosRefire
		POSR C 0 A_FaceTarget
		POSR C 2 A_Recoil(-3)
		POSR C 0 A_FaceTarget
		POSR C 2 A_Recoil(-3)
		POSR D 0 A_JumpIfCloser(120, "Detonate")
		POSR D 0 A_FaceTarget
		POSR D 0 A_CustomBulletAttack(40, 20, 1, 5, "LeeroyBulletPuff", 2048, 1)
		POSR D 2 BRIGHT A_Recoil(-3)
		POSR D 0 A_FaceTarget
		POSR D 2 BRIGHT A_Recoil(-3)
		POSR D 0 A_JumpIf(momx+momy+momz == 0, "See")
		POSR D 0 A_JumpIfInTargetInventory("ValkrieCannons", 1, "JumpCheck1")
		POSR A 0 A_CPosRefire
		goto Missile+3
	JumpCheck1:
		POSR D 0 A_JumpIfCloser(640, "JumpCheck2")
		goto Missile+3
	JumpCheck2:
		POSR D 0 A_CheckFloor("JumpAtShip")
		goto Missile+3
	JumpAtShip:
		POSR D 0 ThrustThingZ(0, 76, 0, 0)
		goto Missile+3
	Pain:
		POSS G 3 A_StopSoundEx("SoundSlot6")
		POSS G 3 A_Pain
		goto See
	Burn:
	Disintegrate:
	Ethereal:
	XDeath:
	Detonate:
		POSS M 1 A_Die
		goto Death
	Death:
		POSS M 4 A_StopSoundEx("SoundSlot6")
		POSS M 0 bright A_PlaySound("MassiveExplosion")
		POSS M 0 bright A_Explode(136, 136, 1)
		POSS M 0 bright A_SpawnItemEx("SledgeHammerExplosionLarge", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		POSS MMMMMM 0 bright A_SpawnItemEx("SledgeHammerExplosionSmall", Random(-48, 48), Random(-48, 48), Random(-48, 48), 0, 0, 0, 0, 128, 0)
		POSS M 1 A_StopSoundEx("SoundSlot6")
		POSS NNNNNN 0 bright A_SpawnItemEx("SledgeHammerExplosionSmall", Random(-48, 48), Random(-48, 48), Random(-48, 48), 0, 0, 0, 0, 128, 0)
		POSS N 5 A_XScream
		POSS OOOO 0 A_SpawnItemEx ("Striker_FlyingBlood", 0, 0, 25, Random(5, 10), Random(5,10), Random(2, 20), Random(-255, 255), 128, 0)
		POSS OOOOOO 0 A_SpawnItemEx("Fleshy_Splatter", 0, 0, 32, random(-2, 2), random(-2, 2), random(1, 14), Random(-180, 180), 128, 0)
		POSS O 5 A_NoBlocking
		POSS PQRST 5
		POSS U 10500
		goto Fade
	Raise:
		POSS UTSRQPONM 4
		POSS M 0 A_StopSoundEx("SoundSlot6")
		goto See

	Cinematic:
		POSS E 4 
		POSR A 0 A_PlaySoundEx("LeeroyJenkins", "Auto", 0, 0.99)
		POSR A 0 A_PlaySoundEx("LeeroyRun", "SoundSlot6", 1, -1)
		POSR A 0 A_AlertMonsters
		POSR A 2 A_Recoil(-3.5)
		POSR A 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSR A 2 A_Recoil(-3)
		POSR B 0 A_CustomBulletAttack(20, 20, 1, 0, "LeeroyBulletPuff", 2048, 1)
		POSR B 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSR B 2 BRIGHT A_Recoil(-3)
		POSR B 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSR B 2 BRIGHT A_Recoil(-3)
		POSR C 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSR C 2 A_Recoil(-3)
		POSR C 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSR C 2 A_Recoil(-3)
		POSR D 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSR D 0 A_CustomBulletAttack(20, 20, 1, 0, "LeeroyBulletPuff", 2048, 1)
		POSR D 2 BRIGHT A_Recoil(-3)
		POSR D 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSR D 2 BRIGHT A_Recoil(-3)
		POSR D 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		goto Cinematic+3
	Cinematic2:
		POSS A 2 A_Recoil(-3)
		POSS A 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSS A 2 A_Recoil(-3)
		POSS A 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSS B 2 A_Recoil(-3)
		POSS B 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSS B 2 A_Recoil(-3)
		POSS B 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSS C 2 A_Recoil(-3)
		POSS C 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSS C 2 A_Recoil(-3)
		POSS C 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSS D 2 A_Recoil(-3)
		POSS D 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		POSS D 2 A_Recoil(-3)
		POSS D 0 A_JumpIf(momx+momy+momz == 0, "Detonate")
		loop
	}
}

actor SM_Archvile : Archvile Replaces Archvile
{
	Dropitem "ElementalSSG", 100
	DropItem "ForceThrust", 80
	DropItem "WeatherMaker", 76
	DropItem "GasPickup", 36
	DropItem "DemonicPower", 200, 64
	DamageFactor "Ice", 1.2
	DamageFactor "Fire", 0.5
	DamageFactor "Holy", 5.0
	DamageFactor "Silver", 1.25
	DamageFactor "VilesBane", 1000
}

actor SM_Fatso : Fatso Replaces Fatso
{
  dropitem "FlameThrowerWeap", 40
  DropItem "GasPickup", 40
  DropItem "FartBombs", 20
  DropItem "FartBombs", 1
  DropItem "IpodLauncher", 1
  DropItem "WeatherMaker", 2
}

ACTOR Hectebus : SM_Fatso 5007
{
   SpawnID 158
   Health 1200
   PainChance 20
   Obituary "$OB_HECTEBUS"
   States
   {
		Spawn:
			HECT AB 15 A_Look
			Loop
		See:
			HECT AABBCCDDEEFF 4 A_Chase
			Loop
		Pain:
			HECT J 3 A_TakeInventory("Firing", 1)
			HECT J 3 A_Pain
			Goto See
		Missile:
			//These Next 2 states replace A_FatRaise and A_HectRaise
			HECT G 0 A_PlaySound("fatso/raiseguns")
			HECT G 20 A_FaceTarget
			HECT H 0 A_GiveInventory("Firing", 1)
			HECT H 0 A_CustomMissile("HectShot",28,0,0,1)
			HECT H 0 A_CustomMissile("HectShot",28,0,5.625,1)
			HECT H 0 A_CustomMissile("HectShot",28,0,11.25,1)
			HECT H 0 A_CustomMissile("HectShot",28,0,16.875,1)
			HECT H 10 BRIGHT
			HECT IG 5 A_FaceTarget
			HECT H 0 A_CustomMissile("HectShot",28,0,0,1)
			HECT H 0 A_CustomMissile("HectShot",28,0,-5.625,1)
			HECT H 0 A_CustomMissile("HectShot",28,0,-11.25,1)
			HECT H 0 A_CustomMissile("HectShot",28,0,-16.875,1)
			HECT H 10 BRIGHT
			HECT IG 5 A_FaceTarget
			HECT H 0 A_CustomMissile("HectShot",28,0,-11.25,1)
			HECT H 0 A_CustomMissile("HectShot",28,0,-5.625,1)
			HECT H 0 A_CustomMissile("HectShot",28,0,0,1)
			HECT H 0 A_CustomMissile("HectShot",28,0,5.625,1)
			HECT H 0 A_CustomMissile("HectShot",28,0,11.25,1)
			HECT H 10 BRIGHT A_TakeInventory("Firing", 1)
			HECT IG 5 A_FaceTarget
			Goto See
		Death:
			HECT K 6
			HECT L 6 A_Scream
			HECT M 6 A_NoBlocking
			HECT NOPQRS 6
			HECT T 10500
			goto Fade
		Raise:
			HECT RQPONMLK 5
			Goto See
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

ACTOR HectShot : FatShot
{
   Speed 22
   Damage 12
   States
   {
   Spawn:
	  HECF AB 4 BRIGHT
	  Loop
   Death:
	  HECF C 8 BRIGHT
	  HECF D 6 BRIGHT
	  HECF E 4 BRIGHT
	  Stop
   }
}

actor Trite 17121
{
	Species "Trite"
	Monster
	Radius 14
	Height 16
	Health 10
	SpawnID 251
	Scale 0.4
	Damage 2
	PoisonDamage 3
	Speed 25
	Painchance 255
	hitobituary "%o got his PINGAS bitten off by a trite!"
	obituary "%o got his PINGAS bitten off by a trite!"
	Gravity 0.7
	DamageType "Trite"
	DamageFactor "Trite", 0
	Bloodcolor "00 FF 00"
	Seesound "tritesee"
	Deathsound "tritedeath"
	PainSound "tritepain"
	MinMissileChance 225
	MaxTargetRange 256
	+DROPOFF
	+LOOKALLAROUND
	+THRUSPECIES
	States
	{
		Spawn:
			TRIT A 10 A_Look
			loop
		See:
			TRIT ABCD 3 A_Chase
			TRIT A 0 A_JumpIf(waterlevel == 3, "Kill")
			TRIT A 0 A_PlaySound("TriteStep")
			loop
		Missile:
			TRIT E 8 A_FaceTarget
			TRIT E 0 A_PlaySound("triteattack")
			TRIT E 0 A_Recoil(-18)
			TRIT E 0 ThrustThingZ(0, 26, 0, 0)
			TRIT FFFFFF 3 A_CustomBulletAttack (4, 1, 1, 1, "TriteHit", 32)
			TRIT E 4
			goto See
		Pain:
			TRIT E 2
			TRIT F 2 A_Pain
			goto See
		Kill:
			TRIT A 1 A_Die
			goto Death
		Death:
			TRIT G 0 A_Scream
			TRIT G 0 A_Fall
			TRIT GHIJ 4
			TRIT J 180
			TRIT JJJJJJJJJJJJ 2 A_FadeOut
			stop

		Disabled:
			TNT1 A 0
			TNT1 A 0 A_JumpIfHealthLower(1, "Death")
			TNT1 A 0 A_ChangeFlag("SOLID", 0)
			TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
			TNT1 A 0 A_ChangeFlag("DORMANT", 1)
			TNT1 A 1
			TNT1 A -1
			loop

		Enabled:
			TNT1 A 0
			TNT1 A 0 A_JumpIfHealthLower(1, "Death")
			TNT1 A 0 A_ChangeFlag("SOLID", 1)
			TNT1 A 0 A_ChangeFlag("SHOOTABLE", 1)
			TNT1 A 0 A_ChangeFlag("DORMANT", 0)
			TNT1 A 1
			goto Spawn
	}
}

actor TriteHit : BulletPuff
{
	DamageType "Trite"
	PoisonDamage 2
	+BLOODLESSIMPACT
	+NOBLOOD
	+PUFFONACTORS
	-BLOODSPLATTER
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 1 A_PlaySound("TriteBite")
		stop
	}
}

actor Hangman 32123
{
  obituary "%o was ambushed by a hangman."
  health 150
  radius 20
  height 56
  mass 0x7fffffff
  speed 0
  MaxTargetRange 145
  painchance 75
  seesound "hanger/sight"
  painsound "hanger/pain"
  deathsound "hanger/death"
  activesound "hanger/active"
  DropItem "HealthBonus2", 128
  DropItem "MaxHealthBonus2", 128
  DropItem "Stimpack2", 128
  DropItem "HealingSerum", 32
  MONSTER
  +FLOORCLIP
  +NOGRAVITY
  +SPAWNCEILING
  +CEILINGHUGGER
  +DONTFALL
  states
  {
  Spawn:
	HANG B 10 A_Look
	loop
  See:
	HANG B 1 A_FaceTarget
	HANG B 0 A_Chase
	loop
  Melee:
  Missile:
	HANG B 8 A_FaceTarget
	HANG A 6 A_CustomMissile("HangerBall", 32, 0, 0, 0)
	HANG AA 0 A_SpawnItemEx("BloodRain", random(-8,8), random(-8,8), random(0, 4), 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
	goto See
  Pain:
	HANG B 2
	HANG A 2 A_Pain
	goto See
  Death:
	HANG A 30 A_Scream
	HANG A 10 A_ChangeFlag("noclip", 1)
	HANG C 0 A_NoBlocking
	HANG C 0 A_XScream
	HANG C 0 A_Explode
	HANG C 0 radius_quake(3, 16, 0, 3, 0)
	HANG C 0 A_PlaySoundEx("BigExplosion", "auto")
	HANG C 0 A_SpawnItemEx("XDeathGibs", 0, 0, 0, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
	HANG CCCCCC 0 A_SpawnItemEx("TurretExplosion", 0, 0, 0, random(-3,3), random(-3,3), random(-3,3), 0, SXF_CLIENTSIDE, 0)
	goto DeathDrip
  DeathDrip:
	HANG C 0 A_SpawnItemEx("BloodRain2", random(-8,8), random(-8,8), random(24, 32), 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
	HANG C 256 A_SpawnItemEx("BloodRain2", random(-8,8), random(-8,8), random(24, 32), 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
	loop
  }
}

actor Gordie_The_Friendly_Mancubus : Fatso 19633
{
	Radius 42
	Species "Player"
	BloodColor Blue
	Translation "64:79=152:159", "48:63=152:152", "236:239=123:126", "2:2=127:127", "1:1=126:126", "13:15=123:125", "32:47=240:247", "16:31=192:207", "208:223=192:201", "160:167=80:95", "112:127=160:167", "168:191=192:207"
	+FRIENDLY
	+AVOIDMELEE
	+QUICKTORETALIATE
	+NORADIUSDMG
	+NOICEDEATH
	+DONTHARMSPECIES
	+NOTARGET
	+MISSILEMORE
	+ALWAYSRESPAWN
	+SLIDESONWALLS
	+PUSHABLE
	+NOFEAR
	+NOTIMEFREEZE
	+NEVERFAST
	-COUNTKILL
}

ACTOR Danny_Tourettes_Guard 19632
{
   Species "Player"
   DamageFactor "Tourettes", 0.0
   Game Doom
   Health 200
   Radius 20
   Height 56
   Mass 100
   Speed 6
   PainChance 255
   SeeSound "tourettes/taunt"
   PainSound "tourettes/pain"
   DeathSound "tourettes/death"
   MONSTER
   +AMBUSH
   +AVOIDMELEE
   +FLOORCLIP
   +FRIENDLY
   +SLIDESONWALLS
   +QUICKTORETALIATE
   +TELESTOMP
   +NORADIUSDMG
   +DONTGIB
   +NEVERFAST
   +DROPOFF
   +NOICEDEATH
   +DONTHARMSPECIES
   +PUSHABLE
   +CANPASS
   +CANPUSHWALLS
   +TELESTOMP
   +WINDTHRUST
   -NODROPOFF
   -COUNTKILL
   Translation "112:127=176:191"
   var int user_dodgeshootcount;
   States
   {
		Spawn:
			TGUY A 0 A_Look
			TGUY AAAA 1 A_Wander
			TGUY B 0 A_Look
			TGUY BBBB 1 A_Wander
			TGUY C 0 A_Look
			TGUY CCCC 1 A_Wander
			TGUY D 0 A_Look
			TGUY DDDD 1 A_Wander
			loop
		WalkAnim:
			TGUY ABCD 4
			loop
		See:
			TGUY AAAA 1 A_Chase
			TGUY B 0 A_JumpIfInTargetInventory("Firing", 1, "DodgeShoot")
			TGUY BBBB 1 A_Chase("Melee", "Missile", CHF_NOPLAYACTIVE)
			TGUY A 0 A_Jump(128, "DodgeLeft", "DodgeRight")
			TGUY A 0 A_JumpIfCloser(384, "Backoff")
			TGUY A 0 A_JumpIfInTargetInventory("Firing", 1, "DodgeShoot")
			TGUY A 0 A_Jump(32, "Jump")
			goto See2
		See2:
			TGUY CCCC 1 A_Chase("Melee", "Missile", CHF_NOPLAYACTIVE)
			TGUY D 0 A_JumpIfInTargetInventory("Firing", 1, "DodgeShoot")
			TGUY DDDD 1 A_Chase("Melee", "Missile", CHF_NOPLAYACTIVE)
			TGUY A 0 A_JumpIf(waterlevel >= 1, "EscapeWater")
			TGUY A 0 A_Jump(128, "DodgeLeft2", "DodgeRight2")
			TGUY A 0 A_JumpIfCloser(384, "Backoff2")
			TGUY A 0 A_JumpIfInTargetInventory("Firing", 1, "DodgeShoot")
			TGUY A 0 A_Jump(32, "Jump2")
			TGUY A 0 A_MonsterRefire(0, "Spawn") 
			goto See
		EscapeWater:
			TGUY A 0 A_FaceTarget
			TGUY A 0 A_Recoil(-4)
			TGUY A 0 ThrustThingZ(0, 48, 0, 1)
			goto See
		DodgeLeft:
			TGUY A 0 ThrustThing(angle*256/360-38.7, random(8, 16), 0, 0)
			goto See2
		DodgeRight:
			TGUY A 0 ThrustThing(angle*256/360+38.7, random(8, 16), 0, 0)
			goto See2
		DodgeLeft2:
			TGUY A 0 ThrustThing(angle*256/360-38.7, random(8, 16), 0, 0)
			goto See
		DodgeRight2:
			TGUY A 0 ThrustThing(angle*256/360+38.7, random(8, 16), 0, 0)
			goto See
		BackOff:
			TGUY A 0 A_Recoil(32)
			goto See2
		BackOff2:
			TGUY A 0 A_Recoil(32)
			goto See
		Jump:
			TGUY A 0 A_PlaySound("Jump")
			TGUY A 4 ThrustThingZ(0, 40, 0, 0)
			TGUY BC 4 A_Recoil(random(-2, -4))
			TGUY C 0 A_FastChase
			TGUY D 4 A_Recoil(random(-2, -4))
			TGUY D 0 A_FastChase
			goto See2
		Jump2:
			TGUY A 0 A_PlaySound("Jump")
			TGUY A 4 ThrustThingZ(0, 40, 0, 0)
			TGUY BC 4 A_Recoil(random(-2, -4))
			TGUY C 0 A_FastChase
			TGUY D 4 A_Recoil(random(-2, -4))
			TGUY D 0 A_FastChase
			goto See
		DodgeShoot:
			TGUY E 0 A_SetUserVar("user_dodgeshootcount", 0)
			goto DodgeShootLoop
		DodgeShootLoop:
			TGUY E 0 A_SetUserVar("user_dodgeshootcount", user_dodgeshootcount+1)
			TGUY E 2 A_FaceTarget
			TGUY F 0 A_Recoil(3)
			TGUY F 0 ThrustThing(angle*256/360+38.7, random(6, 10), 0, 0)
			TGUY F 0 Bright A_PlayWeaponSound("weapons/chngun")
			TGUY F 2 Bright A_CustomBulletAttack(4, 3, 1, 8, "Tourettes_BulletPuff")
			TGUY F 0 A_JumpIf(momx < 2 && momy < 2 && momx > -2 && momy > -2, "DodgeShootLeftLoop")
			TGUY F 0 A_MonsterRefire(0, "Spawn") 
			TGUY F 0 A_JumpIf(user_dodgeshootcount < 6, "DodgeShootLoop")
			goto See
		DodgeShootLeftLoop:
			TGUY E 0 A_SetUserVar("user_dodgeshootcount", user_dodgeshootcount+1)
			TGUY E 2 A_FaceTarget
			TGUY F 0 A_Recoil(3)
			TGUY F 0 ThrustThing(angle*256/360-38.7, random(6, 10), 0, 0)
			TGUY F 0 Bright A_PlayWeaponSound("weapons/chngun")
			TGUY F 2 Bright A_CustomBulletAttack(4, 3, 1, 8, "Tourettes_BulletPuff")
			TGUY F 0 A_JumpIf(momx < 2 && momy < 2 && momx > -2 && momy > -2, "DodgeShoot")
			TGUY F 0 A_MonsterRefire(0, "Spawn") 
			TGUY F 0 A_JumpIf(user_dodgeshootcount < 6, "DodgeShootLeftLoop")
			goto See
		Melee:
		Missile:
			TGUY E 0 A_Jump(128, "Missile_Plasma")
			TGUY E 3 A_FaceTarget
			TGUY F 0 Bright A_PlayWeaponSound("weapons/sshotf")
			TGUY F 8 Bright A_CustomBulletAttack(6, 4, 14, 5, "Tourettes_BulletPuff")
			TGUY E 8
			TGUY A 0 A_PlaySoundEx("weapons/sshoto", "auto", 0)
			TGUY AAAAABBBBB 1 A_Wander
			TGUY B 0 A_PlaySoundEx("weapons/sshotl", "auto", 0)
			TGUY CCCCCDDDDD 1 A_Wander
			TGUY D 0 A_PlaySoundEx("weapons/sshotc", "auto", 0)
			TGUY D 0 A_JumpIfInTargetInventory("Firing", 1, "DodgeShoot")
			TGUY AAAAABBBBB 1 A_Wander
			TGUY CCCCCDDDDD 1 A_FastChase
			Goto See
		Missile_Plasma:
			TGUY E 2 A_FaceTarget
			TGUY F 2 Bright A_CustomMissile("Tourettes_NewPlasma")
			TGUY E 0 A_Jump(128, "PlasmaDodgeLeft", "PlasmaDodgeRight")
			TGUY E 0 A_JumpIfCloser(384, "PlasmaBackoff")
			goto Missile_PlasmaChecks
		Missile_PlasmaChecks:
			TGUY E 0 A_JumpIfInTargetInventory("Firing", 1, "DodgeShoot")
			TGUY E 0 A_CposRefire
			TGUY E 0 A_Jump(48, "See")
			goto Missile_Plasma
		PlasmaDodgeLeft:
			TGUY E 0 ThrustThing(angle*256/360-38.7, random(6, 12), 0, 0)
			goto Missile_PlasmaChecks
		PlasmaDodgeRight:
			TGUY E 0 ThrustThing(angle*256/360+38.7, random(6, 12), 0, 0)
			goto Missile_PlasmaChecks
		PlasmaBackoff:
			TGUY E 0 A_Recoil(8)
			goto Missile_Plasma
		Pain:
			TGUY G 0 A_Recoil(4)
			TGUY G 0 A_Jump(128, "PainDodgeRight")
			goto PainDodgeLeft
		PainDodgeLeft:
			TGUY G 0 ThrustThing(angle*256/360+166.7, random(8, 16), 0, 0)
			goto PainFinish
		PainDodgeRight:
			TGUY G 0 ThrustThing(angle*256/360-166.7, random(8, 16), 0, 0)
			goto PainFinish
		PainFinish:
			TGUY G 3 A_Pain
			TGUY G 0 A_FastChase
			TGUY G 0 A_JumpIfInTargetInventory("Firing", 1, "DodgeShoot")
			Goto See
		Death:
			TGUY H 5
			TGUY I 5 A_Scream
			TGUY J 5 A_NoBlocking
			TGUY KLM 5
			TGUY N 200
			TGUY N 150 A_PlaySoundEx("tourettes/death", "Auto", 0)
			TGUY MLKJIH 5
			TGUY A 50 A_PlaySoundEx("tourettes/taunt", "Auto", 0)
			TGUY A 1 A_SpawnItemEx("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TGUY A 0 A_Respawn
			Stop
		Raise:
			TGUY MLKJIH 5
			Goto See
		XDeathCinematic:
			TGUY O 5 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
			TGUY P 5 A_XScream
			TGUY Q 5 A_NoBlocking
			TGUY RSTUV 5
			TGUY W -1
			Stop
   }
}