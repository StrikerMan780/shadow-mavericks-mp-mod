ACTOR MolotovCocktail : CustomInventory 25121
{
	Tag "Molotov Cocktail"
	Scale 0.25
	inventory.pickupmessage "Molotov Cocktail"
	inventory.pickupsound "misc/p_pkup"
	inventory.icon ARTIMOLO
	inventory.amount 1
	inventory.maxamount 12
	Obituary "%o can't handle %k's cocktail."
	+FLOATBOB
	+INVBAR
	+INVENTORY.KEEPDEPLETED
	States
	{
		Spawn:
			MOGP A -1
			Loop
		Use:
			//TNT1 A 1 A_FireCustomMissile("Bottlex", 0, 0, 0, 0)
			TNT1 A 0 A_PlaySound("skeleton/swing", CHAN_AUTO)
			TNT1 A 0 A_SpawnItemEx("Bottlex", cos(-pitch)*1, 0, 32+(sin(pitch)*1),cos(-pitch)*32,0,sin(-pitch)*32, 0, 0, 0)
			stop
	}
}

ACTOR MolotovCocktail_Dropped : MolotovCocktail
{
	inventory.amount 1
	inventory.maxamount 0
	-INVBAR
	-INVENTORY.KEEPDEPLETED
	+INVENTORY.AUTOACTIVATE
	States
	{
		Spawn:
			MOGP A 15000
			goto Fade
		Fade:
			MOGP A 1 A_FadeOut(0.01)
			loop
		Use:
			TNT1 A 1 A_GiveInventory("MolotovCocktail", 1)
			stop
	}
}

actor SerumSpawner : RandomSpawner 10592
{
	DropItem "HealingSerum"
}

ACTOR HealingSerum : ArtiHealth replaces ArtiHealth
{
  Tag "Healing Serum"
  Inventory.Icon ARTIHSER
  Inventory.PickupSound "misc/p_pkup"
  Inventory.PickupMessage "Healing Serum"
  Inventory.UseSound "HealingSerum"
  HealthPickup.AutoUse 0
  States
  {
	Spawn:
		PTN2 ABC 4
		Loop
  }
}

actor SaberColor : CustomInventory
{
	Tag "Change Saber Color"
	inventory.pickupsound "misc/p_pkup"
	inventory.icon ARTICOLO
	inventory.amount 2
	inventory.maxamount 2
	+COUNTITEM
	+INVENTORY.UNDROPPABLE
	+INVBAR
	states
	{
	Spawn:
		LCHN A -1
		stop
	Use:
		TNT1 A 0 A_Jumpifinventory("SaberRed", 1, "UseBlue")
		TNT1 A 0 A_Jumpifinventory("SaberBlue", 1, "UseGreen")
		TNT1 A 0
		goto UseRed
	UseRed:
		TNT1 A 0 A_TakeInventory("SaberGreen", 255)
		TNT1 A 0 A_GiveInventory ("SaberRed", 1)
		TNT1 A 0 A_GiveInventory("SaberColor", 1)
		stop
	UseBlue:
		TNT1 A 0 A_TakeInventory("SaberRed", 255)
		TNT1 A 0 A_GiveInventory("SaberBlue",1)
		TNT1 A 0 A_GiveInventory("SaberColor", 1)
		stop
	UseGreen:
		TNT1 A 0 A_TakeInventory("SaberBlue", 255)
		TNT1 A 0 A_GiveInventory("SaberGreen",1)
		TNT1 A 0 A_GiveInventory("SaberColor", 1)
		stop
	}
}

ACTOR FragGrenade : CustomInventory
{
	Tag "Frag Grenade"
	inventory.pickupmessage "Frag Grenade"
	inventory.pickupsound "misc/p_pkup"
	inventory.icon ARTIFRAG
	inventory.amount 1
	inventory.maxamount 12
	+FLOATBOB
	+INVBAR
	+INVENTORY.KEEPDEPLETED
	States
	{
		Spawn:
			FRAG A -1
			Loop
		Use:
			TNT1 A 0 A_PlaySound("FragGrenadeUse", CHAN_AUTO)
			TNT1 A 0 A_PlaySound("skeleton/swing", CHAN_AUTO)
			TNT1 A 0 A_SpawnItemEx("FragGrenadeX", cos(-pitch)*1, 0, 32+(sin(pitch)*1),cos(-pitch)*8,0,(sin(-pitch)*8)+4, 0, 0, 0)
			stop
	}
}

actor FragGrenadeX
{
	Obituary "%o tried to disarm %k's grenade."
	projectile
	Radius 4
	Height 8
	Speed 15
	BounceType Doom
	BounceCount 5
	BounceSound "FragGrenadeBounce"
	+BOUNCEONACTORS
	+CANBOUNCEWATER
	-NOGRAVITY
	-NOTELEPORT
	Args 105
	States
	{
		Spawn:
			FRAG AA 3
			goto Armed
		Armed:
			FRAG B 1 A_CountdownArg(0, "Explode")
			loop
		Death:
			FRAG C 1 A_CountdownArg(0, "Explode")
			loop
		Explode:
			TNT1 A 0 A_PlaySound("BigExplosion", CHAN_AUTO)
			TNT1 A 0 A_Explode(50, 128, XF_HURTSOURCE, true, 96)
			goto Shrapnel
		Shrapnel:
			TNT1 AAAAAAAAAAAAAAA 0 A_CustomMissile("FragShard",8,0,Random(-180,180), CMF_AIMDIRECTION | CMF_TRACKOWNER, frandom(1.0, 45.0))
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("FragShard_Fast",8,0,Random(-180,180), CMF_AIMDIRECTION | CMF_TRACKOWNER, frandom(1.0, 22.5))
			goto End
		End:
			TNT1 AAAAA 0 A_SpawnItemEx("RPGSpark", 0, 0, 0, frandom(0.0,12.0), 0, frandom(1.0,12.0), random(-180,180), SXF_CLIENTSIDE, 0)
			TNT1 AAAA 0 A_SpawnItemEx("FlakExplosion", 0, 0, 0, (Random(-200,200)/100.0), (Random(-200,200)/100.0), (Random(-200,200)/100.0), random(-180,180), SXF_CLIENTSIDE, 0)
			TNT1 A 2
			stop
	}
}

actor FragShard : FlakShard
{
	obituary "%o was torn apart by %k's shrapnel."
	BounceCount 6
	BounceFactor 0.75
}

actor FragShard_Fast : FastProjectile
{
	Decal "BulletChip"
	Projectile
	obituary "%o had %p flesh torn off by %k's shrapnel"
	Radius 2
	Height 2
	Speed 150
	Damage (10)
	RenderStyle Add
	Alpha 1.0
	+BLOODSPLATTER
	+RIPPER
	States
	{
		Spawn:
			PUFF A 1 bright
			loop
		Death:
			TNT1 A 0
			TNT1 A 1 A_SpawnItemEx("NewBulletPuff_NoMarker", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			TNT1 A 1
			stop
	}
}

actor ForcePull : CustomInventory 10593
{
	inventory.pickupmessage "Force Pull!"
	inventory.pickupsound "misc/p_pkup"
	inventory.icon ARTIPULL
	inventory.amount 10
	inventory.maxamount 30
	Scale 0.2
	Renderstyle Add
	Alpha 0.8
	+COUNTITEM
	+FLOATBOB
	+INVBAR
	States
	{
	Spawn:
		PULL A 1 Bright A_SeekerMissile (90, 90)
		loop
	Use:
		TNT1 A 0 A_PlaySound("ForcePull")
		TNT1 A 0 A_FireCustomMissile("ForcePullAttack", 0, 0, 0, 0)
		TNT1 A 0 A_SpawnItem("ForceFX", 5, 36, 0, 0)
		stop
	}
}

ACTOR GrenadierPack : Custominventory 21321
{
	inventory.pickupmessage "Grenadier Pack!"
	inventory.pickupsound "misc/p_pkup"
	States
	{
	Spawn:
		BPK2 A -1
		loop
	Pickup:
		TNT1 A 0 A_GiveInventory ("FragGrenade", 2)
		TNT1 A 0 A_GiveInventory ("MolotovCocktail", 2)
		TNT1 A 0 A_GiveInventory ("HealingSerum", 2)
		stop
	}
}