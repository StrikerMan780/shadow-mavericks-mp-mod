ACTOR MolotovCocktail : CustomInventory 25121
{
	Tag "Molotov Cocktail"
	Scale 0.25
	inventory.pickupmessage "Molotov Cocktail"
	inventory.pickupsound "misc/p_pkup"
	inventory.icon ARTIMOLO
	inventory.amount 1
	inventory.maxamount 12
	Obituary "%o can't handle %k's cocktail."
	+FLOATBOB
	+INVBAR
	+INVENTORY.KEEPDEPLETED
	States
	{
		Spawn:
			MOGP A 0
			MOGP A -1 A_CheckFlag("DROPPED", "Dropped")
			Loop
		Use:
			//TNT1 A 1 A_FireCustomMissile("Bottlex", 0, 0, 0, 0)
			TNT1 A 0 A_PlaySound("skeleton/swing", CHAN_AUTO)
			TNT1 A 0 A_SpawnItemEx("Bottlex", cos(-pitch)*1, 0, 32+(sin(pitch)*1),cos(-pitch)*32,0,sin(-pitch)*32, 0, 0, 0)
			stop
		Dropped:
			"####" "#" 10500
			goto Fade
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

actor SerumSpawner : RandomSpawner 10592
{
	DropItem "HealingSerum"
}

ACTOR HealingSerum : ArtiHealth replaces ArtiHealth
{
  Tag "Healing Serum"
  Inventory.Icon ARTIHSER
  Inventory.PickupSound "misc/p_pkup"
  Inventory.PickupMessage "Healing Serum"
  Inventory.UseSound "HealingSerum"
  HealthPickup.AutoUse 0
  States
  {
	Spawn:
		PTN2 A 0
		PTN2 A -1 A_CheckFlag("DROPPED", "Dropped")
		Loop
	Dropped:
		"####" "#" 10500
		goto Fade
	Fade:
		"####" "#" 1 A_FadeOut(0.05)
		loop
  }
}

ACTOR FragGrenade : CustomInventory
{
	Tag "Frag Grenade"
	inventory.pickupmessage "Frag Grenade"
	inventory.pickupsound "FragGrenadeUse"
	inventory.icon ARTIFRAG
	inventory.amount 1
	inventory.maxamount 12
	+FLOATBOB
	+INVBAR
	+INVENTORY.KEEPDEPLETED
	States
	{
		Spawn:
			FRAG A 0
			FRAG A -1 A_CheckFlag("DROPPED", "Dropped")
			Loop
		Use:
			TNT1 A 0 A_PlaySound("FragGrenadeUse", CHAN_AUTO)
			TNT1 A 0 A_PlaySound("skeleton/swing", CHAN_AUTO)
			TNT1 A 0 A_SpawnItemEx("FragGrenadeX", cos(-pitch)*1, 0, 32+(sin(pitch)*1),cos(-pitch)*8,0,(sin(-pitch)*8)+4, 0, 0, 0)
			stop
		Dropped:
			"####" "#" 10500
			goto Fade
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

actor FragGrenadeX
{
	Obituary "%o tried to disarm %k's grenade."
	projectile
	Radius 4
	Height 8
	Speed 15
	BounceType Hexen
	BounceCount 4
	BounceSound "FragGrenadeBounce"
	+BOUNCEONACTORS
	+CANBOUNCEWATER
	+CANPASS
	+BOUNCEAUTOOFFFLOORONLY
	-NOGRAVITY
	-NOTELEPORT
	-USEBOUNCESTATE
	States
	{
		Spawn:
			FRAG AA 3
			goto Armed
		Armed:
			FRAG B 350
			loop
		Death:
			FRAG C 32
			goto Explode
		Explode:
			TNT1 A 0 A_PlaySound("BigExplosion", CHAN_AUTO)
			TNT1 A 0 A_Explode(50, 128, XF_HURTSOURCE, true, 96)
			goto Shrapnel
		Shrapnel:
			TNT1 AAAAAAAAAAAAAAA 0 A_CustomMissile("FragShard",8,0,Random(-180,180), CMF_AIMDIRECTION | CMF_TRACKOWNER, frandom(1.0, 45.0))
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("FragShard_Fast",8,0,Random(-180,180), CMF_AIMDIRECTION | CMF_TRACKOWNER, frandom(1.0, 22.5))
			goto End
		End:
			TNT1 AAAAA 0 A_SpawnItemEx("RPGSpark", 0, 0, 0, frandom(0.0,12.0), 0, frandom(1.0,12.0), random(-180,180), SXF_CLIENTSIDE, 0)
			TNT1 AAAA 0 A_SpawnItemEx("FlakExplosion", 0, 0, 0, (Random(-200,200)/100.0), (Random(-200,200)/100.0), (Random(-200,200)/100.0), random(-180,180), SXF_CLIENTSIDE, 0)
			TNT1 A 2
			stop
	}
}

actor FragGrenadeX_Monster : FragGrenadeX
{
	DamageType "EnemyFrag"
	Obituary "%o tried to disarm a zombie's grenade."
	States
	{
		Death:
			FRAG C 32
			goto Explode
		Explode:
			TNT1 A 0 A_PlaySound("BigExplosion", CHAN_AUTO)
			TNT1 A 0 A_Explode(25, 128, 0, false, 96)
			goto Shrapnel
		Shrapnel:
			TNT1 AAAAAAAA 0 A_CustomMissile("FragShard_Monster",8,0,Random(-180,180), CMF_AIMDIRECTION | CMF_TRACKOWNER, frandom(1.0, 45.0))
			TNT1 AAAAAAAAAA 0 A_CustomMissile("FragShard_Fast_Monster",8,0,Random(-180,180), CMF_AIMDIRECTION | CMF_TRACKOWNER, frandom(1.0, 22.5))
			goto End
	}
}

actor FragShard : FlakShard
{
	obituary "%o was torn apart by %k's shrapnel."
	BounceCount 6
	BounceFactor 0.75
}

actor FragShard_Monster : FragShard
{
	Obituary "%o was torn apart by a zombie's shrapnel."
}

actor FragShard_Fast : FastProjectile
{
	Decal "BulletChip"
	Projectile
	obituary "%o had %p flesh torn off by %k's shrapnel."
	Radius 2
	Height 2
	Speed 150
	Damage (10)
	RenderStyle Add
	Alpha 1.0
	+BLOODSPLATTER
	+RIPPER
	States
	{
		Spawn:
			PUFF A 1 bright
			loop
		Death:
			TNT1 A 0
			TNT1 A 1 A_SpawnItemEx("NewBulletPuff_NoMarker", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			TNT1 A 1
			stop
	}
}

actor FragShard_Fast_Monster : FragShard_Fast
{
	Obituary "%o had %p flesh torn off by a zombie's shrapnel."
	Damage (5)
}

actor ForcePull : CustomInventory 10593
{
	inventory.pickupmessage "Force Pull!"
	inventory.pickupsound "misc/p_pkup"
	inventory.icon ARTIPULL
	inventory.amount 10
	inventory.maxamount 30
	Scale 0.2
	Renderstyle Add
	Alpha 0.8
	+COUNTITEM
	+FLOATBOB
	+INVBAR
	States
	{
	Spawn:
		PULL A 1 Bright A_SeekerMissile (90, 90)
		loop
	Use:
		TNT1 A 0 A_PlaySound("ForcePull")
		TNT1 A 0 A_FireCustomMissile("ForcePullAttack", 0, 0, 0, 0)
		TNT1 A 0 A_SpawnItem("ForceFX", 5, 36, 0, 0)
		stop
	}
}

ACTOR GrenadierPack : Custominventory 21321
{
	inventory.pickupmessage "Grenadier Pack!"
	inventory.pickupsound "misc/p_pkup"
	States
	{
	Spawn:
		BPK2 A -1
		loop
	Pickup:
		TNT1 A 0 A_GiveInventory ("FragGrenade", 2)
		TNT1 A 0 A_GiveInventory ("MolotovCocktail", 2)
		TNT1 A 0 A_GiveInventory ("HealingSerum", 2)
		stop
	}
}