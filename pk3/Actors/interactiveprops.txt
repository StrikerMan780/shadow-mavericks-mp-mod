actor DrinkingFountain : SwitchableDecoration 16541
{
	//$Category SMMP Interactive Props
	//$Title Drinking Fountain
	//$Color 13
	Activation THINGSPEC_ThingTargets | THINGSPEC_ThingActs
	height 16
	radius 12
	XScale 0.70
	YScale 0.75
	+SOLID
	+USESPECIAL
	states
	{
		Spawn:
			FOUN A 0
			FOUN A -1 A_SetSpecial(130, 0)
			loop
		Active:
			FOUN B 0 A_ChangeFlag("USESPECIAL", 0)
			FOUN B 0 A_PlaySound("DrinkingFountain")
			FOUN BCDBCDBCDBCDBCDBCDBCDBCDB 1 A_GiveToTarget("Health", 1)
			FOUN CDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCD 1
			FOUN A 0 A_ChangeFlag("USESPECIAL", 1)
			goto Spawn
	}
}

actor Stripper : SwitchableDecoration 30107
{	
	//$Category SMMP Interactive Props
	//$Title Duke3D Stripper
	//$Color 13
	Activation THINGSPEC_ThingTargets | THINGSPEC_ThingActs
	Radius 16
	Height 56
	Scale 0.62
	+SOLID
	+USESPECIAL
	States
	{
		Spawn:
			STRP A 0
			STRP A 0 A_SetSpecial(130, 0)
			goto Dance
		Dance:
			STRP ABCDEDCB 5
			loop
		Active:
			STRP A 0 A_JumpIfInTargetInventory("GBTWMoney", 5, "Strip")
			goto Dance
		Strip:
			STRP F 0 A_ChangeFlag("USESPECIAL", 0)
			STRP F 0 A_TakeFromTarget("GBTWMoney", 5)
			STRP F 10 A_Playsound("stripper")
			STRP GHGHGHGHGHGHGHGHGH 3 A_GiveToTarget("Health", 1)
			STRP F 7
			STRP F 0 A_ChangeFlag("USESPECIAL", 1)
			goto Dance
	}
}

actor 8trackStereo : SwitchableDecoration 16542
{
	//$Category SMMP Interactive Props
	//$Title 8-Track Stereo/Record Player (Abbey Road)
	//$Color 13
	Activation THINGSPEC_ThingTargets | THINGSPEC_ThingActs
	height 24
	radius 20
	+SOLID
	+USESPECIAL
	var int user_playing;
	states
	{
		Spawn:
			FOUN A 0
			FOUN A -1 A_SetSpecial(130, 0)
			loop
		StopPlaying:
			FOUN A 0 A_SetUserVar("user_playing", 0)
			FOUN A 35 A_StopSound(CHAN_BODY)
			goto Spawn
		Active:
			FOUN A 0 A_JumpIf(user_playing == 1, "StopPlaying")
			FOUN A 0 A_SetUserVar("user_playing", 1)
			FOUN A 0 A_PlaySound("HereComesTheSun", CHAN_BODY)
			goto Playing
		Playing:
			FOUN ABCDEFGHIJKLMNOP 16
			loop
	}
}

actor VendingMachine_Fanta : SwitchableDecoration 20526
{
	//$Category SMMP Interactive Props
	//$Title Fanta Vending Machine
	//$Color 13
	Activation THINGSPEC_ThingTargets | THINGSPEC_ThingActs
	height 72
	radius 24
	+SOLID
	+USESPECIAL
	states
	{
		Spawn:
			FOUN A 0
			FOUN A -1 A_SetSpecial(130, 0)
			loop
		Active:
			FOUN A 0 A_JumpIfInTargetInventory("GBTWMoney", 2, "Dispense")
			goto Spawn
		Dispense:
			FOUN A 0 A_ChangeFlag("USESPECIAL", 0)
			FOUN A 0 A_TakeFromTarget("GBTWMoney", 2)
			FOUN A 30 A_PlaySound("Vend", CHAN_AUTO)
			FOUN A -1 A_Jump(256, "Pepsi", "Fanta", "CocaCola", "MDHitler")
			stop
			
		Pepsi:
			FOUN A 20 A_SpawnItemEx("Pepsi", 24, 0, 10, frandom(2.0, 4.0), 0, frandom(0.5,1.0), frandom(-2.0, 2.0), SXF_NOCHECKPOSITION, 0)
			goto Reactivate
			
		Fanta:
			FOUN A 20 A_SpawnItemEx("Fanta", 24, 0, 10, frandom(2.0, 4.0), 0, frandom(0.5,1.0), frandom(-2.0, .0), SXF_NOCHECKPOSITION, 0)
			goto Reactivate
			
		CocaCola:
			FOUN A 20 A_SpawnItemEx("CocaCola", 24, 0, 10, frandom(2.0, 4.0), 0, frandom(0.5,1.0), frandom(-2.0, .0), SXF_NOCHECKPOSITION, 0)
			goto Reactivate
			
		MDHitler:
			FOUN A 20 A_SpawnItemEx("MDHitler", 24, 0, 10, frandom(2.0, 4.0), 0, frandom(0.5,1.0), frandom(-2.0, .0), SXF_NOCHECKPOSITION, 0)
			goto Reactivate
			
		Reactivate:
			FOUN A 0 A_ChangeFlag("USESPECIAL", 1)
			goto Spawn
	}
}

actor VendingMachine_CocaCola : VendingMachine_Fanta 20527
{
	//$Category SMMP Interactive Props
	//$Title Coca-Cola Vending Machine
	//$Color 13
}

actor VendingMachine_Pepsi : VendingMachine_Fanta 20528
{
	//$Category SMMP Interactive Props
	//$Title Pepsi Vending Machine
	//$Color 13
	States
	{
		Dispense:
			FOUN A 0 A_ChangeFlag("USESPECIAL", 0)
			FOUN A 0 A_TakeFromTarget("GBTWMoney", 2)
			FOUN A 30 A_PlaySound("Vend", CHAN_AUTO)
			goto Pepsi
			
		Pepsi:
			FOUN A 20 A_SpawnItemEx("Pepsi", 24, 4, 10, frandom(2.0, 4.0), 0, frandom(0.5,1.0), frandom(-2.0, 2.0), SXF_NOCHECKPOSITION, 0)
			goto Reactivate
	}
}

actor VendingMachine_Hitler : VendingMachine_Fanta 20529
{
	//$Category SMMP Interactive Props
	//$Title Mountain Dew HDNW Vending Machine
	//$Color 13
}

actor Macintosh 20523
{
	//$Category SMMP Interactive Props
	//$Title Macintosh IMac G3
	//$Color 13
	Health 100
	Height 32
	DeathHeight 32
	Radius 16
	Scale 0.6
	Mass 0x7fffffff
	+SOLID
	+FORCEYBILLBOARD
	+SHOOTABLE
	+NOBLOOD
	States
	{
		Spawn:
			MACI A -1
			loop
		Death:
			MACI A 5 A_PlaySoundEx("FuckingWindows98", "Auto", 0)
			TNT1 A 0 A_Explode
			TNT1 A 0 A_NoBlocking
			TNT1 AAAAAAAA 0 A_SpawnItemEx("TurretExplosion", 0, 0, 0, random(-3,3), random(-3,3), random(-3,3), 0, SXF_CLIENTSIDE, 0)
			TNT1 A 1050
			TNT1 A 0 A_SpawnItemEx("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 A 0 A_SpawnItemEx("Macintosh", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
			stop
	}
}