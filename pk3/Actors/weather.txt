actor RainSpawner 16000
{
	//$Category SMMP Effect Spawners
	//$Title Rain Spawner
	//$Arg0 Spawn Radius
	//$Arg1 Rain Type
	//$Arg2 No Spawn Chance
	//$Sprite RAINA0
	//$Color 11
	Radius 16
	Height 1
	Speed 0
	Mass 9999999
	Scale 0.08
	+SPAWNCEILING
	+NOGRAVITY
	+CORPSE
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(args[1] == 1, "BloodRain")
			goto Rain
		Rain:
			TNT1 A 4 A_SpawnItemEX("Rain2", Random(args[0]*-1,args[0]), Random(args[0]*-1,args[0]), -1, 0, 0, 0, 0, SXF_CLIENTSIDE, args[2])
			Loop
		BloodRain:
			TNT1 A 4 A_SpawnItemEX("BloodRain2", Random(args[0]*-1,args[0]), Random(args[0]*-1,args[0]), -1, 0, 0, 0, 0, SXF_CLIENTSIDE, args[2])
			Loop
	}
}

actor VolumetricFogSpawner 20001
{
	//$Category SMMP Effect Spawners
	//$Title Volumetric Fog Spawner
	//$Sprite FOG1A0
	//$Color 11
	Radius 64
	yscale 0.08
	+ALLOWCLIENTSPAWN
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOCLIP
	+NOBLOCKMAP
	states
	{
	Spawn:
		TNT1 A 1
		TNT1 AAAAAAAA 0 A_SpawnItemEx("VolumetricFogSprite", Random(-64, 64), Random(-64, 64), 0, 0, 0, 0, 0, 128, 48)
		stop
	}
}

actor VolumetricFogSprite
{
	yscale 0.08
	renderstyle Add
	alpha 0.6
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+ALLOWCLIENTSPAWN
	+CLIENTSIDEONLY
	+NOCLIP
	+NOBLOCKMAP
	states
	{
	Spawn:
		FOG1 A -1
		loop
	}
}


actor Drip : BaseEffect
{
	RenderStyle Translucent
	Alpha 0.5
	Scale 0.64
	+CLIENTSIDEONLY
	+NOCLIP
	+FORCEYBILLBOARD
	-FORCEXYBILLBOARD
	States
	{
		Spawn:
			DRIP ABCDEFGHI 1
			stop
	}
}

actor BloodDrip : Drip
{
	Alpha 0.6
	States
	{
		Spawn:
			BRIP ABCDEFGHI 1
			stop
	}
}

ACTOR Rain
{
	Radius 1
	Height 1
	Gravity 2
	Alpha 0.6
	YScale 0.12
	XScale 0.08
	RenderStyle Translucent
	+NOBLOCKMAP
	+NOINTERACTION
	+NOGRAVITY
	+DONTSPLASH
	+DONTREFLECT
	+DROPOFF
	+FORCEYBILLBOARD
	+THRUACTORS
	+CANNOTPUSH
	+MISSILE
	+CLIENTSIDEONLY
	+CORPSE
	+SKYEXPLODE
	-FORCEXYBILLBOARD
	-SOLID
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("CHECKSKY", 1) == false, "Disappear")
		TNT1 A 0 ThrustThingZ(0, 150, 1, 0)
		goto Fall
	Fall:
		RAIN A 1 A_JumpIf(z < ceilingz, "EnableInteraction")
		Loop
	EnableInteraction:
		RAIN A 0
		RAIN A 0 A_SpawnItemEx("Rain2", 0, 0, 64, 0, 0, velz, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION, 0)
		stop
	Disappear:
		TNT1 A 0
		TNT1 A 1
		stop
	}
}

actor Rain2 : Rain
{
	-NOGRAVITY
	-NOINTERACTION
	States
	{
		Death:
			RAIN A 0 ThrustThingZ(0, 150, 1, 0)
			goto Spawn
		Spawn:
			RAIN A 1 A_JumpIf(waterlevel > 0, "Disappear")
			Loop
		Crash:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Drip", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 128)
			Stop
	}
}

actor BloodRain : Rain
{
	Alpha 0.75
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("CHECKSKY", 1) == false, "Disappear")
		TNT1 A 0 ThrustThingZ(0, 150, 1, 0)
		goto Fall
	Fall:
		BLDR A 1 A_JumpIf(z < ceilingz, "EnableInteraction")
		Loop
	EnableInteraction:
		BLDR A 0
		BLDR A 0 A_SpawnItemEx("BloodRain2", 0, 0, 64, 0, 0, velz, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION, 0)
		stop
	}
}

actor BloodRain2 : BloodRain
{
	-NOGRAVITY
	-NOINTERACTION
	States
	{
		Death:
			BLDR A 1 ThrustThingZ(0, 150, 1, 0)
			goto Spawn
		Spawn:
			BLDR A 1 A_JumpIf(waterlevel > 0, "Disappear")
			Loop
		Crash:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("BloodDrip", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 128)
			Stop
	}
}
