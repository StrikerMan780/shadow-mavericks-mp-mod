actor BasePuff
{
	Radius 4
	Height 2
	+FIXMAPTHINGPOS
	+FORCEXYBILLBOARD
	+PUFFGETSOWNER
	+NOINTERACTION
	+NOBLOCKMAP
	+NOGRAVITY
}

actor NullPuff : BasePuff
{
	+BLOODLESSIMPACT
	+PUFFONACTORS
	states
	{
		Spawn:
			TNT1 A 1
			stop
	}
}

Actor BulletBlast
{
	Radius 1
	Height 1
	RenderStyle Add
	Alpha .5
	XScale 0.1
	YScale 0.05
	+DONTSPLASH
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	-ACTIVATEPCROSS
	-ACTIVATEIMPACT
	
	States
	{
	Spawn:
		PBLS A 0
		PBLS A 0 A_Jump(256, "Blast1", "Blast2", "Blast3", "Blast4")
		goto Blast1
	
	Blast1:
		PBLS ABCDE 1 BRIGHT A_SetScale(scalex+0.1, scaley+0.1)
		goto Fade1	
	Fade1:
		PBLS E 1 BRIGHT A_SetScale(scalex+0.1, scaley+0.1)
		PBLS E 0 BRIGHT A_FadeOut(0.1)
		loop
	
	Blast2:
		PBLS A 0 BRIGHT A_SetScale(scalex*-1.0, scaley*-1.0)
		PBLS ABCDE 1 BRIGHT A_SetScale(scalex-0.1, scaley-0.1)
		goto Fade2	
	Fade2:
		PBLS E 1 BRIGHT A_SetScale(scalex-0.1, scaley-0.1)
		PBLS E 0 BRIGHT A_FadeOut(0.1)
		loop
		
	Blast3:
		PBLS A 0 BRIGHT A_SetScale(scalex, scaley*-1.0)
		PBLS ABCDE 1 BRIGHT A_SetScale(scalex+0.1, scaley-0.1)
		goto Fade3	
	Fade3:
		PBLS E 1 BRIGHT A_SetScale(scalex+0.1, scaley-0.1)
		PBLS E 0 BRIGHT A_FadeOut(0.1)
		loop
		
	Blast4:
		PBLS A 0 BRIGHT A_SetScale(scalex*-1.0, scaley)
		PBLS ABCDE 1 BRIGHT A_SetScale(scalex-0.1, scaley+0.1)
		goto Fade4	
	Fade4:
		PBLS E 1 BRIGHT A_SetScale(scalex-0.1, scaley+0.1)
		PBLS E 0 BRIGHT A_FadeOut(0.1)
		loop
		
	Death:
		TNT1 A 1
		stop
	}
}

Actor BulletSmoke : BaseEffect
{
	RenderStyle Add
	Alpha 0.5
	Scale 0.15
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(Waterlevel == 3, "InWater")
		SMO2 ABCDEFGHIJKLMNOPQ 2
		stop
	InWater:
		TNT1 AA 0 BRIGHT A_SpawnItemEX("Bubble", Random(-4,4), Random(-4,4), Random(-4,4), 0, 0, Random(1,2), 0, 128, 76)
		TNT1 A 1
		stop
	}
}

Actor BulletSmoke2
{
	Radius 1
	Height 1
	Speed  0
	RenderStyle Add
	Alpha 0.4
	Scale 0.4
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+NOCLIP
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	-ACTIVATEPCROSS
	-ACTIVATEIMPACT
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(Waterlevel == 3, "InWater")
		PUF2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		PUF3 ABC 1
		stop
	InWater:
		TNT1 AA 0 BRIGHT A_SpawnItemEX("Bubble", Random(-4,4), Random(-4,4), Random(-4,4), 0, 0, Random(1,2), 0, 128, 0)
		TNT1 A 1
		stop
	}
}

Actor BulletDebris
{
	Radius 1
	Height 1
	Speed  3
	Damage 0
	Health 1
	RenderStyle Translucent
	Alpha 1.0
	Scale 0.15
	Gravity 0.6
	PROJECTILE
	+HEXENBOUNCE
	+DONTSPLASH
	+FORCEXYBILLBOARD
    -ACTIVATEIMPACT
	-ACTIVATEPCROSS
	-NOGRAVITY
	States
	{
	Spawn:
		DEBR A 0
		DEBR A 0 A_Jump(128, "Spawn2", "Spawn3", "Spawn4")
		DEBR A 6
		goto Fade1
	Fade1:
		DEBR A 1 A_Fadeout(0.08)
		loop
	Spawn2:
		DEBR B 6
		goto Fade2
	Fade2:
		DEBR B 1 A_Fadeout(0.08)
		loop
	Spawn3:
		DEBR C 6
		goto Fade3
	Fade3:
		DEBR C 1 A_Fadeout(0.08)
		loop
	Spawn4:
		DEBR D 6
		goto Fade4
	Fade4:
		DEBR D 1 A_Fadeout(0.08)
		loop
	Death:
		TNT1 A 1
		stop
	}
}

actor RicochetSpawner
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+ALLOWCLIENTSPAWN
	+INVISIBLE
	+NOCLIP
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(Waterlevel == 3, "InWater")
		TNT1 A 1 A_CustomMissile("RicochetSpark",5,0,Random(-360,360), 2, random(-360, 360))
		stop
	InWater:
		TNT1 A 1
		stop
	}
}

Actor RicochetSpark
{
	Radius 0
	Height 1
	Speed  18
	Damage 0
	Health 1
	RenderStyle Add
	Alpha 0.5
	PROJECTILE
	+DOOMBOUNCE
	+CLIENTSIDEONLY
	+DONTSPLASH
    +FORCEXYBILLBOARD
    -ACTIVATEPCROSS
    -ACTIVATEIMPACT
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Playsound("Ricochet")
		ORAN A 7 Bright
		stop
	Death:
		TNT1 A 1 Bright
		TNT1 A 1 Bright
		Stop
	}
}

actor NewBulletPuffSpark
{
	Radius 2
	Height 2
	Speed 0
	RenderStyle Add
	Alpha 1.0
	Scale 0.45
	Gravity 0.3
	Projectile
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+NOTELEPORT
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	-NOGRAVITY
	States
	{
	Spawn:
		ORAN A 0
		ORAN A 0 BRIGHT A_JumpIf(Waterlevel == 3, "Death")
		ORAN A 1 BRIGHT A_FadeOut(0.1)
		goto Spawn+1
	Death:
		TNT1 AA 1
		stop
	}
}

actor NewBulletPuff : BasePuff Replaces BulletPuff
{
	Decal Bulletchip
	RenderStyle Add
	Alpha 1.0
	+PUFFONACTORS
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_GiveToTarget("HitConfirmation", 1)
		stop
	Crash:
		PUFF A 0
		PUFF AAA 0 A_SpawnItemEx("BulletDebris", 0, 0, 0, (random(-200,200)/100.0), (random(-200,200)/100.0), (random(100,600)/100.0), random(-360,360), 128, 76)
		PUFF A 0 A_SpawnItemEX("BulletBlast", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 128)
		PUFF AAA 0 A_SpawnItemEx("NewBulletPuffSpark", 0, 0, 0, (random(-100,100)/100.0), (random(-100,100)/100.0), (random(0,200)/100.0), random(-180,180), 128, 48)
		PUFF A 0 A_SpawnItemEX("RicochetSpawner", 0, 0, 0, 0, 0, 0, 0, 128, 224)
		goto Puff
	Puff:
		PUFF A 2 bright Light("BPUFF1")
		PUFF B 1 bright Light("BPUFF2")
		goto Smoke
	Smoke:
		PUFF B 0 A_Jump(64, "Smoke2")
		PUFF B 0 A_SpawnItemEX("BulletSmoke", 0, 0, 0, 0, 0, 1, 0, SXF_CLIENTSIDE, 0)
		stop
	Smoke2:
		PUFF A 0 A_SpawnItemEX("BulletSmoke2", 0, 0, 0, 0, 0, 1, 0, SXF_CLIENTSIDE, 0)
		stop
	Melee:
		PUFF CD 4
		stop
	}
}

actor NewBulletPuff_NoMarker : NewBulletPuff
{
	States
	{
		Spawn:
			TNT1 A 0
			goto Crash
	}
}

actor ChainsawPuff : NewBulletPuff
{
	States
	{
	Crash:
		PUFF A 0
		PUFF AA 0 A_SpawnItemEx("BulletDebris", 0, 0, 0, (random(-200,200)/100.0), (random(-200,200)/100.0), (random(100,600)/100.0), random(-360,360), 128, 76)
		PUFF AAA 0 A_SpawnItemEx("NewBulletPuffSpark", 0, 0, 0, (random(-100,100)/100.0), (random(-100,100)/100.0), (random(0,200)/100.0), random(-180,180), 128, 48)
		goto Puff
	}
}

actor Tourettes_BulletPuff : NewBulletPuff
{
	DamageType "Tourettes"
}

actor SilverBulletPuff : NewBulletPuff
{
	DamageType "Silver"
	states
	{
	Crash:
		PUFF A 0
		PUFF AAA 0 A_SpawnItemEx("BulletDebris", 0, 0, 0, (random(-200,200)/100.0), (random(-200,200)/100.0), (random(100,600)/100.0), random(-360,360), 128, 76)
		PUFF A 0 A_SpawnItemEX("BulletBlast", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 128)
		PUFF AAA 0 A_SpawnItemEx("NewBulletPuffSpark", 0, 0, 0, (random(-100,100)/100.0), (random(-100,100)/100.0), (random(0,200)/100.0), random(-180,180), 128, 48)
		PUFF A 0 A_SpawnItemEX("RicochetSpawner", 0, 0, 0, 0, 0, 0, 0, 128, 224)
		goto Puff
	Puff:
		PUFS A 2 bright
		PUFS B 1 bright
		goto Smoke
	}
}

actor SilverBulletPuff2 : SilverBulletPuff
{
	obituary "%o was torn apart by %k's .454 Casull Rounds"
}

actor SniperPuff : NewBulletPuff
{
	DamageType "Sniper"
}

actor MinigunPuff : NewBulletPuff
{
	+ISMONSTER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("MinigunTracerSpawner", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			goto "Super::Spawn"
		Crash:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("MinigunTracerSpawner", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			goto "Super::Crash"
	}
}

actor SSGPuff : NewBulletPuff
{
	DamageType "SSG"
	+ISMONSTER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("PelletTracerSpawner", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			goto "Super::Spawn"
		Crash:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("PelletTracerSpawner", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			goto "Super::Crash"
	}
}

actor QuadPuff : SSGPuff
{
	DamageType "QuadSSG"
}

actor ShotgunPuff : SSGPuff
{
	DamageType "HeadBurst"
}

actor FistPuff : BulletPuff
{
	DamageType "Fist"
	SeeSound "*fist"
	AttackSound "BootHit"
}

actor KickPuff : FistPuff
{
	+PUFFGETSOWNER
	obituary "%k kicked %o's ass."
	States
	{
		Melee:
		PUFF C 0 A_PlaySound("DukeKick")
		PUFF CD 2
		stop
	}
}

actor FistPuff2 : BulletPuff
{
	SeeSound "*fist"
	AttackSound "BootHit"
}

actor FistPuff3 : BulletPuff
{
	DamageType "Fist"
	SeeSound "*fist"
	AttackSound "skeleton/melee"
}

actor FistPuff4 : BulletPuff
{
	SeeSound "*fist"
	AttackSound "skeleton/melee"
}

actor KickPuff2 : FistPuff2
{
	+PUFFGETSOWNER
	obituary "%k kicked %o's ass."
	States
	{
		Melee:
		PUFF C 0 A_PlaySound("DukeKick")
		PUFF CD 2
		stop
	}
}

actor BootHit
{
	radius 1
	height 1
	+PUFFONACTORS
	States
	{
	Spawn:
	 TNT1 A 0
	 TNT1 A 1 A_Playsound("BootHit")
	 stop
	}
}

ACTOR HeadLight
{
   +PUFFONACTORS
   +NOBLOCKMAP
   +BLOODLESSIMPACT
   +DROPOFF
   +DONTSPLASH
   +NOINTERACTION
   +ALLOWCLIENTSPAWN
   -BLOODSPLATTER
   States
   {
   Spawn:
	  NULL A 2 BRIGHT
	  stop
   }
}

actor SaberSpark
{
	Radius 2
	Height 2
	Scale 0.5
	RenderStyle Add
	Alpha 1.0
	+MISSILE
	+NOBLOCKMAP
	+DROPOFF
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			SPRK A 2 BRIGHT
			SPRK B 1 BRIGHT
			SPRK C 1 BRIGHT
			SPRK D 1 BRIGHT
			Stop
		Death:
			TNT1 AA 1
			stop
	}
}

ACTOR SaberPuff
{
   DamageType "Saber"
   RenderStyle Add
   Alpha 1.0
   Scale 0.5
   +NOBLOCKMAP
   +DROPOFF
   +THRUGHOST
   +GHOST
   +NODAMAGETHRUST
   +FORCEXYBILLBOARD
   +DONTSPLASH
   States
   {
   Spawn:
	  SPRK A 0
	  SPRK A 0 A_Jump(100, "SparkSound")
	  SPRK A 2 BRIGHT A_SpawnItemEx("SaberSpark", 0, 0, 0, random(-4,4), random(-4,4), random(0, 4), random(-180, 180), SXF_CLIENTSIDE, 0)
	  SPRK B 2 BRIGHT A_SpawnItemEx("SaberSpark", 0, 0, 0, random(-4,4), random(-4,4), random(0, 4), random(-180, 180), SXF_CLIENTSIDE, 0)
	  SPRK C 2 BRIGHT
	  SPRK D 2 BRIGHT
	  Stop
   SparkSound:
	  SPRK A 0 BRIGHT A_PlaySound("saberhit", CHAN_AUTO)
	  SPRK AAAAAAA 0 A_SpawnItemEx("SaberSpark", 0, 0, 0, random(-8,8), random(-8,8), random(0, 8), random(-180, 180), SXF_CLIENTSIDE, 0)
	  goto Spawn+2
   }
}

actor DukeShotgunBulletPuff : NewBulletPuff
{
	+PUFFGETSOWNER
	obituary "%o ate %k's load."
	DamageType "HeadBurst"
}

actor SPASPuff : NewBulletPuff
{
	obituary "%o was violated by %k's SPAS-12."
	DamageType "HeadBurst"
}

actor FreezeSparks : BaseEffect
{
	Scale 0.5
	RenderStyle Add
	Alpha 1.0
	States
	{
		Spawn:
			BOLT ABCDEFGHIJKL 1 BRIGHT A_FadeOut(0.05)
			stop
	}	
}

actor ElectricSnowflake : BaseEffect
{
	Scale 0.05
	RenderStyle Translucent
	Alpha 0.8
	States
	{
		Spawn:
			SFLK ABCDEFGHIJKLMNOP 2 A_FadeOut(0.05)
			loop
	}	
}

ACTOR FreezeShock : BasePuff
{
	Scale 0.75
	Alpha 0.75
	Renderstyle Add
	obituary "%o suffered from cold shock due to %k's freezebolt!"
	+PUFFONACTORS
	+BLOODLESSIMPACT
	+PIERCEARMOR
	+ISMONSTER
	-BLOODSPLATTER
	DamageType "Ice"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			TNT1 A 0 A_SpawnItemEx("BoltTracerSpawner", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			goto Crash
		Crash:
			BOLT A 0
			BOLT A 0 A_SpawnItemEx("BoltTracerSpawner", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			BOLT A 0 A_Playsound("Freezehit")
			BOTT AAAA 0 BRIGHT A_SpawnItemEx("ElectricSnowflake", 0, 0, 0, (random(-200,200)/100.0), (random(-200,200)/100.0), (random(-200,200)/100.0), random(-180,180), SXF_CLIENTSIDE, 96)
			BOLT ABCDEFGHIJKL 1 BRIGHT A_SpawnItemEx("FreezeSparks", 0, 0, 0, (random(-400,400)/100.0), (random(-400,400)/100.0), (random(-400,400)/100.0), random(-180,180), SXF_CLIENTSIDE, 128)
			Stop
	}
}

actor BlazeFlames : BasicFlame
{
	Scale 0.25
	+NOINTERACTION
	States
	{
		Death:
			TNT1 AA 1
			stop
	}
}

actor BlazePuff : BasePuff
{
	Decal BlazeChip
	Height 4
	Scale 0.3
	DamageType "Fire"
	+PUFFONACTORS
	+ISMONSTER
	States
	{
		Spawn:
  			TNT1 A 0
  			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
  			goto Crash
		Crash:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("vile/firecrkl")
			TNT1 A 0 A_SpawnItemEx("FireTracerSpawner", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
			TNT1 A 1 A_SpawnItemEx("BlazeFlames", Random(-4,4), Random(-4,4), 0, 0, 0, (Random(1,150)/100.0), 0, SXF_CLIENTSIDE, 0)
			TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("BlazeFlames", Random(-4,4), Random(-4,4), 0, 0, 0, (Random(1,150)/100.0), 0, SXF_CLIENTSIDE, 200)
			stop
	}
}

ACTOR BlitzShock : BasePuff
{
    Decal BlitzChip
	Scale 0.5
	Alpha 0.9
	Renderstyle Add
	obituary "%o was zapped by %k's blitz ssg!"
	+PUFFONACTORS
	DamageType "Electric"
	States
	{
	Spawn:
  		TNT1 A 0
  		TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
  		goto Crash
	Crash:
		YBOL A 0
		YBOL A 0 A_Playsound("Blitzhit")
		YBOL ABCDEFGHIJKL 2 BRIGHT
		Stop
	}
}

actor FrostPuff : BlazePuff
{
	Decal BulletChip
	obituary "%o was frozen to the core by %k's Blizzard SSG."
	DamageType "Ice"
	Scale 0.15
	States
	{
 	Spawn:
  		TNT1 A 0
  		TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
  		goto Crash
 	Crash:
 		TNT1 A 0
 		TNT1 A 0 A_PlaySound("IceShot")
		TNT1 A 0 A_SpawnItemEx("FrostTracerSpawner", 0, 0, 0, 0, 0, 0, 0,  SXF_SETMASTER | SXF_TRANSFERPOINTERS, 0)
 		FRZE ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
		FRZ2 ABCDEFGHI 1 BRIGHT
		stop
	}
}

actor SonicWaveBlast
{
  renderstyle add
  +PUFFONACTORS
  +FIXMAPTHINGPOS
  +NOGRAVITY
  +EXTREMEDEATH
  +PUFFGETSOWNER
//	+FORCERADIUSDMG
  +FORCEXYBILLBOARD
  obituary "%o was blasted to bits by %k's sonic wave cannon."
  DamageType "Obliterate"
  alpha 0.9
  Scale 1.4
  states
  {
  Spawn:
	CEXP A 0
	CEXP A 0 A_PlaySound("weapons/grenlx")
	CEXP A 0 A_Explode(500, 200)
	CEXP AAAAAAA 0 A_SpawnItemEX("SonicWaveFire", Random(-8, 8), Random(-8, 8), Random(-8, 8), Random(-5, 5), Random(-5, 5), Random(-5, 5), Random(-360, 360), 128, 0)
	CEXP AAAAAAA 0 A_SpawnItemEX("SonicWaveMist", 0, 0, 0, Random(-12, 12), Random(-12, 12), Random(-12, 12), Random(-360, 360), 128, 0)
	CEXP ABC 3 bright A_SpawnItemEX("SonicWaveFire", Random(-8, 8), Random(-8, 8), Random(-8, 8), Random(-7, 7), Random(-7, 7), Random(-7, 7), Random(-360, 360), 128, 0)
	stop
  }
}

actor SonicWaveFire
{
 Height 1
 Radius 1
 Speed 3
 RenderStyle Add
 Alpha 0.9
 +NOGRAVITY
 +FORCEXYBILLBOARD
 States
 {
 Spawn:
 CEXP AABBCC 2 BRIGHT A_FadeOut
 stop
 }
}

actor SonicWaveMist
{
	Projectile
	Radius 1
	Height 1
	Damage 0
	Speed 10
	Scale 0.4
	RenderStyle Add
	Alpha 1.0
	+FORCEXYBILLBOARD
	+DONTSPLASH
	-BLOODSPLATTER
	+BLOODLESSIMPACT
	States
	{
	Spawn:
		TNT1 A 0
		BLSE ABCDE 1 BRIGHT
		stop
	Death:
		TNT1 A 0
		TNT1 A 1
		stop
	}
}

actor LinkSwordSpark : RicochetSpark
{
 +FORCEXYBILLBOARD
 States
 {
 Spawn:
		TNT1 A 0
		ORAN A 7
		stop
 }
}

actor MasterSwordHit
{
 Radius 8
 Height 8
 RenderStyle Add
 Alpha 0.8
 Scale 0.4
 DamageType "Holy"
 obituary "%o was slain by %k's master sword."
 +NOGRAVITY
 +PUFFONACTORS
 +FORCEXYBILLBOARD
 AttackSound LinkSwordWall
 SeeSound LinkSwordHit
 States
 {
  Crash:
  TNT1 A 0
  TNT1 AAAAAAAAA 0 A_CustomMissile("LinkSwordSpark",5,0,Random(-360,360), 2, random(-360, 360))
  Spawn:
  HLPS DEFGH 2 BRIGHT
  stop
 }
}

actor LeeroyBulletPuff : NewBulletPuff
{
	DamageType "Leeroy"
	-BLOODSPLATTER
	+BLOODLESSIMPACT
}