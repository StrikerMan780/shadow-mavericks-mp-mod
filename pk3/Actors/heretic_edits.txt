ACTOR SMMP_HereticImpBall : HereticImpBall replaces HereticImpBall
{
	Speed 30
	FastSpeed 50
	Damage 2
	Alpha 1.0
	RenderStyle Add
	States
	{
	Spawn:
		FX10 AABBCC 1 Bright A_SpawnItemEx("HereticImpBallTrail", 0, 0, 0, random(-1,1), random(-1,1), random(-1,1), random(-180,180), SXF_CLIENTSIDE, 0)
		Loop
	Death:
		FX10 DEFG 5 Bright
		Stop
	}
}

actor HereticImpBallTrail
{
	Radius 2
	Height 2
	Alpha 1.0
	Renderstyle Add
	+NOGRAVITY
	+NOBLOCKMAP
	+NOINTERACTION
	States
	{
		Spawn:
			FX10 DEFG 1 Bright
			stop
	}
}

actor SM_HereticImp : HereticImp
{
	+FLOATBOB
	DropItem "HealthBonus2", 48
	States
	{
		Melee:
			IMPX DE 4 A_FaceTarget
			IMPX F 6 A_CustomMeleeAttack(random[ImpMeAttack](5,12), "himp/attack", "himp/attack")
			Goto See
		Missile:
			IMPX A 0 A_Jump(75, "Throw")
			IMPX A 8 A_FaceTarget
			IMPX B 6 A_SkullAttack(12)
			IMPX CBAB 6
			Goto Missile+3
		Throw:
			IMPX DE 4 A_FaceTarget
			IMPX F 6 A_CustomComboAttack("HereticImpBall", 32, random[ImpMsAttack2](5,12), "himp/leaderattack")
			Goto See
		Death:
			IMPX S 0 A_ChangeFlag("FLOATBOB", 0)
			IMPX G 4 A_ImpDeath
			IMPX H 5
			Wait
		XDeath:
			IMPX S 0 A_ChangeFlag("FLOATBOB", 0)
			IMPX S 5 A_ImpXDeath1
			IMPX TU 5
			IMPX V 5 A_Gravity
			IMPX W 5 
			Wait
		Crash:
			IMPX I 7 A_ImpExplode
			IMPX J 7 A_ScreamAndUnblock
			IMPX K 7
			IMPX L 10500
			goto Fade
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
		XCrash:
			IMPX X 7
			IMPX Y 7 A_NoBlocking
			IMPX Z 10500
			goto Fade
	}
}

actor SM_HereticImpLeader : SM_HereticImp replaces HereticImpLeader
{
	Health 80
	PainChance 128
	Speed 12
	-MISSILEMORE
	+MISSILEEVENMORE
	+FLOATBOB
	AttackSound "himp/attack"
	DropItem "HealthBonus2", 64
	States
	{
		Melee:
			IMPX DE 3 A_FaceTarget
			IMPX F 5 A_CustomMeleeAttack(random[ImpMeAttack](5,12), "himp/attack", "himp/attack")
			Goto See
		Missile:
			IMPX A 0 A_Jump(132, "Throw")
			IMPX A 6 A_FaceTarget
			IMPX B 6 A_SkullAttack(12)
			IMPX CBAB 6
			Goto Missile+3
		Throw:
			IMPX DE 3 A_FaceTarget
			IMPX F 5 A_CustomMissile("HereticImpBall", 32, 0, 0, CMF_AIMOFFSET) 
			IMPX DE 3 A_FaceTarget
			IMPX F 5 A_CustomMissile("HereticImpBall", 32, 0, frandom(-2.0, 2.0), CMF_AIMOFFSET, frandom(-2.0, 2.0)) 
			IMPX DE 3 A_FaceTarget
			IMPX F 5 A_CustomMissile("HereticImpBall", 32, 0, frandom(-2.0, 2.0), CMF_AIMOFFSET, frandom(-2.0, 2.0)) 
			Goto See
	}
}

actor Float_Fleshspawn : Fleshspawn
{
	+SPAWNFLOAT
}

actor SM_Mummy : Mummy
{
	hitobituary "%o was beaten down by a flesh golem"
	obituary "%o was flayed by a flesh golem"
	PainChance 96
	
	States
	{
		See:
			MUMM AABBCCDD 2 A_Chase
			Loop
		Melee:
			MUMM E 5 A_FaceTarget
			MUMM F 5 A_CustomMeleeAttack(random[MummyAttack](1,8)*2, "mummy/attack2", "mummy/attack")
			MUMM G 5
			Goto See
		Death:
			MUMM I 5
			MUMM JJJJJJJJJJ 0 A_SpawnItemEx("Fleshy_Splatter", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(24.0, 48.0), MOMX+frandom(-8.0, 8.0), MOMY+frandom(-8.0, 8.0), MOMZ+frandom(2.0, 12.0), Random(-180, 180), SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 64)
			MUMM J 5 A_Scream
			MUMM K 5 A_SpawnItemEx("MummySoul", 0,0,10, 0,0,1)
			MUMM L 5
			MUMM M 5 A_NoBlocking
			MUMM NO 5
			MUMM P 10500
			goto Fade
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

actor SM_MummyLeader : SM_Mummy 
{
	Health 125
	PainChance 64
	States
	{
		Missile:
			MUMM X 3 A_FaceTarget
			MUMM Y 3 Bright A_FaceTarget
			MUMM X 3 A_FaceTarget
			MUMM Y 3 Bright A_FaceTarget
			MUMM X 3 A_FaceTarget
			MUMM Y 3 Bright A_CustomComboAttack("MummyFX1", 32, random[MummyAttack2](1,8)*2, "mummy/attack2")
			Goto See
	}
}

ACTOR SM_Knight : Knight
{
	Health 200
	Radius 24
	Height 64
	Speed 8
	Scale 0.88
	Obituary "%o was slain by a fallen crusader."
	HitObituary "%o was hacked to pieces by a fallen crusader."
	DropItem "HealthBonus2", 64
	DropItem "ArmorBonus2", 64
	DropItem "MaxHealthBonus2", 48
	DropItem "MaxArmorBonus2", 48
	States
	{
		Spawn:
			KNIG AB 10 A_Look
			Loop
		See:
			KNIG AABBCCDD 2 A_Chase
			Loop
		Melee:
			KNIG E 8 A_FaceTarget
			KNIG F 4 A_FaceTarget
			KNIG G 6 A_KnightAttack
			Goto See
		Missile:
			KNIG E 0 A_Jump(32, "Missile2")
			KNIG E 8 A_FaceTarget
			KNIG F 4 A_FaceTarget
			KNIG G 6 A_KnightAttack
			Goto See
		Missile2:
			KNIG E 8 A_FaceTarget
			KNIG F 4 A_FaceTarget
			KNIG G 6 A_KnightAttack
			KNIG E 8 A_FaceTarget
			KNIG F 4 A_FaceTarget
			KNIG G 6 A_KnightAttack
			KNIG E 8 A_FaceTarget
			KNIG F 4 A_FaceTarget
			KNIG G 6 A_KnightAttack
			goto See
		Pain:
			KNIG H 3
			KNIG H 3 A_Pain
			KNIG H 0 A_Jump(64, "Missile")
			Goto See
		Death:
			KNIG I 6
			KNIG J 6 A_Scream
			KNIG K 4
			KNIG L 4 A_NoBlocking
			KNIG MN 4
			KNIG O 10500
			goto Fade
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

actor SM_KnightGhost : SM_Knight replaces KnightGhost
{
	Speed 6
	RenderStyle Add
	Alpha 0.4
	BloodType "GhostFire"
	+NOBLOODDECALS
	States
	{
		Death:
			KNIG I 6
			KNIG J 6 A_Scream
			KNIG K 0 A_NoBlocking
			KNIG K 4 A_SetTranslucent(0.35)
			KNIG L 4 A_SetTranslucent(0.30)
			KNIG M 4 A_SetTranslucent(0.25)
			KNIG N 4 A_SetTranslucent(0.20)
			KNIG O 2 A_SetTranslucent(0.15)
			KNIG O 2 A_SetTranslucent(0.10)
			Stop
	}
}

actor FallenCrusaderAxe replaces KnightAxe
{
  Speed 15
  FastSpeed 20
  Damage 3
  Projectile
  -NOBLOCKMAP
  -ACTIVATEIMPACT
  -ACTIVATEPCROSS
  +WINDTHRUST
  +FORCEXYBILLBOARD
  DeathSound "hknight/hit"
  States
  {
  Spawn:
    SPAX A 2 A_PlaySoundEx("hknight/axewhoosh", "Auto", 0)
    SPAX BC 2
    Loop
  Death:
	SPAX D 0 A_SetTranslucent(0.75, 0)
    SPAX DEF 2 A_SpawnItemEx("CrusaderAxeBurst", 0, 0, 0, (random(-400,400)/100.0), (random(-400,400)/100.0), (random(-400,400)/100.0), random(-180,180), SXF_CLIENTSIDE, 0)
    Stop
  }
}

actor FallenCrusaderSuperAxe : FallenCrusaderAxe replaces RedAxe
{
	Speed 20
	FastSpeed 25
	Damage 7
	SeeSound "hknight/superattack"
	States
	{
		Spawn:
			SPAX A 0 A_PlaySoundEx("hknight/axewhoosh", "Auto", 0)
			SPAX ABC 2 A_SpawnItemEx("BloodSprayParticle", 0, 0, 0, 0, 0, random(-4, 4), 0, SXF_CLIENTSIDE, 0)
			Loop
		Death:
			SPAX D 0 A_SetTranslucent(0.75, 0)
			SPAX DDD 0 A_SpawnItemEx("Striker_FlyingBlood", 0, 0, 0, (random(-800,800)/100.0), (random(-800,800)/100.0), (random(100,800)/100.0), random(-180,180), SXF_CLIENTSIDE, 0)
			SPAX DDD 0 A_SpawnItemEx("CrusaderAxeBurst", 0, 0, 0, (random(-400,400)/100.0), (random(-400,400)/100.0), (random(-400,400)/100.0), random(-180,180), SXF_CLIENTSIDE, 0)
			SPAX DEF 2 A_SpawnItemEx("CrusaderAxeBurst", 0, 0, 0, (random(-400,400)/100.0), (random(-400,400)/100.0), (random(-400,400)/100.0), random(-180,180), SXF_CLIENTSIDE, 0)
			Stop
	}
}

actor SMMP_Pod : Pod replaces Pod
{
	States
	{
		Death:
			PPOD B 5 A_RemovePod
			PPOD C 4 A_Scream
			PPOD D 0 radius_quake(3, 16, 0, 3, 0)
			PPOD D 4 A_Explode
			PPOD E 0 A_Jump(64, "Death2")
			PPOD EEE 0 A_SpawnItemEx ("Striker_FlyingBlood", 0, 0, 24, MOMX+Random(-10, 10), MOMY+Random(-10,10), MOMZ+Random(2, 20), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
			PPOD EEEEEEEE 0 A_SpawnItemEx("Fleshy_Splatter", 0, 0, 24, MOMX+random(-2, 2), MOMY+random(-2, 2), MOMZ+random(1, 14), Random(-180, 180), SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
			PPOD E 3
			PPOD F 2
			Stop
		Death2:
			PPOD E 0 A_XScream
			PPOD E 3 A_SpawnItemEx("XDeathGibs", 0, 0, 16, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0) 
			PPOD F 2
			Stop
	}
}

actor FirebornBlur : BaseEffect
{
	Alpha 0.75
	States
	{
		Spawn:
			TNT1 A 2
			CLNK AABBCCDD 1 A_FadeOut(0.09375)
			stop
	}
} 

actor FirebornFlames : IgnitedSurface
{
	obituary "%o trodden on the remains of a Fireborn."
	Speed 4
	States
	{
	Spawn:
		TFIR A 0 A_CountDown
		TNT1 A 0 A_SpawnItemEx("BasicFlame", random(-10,10), Random(-10,10), 0, 0, 0, Random(1, 2), 0, 128, 64)
		TNT1 A 2 A_Explode(8, 24, 1)
		TNT1 A 0 A_JumpIf(waterlevel == 3, "Disappear")
		loop
	}
}

actor Fireborn 22121
{
	//$Category SMMP Monsters
	//$Title Fireborn
	//$Color 12
	Health 100
	Radius 20
	Height 64
	Mass 120
	Speed 10
	PainChance 64
	DamageFactor "Fire", 0.25
	MinMissileChance 128
	MeleeThreshold 128
	MaxTargetRange 256
	BloodType "FirebornBlood"
	Monster
	+FLOORCLIP
	+DONTRIP
	SeeSound "clink/sight"
	PainSound "clink/pain"
	DeathSound "clink/death"
	ActiveSound "clink/active"
	Obituary "%o was immolated by a Fireborn."
	HitObituary "%o was cut down by a Fireborn."
	DropItem "CellAmmo", 84, 20
	DropItem "ProtonAmmo", 84, 8
	DropItem "MaxArmorBonus2", 84
	DropItem "MaxHealthBonus2", 32
	States
	{
		Spawn:
			CLNK A 0 A_Look
			CLNK A 6 A_Wander
			CLNK B 0 A_Look
			CLNK B 6 A_Wander
			CLNK C 0 A_Look
			CLNK C 6 A_Wander
			CLNK D 0 A_Look
			CLNK D 6 A_Wander
			Loop
		See:
			CLNK AABBCCDD 2 A_Chase
			CLNK A 0 A_JumpIf(Health <= 50, "GoBerserk")
			Loop
		GoBerserk:
			CLNK A 0 A_PlaySound("clink/sight", CHAN_AUTO)
			CLNK AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("FirebornChunk", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(24.0, 48.0), frandom(-10.0, 10.0), frandom(-10.0, 10.0), frandom(2.0, 16.0), random(-180, 180), SXF_CLIENTSIDE, 16)
			CLNK AAAAAAAA 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(2.0, 4.0), random(-180, 180), SXF_CLIENTSIDE, 0)
			CLNK A 0 A_SpawnItemEx("Fireborn_PissedOff", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS | SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION, 0)
			CLNK A 0 A_Die("RemoveSelf")
			stop
		Death.RemoveSelf:
			CLNK A 0
			CLNK A 0
			stop
		Melee:
			CLNK E 0 A_JumpIf(Health <= 50, "GoBerserk")
			CLNK E 0 A_PlaySound("clink/attack", CHAN_AUTO)
			CLNK E 3 A_FaceTarget
			CLNK E 0 A_Recoil(-2)
			CLNK F 3 A_FaceTarget
			CLNK G 4 A_CustomMeleeAttack(10, "LinkSwordHit")
			Goto See
		Missile:
			CLNK A 0 A_FaceTarget
			CLNK A 0 A_Recoil(-8)
			CLNK AABB 1 A_SpawnItemEx("FirebornBlur", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			CLNK C 0 A_Chase("Melee", "", CHF_DONTMOVE)
			CLNK C 0 A_FaceTarget
			CLNK C 0 A_Recoil(-8)
			CLNK CCDD 1 A_SpawnItemEx("FirebornBlur", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			CLNK A 0 A_Chase("Melee", "", CHF_DONTMOVE)
			CLNK A 0 A_FaceTarget
			CLNK A 0 A_Recoil(-8)
			CLNK AABB 1 A_SpawnItemEx("FirebornBlur", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			CLNK C 0 A_Chase("Melee", "", CHF_DONTMOVE)
			CLNK C 0 A_FaceTarget
			CLNK C 0 A_Recoil(-8)
			CLNK CCDD 1 A_SpawnItemEx("FirebornBlur", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			CLNK A 0 A_Chase("Melee", "", CHF_DONTMOVE)
			CLNK A 0 A_FaceTarget
			CLNK A 0 A_Recoil(-8)
			goto See
		Pain:
			CLNK H 2
			CLNK H 2 A_Pain
			CLNK H 0 A_FastChase
			Goto See
		Death:
			CLNK IJ 6
			CLNK K 5 A_Scream
			CLNK L 0 A_PlaySound("world/barrelx", CHAN_AUTO)
			CLNK LLLL 0 A_SpawnItemEx("FirebornFlames", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(24.0, 48.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(1.0, 6.0), random(-180, 180), SXF_TRANSFERPOINTERS, 64)
			CLNK LLLLLLLLLLLLL 0 A_SpawnItemEx("FirebornLava", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(24.0, 48.0), frandom(-8.0, 8.0), frandom(-8.0, 8.0), frandom(2.0, 12.0), random(-180, 180), SXF_CLIENTSIDE, 64)
			CLNK LLLLLLLLLLLLL 0 A_SpawnItemEx("FirebornChunk", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(24.0, 48.0), frandom(-10.0, 10.0), frandom(-10.0, 10.0), frandom(2.0, 16.0), random(-180, 180), SXF_CLIENTSIDE, 64)
			CLNK L 5 A_NoBlocking
			CLNK MN 5
			CLNK O 10500
			goto Fade
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

actor Fireborn_PissedOff : Fireborn
{
	Health 75
	Speed 12
	DamageFactor "Fire", 0.0
	PainChance 32
	MinMissileChance 80
	MaxTargetRange 512
	Obituary "%o was immolated by a pissed-off Fireborn."
	HitObituary "%o was sheared in two and cauterized by a Fireborn."
	-DONTRIP
	States
	{
		Spawn:
			CLN2 AA 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 A 0 A_Look
			CLN2 A 6 A_Wander
			CLN2 BB 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 B 0 A_Look
			CLN2 B 6 A_Wander
			CLN2 CC 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 C 0 A_Look
			CLN2 C 6 A_Wander
			CLN2 DD 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 D 0 A_Look
			CLN2 D 6 A_Wander
			Loop
		See:
			CLN2 AA 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 AA 2 A_Chase
			CLN2 BB 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 BB 2 A_Chase
			CLN2 CC 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 CC 2 A_Chase
			CLN2 DD 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 DD 2 A_Chase
			Loop
		Melee:
			CLN2 E 0 A_PlaySound("clink/attack", CHAN_AUTO)
			CLN2 E 3 A_FaceTarget
			CLN2 E 0 A_Recoil(-2)
			CLN2 F 3 A_FaceTarget
			CLN2 G 4 A_CustomMeleeAttack(20, "LinkSwordHit")
			Goto See
		Missile:
			CLN2 A 0 A_FaceTarget
			CLN2 A 0 A_Recoil(-8)
			CLN2 AA 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 AABB 1 A_SpawnItemEx("FirebornBlur", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			CLN2 C 0 A_Chase("Melee", "", CHF_DONTMOVE)
			CLN2 C 0 A_FaceTarget
			CLN2 C 0 A_Recoil(-8)
			CLN2 CC 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 CCDD 1 A_SpawnItemEx("FirebornBlur", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			CLN2 A 0 A_Chase("Melee", "", CHF_DONTMOVE)
			CLN2 A 0 A_FaceTarget
			CLN2 A 0 A_Recoil(-8)
			CLN2 AA 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 AABB 1 A_SpawnItemEx("FirebornBlur", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			CLN2 C 0 A_Chase("Melee", "", CHF_DONTMOVE)
			CLN2 C 0 A_FaceTarget
			CLN2 C 0 A_Recoil(-8)
			CLN2 CC 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			CLN2 CCDD 1 A_SpawnItemEx("FirebornBlur", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			CLN2 A 0 A_Chase("Melee", "", CHF_DONTMOVE)
			CLN2 A 0 A_FaceTarget
			CLN2 A 0 A_Recoil(-8)
			CLN2 AA 0 A_SpawnItemEx("BasicFlame", frandom(-16.0, 16.0), frandom(-16.0, 16.0), frandom(0.0, 56.0), 0, 0, frandom(1.0, 2.0), 0, SXF_CLIENTSIDE, 0)
			goto See
		Pain:
			CLN2 H 2
			CLN2 H 2 A_Pain
			CLN2 H 0 A_FastChase
			Goto See
	}
}

ACTOR SuperBeast : Beast replaces Beast
{
	Health 250
	Radius 32
	Height 74
	Mass 400
	Speed 14
	Painchance 100
	Monster
	+FLOORCLIP
	+DONTHARMCLASS
	+DONTHARMSPECIES
	SeeSound "beast/sight"
	AttackSound "beast/attack"
	PainSound "beast/pain"
	DeathSound "beast/death"
	ActiveSound "beast/active"
	Obituary "%o was incinerated by a superbeast."
	HitObituary "%o was clocked in the head by a superbeast."
	DropItem "CellAmmo", 90
	States
	{
	Spawn:
		BEAS AB 10 A_Look
		Loop
	See:
		BEAS A 0 A_PlaySound("beast/walk", CHAN_AUTO)
		BEAS A 0 A_Quake(1, 4, 0, 320, "")
		BEAS AB 3 A_Chase
		BEAS C 0 A_PlaySound("beast/walk", CHAN_AUTO)
		BEAS C 0 A_Quake(1, 4, 0, 320, "")
		BEAS CDEF 3 A_Chase
		Loop
	Missile:
		BEAS H 10 A_FaceTarget
		BEAS I 0 A_FaceTarget
		BEAS I 0 A_CustomMissile ("BeastBall2", 48, 0, frandom(-4.0, 4.0), CMF_AIMOFFSET | CMF_OFFSETPITCH, frandom(-2.0, 2.0))
		BEAS I 5 A_CustomMissile ("BeastBall2", 48, 0, frandom(-4.0, 4.0), CMF_AIMOFFSET | CMF_OFFSETPITCH, frandom(-2.0, 2.0))
		BEAS I 0 A_FaceTarget
		BEAS I 0 A_CustomMissile ("BeastBall2", 48, 0, frandom(-4.0, 4.0), CMF_AIMOFFSET | CMF_OFFSETPITCH, frandom(-2.0, 2.0))
		BEAS I 5 A_CustomMissile ("BeastBall2", 48, 0, frandom(-4.0, 4.0), CMF_AIMOFFSET | CMF_OFFSETPITCH, frandom(-2.0, 2.0))
		Goto See
	Pain:
		BEAS G 3
		BEAS G 3 A_Pain
		Goto See
	Death:
		BEAS R 5
		BEAS S 5 A_Scream
		BEAS TUV 5
		BEAS W 0 A_Quake(2, 8, 0, 512, "")
		BEAS W 0 A_PlaySound("beast/crash", CHAN_AUTO)
		BEAS W 4 A_NoBlocking
		BEAS XY 4
		BEAS Z -1
		Stop
	XDeath:
		BEAS J 5
		BEAS K 6 A_Scream
		BEAS L 5
		BEAS M 6
		BEAS N 5
		BEAS O 6 A_NoBlocking
		BEAS P 5
		BEAS Q -1
		Stop
	}
}	

ACTOR BeastBall2 : BeastBall replaces BeastBall
{
	Speed 15
	FastSpeed 20
	Damage (5)
	Projectile
	DamageType "Fire"
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	-NOBLOCKMAP
	-SPAWNSOUNDSOURCE
	+WINDTHRUST
	+DONTHARMSPECIES
	RenderStyle Add
	SeeSound "beast/attack"
	DeathSound "MagmaHit"
	States
	{
	Spawn:
		FRB1 A 0 BRIGHT A_SpawnItemEx("BasicFlame", 0, 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), 0, SXF_CLIENTSIDE, 75)
		FRB1 ABC 1 BRIGHT A_SpawnItemEx("BeastBallPuff", 0, 0, 0, frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(-1.0,1.0),0,SXF_ABSOLUTEPOSITION, 32)
		Loop
	Death:
		FRB1 DDDDDDDDDDD 0 A_SpawnItemEx("BeastBallPuff", 0, 0, 0, frandom(-3.0,3.0),frandom(-3.0,3.0),frandom(-3.0,3.0),0,SXF_ABSOLUTEPOSITION, 0)
		FRB1 DDDDD 0 A_SpawnItemEx("BasicFlame", 0, 0, 0, frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), 0, SXF_CLIENTSIDE, 0)
		FRB1 D 0 A_Quake(2, 8, 0, 150, "")
		FRB1 D 0 A_Explode(16, 64, 0, false, 32) 
		FRB1 DE 4 BRIGHT A_SpawnItemEx("BasicFlame", 0, 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), 0, SXF_CLIENTSIDE, 0)
		FRB1 FGH 4
		Stop
	}
}

ACTOR BeastBallPuff : Puffy replaces Puffy
{
	Radius 6
	Height 8
	Speed 10
	+NOBLOCKMAP
	+NOGRAVITY
	+MISSILE
	+NOTELEPORT
	+DONTSPLASH
	RenderStyle Add
	States
	{
	Spawn:
		FRB1 DE 2 BRIGHT
		FRB1 FGH 2
		Stop
	}
}