actor BetterBlood : Blood replaces Blood
{
 +FORCEXYBILLBOARD
 +CLIENTSIDEONLY
 RenderStyle Translucent
 Alpha 0.7
 game Doom
 states
 {
 Spawn:
  BLUD A 0
  BLUD A 0 A_SpawnItemEX("bloodspray", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION, 156)
  BLUD A 0 A_SpawnItemEX("bloodmist", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION, 232)
  BLUD A 0 A_SpawnItemEx("Striker_FlyingBlood", 0, 0, 0, frandom(1.0, 4.0), frandom(1.0, 4.0), frandom(1.0, 12.0), Random(-180, 180), SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION, 186)
  BLUD A 0 A_SpawnItemEx("Fleshy_Splatter", 0, 0, 0, frandom(1.0, 2.0), frandom(1.0, 2.0), frandom(1.0, 8.0), Random(-180, 180), SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION, 224)
  BLUD A 0 A_PlaySound("FleshImpact")
  BLUD C 8
  BLUD B 8
  BLUD A 8
  TNT1 A 0
  stop
 }
}

actor Striker_FlyingBlood
{
 Game Doom
 Scale 0.7
 Health 1
 Radius 4
 Height 1
 Mass 1
 ReactionTime 160
 RenderStyle Translucent
 Alpha 0.56
 +CORPSE
 +NOTELEPORT
 +NOBLOCKMAP
 +DONTSPLASH
 +NOBLOCKMONST
 +FORCEXYBILLBOARD
 -ACTIVATEIMPACT
 -ACTIVATEPCROSS
 States
 {
 Spawn:
  TNT1 A 0 A_CountDown
  BLUD C 1 A_SpawnItemEX("Striker_FlyingBloodTrail", 0, 0, 0, frandom(-1.0,1.0), frandom(-1.0,1.0), frandom(-1.0,1.0), 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION, 0)
  Loop
 Crash:
  TNT1 A 1 A_SpawnItemEX("Striker_BloodSpot", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION, 0)
  TNT1 A 0
  Stop
 }
}

actor Striker_BloodSpot
{
 Game Doom
 Radius 1
 Height 1
 Mass 1
 Scale 0.7
 RenderStyle Translucent
 Alpha 0.6
 +NOTELEPORT
 +NOBLOCKMAP
 +MOVEWITHSECTOR
 +CORPSE
 +DONTSPLASH
 +FORCEXYBILLBOARD
 States
 {
 Spawn:
  TNT1 A 0
  TNT1 A 0
  BLUD D 650
  BLUD E 50
  BLUD EEEE 1 A_FadeOut
  Stop
 }
}

actor Striker_FlyingBloodTrail
{
 Game Doom
 Scale 0.7
 Height 1
 Radius 1
 Mass 1
 RenderStyle Translucent
 Alpha 0.56
 -NOGRAVITY
 +NOTELEPORT
 +NOBLOCKMAP
 +CORPSE
 +DONTSPLASH
 +FORCEXYBILLBOARD
 -ACTIVATEIMPACT
 -ACTIVATEPCROSS
 States
 {
  Spawn:
  	BLUD CBAA 4
  	Stop
  Crash:
	BLUD E 0 A_Stop
  	BLUD E 100 A_SetTranslucent(0.6,0)
  	BLUD EEEEEEEEEEEE 1 A_FadeOut
  	stop
 }
}

actor Fleshy_Splatter
{
 Game Doom
 Health 1
 Radius 4
 Height 1
 Mass 1
 ReactionTime 160
 +CORPSE
 +NOTELEPORT
 +NOBLOCKMAP
 +DONTSPLASH
 +NOBLOCKMONST
 +FORCEXYBILLBOARD
 States
 {
	Spawn:
	  TNT1 A 0
	  TNT1 A 0 A_Jump(127, "Spawn2")
	  TNT1 A 0 A_CountDown
	  TNT1 A 2 A_SpawnItemEX("Fleshy_SplatterTrail", 0, 0, 0, 0, 0, 0, 0, 129, 0)
	  goto Spawn+2
	Spawn2:
	  TNT1 A 0 A_CountDown
	  TNT1 A 2 A_SpawnItemEX("BloodSprayParticle", 0, 0, 0, 0, 0, 0, 0, 129, 0)
	  Loop
	Crash:
	  TNT1 A 0 A_PlaySound("FleshySplat")
	  TNT1 A 1 A_SpawnItemEX("Fleshy_BloodSpot", 0, 0, 0, 0, 0, 0, 0, 129, 0)
	  TNT1 A 0
	  Stop
 }
}

actor Fleshy_BloodSpot
{
 Game Doom
 Radius 1
 Height 1
 Mass 1
 RenderStyle Translucent
 Alpha 0.6
 +NOTELEPORT
 +NOBLOCKMAP
 +MOVEWITHSECTOR
 +CORPSE
 +DONTSPLASH
 +FORCEXYBILLBOARD
 States
 {
 Spawn:
  TNT1 A 0
  TNT1 A 0
  BLD2 E 400
  BLD2 EEEEEE 1 A_FadeOut
  Stop
 }
}

actor Fleshy_SplatterTrail
{
 Game Doom
 Height 1
 Radius 1
 Mass 1
 Gravity 0.2
 RenderStyle Translucent
 Alpha 0.7
 Scale 0.7
 -NOGRAVITY
 +NOTELEPORT
 +NOBLOCKMAP
 +CORPSE
 +DONTSPLASH
 +FORCEXYBILLBOARD
 States
 {
  Spawn:
  BLD2 ABCD 4
  Stop
 }
}

actor Bloodspray
{
	Radius 1
	Height 1
	Speed 0
	Scale 0.16
	RenderStyle Translucent
	Alpha 0.75
	+CORPSE
	+DONTSPLASH
	+NOGRAVITY
	+NOBLOCKMAP
	+ALLOWCLIENTSPAWN
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(127, "Spawn1", "Spawn3")
		TNT1 A 0 A_Jump(64, "Spawn2")
		TNT1 A 0 A_PlaySound("FleshTear")
		goto Spawn1
	Spawn1:
		TNT1 AAAAAAAAA 1 A_SpawnItemEx("BloodSprayParticle", 0, 0, 0, random(-2,2), random(-2,2), random(-2,2), random(-180,180), 129, 24)
		Stop
	Spawn2:
		SPRY ABCDEFGHIJKLMNO 1 A_Fadeout(0.04)
		stop
	Spawn3:
		TNT1 AAAAAAAAA 1 A_SpawnItemEx("BloodSprayParticleSmall", 0, 0, 0, random(-2,2), random(-2,2), random(4,14), random(-180,180), 129, 24)
		stop
	Crash:
		TNT1 A 1
		stop
	Death:
		TNT1 A 1
		Stop
	}
}

actor BloodSprayParticle : BloodSpray
{
	Scale 0.15
	states
	{
		Spawn:
			SPRY A 0
			SPRY A 0 A_Jump(127, "Spawn2")
			SPRY ABCDEFGHIJKLMNO 1 A_Fadeout(0.04)
			stop
		Spawn2:
			SPR2 ABCDEFGHIJKLMNO 1 A_Fadeout(0.04)
			stop
	}
}

actor BloodSprayParticleSmall : BloodSprayParticle
{
	Scale 0.08
	-NOGRAVITY
	-NOINTERACTION
	states
	{
		Spawn:
			SPRY A 0
			SPRY A 0 A_Jump(127, "Spawn2")
			SPRY ABCDEF 2 A_Fadeout(0.04)
			SPRY GHIJKLMNO 1 A_Fadeout(0.04)
			stop
		Spawn2:
			SPR2 ABCDEF 2 A_Fadeout(0.04)
			SPR2 GHIJKLMNO 1 A_Fadeout(0.04)
			stop
	}
}

actor Bloodmist : BaseEffect
{
	Scale 0.4
	RenderStyle Translucent
	Alpha 0.25
	States
	{
	Spawn:
		PUF4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		PUF5 A 1
		Stop
	}
}

actor GoreSmear : BaseEffect
{
	Scale 0.025
	States
	{
		Spawn:
			SMEA A 1 A_SetScale(scalex+0.25)
			SMEA A 0 A_JumpIf(scalex >= 0.75, "Fade")
			loop
		Fade:
			SMEA A 1 A_SetScale(scalex+0.125)
			SMEA A 0 A_FadeOut(0.125)
			loop
	}
}

actor BrainExplosion : BaseEffect
{
	Scale 0.25
	States
	{
		Spawn:
			BRNE ABCDEFGHIJK 1
			BRNE KK 1 A_FadeOut(0.25)
			stop
	}
}

ACTOR BouncingGib1
{
	Radius 4
	Height 4
	Health 1
	Speed 10
	Mass 9
	Gravity 1.5
	BounceCount 5
	BounceFactor 0.8
	SeeSound "FleshImpact"
	+MISSILE
	+HEXENBOUNCE
	-NOGRAVITY
	+NOTELEPORT
	+DONTSPLASH
	+CORPSE
	+NOBLOCKMAP
 	+NOBLOCKMONST
 	+FORCEXYBILLBOARD
	-ACTIVATEPCROSS
    -ACTIVATEIMPACT
    Decal BloodSplat
	States
	{
	Spawn:
		GBRN A 0
		GBRN A 0 A_StopSound
		GBRN ABCDABCDABCD 2 A_SpawnItemEx("BloodSprayParticle", 0, 0, 0, random(-2,2), random(-2,2), random(-2,2), random(-180,180), 128, 0)
		GBRN ABCDABCD 2
		goto Fade
	Fade:
		GBRN AABBCCDD 1 A_FadeOut(0.04)
		loop
	Death:
		TNT1 AA 1
		stop
	}
}

actor BouncingGib2 : BouncingGib1
{
	BounceFactor 0.82
	BounceCount 4
}

actor BouncingGib3 : BouncingGib1
{
	BounceFactor 0.78
}

ACTOR GibMeat
{
	Radius 4
	Height 4
	Health 1
	Speed 10
	Mass 9
	Scale .8
	Gravity 0.8
	+MISSILE
	+DONTSPLASH
	+CORPSE
	+NOBLOCKMAP
 	+NOBLOCKMONST
 	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	-ACTIVATEPCROSS
    -ACTIVATEIMPACT
	-NOGRAVITY
    Decal BloodSplat
	States
	{
	Death:
	Spawn:
		GMEA A 0
		GMEA A 0 A_Jump(128, "Spawn2")
		GMEA A -1
		loop
	Spawn2:
		GMEA B -1
		loop
	Crash:
		GBRN A 0 A_PlaySound("FleshySplat")
		GBRN EFGHIJK 3
		stop
	}
}

actor GibMeatSmall : GibMeat
{
	Scale 0.5
}

ACTOR GibBrain : GibMeat
{
	States
	{
	Death:
	Spawn:
		GBRN ABCD 4
		loop
	Crash:
		GBRN A 0 A_PlaySound("FleshySplat")
		GBRN EFGHIJK 3
		stop
	}
}

ACTOR GibLimb : GibMeat
{
	States
	{
	Death:
	Spawn:
		GLIM ABCD 4
		loop
	Crash:
		GLIM A 0 A_PlaySound("FleshySplat")
		GLIM EFGHIJK 3
		stop
	}
}

ACTOR GibOrgan : GibMeat
{
	States
	{
	Death:
	Spawn:
		GORG ABCD 4
		loop
	Crash:
		GORG EFGHIJK 3
		stop
	}
}

actor XDeathGibs
{
	Radius 16
	Height 56
	Mass 100
	Speed 1
	Health 100
	PainChance 255
	+SLIDESONWALLS
	+DROPOFF
	+CANPASS
	+WINDTHRUST
	+NOBLOCKMONST
	+NOBLOCKMAP
	+THRUGHOST
	+GHOST
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("GoreSmear", 0, 0, 26, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TNT1 AAA 0 A_SpawnItemEx("BouncingGib1", random(-16,16), random(-16,16), random(16, 46), MOMX+Random(-8, 8), MOMY+Random(-8,8), MOMZ+Random(8, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 48)
		TNT1 AAA 0 A_SpawnItemEx("BouncingGib2", random(-16,16), random(-16,16), random(16, 46), MOMX+Random(-8, 8), MOMY+Random(-8,8), MOMZ+Random(8, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 48)
		TNT1 AAA 0 A_SpawnItemEx("BouncingGib3", random(-16,16), random(-16,16), random(16, 46), MOMX+Random(-8, 8), MOMY+Random(-8,8), MOMZ+Random(8, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 48)
		
		TNT1 AAAA 1 A_SpawnItemEx ("Striker_FlyingBlood", 0, 0, 25, MOMX+Random(-10, 10), MOMY+Random(-10,10), MOMZ+Random(6, 20), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 8)
		TNT1 AAAA 0 A_SpawnItemEx("Fleshy_Splatter", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 14), Random(-180, 180), SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 AAAA 1 A_SpawnItemEx ("Striker_FlyingBlood", 0, 0, 25, MOMX+Random(-10, 10), MOMY+Random(-10,10), MOMZ+Random(6, 20), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 8)
		TNT1 AAAA 0 A_SpawnItemEx("Fleshy_Splatter", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 14), Random(-180, 180), SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		
		TNT1 A 1 A_SpawnItemEx("GibBrain", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		TNT1 AA 1 A_SpawnItemEx("GibLimb", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibBrain", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		TNT1 AA 1 A_SpawnItemEx("GibLimb", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		
		TNT1 A 1 A_SpawnItemEx("GibBrain", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibOrgan", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibBrain", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibOrgan", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibMeat", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibOrgan", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibMeat", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibOrgan", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibMeat", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibOrgan", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		Stop
	}
}

actor MapSpotGibs : XDeathGibs
{
	Height 4
	Radius 4
	Speed 0
	+NOINTERACTION
}

ACTOR BurnDeath2
{
	Radius 12
	Height 56
	Speed 0
	Scale 0.64
	+FLOORCLIP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("BurningUp")
		1BRN ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
		2BRN ABCDEFGHIJKLMN 1 BRIGHT
		2BRN O 0 A_PlaySound("Collapse")
		2BRN OPQRSTUV 2
		2BRN V 0 A_UnsetSolid
		2BRN V 10500
		goto Fade
	Fade:
		2BRN V 1 A_FadeOut(0.01)
		loop
	}
}

ACTOR BurnedSkeleton1
{
	Radius 12
	Height 56
	+SOLID
	+FLOORCLIP
	Scale 0.60
	States
	{
	Spawn:
		2BRN OPQRSTUV 2
		2BRN V 0 A_UnSetSolid
		2BRN V 10500
		goto Fade
	Fade:
		2BRN V 1 A_FadeOut(0.01)
		loop
	}
}

ACTOR FlyingImpHead
{
	Radius 4
	Height 4
	Health 1
	Speed 0
	Mass 9
	Gravity 0.5
	BounceCount 4
	BounceFactor 0.75
	SeeSound "FleshImpact"
	+HEXENBOUNCE
	+MISSILE
	+NOTELEPORT
	+DONTSPLASH
	+NOBLOCKMAP
 	+NOBLOCKMONST
 	+FORCEXYBILLBOARD
	-NOGRAVITY
	-ACTIVATEPCROSS
    -ACTIVATEIMPACT
    Decal BloodSplat
	States
	{
	Spawn:
		IHED A 4 A_SpawnItemEx("BloodSprayParticleSmall", 0, 0, 0, random(-2,2), random(-2,2), random(-4, -2), random(-180,180), SXF_CLIENTSIDE, 0)
		loop
	Death:
		IHED M 30
		IHED M 0 A_Jump(96, "JumpUp")
		IHED M 10500
		goto Fade
	Fade:
		IHED M 1 A_FadeOut(0.05)
		loop
	JumpUp:
		IHED B 0 A_ChangeFlag("NOGRAVITY", 1)
		IHED B 0 A_ChangeFlag("FLOATBOB", 1)
		IHED B 0 A_ChangeFlag("MISSILE", 0)
		IHED B 0 A_ChangeFlag("HEXENBOUNCE", 0)
		IHED B 0 ThrustThingZ(0, 12, 0, 0)
		goto Yeah
	Yeah:
		IHED A 0
		IHED A 0 A_PlaySound("NYC", CHAN_AUTO)
		IHED ABABABABABAB 1
		IHED A 0 A_Stop
		IHED ABABABABABABABABABABABABABAB 1
		IHED ABABABABAB 2
		IHED CBABCB 2
		goto Drop1
	Drop1:
		IHED N 0 A_ChangeFlag("NOGRAVITY", 0)
		IHED N 0 A_ChangeFlag("FLOATBOB", 0)
		IHED N 0 A_ChangeFlag("LOWGRAVITY", 1)
		IHED N 20
		goto NewYorkCity
	NewYorkCity:
		IHED A 0 A_ChangeFlag("NOGRAVITY", 1)
		IHED A 0 A_ChangeFlag("FLOATBOB", 1)
		IHED A 0 ThrustThingZ(0, 7, 0, 0)
		IHED ABABABCDABAB 1
		IHED A 0 A_Stop
		IHED ABCDABABABABABABABABABABABABABCDABABABABABACBDABABABABCDABABABABABABABABABABABCDABCDABABABABABCD 1
		goto Drop2
	Drop2:
		IHED O 0 A_ChangeFlag("NOGRAVITY", 0)
		IHED O 0 A_ChangeFlag("FLOATBOB", 0)
		IHED O 0 A_ChangeFlag("LOWGRAVITY", 1)
		IHED O 27
		goto AHAHA
	AHAHA:
		IHED A 0 A_ChangeFlag("NOGRAVITY", 1)
		IHED A 0 A_ChangeFlag("FLOATBOB", 1)
		IHED A 0 ThrustThingZ(0, 12, 0, 0)
		IHED ABABABCDABAB 1
		IHED A 0 A_Stop
		IHED ABABEFGHIJKLEFGHIJKLEFGHIJKLEFGHIJKLEFGHIJKLEFGHIJKLEFGHIJKLEFGHIJKLEFGHIJKLEFGHIJKL 1
		goto Boom
	Boom:
		IHED A 0
		IHED A 0 A_Explode(64, 64)
		IHED A 0 A_PlaySound("TechPlasmaSuperBoom", CHAN_AUTO)
		IHED A 0 A_PlaySound("misc/gibbed", CHAN_AUTO)
		IHED AAAAAA 0 A_SpawnItemEx("HellfireRocketFire", Random(-10,10), Random(-10,10), Random(-10,10), Random(-6,6), Random(-6,6), Random(-6,6), random(-360,360), SXF_CLIENTSIDE, 0)
		IHED AAAAAA 0 A_SpawnItemEx("Striker_FlyingBlood", 0, 0, 0, random(-4,4), random(-4,4), random(4, 16), random(-180,180), SXF_CLIENTSIDE, 0)
		TNT1 AA 1
		stop
	}
}

actor FirebornBlood : Blood
{
	States
	{
		Spawn:
			TNT1 AAA 0
			TNT1 A 0 A_SpawnItemEx("FirebornLava", 0, 0, 0, frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(1.0, 6.0), random(-180, 180), SXF_CLIENTSIDE, 64)
			TNT1 AA 0 A_SpawnItemEx("FirebornChunk", 0, 0, 0, frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(1.0, 8.0), random(-180, 180), SXF_CLIENTSIDE, 128)
			stop
	}
}

actor FirebornChunk : BaseEffect
{
	Scale 0.64
	BounceType Hexen
	BounceCount 5
	BounceFactor 0.7
	+MISSILE
	-NOINTERACTION
	-NOGRAVITY
	
	var int user_type;
	
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetUserVar("user_type", random(1, 5))
			TNT1 A 0 A_JumpIf(user_type == 5, "Chunk5")
			TNT1 A 0 A_JumpIf(user_type == 4, "Chunk4")
			TNT1 A 0 A_JumpIf(user_type == 3, "Chunk3")
			TNT1 A 0 A_JumpIf(user_type == 2, "Chunk2")
			TNT1 A 0 A_JumpIf(user_type == 1, "Chunk1")
			TNT1 A 0
			stop
		Chunk1:
			CLKC A -1
			loop
		Chunk2:
			CLKC B -1
			loop
		Chunk3:
			CLKC C -1
			loop
		Chunk4:
			CLKC D -1
			loop
		Chunk5:
			CLKC E -1
			loop
		Death:
			TNT1 A 0 A_JumpIf(user_type == 5, "Death5")
			TNT1 A 0 A_JumpIf(user_type == 4, "Death4")
			TNT1 A 0 A_JumpIf(user_type == 3, "Death3")
			TNT1 A 0 A_JumpIf(user_type == 2, "Death2")
			TNT1 A 0 A_JumpIf(user_type == 1, "Death1")
			TNT1 AA 1
			stop
		Death1:
			CLKC A 60
			goto Fade1
		Death2:
			CLKC B 60
			goto Fade2
		Death3:
			CLKC C 60
			goto Fade3
		Death4:
			CLKC F 60
			goto Fade4
		Death5:
			CLKC G 60
			goto Fade5
		Fade1:
			CLKC A 1 A_FadeOut(0.075)
			loop
		Fade2:
			CLKC B 1 A_FadeOut(0.075)
			loop
		Fade3:
			CLKC C 1 A_FadeOut(0.075)
			loop
		Fade4:
			CLKC F 1 A_FadeOut(0.075)
			loop
		Fade5:
			CLKC G 1 A_FadeOut(0.075)
			loop
	}
}

actor FirebornLavaTrail : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	Scale 0.35
	States
	{
		Spawn:
			TNT1 A 2 BRIGHT
			goto Fade
		Fade:
			CLKB A 1 BRIGHT A_FadeOut(0.1)
			loop
	}
}

ACTOR FirebornLava : BaseEffect
{
	Gravity 0.7
	Scale 0.35
	RenderStyle Add
	Alpha 1.0
	+MISSILE
	+CORPSE
	-NOGRAVITY
	-NOINTERACTION
	States
	{
	Death:
	Spawn:
		CLKB AABB 1 BRIGHT A_SpawnItemEx("FirebornLavaTrail", 0, 0, 0, frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), random(-180, 180), SXF_CLIENTSIDE, 0)
		loop
	Crash:
		CLKB C 0 A_PlaySound("burnsmall")
		CLKB CC 0 A_SpawnItemEx("BulletSmoke", frandom(-8.0, 8.0), frandom(-8.0, 8.0), 1, 0, 0, frandom(2.0, 3.0), random(-180,180), SXF_CLIENTSIDE, 0)
		CLKB CDEFGHIJKL 2 BRIGHT
		stop
	Disappear:
		TNT1 A 0
		TNT1 A 1
		stop
	}
}