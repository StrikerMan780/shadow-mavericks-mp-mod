actor BetterBlood : Blood replaces Blood
{
 +FORCEXYBILLBOARD
 +CLIENTSIDEONLY
 RenderStyle Translucent
 Alpha 0.7
 game Doom
 states
 {
 Spawn:
  BLUD A 0
  BLUD A 0 A_SpawnItemEX("bloodspray", 0, 0, 0, 0, 0, 0, 0, 129, 156)
  BLUD A 0 A_SpawnItemEX("bloodmist", 0, 0, 0, 0, 0, 0, 0, 129, 232)
  BLUD A 0 A_SpawnItemEx("Striker_FlyingBlood", 0, 0, 0, random(1, 4), random(1, 4), random(1, 12), Random(-180, 180), 129, 186)
  BLUD A 0 A_SpawnItemEx("Fleshy_Splatter", 0, 0, 0, random(1, 2), random(1, 2), random(1, 8), Random(-180, 180), 129, 224)
  BLUD A 0 A_PlaySound("FleshImpact")
  BLUD C 8
  BLUD B 8
  BLUD A 8
  TNT1 A 0
  stop
 }
}

actor Striker_FlyingBlood
{
 Game Doom
 Scale 0.7
 Health 1
 Radius 4
 Height 1
 Mass 1
 ReactionTime 160
 RenderStyle Translucent
 Alpha 0.56
 +CORPSE
 +NOTELEPORT
 +NOBLOCKMAP
 +DONTSPLASH
 +NOBLOCKMONST
 +FORCEXYBILLBOARD
 -ACTIVATEIMPACT
 -ACTIVATEPCROSS
 States
 {
 Spawn:
  TNT1 A 0 A_CountDown
  BLUD C 1 A_SpawnItemEX("Striker_FlyingBloodTrail", 0, 0, 0, Random(-1,1), Random(-1,1), Random(-1,1), 0, 129, 0)
  Loop
 Crash:
  TNT1 A 1 A_SpawnItemEX("Striker_BloodSpot", 0, 0, 0, 0, 0, 0, 0, 129, 0)
  TNT1 A 0
  Stop
 }
}

actor Striker_BloodSpot
{
 Game Doom
 Radius 1
 Height 1
 Mass 1
 Scale 0.7
 RenderStyle Translucent
 Alpha 0.6
 +NOTELEPORT
 +NOBLOCKMAP
 +MOVEWITHSECTOR
 +CORPSE
 +DONTSPLASH
 +FORCEXYBILLBOARD
 States
 {
 Spawn:
  TNT1 A 0
  TNT1 A 0
  BLUD D 650
  BLUD E 50
  BLUD EEEE 1 A_FadeOut
  Stop
 }
}

actor Striker_FlyingBloodTrail
{
 Game Doom
 Scale 0.7
 Height 1
 Radius 1
 Mass 1
 RenderStyle Translucent
 Alpha 0.56
 -NOGRAVITY
 +NOTELEPORT
 +NOBLOCKMAP
 +CORPSE
 +DONTSPLASH
 +FORCEXYBILLBOARD
 -ACTIVATEIMPACT
 -ACTIVATEPCROSS
 States
 {
  Spawn:
  	BLUD CBAA 4
  	Stop
  Crash:
    BLUD E 0 A_CheckSight("Disappear")
  	BLUD E 100 A_SetTranslucent(0.6,0)
  	BLUD EEEEEEEEEEEE 1 A_FadeOut
  	stop
  Disappear:
  	TNT1 AA 1
  	stop
 }
}

actor Fleshy_Splatter
{
 Game Doom
 Health 1
 Radius 4
 Height 1
 Mass 1
 ReactionTime 160
 +CORPSE
 +NOTELEPORT
 +NOBLOCKMAP
 +DONTSPLASH
 +NOBLOCKMONST
 +FORCEXYBILLBOARD
 States
 {
 Spawn:
  TNT1 A 0
  TNT1 A 0 A_Jump(127, "Spawn2")
  TNT1 A 0 A_CountDown
  TNT1 A 2 A_SpawnItemEX("Fleshy_SplatterTrail", 0, 0, 0, 0, 0, 0, 0, 129, 0)
  goto Spawn+2
 Spawn2:
  TNT1 A 0 A_CountDown
  TNT1 A 2 A_SpawnItemEX("BloodSprayParticle", 0, 0, 0, 0, 0, 0, 0, 129, 0)
  Loop
 Crash:
  TNT1 A 0 A_PlaySound("FleshySplat")
  TNT1 A 0 A_CheckSight("Disappear")
  TNT1 A 1 A_SpawnItemEX("Fleshy_BloodSpot", 0, 0, 0, 0, 0, 0, 0, 129, 0)
  TNT1 A 0
  Stop
 Disappear:
  	TNT1 AA 1
  	stop
 }
}

actor Fleshy_BloodSpot
{
 Game Doom
 Radius 1
 Height 1
 Mass 1
 RenderStyle Translucent
 Alpha 0.6
 +NOTELEPORT
 +NOBLOCKMAP
 +MOVEWITHSECTOR
 +CORPSE
 +DONTSPLASH
 +FORCEXYBILLBOARD
 States
 {
 Spawn:
  TNT1 A 0
  TNT1 A 0
  BLD2 E 400
  BLD2 EEEEEE 1 A_FadeOut
  Stop
 }
}

actor Fleshy_SplatterTrail
{
 Game Doom
 Height 1
 Radius 1
 Mass 1
 Gravity 0.2
 RenderStyle Translucent
 Alpha 0.7
 Scale 0.7
 -NOGRAVITY
 +NOTELEPORT
 +NOBLOCKMAP
 +CORPSE
 +DONTSPLASH
 +FORCEXYBILLBOARD
 States
 {
  Spawn:
  BLD2 ABCD 4
  Stop
 }
}

actor Bloodspray
{
	Radius 1
	Height 1
	Speed 0
	Scale 0.16
	RenderStyle Translucent
	Alpha 0.75
	+CORPSE
	+DONTSPLASH
	+NOGRAVITY
	+NOBLOCKMAP
	+ALLOWCLIENTSPAWN
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(127, "Spawn1", "Spawn3")
		TNT1 A 0 A_Jump(64, "Spawn2")
		TNT1 A 0 A_PlaySound("FleshTear")
		goto Spawn1
	Spawn1:
		TNT1 AAAAAAAAA 1 A_SpawnItemEx("BloodSprayParticle", 0, 0, 0, random(-2,2), random(-2,2), random(-2,2), random(-180,180), 129, 24)
		Stop
	Spawn2:
		SPRY ABCDEFGHIJKLMNO 1 A_Fadeout(0.04)
		stop
	Spawn3:
		TNT1 AAAAAAAAA 1 A_SpawnItemEx("BloodSprayParticleSmall", 0, 0, 0, random(-2,2), random(-2,2), random(4,14), random(-180,180), 129, 24)
		stop
	Crash:
		TNT1 A 1
		stop
	Death:
		TNT1 A 1
		Stop
	}
}

actor BloodSprayParticle : BloodSpray
{
	Scale 0.15
	states
	{
		Spawn:
			SPRY A 0
			SPRY A 0 A_Jump(127, "Spawn2")
			SPRY ABCDEFGHIJKLMNO 1 A_Fadeout(0.04)
			stop
		Spawn2:
			SPR2 ABCDEFGHIJKLMNO 1 A_Fadeout(0.04)
			stop
	}
}

actor BloodSprayParticleSmall : BloodSprayParticle
{
	Scale 0.08
	-NOGRAVITY
	-NOINTERACTION
	states
	{
		Spawn:
			SPRY A 0
			SPRY A 0 A_Jump(127, "Spawn2")
			SPRY ABCDEF 2 A_Fadeout(0.04)
			SPRY GHIJKLMNO 1 A_Fadeout(0.04)
			stop
		Spawn2:
			SPR2 ABCDEF 2 A_Fadeout(0.04)
			SPR2 GHIJKLMNO 1 A_Fadeout(0.04)
			stop
	}
}

actor Bloodmist : BaseEffect
{
	Scale 0.4
	RenderStyle Translucent
	Alpha 0.25
	States
	{
	Spawn:
		PUF4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		PUF5 A 1
		Stop
	}
}

actor GoreSmear : BaseEffect
{
	States
	{
		Spawn:
			SMEA ABCDEFGH 1
			SMEA IJKLMNO 1 A_FadeOut(0.12)
			stop
	}
}

actor BrainExplosion : BaseEffect
{
	Scale 0.25
	States
	{
		Spawn:
			BRNE ABCDEFGHIJK 1
			BRNE KK 1 A_FadeOut(0.25)
			stop
	}
}

ACTOR BouncingGib1
{
	Radius 4
	Height 4
	Health 1
	Speed 10
	Mass 9
	Gravity 1.5
	BounceCount 5
	BounceFactor 0.8
	SeeSound "FleshImpact"
	+MISSILE
	+HEXENBOUNCE
	-NOGRAVITY
	+NOTELEPORT
	+DONTSPLASH
	+CORPSE
	+NOBLOCKMAP
 	+NOBLOCKMONST
 	+FORCEXYBILLBOARD
	-ACTIVATEPCROSS
    -ACTIVATEIMPACT
    Decal BloodSplat
	States
	{
	Spawn:
		GBRN A 0
		GBRN A 0 A_StopSound
		GBRN ABCDABCDABCD 2 A_SpawnItemEx("BloodSprayParticle", 0, 0, 0, random(-2,2), random(-2,2), random(-2,2), random(-180,180), 128, 0)
		GBRN ABCDABCD 2
		goto Fade
	Fade:
		GBRN AABBCCDD 1 A_FadeOut(0.04)
		loop
	Death:
		TNT1 AA 1
		stop
	}
}

actor BouncingGib2 : BouncingGib1
{
	BounceFactor 0.82
	BounceCount 4
}

actor BouncingGib3 : BouncingGib1
{
	BounceFactor 0.78
}

ACTOR GibMeat
{
	Radius 4
	Height 4
	Health 1
	Speed 10
	Mass 9
	Scale .8
	Gravity 0.8
	+MISSILE
	-NOGRAVITY
	+DONTSPLASH
	+CORPSE
	+NOBLOCKMAP
 	+NOBLOCKMONST
 	+FORCEXYBILLBOARD
	-ACTIVATEPCROSS
    -ACTIVATEIMPACT
    Decal BloodSplat
	States
	{
	Death:
	Spawn:
		GMEA A 0
		GMEA A 0 A_Jump(128, "Spawn2")
		GMEA A -1
		loop
	Spawn2:
		GMEA B -1
		loop
	Crash:
		GBRN A 0 A_PlaySound("FleshySplat")
		GBRN EFGHIJK 3
		stop
	}
}

actor GibMeatSmall : GibMeat
{
	Scale 0.5
}

ACTOR GibBrain : GibMeat
{
	States
	{
	Death:
	Spawn:
		GBRN ABCD 4
		loop
	Crash:
		GBRN A 0 A_PlaySound("FleshySplat")
		GBRN EFGHIJK 3
		stop
	}
}

ACTOR GibLimb : GibMeat
{
	States
	{
	Death:
	Spawn:
		GLIM ABCD 4
		loop
	Crash:
		GLIM A 0 A_PlaySound("FleshySplat")
		GLIM EFGHIJK 3
		stop
	}
}

ACTOR GibOrgan : GibMeat
{
	States
	{
	Death:
	Spawn:
		GORG ABCD 4
		loop
	Crash:
		GORG EFGHIJK 3
		stop
	}
}

actor XDeathGibs
{
	Radius 16
	Height 56
	Mass 100
	Speed 1
	Health 100
	PainChance 255
	+SLIDESONWALLS
	+DROPOFF
	+CANPASS
	+WINDTHRUST
	+NOBLOCKMONST
	+NOBLOCKMAP
	+THRUGHOST
	+GHOST
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("GoreSmear", 0, 0, 26, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TNT1 AAA 0 A_SpawnItemEx("BouncingGib1", random(-16,16), random(-16,16), random(16, 46), MOMX+Random(-8, 8), MOMY+Random(-8,8), MOMZ+Random(8, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 48)
		TNT1 AAA 0 A_SpawnItemEx("BouncingGib2", random(-16,16), random(-16,16), random(16, 46), MOMX+Random(-8, 8), MOMY+Random(-8,8), MOMZ+Random(8, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 48)
		TNT1 AAA 0 A_SpawnItemEx("BouncingGib3", random(-16,16), random(-16,16), random(16, 46), MOMX+Random(-8, 8), MOMY+Random(-8,8), MOMZ+Random(8, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 48)
		
		TNT1 AAAA 1 A_SpawnItemEx ("Striker_FlyingBlood", 0, 0, 25, MOMX+Random(-10, 10), MOMY+Random(-10,10), MOMZ+Random(6, 20), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 8)
		TNT1 AAAA 0 A_SpawnItemEx("Fleshy_Splatter", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 14), Random(-180, 180), SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 AAAA 1 A_SpawnItemEx ("Striker_FlyingBlood", 0, 0, 25, MOMX+Random(-10, 10), MOMY+Random(-10,10), MOMZ+Random(6, 20), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 8)
		TNT1 AAAA 0 A_SpawnItemEx("Fleshy_Splatter", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 14), Random(-180, 180), SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		
		TNT1 A 1 A_SpawnItemEx("GibBrain", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		TNT1 AA 1 A_SpawnItemEx("GibLimb", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibBrain", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		TNT1 AA 1 A_SpawnItemEx("GibLimb", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 16), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		
		TNT1 A 1 A_SpawnItemEx("GibBrain", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibOrgan", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibBrain", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibOrgan", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibMeat", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibOrgan", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibMeat", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibOrgan", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibMeat", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		TNT1 A 1 A_SpawnItemEx("GibOrgan", 0, 0, 32, MOMX+random(-4, 4), MOMY+random(-4, 4), MOMZ+random(6, 12), 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 16)
		Stop
	}
}

actor MapSpotGibs : XDeathGibs
{
	Height 4
	Radius 4
	Speed 0
	+NOINTERACTION
}

ACTOR BurnDeath2
{
	Radius 12
	Height 56
	Speed 0
	Scale 0.64
	+FLOORCLIP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("BurningUp")
		1BRN ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 BRIGHT
		2BRN ABCDEFGHIJKLMN 1 BRIGHT
		2BRN O 0 A_PlaySound("Collapse")
		2BRN OPQRSTUV 2
		2BRN V 0 A_UnsetSolid
		2BRN V 10500
		goto Fade
	Fade:
		2BRN V 1 A_FadeOut(0.01)
		loop
	}
}

ACTOR BurnedSkeleton1
{
	Radius 12
	Height 56
	+SOLID
	+FLOORCLIP
	Scale 0.60
	States
	{
	Spawn:
		2BRN OPQRSTUV 2
		2BRN V 0 A_UnSetSolid
		2BRN V 10500
		goto Fade
	Fade:
		2BRN V 1 A_FadeOut(0.01)
		loop
	}
}