actor DukeClassCheck : BaseClassCheck
{
}

actor DukeNukem : BaseClass
{
	Scale 0.69

	Player.DisplayName "Duke (WIP)"
	Player.SoundClass "DukeNukem"
	Player.ScoreIcon "DUKEICON"
	Player.CrouchSprite "DUKC"
	
	player.sidemove 0.90
	player.forwardmove 0.90
	Player.JumpZ 10
	
	player.startitem "DukePistol"
	player.startitem "Melee"
	player.startitem "MechAmmo", 48
	player.startitem "DukeClip", 12
	player.startitem "DukeClassCheck", 1
	
	Player.WeaponSlot 1, "Melee", "Excalibat"
	Player.WeaponSlot 2, "DukePistol"
	Player.WeaponSlot 3, "DukeShotgun", "SPAS-12", "AcidSprayer"
	Player.WeaponSlot 4, "RipperChaingun", "M1911"
	Player.WeaponSlot 5, "HellFireCannon", "DukeGL", "DukeRPG"
	Player.WeaponSlot 6, "Freezethrower", "Expander", "ShockRifle"
	Player.WeaponSlot 7, "EnergyCannon", "BFG10k", "NukeLauncher", "Noisy_Cricket"
	Player.WeaponSlot 8, "BioRifle", "Needler"
	Player.WeaponSlot 9, "WeatherMaker", "WookieBowcaster"
	Player.WeaponSlot 0, "SoccerKick", "E_Peen_Joke", "IPodLauncher", "GasolineCan"
	
	States
	{
	Spawn:
		DUKE E 2
		DUKE E 0 A_JumpIf(waterlevel == 3, "SwimStand")
		Loop
	SwimStand:
		DUKS D 2
		DUKS D 0 A_JumpIf(waterlevel <= 1, "Spawn")
		DUKS D 0 A_JumpIf(momx > 2 || momy > 2 || momx < -2 || momy < -2, "SwimSee")
		loop
	See:
		DUKE A 4
		DUKE A 0 A_JumpIf(waterlevel == 3, "SwimSee")
		DUKE B 4
		DUKE B 0 A_JumpIf(waterlevel == 3, "SwimSee")
		DUKE C 4
		DUKE C 0 A_JumpIf(waterlevel == 3, "SwimSee")
		DUKE D 4
		DUKE D 0 A_JumpIf(waterlevel == 3, "SwimSee")
		Loop
	SwimSee:
		DUKS A 5
		DUKS A 0 A_JumpIf(waterlevel <= 1, "See")
		DUKS B 5
		DUKS B 0 A_JumpIf(waterlevel <= 1, "See")
		DUKS C 5
		DUKS C 0 A_JumpIf(waterlevel <= 1, "See")
		DUKS C 5
		DUKS C 0 A_JumpIf(waterlevel <= 1, "See")
		DUKS D 5
		DUKS D 0 A_JumpIf(waterlevel <= 1, "See")
		DUKS D 0 A_JumpIf(momx < 2 && momy < 2 && momx > -2 && momy > -2, "SwimStand")
		loop
	Missile:
		DUKE E 0 A_JumpIf(waterlevel == 3, "SwimMissile")
		DUKE E 1 A_JumpIfInventory("DukeKickCheck", 1, "Kick")
		DUKE E 1 A_JumpIfInventory("DukeKickCheck", 1, "Kick")
		DUKE E 10
		Goto Spawn
	Kick:
		DUKE D 3
		DUKK A 6
		DUKK B 7
		goto Spawn
	SwimMissile:
		DUKS D 0 A_JumpIf(waterlevel <= 1, "See")
		DUKS D 12
		Goto SwimStand
	Melee:
		DUKE F 0 A_JumpIf(waterlevel == 3, "SwimMelee")
		DUKE F 6 BRIGHT
		Goto Missile
	SwimMelee:
		DUKS D 0 A_JumpIf(waterlevel <= 1, "See")
		DUKS D 12
		Goto SwimStand
	Pain:
		DUKE G 4
		DUKE G 4 A_Pain
		Goto Spawn
	Disintegrate:
		NULL A 0 A_Playsound("disintegrate")
		NULL A 0 A_NoBlocking
		NULL A 0 A_SpawnItemEx("DisintegratingPlayer", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		NULL A 3
		NULL A -1
		stop
	Death.Fist:
		DUKE H 0 A_GiveToTarget("KnockoutTrigger", 1)
		goto Death
	Death.QuadSSG:
		DUKE H 0
		DUKE H 0 ThrustThingZ(0, Random(28, 40), 0, 1)
	Death:
		DUKE H 6
		DUKE I 6 A_PlayerScream
		DUKE J 6 A_NoBlocking
		DUKE KLM 6
		DUKE N -1
		Stop
	XDeath:
		DUKE O 0 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		DUKE O 3
		DUKE P 3 A_XScream
		DUKE Q 3 A_NoBlocking
		DUKE RSTUV 3
		DUKE W -1
		Stop
	Death.Microwave:
		"####" "#" 0 A_PlaySound("DukeGrow")
		goto GrowLoop
	GrowLoop:
		"####" "#" 1 A_SetScale(scalex+0.05, scaley+0.05)
		"####" "#" 0 A_JumpIf(scalex >= 2.20, "GrowExplode")
		loop
	GrowExplode:
		POL5 A 0 A_PlaySound("DukeExplosion")
		POL5 A 0 A_XScream
		POL5 A 0 A_Fall
		POL5 A 0 A_Explode
		POL5 A 0 A_SpawnItemEx("DukeExplosion", 0, 0, 40, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		POL5 A 0 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		POL5 A -1 A_SetScale(1.0, 1.0)
		stop
	Ice:
		DUKE E 5 A_GenericFreezeDeath
		DUKE E 1 A_FreezeDeathChunks
		Wait
	}
}