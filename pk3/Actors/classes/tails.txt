// Sprites thanks to SSNTails, and the SRB2 Development Team.
// Scripting and Character Voice by StrikerMan780.

actor TailsClassCheck : BaseClassCheck
{
}

actor Tails : BaseClass
{
	Scale 0.82
	Gravity 0.82
	
	Player.ForwardMove 1
	Player.SideMove 1
	Player.JumpZ 8.0
	Player.AttackZOffset 3
	Player.Viewheight 36
	
	Player.DisplayName "Tails"
	Player.SoundClass "tails"
	Player.ScoreIcon "TAILICON"
	
	Player.StartItem "Tails_TechPistol"
	Player.StartItem "Melee"
	Player.StartItem "ChaosFlames"
	Player.StartItem "TailsFlightCheck", 50
	Player.StartItem "TailsWeaponUpgrade", 1
	Player.StartItem "VilesBane", 5
	Player.StartItem "TailsClassCheck", 1
	Player.StartItem "CellAmmo", 50
	Player.StartItem "MechAmmo", 50
	Player.StartItem "SpecialMove", 2

	Player.WeaponSlot 1, "Melee", "Excalibat"
	Player.WeaponSlot 2, "Tails_TechPistol"
	Player.WeaponSlot 3, "PIPS", "Keyring", "IceSSG"
	Player.WeaponSlot 4, "BCF-2Repeaters", "Mp40"
	Player.WeaponSlot 5, "StickyBombs", "ProtonTL"
	Player.WeaponSlot 6, "HaloPlasmaRifle", "Cyclone", "ShockRifle"
	Player.WeaponSlot 7, "Supra", "BFG10k", "NukeLauncher", "Noisy_Cricket"
	Player.WeaponSlot 8, "BioRifle", "Needler"
	Player.WeaponSlot 9, "WeatherMaker", "EtherealCrossbow", "ChaosFlames"
	Player.WeaponSlot 0, "SoccerKick", "E_Peen_Joke", "IPodLauncher", "GasolineCan"
	
	States
	{
	Spawn:
		TAIL A 0
		TAIL A 0 ACS_NamedExecuteAlways("STOPSPIN", 0)
		TAIL A 0 A_JumpIfInventory("PowerFlight", 1, "Fly")
		TAIL A 6 ACS_NamedExecuteAlways("FLIGHTCHECK", 0)
		loop
	Fly:
		TAIF A 0 A_JumpIfInventory("RedFlag", 1, "TakeCarry")
		TAIF A 0 A_JumpIfInventory("BlueFlag", 1, "TakeCarry")
		TAIF A 0 A_JumpIfInventory("WhiteFlag", 1, "TakeCarry")
		TAIF A 0 A_JumpIfInventory("RedSkullST", 1, "TakeCarry")
		TAIF A 0 A_JumpIfInventory("BlueSkullST", 1, "TakeCarry")
		TAIF A 0 A_JumpIfInventory("PowerPossessionArtifact", 1, "TakeCarry")
		TAIF AB 3
		TAIF A 0 A_PlaySound("TailsFlying")
		TAIF A 0 A_JumpIfInventory("PowerFlight", 1, "Fly")
		goto Spawn
	TakeCarry:
		TAIF A 0 A_TakeInventory("PowerFlight", 1)
		goto Spawn
	FlyAttack:
		TAIF BCDEF 1
		TAIF A 0 A_PlaySound("TailsFlying")
		goto Fly
	See:
		TAIL A 0 ACS_NamedExecute("INIT_FOOTSTEPS", 0)
		TAIL A 0 A_JumpIfInventory("PowerFlight", 1, "Fly")
		TAIL ABCD 4
		TAIL A 0 ACS_NamedExecuteAlways("FLIGHTCHECK", 0)
		Loop
	Burn:
		BURN A 0 A_JumpIfInventory("Kamikaze", 1, "KamikazeDeath")
		BURN A 0 A_Playsound("firedeath")
		BURN A 0 A_NoBlocking
		BURN ABCDEFGHIJKLMNOPQRSTUV 4 BRIGHT
		BURN V -1
		stop
	Disintegrate:
		NULL A 0 A_JumpIfInventory("Kamikaze", 1, "KamikazeDeath")
		NULL A 0 A_Playsound("disintegrate")
		NULL A 0 A_NoBlocking
		NULL A 0 A_SpawnItemEx("DisintegratingPlayer", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		NULL A 3
		NULL A -1
		stop
	Missile:
		TAIL E 0 A_JumpIfInventory("PowerFlight", 1, "FlyAttack")
		TAIL E 12
		TAIL E 0 ACS_NamedExecuteAlways("STOPSPIN", 0)
		Goto Spawn
	Melee:
		TAIL F 0 A_JumpIfInventory("PowerFlight", 1, "FlyAttack")
		TAIL F 6 BRIGHT
		TAIL F 0 ACS_NamedExecuteAlways("STOPSPIN", 0)
		Goto Missile
	Pain:
		TAIL G 0 ACS_NamedExecuteAlways("STOPCAMERA", 0)
		TAIL G 0 ACS_NamedExecuteAlways("STOPSPIN", 0)
		TAIL G 4
		TAIL G 4 A_Pain
		TAIL A 0 A_JumpIfInventory("PowerFlight", 1, "Fly")
		Goto Spawn
	Death.Fist:
		TAIL H 0 A_GiveToTarget("KnockoutTrigger", 1)
		goto Death
	Death.QuadSSG:
		TAIL H 0
		TAIL H 0 ThrustThingZ(0, Random(28, 40), 0, 1)
		goto Death
	Death:
		TAIL H 0 A_JumpIfInventory("Kamikaze", 1, "KamikazeDeath")
		TAIL H 7
		TAIL I 7 A_PlayerScream
		TAIL J 7 A_NoBlocking
		TAIL KLM 7
		TAIL N -1
		Stop
	XDeath:
		TAIL O 0 A_GiveToTarget("SpeakOneLiner", 1)
		TAIL O 0 A_JumpIfInventory("Kamikaze", 1, "KamikazeDeath")
		TAIL O 5 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		TAIL P 5 A_XScream
		TAIL Q 5 A_NoBlocking
		TAIL RSTUV 5
		TAIL W -1
		Stop
	Death.ChaosFire:
		TAIL GG 0 A_Stop
		TAIL G 0 A_JumpIfInventory("Kamikaze", 1, "KamikazeDeath")
		TAIL G 5 A_PlayerScream
		TAIL G 0 A_NoBlocking
		TAIL G 0 A_SpawnItemEX("BlackFlameDeath", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.9, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.85, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.8, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.75, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.7, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.65, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.6, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.55, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.5, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.45, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.4, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.35, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.3, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.25, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.2, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.15, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.1, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.05, 1)
		TAIL GG 0 A_SpawnItemEX("Striker_FlyingBlood", Random(-16, 16), Random(-16, 16), Random(0, 56), 0, 0, 0, 0, 128, 0)
		TAIL G 2 A_SetTranslucent(0.0, 1)
		TAIL N -1
		stop
	Death.Ethereal:
		TAIL G 0 A_JumpIfInventory("Kamikaze", 1, "KamikazeDeath")
		TAIL G 0 A_SetTranslucent(1.0, 0)
		TAIL G 1 A_PlayerScream
		goto EtherealLoop
	EtherealLoop:
		TAIL G 0 A_GiveInventory("GenericTimer", 1)
		TAIL G 1 A_SpawnItemEX("EtherealMist", Random(-8, 8), Random(-8, 8), Random(0, 40), Random(-1, 1), Random(-1, 1), Random(-1, 1), 0, 128, 0)
		TAIL GGGG 0 A_SpawnItemEX("EtherealMist", Random(-8, 8), Random(-8, 8), Random(0, 40), Random(-1, 1), Random(-1, 1), Random(-1, 1), 0, 128, 0)
		TAIL G 0 A_FadeOut(0.01)
		TAIL G 0 A_JumpIfInventory("GenericTimer", 70, "EtherealLoop2Start")
		loop
	EtherealLoop2Start:
		TAIL G 0 A_TakeInventory("GenericTimer", 9999)
		goto EtherealLoop2
	EtherealLoop2:
		NULL A 0 A_GiveInventory("GenericTimer", 1)
		NULL A 1 A_SpawnItemEX("EtherealMist", Random(-8, 8), Random(-8, 8), Random(0, 40), Random(-1, 1), Random(-1, 1), Random(-1, 1), 0, 128, 0)
		NULL AAAA 0 A_SpawnItemEX("EtherealMist", Random(-8, 8), Random(-8, 8), Random(0, 40), Random(-1, 1), Random(-1, 1), Random(-1, 1), 0, 128, 0)
		NULL A 0 A_JumpIfInventory("GenericTimer", 45, "EtherealEnd")
		loop
	EtherealEnd:
		NULL A 0 A_TakeInventory("GenericTimer", 9999)
		NULL A -1
		stop
	Spin:
		TSPN A 0 A_ChangeFlag ("NOPAIN", 1)
		TSPN A 0 ACS_NamedExecuteAlways("STARTCAMERA", 0) // Chasecam
		TSPN A 0 A_Recoil(-10)
		TSPN AAA 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN A 0 A_Recoil(-9)
		TSPN BBB 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN B 0 A_Recoil(-8)
		TSPN CCC 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN C 0 A_Recoil(-8)
		TSPN AAA 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN A 0 A_Recoil(-8)
		TSPN BBB 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN B 0 A_Recoil(-8)
		TSPN CCC 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN AB 2
		TSPN C 3
		TAIL A 0 ACS_NamedExecuteAlways("STOPCAMERA", 0)
		TNT1 A 0 ACS_NamedExecuteAlways("STOPSPIN", 0)
		TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
		goto See
	Death.Microwave:
		"####" "#" 0 A_JumpIfInventory("Kamikaze", 1, "KamikazeDeath")
		"####" "#" 0 A_PlaySound("DukeGrow")
		goto GrowLoop
	GrowLoop:
		"####" "#" 1 A_SetScale(scalex+0.05, scaley+0.05)
		"####" "#" 0 A_JumpIf(scalex >= 2.32, "GrowExplode")
		loop
	GrowExplode:
		POL5 A 0 A_PlaySound("DukeExplosion")
		POL5 A 0 A_XScream
		POL5 A 0 A_Fall
		POL5 A 0 A_Explode
		POL5 A 0 A_GiveToTarget("SpeakOneLiner", 1)
		POL5 A 0 A_SpawnItemEx("DukeExplosion", 0, 0, 40, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		POL5 A 0 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		POL5 A -1 A_SetScale(1.0, 1.0)
		stop
	Ice:
		TAIL E 0 A_JumpIfInventory("Kamikaze", 1, "KamikazeDeath")
		TAIL E 5 A_GenericFreezeDeath
		TAIL E 1 A_FreezeDeathChunks
		Wait
	Death.IceRod:
		TAIL E 1 A_GenericFreezeDeath
		TAIL E 0 A_JumpIfInventory("Kamikaze", 1, "KamikazeDeath")
		TAIL EEEEEE 0 A_SpawnItemEx("IceRodFog4", 0, 0, 0, random(-8,8), random(-8,8), random(-8,8), 0, 128, 0)
		TAIL E 0 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		TAIL E 1 A_IceGuyDie
		stop
	KamikazeDeath:
		TAIL O 0
		TAIL O 0 A_TakeInventory("Kamikaze", 1)
		TAIL O 0 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		TAIL O 5 A_SpawnItemEx("ScreamingKamikazeSoul", 0, 0, 8, 0, 0, 1, 0, 0, 0)
		TAIL O 5 A_SpawnItemEx("KamikazeExplode", 0, 0, 2, 0, 0, 0, 0, 0, 0)
		TAIL Q 5 A_XScream
		TAIL R 5 A_NoBlocking
		TAIL STUV 5
		TAIL W -1
		stop
	}
}

actor DashDamager
{
	Projectile
	Radius 11
	Height 11
	Damage 5
	RenderStyle Translucent
	Alpha 0.6
	Speed 0
	DamageType "SpinDash"
	Scale 0.82
	Obituary "%o was spin-dashed by %k"
	+NOEXTREMEDEATH
	+NOCLIP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_Explode(32, 32, false, false, 32)
			TSPT A 1 A_FadeOut(0.1)
			goto Spawn+2
		Death:
			TNT1 A 1
			stop
	}
}

actor TailsWeaponUpgrade : CustomInventory
{
	Scale 0.80
	inventory.amount 1
	inventory.maxamount 1
	inventory.icon "TOOLICON"
	+INVBAR
	+FLOATBOB
	+NOGRAVITY
	-INTERHUBSTRIP
	+INVENTORY.UNDROPPABLE
	+INVENTORY.HUBPOWER
	states
	{
	Use:
		RCOR A 1 A_JumpIfInventory("HaloPlasmaRifle", 1, "Check1")
		RCOR A 0 ACS_NamedExecute("TPUPGRADE", 0, 1)
		stop
	Check1:
		RCOR A 1 A_JumpIfInventory("StickyBombs", 1, "Working")
		RCOR A 0 ACS_NamedExecute("TPUPGRADE", 0, 1)
		stop
	Working:
		RCOR A 1 ACS_NamedExecute("TPUPGRADE", 0, 0)
		RCOR A 1 A_PlaySound("Upgrade")
		stop
	}
}

actor TailsFlightTrigger : CustomInventory
{
	inventory.icon "ARTIFLY"
	inventory.amount 1
	inventory.maxamount 1
	+INVENTORY.QUIET
	+INVENTORY.INVBAR
	+INVENTORY.HUBPOWER
	+INVENTORY.KEEPDEPLETED
	+INVENTORY.ALWAYSPICKUP
	-INVENTORY.AUTOACTIVATE
	-INVENTORY.INTERHUBSTRIP
	+INVENTORY.UNDROPPABLE
	+COUNTITEM
	states
	{
	Spawn:
		LCHN A -1
		stop
	Use:
		TNT1 A 0
		TNT1 A 0 A_JumpifInventory("CannotUse", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("RedFlag", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("BlueFlag", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("WhiteFlag", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("RedSkullST", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("BlueSkullST", 1, "GoDie")
		TNT1 A 0 A_JumpIfInventory("PowerPossessionArtifact", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("PowerFlight", 1, "GoDie")
		TNT1 A 0
		goto GoFly
	GoFly:
		TNT1 A 0 A_GiveInventory("TailsFlight", 1)
		stop
	GoDie:
		TNT1 A 0
		stop
	}
}

actor TailsFlight : PowerupGiver
{
	inventory.amount 1
	inventory.maxamount 1
	powerup.duration 372
	powerup.type "Flight"
	+INVENTORY.QUIET
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.UNDROPPABLE
	+COUNTITEM
	states
	{
		Spawn:
			TNT1 A 1
			loop
	}
}