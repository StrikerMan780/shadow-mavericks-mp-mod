// Sprites thanks to SSNTails, and the SRB2 Development Team.
// Scripting and Character Voice by StrikerMan780.

actor TailsClassCheck : BaseClassCheck
{
}

actor Tails : BaseClass
{
	Scale 0.82
	Gravity 0.82
	
	Player.ForwardMove 1
	Player.SideMove 1
	Player.JumpZ 8.0
	Player.AttackZOffset 3
	Player.Viewheight 36
	
	Player.DisplayName "Tails"
	Player.SoundClass "tails"
	Player.ScoreIcon "TAILICON"
	Player.CrouchSprite "TSPN"
	
	Player.StartItem "Tails_TechPistol"
	Player.StartItem "Melee"
	Player.StartItem "TailsFlightCheck", 50
	Player.StartItem "TailsWeaponUpgrade", 1
	Player.StartItem "VilesBane", 5
	Player.StartItem "TailsClassCheck", 1
	Player.StartItem "CellAmmo", 50
	Player.StartItem "MechAmmo", 50

	Player.WeaponSlot 1, "Melee", "Excalibat"
	Player.WeaponSlot 2, "Tails_TechPistol"
	Player.WeaponSlot 3, "PIPS", "Keyring", "IceSSG"
	Player.WeaponSlot 4, "BCF-2Repeaters", "Mp40"
	Player.WeaponSlot 5, "StickyBombs", "ProtonTL", "HellfireCannon"
	Player.WeaponSlot 6, "HaloPlasmaRifle", "Cyclone", "ShockRifle"
	Player.WeaponSlot 7, "Supra", "BFG10k", "NukeLauncher", "Noisy_Cricket"
	Player.WeaponSlot 8, "BioRifle", "Needler", "TailsSniperRifle"
	Player.WeaponSlot 9, "WeatherMaker", "WookieBowcaster"
	Player.WeaponSlot 0, "E_Peen_Joke", "IPodLauncher", "GasolineCan", "FlameThrowerWeap"
	
	States
	{
	Spawn:
		TAIL A 0
		TAIL A 0 A_JumpIfInventory("PowerFlight", 1, "Fly")
		TAIL A 6 ACS_NamedExecuteAlways("FLIGHTCHECK", 0)
		loop
	Fly:
		TAIF A 0 A_JumpIfInventory("RedFlag", 1, "TakeCarry")
		TAIF A 0 A_JumpIfInventory("BlueFlag", 1, "TakeCarry")
		TAIF A 0 A_JumpIfInventory("WhiteFlag", 1, "TakeCarry")
		TAIF A 0 A_JumpIfInventory("RedSkullST", 1, "TakeCarry")
		TAIF A 0 A_JumpIfInventory("BlueSkullST", 1, "TakeCarry")
		TAIF A 0 A_JumpIfInventory("PowerPossessionArtifact", 1, "TakeCarry")
		TAIF AB 3
		TAIF A 0 A_PlaySound("TailsFlying")
		TAIF A 0 A_JumpIfInventory("PowerFlight", 1, "Fly")
		goto Spawn
	TakeCarry:
		TAIF A 0 A_TakeInventory("PowerFlight", 1)
		goto Spawn
	FlyAttack:
		TAIF BCDEF 1
		TAIF A 0 A_PlaySound("TailsFlying")
		goto Fly
	See:
		TAIL A 0 A_JumpIfInventory("PowerFlight", 1, "Fly")
		TAIL ABCD 4
		TAIL A 0 ACS_NamedExecuteAlways("FLIGHTCHECK", 0)
		Loop
	Burn:
		BURN A 0 A_Playsound("firedeath")
		BURN A 0 A_NoBlocking
		BURN ABCDEFGHIJKLMNOPQRSTUV 4 BRIGHT
		BURN V -1
		stop
	Disintegrate:
		NULL A 0 A_Playsound("disintegrate")
		NULL A 0 A_NoBlocking
		NULL A 0 A_SpawnItemEx("DisintegratingPlayer", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		NULL A 3
		NULL A -1
		stop
	Missile:
		TAIL E 0 ACS_NamedExecuteAlways("STOPABILITY", 0)
		TAIL E 0 A_JumpIfInventory("PowerFlight", 1, "FlyAttack")
		TAIL E 12
		Goto Spawn
	Melee:
		TAIL F 0 ACS_NamedExecuteAlways("STOPABILITY", 0)
		TAIL F 0 A_JumpIfInventory("PowerFlight", 1, "FlyAttack")
		TAIL F 6 BRIGHT
		Goto Missile
	Pain:
		TAIL G 4 
		TAIL G 4 A_Pain
		TAIL A 0 A_JumpIfInventory("PowerFlight", 1, "Fly")
		Goto Spawn
	Death.Fist:
		TAIL H 0 A_GiveToTarget("KnockoutTrigger", 1)
		goto Death
	Death.QuadSSG:
		TAIL H 0
		TAIL H 0 ThrustThingZ(0, Random(28, 40), 0, 1)
		goto Death
	Death:
		TAIL H 7
		TAIL I 7 A_PlayerScream
		TAIL J 7 A_NoBlocking
		TAIL KLM 7
		TAIL N -1
		Stop
	XDeath:
		TAIL O 5 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		TAIL P 5 A_XScream
		TAIL Q 5 A_NoBlocking
		TAIL RSTUV 5
		TAIL W -1
		Stop
	Spin:
		TSPN A 0 A_ChangeFlag("NOPAIN", 1)
		TSPN A 0 A_PlaySound("SpinDash", CHAN_WEAPON)
		TSPN A 0 ACS_NamedExecuteAlways("STARTCAMERA", 0) // Chasecam
		TSPN A 0 A_Recoil(-10)
		TSPN AAA 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN A 0 A_Recoil(-9)
		TSPN BBB 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN B 0 A_Recoil(-8)
		TSPN CCC 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN C 0 A_Recoil(-8)
		TSPN AAA 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN A 0 A_Recoil(-8)
		TSPN BBB 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN B 0 A_Recoil(-8)
		TSPN CCC 1 A_SpawnItemEX("DashDamager", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION, 0)
		TSPN AB 2
		TSPN C 3
		TAIL A 0 ACS_NamedExecuteAlways("STOPCAMERA", 0)
		TNT1 A 0 ACS_NamedExecuteAlways("STOPABILITY", 0)
		TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
		goto See
	Death.Microwave:
		"####" "#" 0 A_PlaySound("DukeGrow")
		goto GrowLoop
	GrowLoop:
		"####" "#" 1 A_SetScale(scalex+0.05, scaley+0.05)
		"####" "#" 0 A_JumpIf(scalex >= 2.32, "GrowExplode")
		loop
	GrowExplode:
		POL5 A 0 A_PlaySound("DukeExplosion")
		POL5 A 0 A_XScream
		POL5 A 0 A_Fall
		POL5 A 0 A_Explode
		POL5 A 0 A_SpawnItemEx("DukeExplosion", 0, 0, 40, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		POL5 A 0 A_SpawnItemEx("XDeathGibs", 0, 0, 6, MOMX, MOMY, MOMZ, 0, SXF_CLIENTSIDE | SXF_ABSOLUTEMOMENTUM, 0)
		POL5 A -1 A_SetScale(1.0, 1.0)
		stop
	Ice:
		TAIL E 5 A_GenericFreezeDeath
		TAIL E 1 A_FreezeDeathChunks
		Wait
		
	ChaosDash:
		TSPN A 0 A_ChangeFlag("NOPAIN", 1)
		TSPN A 0 A_Stop
		TSPN A 0 A_Quake(1, 105, 0, 300, "world/quake")
		TSPN A 0 ACS_NamedExecuteAlways("STARTCAMERA", 0) // Chasecam
		TCHO A 0 A_PlaySound("ChaosDash", CHAN_AUTO, 1.0, false, 0.5)
		TCHO A 0 A_SpawnItemEx("PSOSymbol1", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TCHO A 0 A_SpawnItemEx("PSOSymbol2", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TCHO A 0 A_SpawnItemEx("ChaosEmeraldEffect", 0, 0, 16.0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TCHO A 105
		TSPN A 0 A_TakeInventory("GenericTimer", 999)
		TSPN A 0 A_PlaySound("SpinDash", CHAN_WEAPON)
		TSPN A 0 A_PlaySound("ChaosFire", CHAN_AUTO, 1.0, false, 0.5)
		TSPN A 0 A_PlaySound("ChaosFlameLoop", CHAN_7, 1.0, true, 0.5)
		goto ChaosLoop
		
	ChaosLoop:
		TSPN A 0 A_GiveInventory("GenericTimer", 1)
		TSPN A 0 A_Quake(1, 9, 0, 256, "world/quake")
		TSPN A 0 A_Recoil(-10)
		TSPN C 0 A_SpawnItemEX("ChaosDashFireball", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TSPN AAA 1 A_SpawnItemEX("ChaosDashDamager", 0, 0, 0, 0, 0, 0, 0, 0, 0)
		TSPN A 0 A_Recoil(-10)
		TSPN C 0 A_SpawnItemEX("ChaosDashFireball", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TSPN BBB 1 A_SpawnItemEX("ChaosDashDamager", 0, 0, 0, 0, 0, 0, 0, 0, 0)
		TSPN B 0 A_Recoil(-10)
		TSPN C 0 A_SpawnItemEX("ChaosDashFireball", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		TSPN CCC 1 A_SpawnItemEX("ChaosDashDamager", 0, 0, 0, 0, 0, 0, 0, 0, 0)
		TSPN A 0 A_JumpIfInventory("GenericTimer", 25, "ChaosEnd")
		loop
		
	ChaosEnd:
		TSPN A 0 A_StopSound(CHAN_7)
		TSPN A 0 A_PlaySound("ChaosDone", CHAN_AUTO, 1.0, false, 0.5)
		TSPN AB 2
		TSPN C 3
		TNT1 A 0 ACS_NamedExecuteAlways("STOPABILITY", 0)
		TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
		goto See
	}
}

actor DashDamager
{
	Projectile
	Radius 11
	Height 11
	Damage 5
	RenderStyle Translucent
	Alpha 0.6
	Speed 0
	DamageType "SpinDash"
	Scale 0.82
	Obituary "%o was spin-dashed by %k"
	+NOEXTREMEDEATH
	+FORCERADIUSDMG
	+NOCLIP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_Explode(32, 32, false, false, 32)
			goto Fade
		Fade:
			TSPT A 1 A_FadeOut(0.1)
			loop
		Death:
			TNT1 A 1
			stop
	}
}

actor ChaosDashDamager : DashDamager
{
	Obituary "%o was consumed by %k's chaos dash."
	DamageType "Extreme"
	Translation "112:127=5:8"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_RadiusThrust(-4000, 160, RTF_NOIMPACTDAMAGE, 128) 
			TNT1 A 0 A_SpawnItemEx("BlackFlame", frandom(-32.0, 32.0), frandom(-32.0, 32.0), frandom(0, 32.0), 0, 0, frandom(0.5, 1.0), 0, SXF_CLIENTSIDE, 0)
			TNT1 A 1 A_Explode(50, 40, false, false, 32)
			goto Fade
		Fade:
			TSPT A 1 Light("BLACKHOLE") A_FadeOut(0.1)
			loop
	}
}

actor ChaosDashFireball : BaseEffect
{
	Alpha 1.0
	RenderStyle "Subtract"
	States
	{
		Spawn:
			NULL AAA 1 BRIGHT NoDelay Light("BLACKHOLE2") A_Warp(AAPTR_TARGET, 0, 0, 20, 0, WARPF_INTERPOLATE | WARPF_COPYINTERPOLATION)
			stop
	}
}

actor PSOSymbol1 : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	-NOINTERACTION
	-NOGRAVITY
	States
	{
		Spawn:
			NULL A 105 BRIGHT Light("PLASMABALL")
			goto Fade
		Fade:
			NULL A 1 BRIGHT Light("PLASMABALL") A_FadeOut(0.05)
			loop
	}
}

actor PSOSymbol2 : PSOSymbol1
{
	States
	{
		Spawn:
			NULL A 105 BRIGHT
			goto Fade
	}
}

actor ChaosEmeraldEffect : BaseEffect
{
	-NOINTERACTION
	RenderStyle Translucent
	Alpha 1.0
	States
	{
		Spawn:
			CHAO A 0 NoDelay A_ChangeVelocity(0.0, 0.0, 4.0, CVF_REPLACE)
			CHAO ABCDABCDABCD 3 A_ScaleVelocity(0.75)
			CHAO A 0 A_SpawnItemEX("ChaosCorruption", 0, 0, 8, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			CHAO ABCD 3 A_ScaleVelocity(0.75)
			goto FadeToBlack
		FadeToBlack:
			CHAO EFGH 3 A_Stop
			goto Blackened
		Blackened:
			CHAO IJKLIJKLIJKLIJKL 3
			CHAO L 0 A_ChangeVelocity(0.0, 0.0, -2.0, CVF_REPLACE)
			goto Fade
		Fade:
			CHAO IIIJJJKKKLLL 1 A_Fadeout(0.1)
			loop
	}
}

actor ChaosCorruption : BaseEffect
{
	Scale 0.1
	RenderStyle Add
	Alpha 1.0
	States
	{
	Spawn:
		WRAT A 0
		WRAT A 0 A_PlaySound("WrathSpawn")
		WRAT A 10 BRIGHT
		WRAT BCDEFGHIJKL 2 BRIGHT
		Stop
	Death:
		TNT1 AA 1
		Stop
	}
}

actor TailsWeaponUpgrade : CustomInventory
{
	Scale 0.80
	inventory.amount 1
	inventory.maxamount 1
	inventory.icon "TOOLICON"
	+INVBAR
	+FLOATBOB
	+NOGRAVITY
	-INTERHUBSTRIP
	+INVENTORY.UNDROPPABLE
	+INVENTORY.HUBPOWER
	states
	{
	Use:
		RCOR A 1 A_JumpIfInventory("HaloPlasmaRifle", 1, "Check1")
		RCOR A 0 ACS_NamedExecute("TPUPGRADE", 0, 1)
		stop
	Check1:
		RCOR A 1 A_JumpIfInventory("StickyBombs", 1, "Working")
		RCOR A 0 ACS_NamedExecute("TPUPGRADE", 0, 1)
		stop
	Working:
		RCOR A 1 ACS_NamedExecute("TPUPGRADE", 0, 0)
		RCOR A 1 A_PlaySound("Upgrade")
		stop
	}
}

actor TailsFlightTrigger : CustomInventory
{
	inventory.icon "ARTIFLY"
	inventory.amount 1
	inventory.maxamount 1
	+INVENTORY.QUIET
	+INVENTORY.INVBAR
	+INVENTORY.HUBPOWER
	+INVENTORY.KEEPDEPLETED
	+INVENTORY.ALWAYSPICKUP
	-INVENTORY.AUTOACTIVATE
	-INVENTORY.INTERHUBSTRIP
	+INVENTORY.UNDROPPABLE
	+COUNTITEM
	states
	{
	Spawn:
		NULL A -1
		stop
	Use:
		TNT1 A 0
		TNT1 A 0 A_JumpifInventory("CannotUse", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("RedFlag", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("BlueFlag", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("WhiteFlag", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("RedSkullST", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("BlueSkullST", 1, "GoDie")
		TNT1 A 0 A_JumpIfInventory("PowerPossessionArtifact", 1, "GoDie")
		TNT1 A 0 A_Jumpifinventory("PowerFlight", 1, "GoDie")
		TNT1 A 0
		goto GoFly
	GoFly:
		TNT1 A 0 A_GiveInventory("TailsFlight", 1)
		stop
	GoDie:
		TNT1 A 0
		stop
	}
}

actor TailsFlight : PowerupGiver
{
	inventory.amount 1
	inventory.maxamount 1
	powerup.duration 372
	powerup.type "Flight"
	+INVENTORY.QUIET
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.UNDROPPABLE
	+COUNTITEM
	states
	{
		Spawn:
			TNT1 A 1
			loop
	}
}