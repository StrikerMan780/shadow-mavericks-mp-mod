actor SM_ArachnotronPowerBeam : PowerBeam
{
	SeeSound "SupraFire"
	obituary "%o was evaporated by an Arachnotron's Supra!"
	Speed 50
	Damage (5)
	-RIPPER
	States
	{
		Death:
			APBX AAAA 0 A_SpawnItemEx("PowerMist", 0, 0, 0, frandom(-4.0,4.0), frandom(-4.0,4.0), frandom(-4.0,4.0), 0, SXF_CLIENTSIDE, 0)
			APBX ABCDE 2 BRIGHT A_SpawnItemEx("PowerMist", 0, 0, 0, frandom(-2.0,2.0), frandom(-2.0,2.0), frandom(-2.0,2.0), 0, SXF_CLIENTSIDE, 0)
			stop
	}
}

actor SM_ArachnotronPlasma : NewPlasma
{
	SeeSound "weapons/plasmaf"
	Damage (10)
	Speed 30
	Obituary "%o was lit up by an Arachnotron."
	States
	{
		XDeath:
			TNT1 A 0
			goto Death
	}
}

actor SMArachnotron : Arachnotron
{
	DropItem "CricketAmmo", 28
	DropItem "CellAmmo"
	States
	{
		Missile:
			BSPI A 20 Bright A_FaceTarget
			BSPI A 0 Bright A_GiveInventory("Firing", 1)
			goto MissileLoop
		MissileLoop:
			BSPI G 0 Bright A_FaceTarget
			BSPI G 2 Bright A_CustomMissile("SM_ArachnotronPowerBeam", 24, 0, frandom(-3.0,3.0), 4)
			BSPI H 1 Bright A_FaceTarget
			BSPI H 0 Bright A_MonsterRefire(10, "StopFiring")
			loop
		StopFiring:
			BSPI H 0 A_TakeInventory("Firing", 1)
			goto See
		Pain:
			BSPI I 3 A_TakeInventory("Firing", 1)
			BSPI I 3 A_Pain
			Goto See+1
		Death:
			BSPI J 20 A_Scream
			BSPI K 7 A_NoBlocking
			BSPI LMNO 7
			BSPI P 10500 A_BossDeath
			goto Fade
		Fade:
			"####" "#" 1 A_FadeOut(0.05)
			loop
	}
}

actor SMPlasmaArachnotron : SMArachnotron
{
	DropItem "PlasmaRifle", 32
	DropItem "CellAmmo", 32
	Translation "160:162=214:216", "226:231=211:215", "163:167=176:180", "192:207=112:127"
	States
	{
		Missile:
			BSPI A 20 Bright A_FaceTarget
			BSPI A 0 Bright A_GiveInventory("Firing", 1)
			goto MissileLoop
		MissileLoop:
			BSPI G 0 Bright A_FaceTarget
			BSPI G 4 Bright A_CustomMissile("SM_ArachnotronPlasma", 24, 0, frandom(-0.5,0.5), 4)
			BSPI H 2 Bright A_FaceTarget
			BSPI H 0 Bright A_MonsterRefire(10, "StopFiring")
			loop
	}
}
