actor PIPSSpark : BaseEffect
{
	BounceType Hexen
	RenderStyle Add
	Scale 0.03
	Alpha 1.0
	+MISSILE
	-NOGRAVITY
	-NOINTERACTION
	States
	{
		Spawn:
			GEMB A 6 BRIGHT
			GEMB A 1 BRIGHT A_FadeOut(0.1)
			goto Spawn+1	
	}
}

ACTOR PIPSHit
{
	Radius 16
	Height 8
	Scale 0.5
	Alpha 1.0
	Renderstyle Add
	+NOGRAVITY
	+FIXMAPTHINGPOS
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	States
	{
	Spawn:
	  PIPH A 0
	  PIPH A 0 A_PlaySoundEx("RepeaterHit", "auto", 0)
	  PIPH ABCDEF 1 BRIGHT
	  Stop
	}
}

actor PIPS_Shot : FastProjectile
{
	obituary "%o was toasted by %k's PIPS!"
	Decal PIPSChip
	Projectile
	Radius 2
	Height 2
	Speed 150
	Damage (3)
	RenderStyle Add
	Alpha 1.0
	Scale 0.25
	+BLOODLESSIMPACT
	+NOGRAVITY
	+NOBOSSRIP
	States
	{
		Spawn:
			PIPH A 1 BRIGHT
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death
		Death:
			TNT1 A 0
			TNT1 A 0 A_Explode(5, 32, 1, 0, 24)
			TNT1 AAA 0 A_SpawnItemEx("PIPSSpark", 0, 0, 0, frandom(-5.0,5.0), frandom(-5.0,5.0), frandom(0.0,7.0), 0, SXF_CLIENTSIDE, 0)
			TNT1 A 1 A_SpawnItemEx("PIPSHit", 0, 0, 0, 0, 0, 0, 0, 0, 0)
			stop
	}
}

actor NeedlerCrystal
{
  Decal Needler
  Projectile
  Height 5
  Radius 5
  Speed 78
  Damage 3
  Scale 0.5
  RenderStyle Add
  Alpha 0.8
  seesound "NeedlerFire"
  deathsound "NeedlerHit"
  +BLOODSPLATTER
  -BLOODLESSIMPACT
  +FORCEXYBILLBOARD
  States
  {
  Spawn:
  	GLSS A 0
  	GLSS A 0 ThrustThingZ(0, random(0, 8), random(0,1), 1)
	GLSS A 1 BRIGHT
	goto Spawn+2
  XDeath:
	TNT1 A 0
	TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
	goto Death
  Death:
	TNT1 A 0
	TNT1 A 0 A_Scream
	TNT1 AA 0 A_CustomMissile ("NeedlerShard", 0, 0, Random(-360, 360), 6, Random(-360, 360))
	TNT1 AA 1
	stop
  }
}

actor NeedlerShard
{
  Projectile
  Height 2
  Radius 2
  Scale 0.5
  Speed 3
  Damage 2
  BounceCount 2
  BounceFactor 0.8
  RenderStyle Add
  Alpha 0.8
  ReactionTime 200
  +DOOMBOUNCE
  +BLOODSPLATTER
  +FORCEXYBILLBOARD
  -BLOODLESSIMPACT
  -NOGRAVITY
  States
  {
  Spawn:
	BSHD ABCDEF 1 BRIGHT A_CountDown
	loop
  Death:
	BSHD F 2 BRIGHT A_FadeOut
	loop
  }
}

actor EnemyNeedlerCrystal : NeedlerCrystal
{
	decal Needler
	OBITUARY "%o was spiked by a Needler Troop."
	Speed 60
	Damage 2
	+FORCEXYBILLBOARD
}

ACTOR BioBall
{
	Radius 8
	Height 1
	Health 1
	Damage 5
	PoisonDamage 50
	Speed 38
	Renderstyle Add
	Alpha 0.9
	SeeSound "BioRifle"
	DeathSound "BioGoo"
	Projectile
	+CORPSE
	+NODAMAGETHRUST
	+DONTSPLASH
	+FORCERADIUSDMG
	-NOGRAVITY
	States
	{
		Spawn:
			SBS3 A 0
			SBS3 A 0 BRIGHT ThrustThingZ(0, random(12, 16), 0, 1)
			SBS3 A 1 BRIGHT A_NoGravity
			SBS3 BC 1 BRIGHT
			SBS3 A 0 BRIGHT A_Gravity
			SBS3 AABBCC 1 BRIGHT
			goto Spawn+5
		Death:
			SBS3 A 0 A_NoGravity
			SBS3 A 0 A_Jump(127, 6)
			SBS3 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT
			SLDG EEEEEEEE 0 A_SpawnItemEx("BioBurstDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(2, 8), 0, 0, 0)
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(112, 100, 1)
			SLDG EFGH 2 BRIGHT
			Stop
		XDeath:
			SBS3 A 0 A_NoGravity
			SBS3 DDDDDDDD 0 A_SpawnItemEx("BioBurstDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(2, 8), 0, 0, 0)
			SBS3 D 0 A_PlaySound("BioBurst")
			SBS3 D 0 A_Explode(32, 64, 1)
			SBS3 DEFGH 1 BRIGHT
			Stop
		Crash:
			SBS4 A 0 A_Jump(127, 6)
			SBS4 A 0 A_Jump(80, 10)
			SBS4 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT A_Explode(2, 14, 0)
			SLDG EEEEEEEE 0 A_SpawnItemEx("BioBurstDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(2, 8), 0, 0, 0)
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(90, 80, 1)
			SLDG EFGH 2 BRIGHT
			Stop
	}
}

ACTOR SlowBioBall : Bioball
{
	Radius 2
	Height 1
	Health 1
	Damage 8
	PoisonDamage 50
	Decal BioRifle
	Speed 10
	-NOGRAVITY
	States
	{
		Spawn:
			SBS3 ABC 1 BRIGHT
			loop
	}
}

actor Enemy_BioBall : BioBall
{
	DamageType "EnemyPoison"
	Damage 2
	PoisonDamage 25
	States
	{
		Spawn:
			SBS3 AABBCC 1 BRIGHT
			loop
	}
}

ACTOR GiantBioBall
{
	Decal BioRifle
	Radius 8
	Height 1
	Damage 14
	PoisonDamage 50
	Speed 38
	Scale 2.0
	Renderstyle Add
	Alpha 0.9
	SeeSound "BioRifle"
	DeathSound "BioGoo"
	Projectile
	+CORPSE
	+NODAMAGETHRUST
	+DONTSPLASH
	+FORCERADIUSDMG
	-NOGRAVITY
	States
	{
		Spawn:
			SBS3 A 0
			SBS3 A 0 BRIGHT ThrustThingZ(0, 14, 0, 1)
			SBS3 A 1 BRIGHT A_NoGravity
			SBS3 BC 1 BRIGHT
			SBS3 A 0 BRIGHT A_Gravity
			SBS3 AABBCC 1 BRIGHT
			goto Spawn+5
		Death:
			SBS3 A 0 A_NoGravity
			SBS3 A 0 A_Jump(127, 6)
			SBS3 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT
			SLDG EEEEEEEE 0 A_CustomMissile("SlowBioBall",17,0,Random(-360,360), 6, random(-360, 360))
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(140, 134, 1)
			SLDG EFGH 2 BRIGHT
			Stop
		XDeath:
			SBS3 A 0 A_NoGravity
			SLDG EEEEEEEE 0 A_CustomMissile("SlowBioBall",17,0,Random(-360,360), 6, random(-360, 360))
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(64, 134, 1)
			SLDG EFGH 2 BRIGHT
			Stop
		Crash:
			SBS4 A 0 A_Jump(127, 6)
			SBS4 A 0 A_Jump(80, 10)
			SBS4 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT A_Explode(5, 24, 0)
			SLDG EEEEEEEE 0 A_CustomMissile("SlowBioBall",17,0,Random(-360,360), 6, random(0, 180))
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(128, 128, 1)
			SLDG EFGH 2 BRIGHT
			Stop
	}
}

ACTOR VulgarShot
{
   Radius 8
   Height 11
   Speed 14
   Damage 4
   PoisonDamage 20
   SeeSound "BioRifle"
   RENDERSTYLE ADD
   ALPHA 0.67
   PROJECTILE
   +THRUGHOST
   +FORCEXYBILLBOARD
   DeathSound "FleshySplat"
   States
   {
   Spawn:
	  FVUL AAABBB 1 Bright A_SpawnItemEx("BarbTrail",Random(-8,8),Random(-8,8),Random(-8,8),0,0,0,0,128,0)
	  loop
   Death:
	  FVUL CDEF 4 Bright
	  stop
   }
}

ACTOR Barbtrail
{
   Radius 0
   Height 1
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.75
   +FORCEXYBILLBOARD
   States
   {
   Spawn:
	  NULL A 1 Bright
	  SSFX ABCDEFG 2 Bright
	  Stop
   }
}

actor HangerTrail : BarbTrail
{
	Translation "112:127=162:167"
}

actor HangerBall
{
  Radius 4
  Height 6
  Speed 10
  FastSpeed 15
  Damage 4
  SeeSound "hanger/attack"
  DeathSound "slimeball/splat"
  Scale 0.5
  Translation "112:127=162:167"
  RenderStyle Add
  Alpha 1.0
  Projectile
  +RANDOMIZE
  -NOGRAVITY
  +LOWGRAVITY
  +SPAWNSOUNDSOURCE
  states
  {
  Spawn:
    FVUL AAA 1 A_SpawnItemEx("HangerTrail", random (-2,2), random (-2,2), random (-2,2), random (-1,1), random (-1,1), random (-1,1), random (-180,180), SXF_CLIENTSIDE, 0)
	FVUL BBB 1 A_SpawnItemEx("HangerTrail", random (-2,2), random (-2,2), random (-2,2), random (-1,1), random (-1,1), random (-1,1), random (-180,180), SXF_CLIENTSIDE, 0)
    loop
  Death:
    FVUL C 0 A_ChangeFlag("NOGRAVITY", 1)
    FVUL CDEF 3
    stop
  }
}

ACTOR SliceProjectile
{
	Health 400
	Height 40
	Radius 18
	Speed 32
	PainSound "saberhit"
	DeathSound "saberhit"
	PainChance 255
	RENDERSTYLE NONE
	+MISSILE
	+NOBLOCKMONST
	+DONTSPLASH
	+SHOOTABLE
	+NOBLOOD
	+NOCLIP
	+GHOST
	+THRUGHOST
	+HEXENBOUNCE
	+CANBOUNCEWATER
	-BOUNCEONACTORS
	States
	{
		Spawn:
			TNT1 AA 1
			Stop
		Pain:
			TNT1 A 1 A_SpawnItemEX("SaberPuff",  0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			TNT1 A 1 A_Pain
			Stop
		Death:
			TNT1 A 1 A_SpawnItemEX("SaberPuff",  0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			TNT1 A 1 A_Scream
			TNT1 A 0
			Stop
	}
}

ACTOR ReflectProjectile : SliceProjectile
{
	Species "SaberReflect"
	PainSound "saberhit"
	DeathSound "saberhit"
	+REFLECTIVE
	+NOBLOOD
	+THRUSPECIES
}

actor BluePlasmaTrail : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	Scale 0.04
	States
	{
	Spawn:
		TNT1 A 0
		BLSE ABCDE 1 BRIGHT
		stop
	Death:
		TNT1 A 0
		TNT1 A 1
		stop
	}
}

actor BluePlasmaFlashSpawner
{
	Projectile
	Radius 2
	Height 2
	Speed 25
	+ALLOWCLIENTSPAWN
	States
	{
		Death:
		Spawn:
			BLSS A 0
			BLSS A 0 A_SpawnItemEx("BluePlasmaFlash1", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
			stop
	}
}

actor BluePlasmaFlash1 : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	Scale 0.064
	States
	{
	Spawn:
		PMUZ AAAA 0 BRIGHT A_SpawnItemEx("BluePlasmaFlash2", 0, 0, 0, 0, (random(-400,400)/100.0), (random(-150,150)/100.0), 0, SXF_CLIENTSIDE, 0)
		goto SpawnLoop
	SpawnLoop:
		PMUZ A 1 BRIGHT A_FadeOut(0.1)
		loop
	Death:
		TNT1 A 0
		TNT1 A 1
		stop
	}
}

actor BluePlasmaFlash2 : BaseEffect
{
	RenderStyle Add
	Alpha 0.75
	Scale 0.04
	States
	{
	Spawn:
		PMUZ B 1 BRIGHT A_FadeOut(0.1)
		loop
	Death:
		TNT1 A 0
		TNT1 A 1
		stop
	}
}

actor BluePlasmaMist : BaseEffect
{
	RenderStyle Add
	Scale 0.10
	States
	{
	Spawn:
		TNT1 A 0
		BLSE ABCDE 1 BRIGHT
		stop
	Death:
		TNT1 A 0
		TNT1 A 1
		stop
	}
}

actor BluePlasma : PlasmaBall
{
	Radius 8
	Height 8
	decal BluePlasmaScorch
	Damage (20)
	Speed 50
	obituary "%k gave %o a serious case of Plasma Burn"
	Alpha 1.0
	RenderStyle Add
	SeeSound ""
	DeathSound "HaloPlasmaHit"
	scale 0.04
	States
	{
	Spawn:
		BLSS A 0
		BLSS A 0 ThrustThingZ(0, Random(0,3), Random(0,1), 1)
		goto SpawnLoop
	SpawnLoop:
		BLSS AB 1 BRIGHT A_SpawnItemEx("BluePlasmaTrail", 0, 0, 0, (random(-80,80)/100.0), (random(-80,80)/100.0), (random(-80,80)/100.0), 0, SXF_CLIENTSIDE, 0)
		loop
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
		goto Death
	Death:
		BLSE AAAAAAA 0 A_SpawnItemEx("BluePlasmaMist", 0, 0, 0, (Random(-400,400)/100.0), (Random(-400,400)/100.0), (Random(-400,400)/100.0), 0, SXF_CLIENTSIDE, 0)
		BLSE ABCDE 2 Bright
		Stop
	}
}

actor PowerMist : BaseEffect
{
	RenderStyle Add
	Scale 0.1
	States
	{
	Spawn:
		TNT1 A 0
		APBX ABCDE 1 BRIGHT
		stop
	Death:
		TNT1 A 0
		TNT1 A 1
		stop
	}
}

actor PowerTrail : BaseEffect
{
	RenderStyle Add
	Alpha 1.0
	Scale 0.02
	States
	{
	Spawn:
		TNT1 A 2
		goto Projectile
	Projectile:
		APLS ABCD 1 A_Fadeout(0.1)
		loop
	Death:
		TNT1 A 0
		TNT1 A 1
		stop
	}
}

actor PowerBeam
{
	Radius 4
	Height 8
	Projectile
	RenderStyle Add
	Alpha 1.0
	obituary "%o was 'sploded by %k's Supra"
	speed 64
	damage (4)
	scale 0.02
	DamageType "Disintegrate"
	Decal "TealPlasmaScorch"
	+RIPPER
	-BLOODSPLATTER
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	States
	{
	Spawn:
		APLS A 1 BRIGHT
		APLS A 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/1.5)+frandom(-0.5,0.5), (MOMY/1.5)+frandom(-0.5,0.5), (MOMZ/1.5)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS A 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/2.0)+frandom(-0.5,0.5), (MOMY/2.0)+frandom(-0.5,0.5), (MOMZ/2.0)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS A 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/2.5)+frandom(-0.5,0.5), (MOMY/2.5)+frandom(-0.5,0.5), (MOMZ/2.5)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS B 1 BRIGHT
		APLS B 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/1.5)+frandom(-0.5,0.5), (MOMY/1.5)+frandom(-0.5,0.5), (MOMZ/1.5)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS B 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/2.0)+frandom(-0.5,0.5), (MOMY/2.0)+frandom(-0.5,0.5), (MOMZ/2.0)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS B 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/2.5)+frandom(-0.5,0.5), (MOMY/2.5)+frandom(-0.5,0.5), (MOMZ/2.5)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS C 1 BRIGHT
		APLS C 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/1.5)+frandom(-0.5,0.5), (MOMY/1.5)+frandom(-0.5,0.5), (MOMZ/1.5)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS C 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/2.0)+frandom(-0.5,0.5), (MOMY/2.0)+frandom(-0.5,0.5), (MOMZ/2.0)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS C 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/2.5)+frandom(-0.5,0.5), (MOMY/2.5)+frandom(-0.5,0.5), (MOMZ/2.5)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS D 1 BRIGHT
		APLS D 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/1.5)+frandom(-0.5,0.5), (MOMY/1.5)+frandom(-0.5,0.5), (MOMZ/1.5)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS D 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/2.0)+frandom(-0.5,0.5), (MOMY/2.0)+frandom(-0.5,0.5), (MOMZ/2.0)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		APLS D 0 A_SpawnItemEx("PowerTrail", 0, 0, 0, (MOMX/2.5)+frandom(-0.5,0.5), (MOMY/2.5)+frandom(-0.5,0.5), (MOMZ/2.5)+frandom(-0.5,0.5), 0, SXF_ABSOLUTEMOMENTUM | SXF_CLIENTSIDE, 240)
		loop
	Death:
		APBX A 0 BRIGHT
		APBX A 0 BRIGHT A_Explode(24, 32, 1)
		APBX AAAA 0 A_SpawnItemEx("PowerMist", 0, 0, 0, frandom(-4.0,4.0), frandom(-4.0,4.0), frandom(-4.0,4.0), 0, SXF_CLIENTSIDE, 0)
		APBX ABCDE 2 BRIGHT A_SpawnItemEx("PowerMist", 0, 0, 0, frandom(-2.0,2.0), frandom(-2.0,2.0), frandom(-2.0,2.0), 0, SXF_CLIENTSIDE, 0)
		stop
	}
}

actor Tourettes_NewPlasma : NewPlasma
{
	DamageType "Tourettes"
	SeeSound "HaloPlasmaRifle"
}

actor IgnitedSurface
{
	Projectile
	Height 5
	Radius 2
	BounceFactor 0.0
	obituary "%o was purified by %k's cleansing flame."
	RenderStyle Add
	Scale 0.5
	ReactionTime 160
	DamageType "Fire"
	-SOLID
	+NODAMAGETHRUST
	+LOWGRAVITY
	-NOGRAVITY
	+HEXENBOUNCE
	+SKYEXPLODE
	+DONTSPLASH
	States
	{
		Spawn:
			TFIR A 0 A_CountDown
			TNT1 A 0 A_SpawnItemEx("BasicFlame", random(-8,8), Random(-8,8), 0, 0, 0, Random(1, 2), 0, 128, 64)
			TNT1 A 2 A_Explode(3, 18, 1)
			TNT1 A 0 A_JumpIf(waterlevel == 3, "Disappear")
			loop
		Death:
			TNT1 A 1
			stop
		Disappear:
			TNT1 A 0
			TNT1 A 1
			stop
	}
}