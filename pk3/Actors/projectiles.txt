actor BulletTracer : FastProjectile
{
	Projectile
	Radius 2
	Height 2
	Speed 232
	RenderStyle Add
	Alpha 1.0
	+NOGRAVITY
	+THRUGHOST
	States
	{
		Spawn:
			PUFF A 1 BRIGHT
			loop
		Crash:
		Death:
			TNT1 A 0
			TNT1 A 1
			stop
	}
}

actor PIPSSpark
{
	Height 1
	Radius 1
	Damage 1
	Speed 10
	Projectile
	-NOGRAVITY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+STRIFEDAMAGE
	BounceType Hexen
	RenderStyle Add
	Scale 0.03
	Alpha 0.9
	States
	{
		Spawn:
			GEMB A 4 BRIGHT
			GEMB A 1 BRIGHT A_FadeOut(0.1)
			goto Spawn+1
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death	
		Death:
			TNT1 AA 1
			stop
	}
}

ACTOR PIPSHit
{
	Radius 16
	Height 8
	Scale 0.5
	Alpha 1.0
	Renderstyle Add
	obituary "%o was singed by %k!"
	+NOGRAVITY
	+FIXMAPTHINGPOS
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	States
	{
	Spawn:
	  PIPH A 0
	  PIPH A 0 A_PlaySoundEx("RepeaterHit", "auto", 0)
	  PIPH ABCDEF 1 BRIGHT
	  Stop
	}
}

actor PIPS_Shot : FastProjectile
{
	Projectile
	Radius 2
	Height 2
	Speed 200
	Damage 2
	RenderStyle Add
	Alpha 1.0
	Scale 0.25
	+NOGRAVITY
	States
	{
		Spawn:
			PIPH A 1 BRIGHT
			loop
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
			goto Death
		Death:
			TNT1 A 0
			TNT1 AAAAAA 0 A_CustomMissile("PIPSSpark", 0, 0, random(-180,180), CMF_AIMDIRECTION | CMF_TRACKOWNER, random(-90,90))
			TNT1 A 1 A_SpawnItemEx("PIPSHit", 0, 0, 0, 0, 0, 0, 0, 0, 0)
			stop
	}
}

actor NeedlerCrystal
{
  Decal Needler
  Projectile
  Height 5
  Radius 5
  Speed 78
  Damage 3
  Scale 0.5
  RenderStyle Add
  Alpha 0.8
  seesound "NeedlerFire"
  deathsound "NeedlerHit"
  +BLOODSPLATTER
  -BLOODLESSIMPACT
  +FORCEXYBILLBOARD
  States
  {
  Spawn:
  	GLSS A 0
  	GLSS A 0 ThrustThingZ(0, random(0, 8), random(0,1), 1)
	GLSS A 1 BRIGHT
	goto Spawn+2
  XDeath:
	TNT1 A 0
	TNT1 A 0 A_GiveToTarget("HitConfirmation", 1)
	goto Death
  Death:
	TNT1 A 0
	TNT1 A 0 A_Scream
	TNT1 AA 0 A_CustomMissile ("NeedlerShard", 0, 0, Random(-360, 360), 6, Random(-360, 360))
	TNT1 AA 1
	stop
  }
}

actor NeedlerShard
{
  Projectile
  Height 2
  Radius 2
  Scale 0.5
  Speed 3
  Damage 2
  BounceCount 2
  BounceFactor 0.8
  RenderStyle Add
  Alpha 0.8
  ReactionTime 200
  +DOOMBOUNCE
  +BLOODSPLATTER
  +FORCEXYBILLBOARD
  -BLOODLESSIMPACT
  -NOGRAVITY
  States
  {
  Spawn:
	BSHD ABCDEF 1 BRIGHT A_CountDown
	loop
  Death:
	BSHD F 2 BRIGHT A_FadeOut
	loop
  }
}

actor EnemyNeedlerCrystal : NeedlerCrystal
{
	decal Needler
	OBITUARY "%o was spiked by a Needler Troop."
	Speed 60
	Damage 2
	+FORCEXYBILLBOARD
}

ACTOR BioBall
{
	Radius 8
	Height 1
	Health 1
	Damage 8
	PoisonDamage 50
	Speed 38
	Renderstyle Add
	Alpha 0.9
	SeeSound "BioRifle"
	DeathSound "BioGoo"
	Projectile
	+RIPPER
	+CORPSE
	+NODAMAGETHRUST
	+DONTSPLASH
	+FORCERADIUSDMG
	-NOGRAVITY
	States
	{
		Spawn:
			SBS3 A 0
			SBS3 A 0 BRIGHT ThrustThingZ(0, random(12, 16), 0, 1)
			SBS3 A 1 BRIGHT A_NoGravity
			SBS3 BC 1 BRIGHT
			SBS3 A 0 BRIGHT A_Gravity
			SBS3 AABBCC 1 BRIGHT
			goto Spawn+5
		Death:
			SBS3 A 0 A_NoGravity
			SBS3 A 0 A_Jump(127, 6)
			SBS3 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT
			SLDG EEEEEEEE 0 A_SpawnItemEx("BioBurstDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(2, 8), 0, 0, 0)
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(112, 100, 1)
			SLDG EFGH 2 BRIGHT
			Stop
		Crash:
			SBS4 A 0 A_Jump(127, 6)
			SBS4 A 0 A_Jump(80, 10)
			SBS4 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT A_Explode(2, 14, 0)
			SLDG EEEEEEEE 0 A_SpawnItemEx("BioBurstDrop", 0, 0, 0, Random(-2, 2), Random(-2, 2), Random(2, 8), 0, 0, 0)
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(90, 80, 1)
			SLDG EFGH 2 BRIGHT
			Stop
	}
}

ACTOR SlowBioBall : Bioball
{
	Radius 2
	Height 1
	Health 1
	Damage 8
	PoisonDamage 50
	Decal BioRifle
	Speed 10
	-NOGRAVITY
	States
	{
		Spawn:
			SBS3 ABC 1 BRIGHT
			loop
	}
}

actor Enemy_BioBall : BioBall
{
	DamageType "EnemyPoison"
	Damage 3
	PoisonDamage 25
	States
	{
		Spawn:
			SBS3 AABBCC 1 BRIGHT
			loop
	}
}

ACTOR GiantBioBall
{
	Decal BioRifle
	Radius 8
	Height 1
	Damage 14
	PoisonDamage 50
	Speed 38
	Scale 2.0
	Renderstyle Add
	Alpha 0.9
	SeeSound "BioRifle"
	DeathSound "BioGoo"
	Projectile
	+RIPPER
	+CORPSE
	+NODAMAGETHRUST
	+DONTSPLASH
	+FORCERADIUSDMG
	-NOGRAVITY
	States
	{
		Spawn:
			SBS3 A 0
			SBS3 A 0 BRIGHT ThrustThingZ(0, 14, 0, 1)
			SBS3 A 1 BRIGHT A_NoGravity
			SBS3 BC 1 BRIGHT
			SBS3 A 0 BRIGHT A_Gravity
			SBS3 AABBCC 1 BRIGHT
			goto Spawn+5
		Death:
			SBS3 A 0 A_NoGravity
			SBS3 A 0 A_Jump(127, 6)
			SBS3 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT
			SLDG EEEEEEEE 0 A_CustomMissile("SlowBioBall",17,0,Random(-360,360), 6, random(-360, 360))
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(140, 134, 1)
			SLDG EFGH 2 BRIGHT
			Stop
		Crash:
			SBS4 A 0 A_Jump(127, 6)
			SBS4 A 0 A_Jump(80, 10)
			SBS4 ABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABCABC 2 BRIGHT A_Explode(5, 24, 0)
			SLDG EEEEEEEE 0 A_CustomMissile("SlowBioBall",17,0,Random(-360,360), 6, random(0, 180))
			SLDG E 0 A_PlaySound("BioBurst")
			SLDG E 0 A_Explode(128, 128, 1)
			SLDG EFGH 2 BRIGHT
			Stop
	}
}

ACTOR VulgarShot
{
   Radius 8
   Height 11
   Speed 14
   Damage 4
   PoisonDamage 20
   SeeSound "BioRifle"
   RENDERSTYLE ADD
   ALPHA 0.67
   PROJECTILE
   +THRUGHOST
   +FORCEXYBILLBOARD
   DeathSound "FleshySplat"
   States
   {
   Spawn:
	  FVUL AAABBB 1 Bright A_SpawnItemEx("BarbTrail",Random(-8,8),Random(-8,8),Random(-8,8),0,0,0,0,128,0)
	  loop
   Death:
	  FVUL CDEF 4 Bright
	  stop
   }
}

ACTOR Barbtrail
{
   Radius 0
   Height 1
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.75
   +FORCEXYBILLBOARD
   States
   {
   Spawn:
	  NULL A 1 Bright
	  SSFX ABCDEFG 2 Bright
	  Stop
   }
}

actor HangerTrail : BarbTrail
{
	Translation "112:127=162:167"
}

actor HangerBall
{
  Radius 4
  Height 6
  Speed 10
  FastSpeed 15
  Damage 4
  SeeSound "hanger/attack"
  DeathSound "slimeball/splat"
  Scale 0.5
  Translation "112:127=162:167"
  RenderStyle Add
  Alpha 1.0
  Projectile
  +RANDOMIZE
  -NOGRAVITY
  +LOWGRAVITY
  +SPAWNSOUNDSOURCE
  states
  {
  Spawn:
    FVUL AAA 1 A_SpawnItemEx("HangerTrail", random (-2,2), random (-2,2), random (-2,2), random (-1,1), random (-1,1), random (-1,1), random (-180,180), SXF_CLIENTSIDE, 0)
	FVUL BBB 1 A_SpawnItemEx("HangerTrail", random (-2,2), random (-2,2), random (-2,2), random (-1,1), random (-1,1), random (-1,1), random (-180,180), SXF_CLIENTSIDE, 0)
    loop
  Death:
    FVUL C 0 A_ChangeFlag("NOGRAVITY", 1)
    FVUL CDEF 3
    stop
  }
}

ACTOR SliceProjectile
{
	Health 5
	Height 35
	Radius 14
	Speed 22
	PainSound "saberhit"
	DeathSound "saberhit"
	RENDERSTYLE NONE
	+NOBLOCKMONST
	+DONTSPLASH
	+SHOOTABLE
	+NOBLOOD
	+NOGRAVITY
	+NOCLIP
	+GHOST
	+THRUGHOST
	States
	{
		Spawn:
			TNT1 AA 1
			Stop
		Pain:
			TNT1 A 1 A_SpawnItemEX("SaberPuff",  0, 0, 0, 0, 0, 0, 0, 128, 0)
			TNT1 A 1 A_Pain
			Stop
		Death:
			TNT1 A 1 A_SpawnItemEX("SaberPuff",  0, 0, 0, 0, 0, 0, 0, 128, 0)
			TNT1 A 1 A_Scream
			Stop
	}
}

ACTOR ReflectProjectile : SliceProjectile
{
	PainChance 255
	PainSound "saberhit"
	DeathSound "saberhit"
	+REFLECTIVE
	+THRUGHOST
	+GHOST
	States
	{
		Spawn:
			TNT1 AA 1
			Stop
		Pain:
			TNT1 A 1
			TNT1 A 1 A_Pain
			Stop
		Death:
			TNT1 A 1
			TNT1 A 1 A_Scream
			Stop
	}
}