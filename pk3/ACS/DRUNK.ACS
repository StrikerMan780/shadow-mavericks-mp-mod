// DRUNK MODE
#library "DRUNK"
#include "zcommon.acs"

#define DrunkTID 8830
#define DrunkHUDID 79

function void makedrunk(void)
{
	GiveInventory("DrunkMode", 1);
	GiveInventory("DrunkenBastard", 1);
	SetWeapon("DrunkenBastard");
    ACS_NamedExecuteAlways("DRUNKSWAY", 0, 0);
}

function void soberup(void)
{
	TakeInventory("DrunkMode", 1);
	TakeInventory("DrunkenBastard", 1);
}

function void checkpermadrunk(void)
{
	if(GetCvar("SMMP_Scrumpy"))
	{
		makedrunk();
	}
	else
	{
		soberup();
	}
}


script "DRUNK_MODE" (void)
{
    makedrunk();
    Delay(35*30);
    soberup();
}

script "DRUNKSWAY" (int permanent) CLIENTSIDE
{
	if(ConsolePlayerNumber() != PlayerNumber())
	{
		Terminate;
	}
	
    int DrunkPlayer = DrunkTID+PlayerNumber();
    int fov;
    
    Thing_ChangeTID(0, DrunkPlayer);
    
    SetHudSize(640, 480, 0);
    SetFont("DRUNKM01");
    HudMessage(s:"a";
    HUDMSG_PLAIN, DrunkHUDID, CR_UNTRANSLATED, 320.0, 240.0, 0);
    
    while(CheckInventory("DrunkMode"))
    {
        int anglex = random(0.0005, 0.0035);
        int angley = random(0.0, 0.0025);
        int directionx = random(0, 1);
        int directiony = random(0, 1);
        for (fov = 90; fov >= 60; fov--)
        {
            SetCameraToTexture(DrunkPlayer, "DRUNKM01", fov);
            
            if(directionx == 0)
            {
                SetActorAngle(DrunkPlayer, GetActorAngle(DrunkPlayer)-anglex);
            }
            else
            {
                SetActorAngle(DrunkPlayer, GetActorAngle(DrunkPlayer)+anglex);
            }
            
            if(directiony == 0)
            {
                SetActorPitch(DrunkPlayer, GetActorPitch(DrunkPlayer)-angley);
            }
            else
            {
                SetActorPitch(DrunkPlayer, GetActorPitch(DrunkPlayer)+angley);
            }
            
            delay(1);
        }
        for (fov = 60; fov <= 90; fov++)
        {
            SetCameraToTexture(DrunkPlayer, "DRUNKM01", fov);
            
            if(directionx == 0)
            {
                SetActorAngle(DrunkPlayer, GetActorAngle(DrunkPlayer)+anglex);
            }
            else
            {
                SetActorAngle(DrunkPlayer, GetActorAngle(DrunkPlayer)-anglex);
            }
            
            if(directiony == 0)
            {
                SetActorPitch(DrunkPlayer, GetActorPitch(DrunkPlayer)+angley);
            }
            else
            {
                SetActorPitch(DrunkPlayer, GetActorPitch(DrunkPlayer)-angley);
            }
            
            delay(1);
        }
        
        delay(1);
    }
    
    Thing_ChangeTID(DrunkPlayer, 0);
    
    HudMessage(s:"a";
    HUDMSG_FADEOUT | HUDMSG_NOTWITHFULLMAP | HUDMSG_LAYER_UNDERHUD, DrunkHUDID, CR_UNTRANSLATED, 320.0, 240.0, 0.5, 1.0);
}

script "DRUNK_DEATH" DEATH
{
	soberup();
}

script "DRUNK_RESPAWN" RESPAWN
{
	checkpermadrunk();
}

script "DRUNK_ENTER" ENTER
{
	checkpermadrunk();
}