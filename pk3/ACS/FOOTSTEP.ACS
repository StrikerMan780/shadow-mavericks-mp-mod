#library "FOOTSTEP"
#include "zcommon.acs"

bool PlayingFootsteps[64];

// Functions
Function int GetVelocity(void)
{
	int vel;
	int x = GetActorVelX(0);
	int y = GetActorVelY(0);
	int angle = VectorAngle(x, y);
	
	if(((angle + 0.125) % 0.5) > 0.25)
	{
		vel = FixedDiv(y, Sin(angle));
	}
	else
	{
		vel = FixedDiv(x, Cos(angle));
	}
	return vel >> 16;
}

// Scripts
Script "FOOTSTEPS" ENTER CLIENTSIDE
{
	int DelayTime, StepVolume, i, SoundPlayed;
	int pnum = PlayerNumber();
	
	if(!PlayingFootsteps[PlayerNumber()])
	{
		PlayingFootsteps[pnum] = true;
		While(PlayingFootsteps[pnum] && PlayerNumber() != -1)
		{
			Delay(1);
			DelayTime = 16 - GetVelocity() / 2;
			StepVolume = 7 * GetVelocity();
			
			if(GetActorZ(0) - GetActorFloorZ(0) == 0)
			{
				str floorTexture = GetActorFloorTexture(0);
				str langDef = StrParam(s:"STEP_", s:floorTexture);
				str stepSound = StrParam(l:langDef);
				
				if (stricmp(langDef, stepSound) == 0)
				{
					ActivatorSound("step/generic", StepVolume);
				}
				else
				{
					ActivatorSound(stepSound, stepVolume);
				}
			}
			
			SoundPlayed = 0;
			Delay(DelayTime);
		}
		
		PlayingFootsteps[pnum] = false;
	}	
}

Script "INIT_FOOTSTEPS" OPEN CLIENTSIDE
{
	for(int i = 0; i < 64; i++)
	{
		if(PlayerInGame(i))
		{
			SetActivatorToPlayer(i);
			ACS_NamedExecuteAlways("FOOTSTEPS", 0);
		}
	}
}

script "FOOTSTEP_DEATH" DEATH CLIENTSIDE
{
	PlayingFootsteps[PlayerNumber()] = false;
}

Script "FOOTSTEP_RESPAWN" RESPAWN CLIENTSIDE
{
	ACS_NamedExecuteAlways("FOOTSTEPS", 0);
}

script "FOOTSTEP_DISCONNECT" (int who) DISCONNECT CLIENTSIDE
{
	PlayingFootsteps[who] = false;
}

script "FOOTSTEP_DEBUG" (void) NET CLIENTSIDE
{
	str floorTexture = GetActorFloorTexture(0);
	print(s:"Floor Texture: ", s:floorTexture);
}